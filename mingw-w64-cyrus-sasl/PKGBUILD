# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_realname=cyrus-sasl
pkgname=("${MINGW_PACKAGE_PREFIX}-cyrus-sasl"
         "${MINGW_PACKAGE_PREFIX}-libsasl"
         "${MINGW_PACKAGE_PREFIX}-libsasl-devel")
pkgver=2.1.26
pkgrel=1
pkgdesc="Cyrus Simple Authentication Service Layer (SASL) library"
arch=('any')
url="http://cyrusimap.web.cmu.edu/"
license=('custom')
groups=('sys-utils')
#makedepends=('heimdal-devel' 'openssl-devel' 'libsqlite-devel' 'libcrypt-devel')
options=('emptydirs' 'strip' '!makeflags')
source=(ftp://ftp.cyrusimap.org/cyrus-sasl/cyrus-sasl-${pkgver}.tar.gz
        01-no-undefined.patch
        02-exeext.patch
        03-fix-plugins.patch
        04-manpage-paths.patch
        05-remove-warnings.patch
        06-fix-sqlite-search.patch
        07-fix-pkgconfig.patch
        08-cyrus-sasl-2.1.26-size_t.patch
        09-cyrus-sasl-2.1.26-msys2.patch
        10-MinGW-w64-already-has-getopt.patch
        11-MinGW-w64-add-disable-win32-reg-option.patch
        12-MinGW-w64-uses-ws2_32-not-socket.patch
        13-MSYS2-Use-cp-f-instead-of-ln-s.patch)
md5sums=('a7f4e5e559a0e37b3ffc438c9456e425'
         '7f52862a74ada70ed2e257990f9b4334'
         'd7ff8a6d1902478190c705a580b4bd80'
         '744e8af08823097021e6087c8baca9c8'
         'abda82e2cb8dca0ebe2fbe9d076c379a'
         '44567c9345424c5c4642c006a39b0446'
         '5b2af6369c98a778b82b53dee65789ca'
         '716c142e9779dd087ce231345adec0c2'
         'bcaafcbc79054e8356217213d6eda16d'
         'b48df2818c4836801445d9dee0adfaa0'
         '23b565ac3921658bf9a2bdd8dc40ed96'
         '13a054e6fc1eccf0a23f9fafaa9b7ec0'
         '140e3926bd5aec3d6a44a275492a05e6'
         '4671c749f26cbeb3beb7372d373fc517')

prepare() {
  cd ${srcdir}/$_realname-$pkgver

  patch -p1 -i ${srcdir}/01-no-undefined.patch
  patch -p1 -i ${srcdir}/02-exeext.patch
  patch -p1 -i ${srcdir}/03-fix-plugins.patch
  patch -p1 -i ${srcdir}/04-manpage-paths.patch
  patch -p1 -i ${srcdir}/05-remove-warnings.patch
  patch -p1 -i ${srcdir}/06-fix-sqlite-search.patch
  patch -p2 -i ${srcdir}/07-fix-pkgconfig.patch
  patch -p1 -i ${srcdir}/08-cyrus-sasl-2.1.26-size_t.patch
  patch -p1 -i ${srcdir}/09-cyrus-sasl-2.1.26-msys2.patch
  patch -p1 -i ${srcdir}/10-MinGW-w64-already-has-getopt.patch
  patch -p1 -i ${srcdir}/11-MinGW-w64-add-disable-win32-reg-option.patch
  patch -p1 -i ${srcdir}/12-MinGW-w64-uses-ws2_32-not-socket.patch
  patch -p1 -i ${srcdir}/13-MSYS2-Use-cp-f-instead-of-ln-s.patch

  sed 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/' -i configure.in
  rm -f config/config.guess config/config.sub 
  rm -f config/ltconfig config/ltmain.sh config/libtool.m4
  rm -fr autom4te.cache
  libtoolize -c
  aclocal -I config -I cmulocal
  automake -a -c
  autoheader
  autoconf
  
  pushd saslauthd
    rm -f config/config.guess config/config.sub 
    rm -f config/ltconfig config/ltmain.sh config/libtool.m4
    rm -fr autom4te.cache
    libtoolize -c
    aclocal -I config -I ../cmulocal -I ../config
    automake -a -c
    autoheader
    autoconf
  popd
}

build() {
  local _plugindir=${MINGW_PREFIX}/lib/sasl2
  cd ${srcdir}/$_realname-$pkgver
  # Useful ref:
  # https://fedorapeople.org/cgit/elmarco/public_git/mingw32-cyrus-sasl.git/tree/mingw32-cyrus-sasl.spec
  # --oldincludedir=${MINGW_PREFIX}/include is so that /usr/include isn't added by default.
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=$CHOST \
    --host=$CHOST \
    --with-configdir=${MINGW_PREFIX}/etc/sasl2:${MINGW_PREFIX}/etc/sasl:${MINGW_PREFIX}/lib/sasl2 \
    --with-plugindir=${_plugindir} \
    --disable-static --enable-shared \
    --enable-sql \
    --with-sqlite3=${MINGW_PREFIX} \
    --disable-ldapdb \
    --disable-win32-reg \
    --oldincludedir=${MINGW_PREFIX}/include \
    --without-saslauthd --without-pwcheck \
    --without-authdaemond \
    sasl_cv_dlsym_adds_uscore=no

  make CFLAGS="$CFLAGS -Wall -Wno-char-subscripts \
               -Wno-unused -Wno-pointer-sign -Wno-uninitialized \
                -D_POSIX \
               -DS_IRUSR=_S_IREAD -DS_IWUSR=_S_IWRITE -DWIN32_LEAN_AND_MEAN" \
       LDFLAGS="$LDFLAGS -lws2_32 -ldl" \
       sasldir=${_plugindir}
}

package_cyrus-sasl() {
  depends=("libsasl=${pkgver}")
  pkgdesc="Cyrus saslauthd SASL authentication daemon"
  #backup=('etc/conf.d/saslauthd')

  cd $_realname-$pkgver/saslauthd
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/${MINGW_PREFIX}/share/licenses/cyrus-sasl"
  cp -pR ../COPYING "${pkgdir}${MINGW_PREFIX}/share/licenses/cyrus-sasl/"
}

package_libsasl() {
  groups=('libraries')
  options=('emptydirs' 'strip' 'libtool')
  pkgdesc="Cyrus Simple Authentication Service Layer (SASL) Library"
  depends=('libcrypt'
           'libopenssl'
           'heimdal-libs'
           'libsqlite'
           "${MINGW_PACKAGE_PREFIX}-dlfcn-svn")
  local _plugindir=${MINGW_PREFIX}/lib/sasl2

  cd cyrus-sasl-$pkgver
  for dir in include lib sasldb plugins utils; do
    pushd ${dir}
    make DESTDIR="${pkgdir}" install sasldir=${_plugindir}
    popd
  done
  rm -f "${pkgdir}"/${MINGW_PREFIX}/lib/sasl2/*.a
  rm -f "${pkgdir}"/${MINGW_PREFIX}/lib/*.a
  rm -f "${pkgdir}"/${MINGW_PREFIX}/lib/*.la
  rm -rf "${pkgdir}"/${MINGW_PREFIX}/include
  install -m755 -d "${pkgdir}/${MINGW_PREFIX}/share/licenses/libsasl"
  install -m644 COPYING "${pkgdir}/${MINGW_PREFIX}/share/licenses/libsasl/"
  # pushd "${pkgdir}"/${MINGW_PREFIX}/lib/sasl2
    # for x in .dll; do
      # mv $x $(echo $x | sed e 's/^msys-/lib/' e 's/[0-9][0-9]\.dll$/.so/')
    # done
  # popd
}

package_libsasl-devel() {
  pkgdesc="Libsasl headers and libraries"
  groups=('development')
  depends=("libsasl=${pkgver}" 'heimdal-devel' 'openssl-devel' 'libsqlite-devel' 'libcrypt-devel')
  options=('strip' 'libtool')
  local _plugindir=${MINGW_PREFIX}/lib/sasl2

  cd $_realname-$pkgver
  for dir in include lib; do
    pushd ${dir}
    make DESTDIR="${pkgdir}" install sasldir=${_plugindir}
    popd
  done
  rm -rf "${pkgdir}"/${MINGW_PREFIX}/bin
}

package_mingw-w64-x86_64-cyrus-sasl() {
  package_cyrus-sasl
}

package_mingw-w64-i686-cyrus-sasl() {
  package_cyrus-sasl
}

package_mingw-w64-x86_64-libsasl() {
  package_libsasl
}

package_mingw-w64-i686-libsasl() {
  package_libsasl
}

package_mingw-w64-x86_64-libsasl-devel() {
  package_libsasl-devel
}

package_mingw-w64-i686-libsasl-devel() {
  package_libsasl-devel
}

# /e/m2/repo/mingw-w64-cyrus-sasl/src/cyrus-sasl-2.1.26/sasldb
