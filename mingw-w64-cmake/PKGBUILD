# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=cmake
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=2.8.12.1
pkgrel=1
pkgdesc="A cross-platform open-source make system (mingw-w64)."
arch=('any')
url="http://www.cmake.org/"
license=("MIT")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config")
depends=("${_mingw_suff}-winpthreads"
		"${_mingw_suff}-curl"
		"${_mingw_suff}-expat"
		"${_mingw_suff}-libarchive"
		"${_mingw_suff}-ncurses"
		"${_mingw_suff}-pkg-config"
		"${_mingw_suff}-zlib")
options=('staticlibs' 'strip')
source=("http://www.cmake.org/files/v2.8/${_realname}-${pkgver}.tar.gz"
        "findfreetype.patch")
md5sums=('9d38cd4e2c94c3cea97d0e2924814acc'
         '327193131a09aba0caba52716cf76dd2')

prepare() {
  cd ${_realname}-${pkgver}
  patch -Np1 < ${srcdir}/findfreetype.patch
}

build() {
	mkdir -p ${pkgdir}${MINGW_PREFIX}
	pushd ${MINGW_PREFIX} > /dev/null
	export PREFIX_LIB=`pwd -W`
	popd > /dev/null
	
    mkdir -p "${srcdir}/${_realname}-${pkgver}-build-${CARCH}"
    cd "${srcdir}/${_realname}-${pkgver}-build-${CARCH}"
	
    MSYSTEM=MINGW "${srcdir}"/${_realname}-${pkgver}/configure \
      --prefix=${PREFIX_LIB} \
      --system-libs \
      --no-qt-gui \
	  --parallel=3
    
	plain "Start building..."
	make
}

package() {
	mkdir -p "${pkgdir}${MINGW_PREFIX}"
	pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
	export PREFIX_WIN=`pwd -W`
	popd > /dev/null

    cd "${srcdir}/${_realname}-${pkgver}-build-${CARCH}"
	./bin/cmake.exe -DCMAKE_INSTALL_PREFIX:PATH=${PREFIX_WIN} -P cmake_install.cmake
	mv ${pkgdir}${MINGW_PREFIX}/man ${pkgdir}${MINGW_PREFIX}/share/
	mv ${pkgdir}${MINGW_PREFIX}/doc ${pkgdir}${MINGW_PREFIX}/share/

    #rm -r "${pkgdir}${MINGW_PREFIX}/share"
}
