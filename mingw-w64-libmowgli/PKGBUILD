# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libmowgli

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.0.0
pkgrel=1
pkgdesc="Performance and usability-oriented extensions to C (mingw-w64)"
arch=('any')
url="http://www.atheme.org/project/mowgli"
license=('GPL')
depends=("${MINGW_PACKAGE_PREFIX}-openssl")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "git")
options=('staticlibs' 'strip')
source=("${_realname}-${pkgver}::git+https://github.com/atheme/libmowgli-2.git#tag=${_realname}-${pkgver}"
        libmowgli-2.0.0-mingw-have-declarations.patch)
md5sums=('SKIP'
         '47ee3521d81255a675ff83f2c48b4909')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/libmowgli-2.0.0-mingw-have-declarations.patch

  #autoreconf -fi
}

build() {
  cd ${srcdir}/${_realname}-${pkgver}
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-shared \
    --enable-static
  make
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}
  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
