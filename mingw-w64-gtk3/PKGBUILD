# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gtk3
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=3.11.8
pkgrel=3
pkgdesc="GObject-based multi-platform GUI toolkit (v3) (mingw-w64)"
arch=('any')
url="http://www.gtk.org"
license=("LGPL")
install=gtk3-${CARCH}.install
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config" "${_mingw_suff}-python2"
		"${_mingw_suff}-gobject-introspection")
# python2 is required to run gdbus-codegen
makedepends+=("autoconf" "automake" "libtool")
# autotools are required because several Makefile.am are modified
depends=("${_mingw_suff}-gcc-libs"
		"${_mingw_suff}-atk>=2.7.5"
		"${_mingw_suff}-pango>=1.32.4"
		"${_mingw_suff}-glib2>=2.37.5"
		"${_mingw_suff}-cairo>=1.12.0"
		"${_mingw_suff}-gdk-pixbuf2>=2.27.1"
		"${_mingw_suff}-shared-mime-info")
options=('strip' 'staticlibs')
source=("http://ftp.gnome.org/pub/gnome/sources/gtk+/${pkgver%.*}/gtk+-${pkgver}.tar.xz"
		0001-REBURN-THE-DEF-FILE.mingw.patch
		0009-give-cc-to-gir-scanner.mingw.patch
		0010-put-gtk-dll-into-path.mingw.patch
		0011-gir-for-gdkwin32.mingw.patch
		0012-embed-manifest.all.patch
		0014-fix-gnu-print-format.patch)
sha256sums=('8c9204254258d49b662f56e8d85abb0c28a2c1267a2ac71124b2366055cb03b5'
            '647c0bae17c9935a6f6b4ac29e59d522d0e9e561edd56e42bcce9fc11ebc235a'
            '458840bc6bedeb7503114c89bf9f9f34766b39f9a6bfaec09af411a023c96a31'
            '75f3bd1098c39ccf881508f4c9d23278465bcfe15beaed4bed2af4b00efa00f3'
            'ff990992fbcdc242a9314d4b01ce2561326dc100b7469a9ff7bfb6cb5962b268'
            '9e48b83d2a5649cf220ccb3b70441dcd24919e5e5e3c7ddf9559523d025a3abc'
            '40698e9d48ac34729c74c89f41efa39562a55ba5e3c2954e44e41d150544c379')

prepare() {
	cd "${srcdir}/gtk+-${pkgver}"
	patch -Np1 -i "${srcdir}/0001-REBURN-THE-DEF-FILE.mingw.patch"
	patch -Np1 -i "${srcdir}/0009-give-cc-to-gir-scanner.mingw.patch"
	patch -Np1 -i "${srcdir}/0010-put-gtk-dll-into-path.mingw.patch"
	patch -Np1 -i "${srcdir}/0011-gir-for-gdkwin32.mingw.patch"
	patch -Np1 -i "${srcdir}/0012-embed-manifest.all.patch"
	patch -Np1 -i "${srcdir}/0014-fix-gnu-print-format.patch"
	autoreconf -i
}

build() {
	export MSYS2_ARG_CONV_EXCL="-//OASIS"
	CFLAGS+=" -mms-bitfields"
	CXXFLAGS=" -mms-bitfields"
	mkdir -p "${srcdir}/build-${MINGW_CHOST}"
	cd "${srcdir}/build-${MINGW_CHOST}"
	${srcdir}/gtk+-${pkgver}/configure \
		--prefix=${MINGW_PREFIX} \
		--build=${MINGW_CHOST} \
		--host=${MINGW_CHOST} \
		--enable-win32-backend \
		--disable-gtk2-dependency \
		--enable-static \
		--enable-shared \
		--enable-introspection \
		--disable-cups
	make
}

package() {
	export MSYS2_ARG_CONV_EXCL="-//OASIS"
	cd "${srcdir}/build-${MINGW_CHOST}"
	make -j1 DESTDIR="$pkgdir" install
	find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' -o -name '*.manifest' | xargs -rtl1 rm
	
	mv ${pkgdir}${MINGW_PREFIX}/bin/gtk-update-icon-cache{,-3.0}.exe
	mv ${pkgdir}${MINGW_PREFIX}/share/man/man1/gtk-update-icon-cache{,-3.0}.1
}
