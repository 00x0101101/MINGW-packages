# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gtk3
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=3.10.6
pkgrel=1
pkgdesc="GObject-based multi-platform GUI toolkit (v3) (mingw-w64)"
arch=('any')
url="http://www.gtk.org"
license=("LGPL")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config" "${_mingw_suff}-python2") #gtk-update-icon-cache 
# python2 is required to run gdbus-codegen
makedepends+=("autoconf" "automake" "libtool")
# autotools are required because several Makefile.am are modified
depends=(
"${_mingw_suff}-crt"
"${_mingw_suff}-atk>=2.7.5"
"${_mingw_suff}-pango>=1.32.4"
"${_mingw_suff}-glib2>=2.37.5"
"${_mingw_suff}-cairo>=1.12.0"
"${_mingw_suff}-gdk-pixbuf2>=2.27.1")
options=('strip' 'staticlibs')

source=(
"http://ftp.gnome.org/pub/gnome/sources/gtk+/${pkgver%.*}/gtk+-${pkgver}.tar.xz"
"0004-BURN-THE-.DEF.patch"
"0005-Remove-gobject-introspection.patch")

# The second source file is downloaded from Fedora Project

sha256sums=('3c24c57fc5cb7a5ea39d3a3ff7b12be63d7f21a52fd6f20f7b983e19b7b0268a'
            'b35ecacab70680391e06af85893b7aa8612f0812ded35395b6e93f49e8eab92b'
            'e9e29fbf7444e449def11a31a6c2682932df416b1b5c82dbe0eac54e3fe51dd9')

prepare() {
  cd "${srcdir}/gtk+-${pkgver}"
  patch -Np1 < "../0004-BURN-THE-.DEF.patch"
  patch -Np1 < "../0005-Remove-gobject-introspection.patch"
  autoreconf -i
}

build() {
    CFLAGS+=" -mms-bitfields"
    CXXFLAGS=" -mms-bitfields"
    mkdir -p "${srcdir}/build-${MINGW_CHOST}"
    cd "${srcdir}/build-${MINGW_CHOST}"
    ${srcdir}/gtk+-${pkgver}/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --enable-win32-backend \
      --enable-gtk2-dependency \
      --enable-static \
      --enable-shared \
      --disable-cups
    make
}

package() {
    cd "${srcdir}/build-${MINGW_CHOST}"
    make -j1 DESTDIR="$pkgdir" install
    find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' -o -name '*.manifest' | xargs -rtl1 rm
    rm -r "${pkgdir}${MINGW_PREFIX}/etc"
    rm -r "${pkgdir}${MINGW_PREFIX}/share/man"
}
