# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gtk3

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.13.5
pkgrel=2
pkgdesc="GObject-based multi-platform GUI toolkit (v3) (mingw-w64)"
arch=('any')
url="http://www.gtk.org"
license=("LGPL")
install=gtk3-${CARCH}.install
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
            "${MINGW_PACKAGE_PREFIX}-pkg-config"
            "${MINGW_PACKAGE_PREFIX}-python2"
            "${MINGW_PACKAGE_PREFIX}-gobject-introspection")
# python2 is required to run gdbus-codegen
makedepends+=("autoconf" "automake" "libtool")
# autotools are required because several Makefile.am are modified
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
        "${MINGW_PACKAGE_PREFIX}-atk"
        "${MINGW_PACKAGE_PREFIX}-pango"
        "${MINGW_PACKAGE_PREFIX}-glib2>=2.41.0"
        "${MINGW_PACKAGE_PREFIX}-cairo"
        "${MINGW_PACKAGE_PREFIX}-gdk-pixbuf2"
        "${MINGW_PACKAGE_PREFIX}-shared-mime-info")
options=('strip' 'staticlibs')
source=("http://ftp.gnome.org/pub/gnome/sources/gtk+/${pkgver%.*}/gtk+-${pkgver}.tar.xz"
        0010-put-gtk-dll-into-path.mingw.patch
        0012-embed-manifest.all.patch
        0013-fix-mingw-w64-fstat-call.mingw.patch
        0014-fix-gnu-print-format.patch
        0015-Suppress-a-warning-from-SetWindowLongPtr.all.patch
        0035-enable-rgba-w32-windows.all.patch
        0037-W32-better-dwm-loading-and-support-dwm-detection.all.patch
        0053-dont-forget-to-bring-the-hwnd.all.patch
        0054-no-transparency-for-children.all.patch
        0055-opacity-for-non-toplevels-is-not-an-error.all.patch
        0056-dont-unregister-what-is-not-registered.all.patch
        0057-ensure-operation-is-around.all.patch
        0059-Make-sure-native-W32-print-dialog-uses-visual-styles.all.patch
        0060-missing-w32-theme.all.patch
        popover.ui)
md5sums=('1caa63f393765973960bc095ab06449c'
         '9e0296da2986be7697cf343563b85d1f'
         '47aa5b0d120102d526d2f781338f93b1'
         '21789d52c1debcab59f8b6a99232de68'
         '3052a2c4dac22042780471b14463ea89'
         '36ad8b20dd38e586e92ddb39c59eb923'
         '397bf8001d3da75e06b5794e378ae25f'
         'ad383a497a9134355e0a549f844c3a33'
         'dae9994d6daa1152868fa121029ae873'
         '31d098e6856ddc8dde7259ae572f8bf1'
         '0267a2e486196c274735212496954b79'
         '795d81b26e25ebe3887de61ee97f3cf6'
         '5b16fc960015919c119998db2de1844a'
         '9eb2f2529f9ba35edb912a8d8c02c635'
         '553ffdb5e20fff064d88b3778dc8b3db'
         '8e7e3fc969a4ef0aee0be83a9a19b9d1')

prepare() {
  cd "${srcdir}/gtk+-${pkgver}"
  patch -Np1 -i "${srcdir}"/0010-put-gtk-dll-into-path.mingw.patch
  patch -Np1 -i "${srcdir}"/0012-embed-manifest.all.patch
  patch -Np1 -i "${srcdir}"/0013-fix-mingw-w64-fstat-call.mingw.patch
  patch -Np1 -i "${srcdir}"/0014-fix-gnu-print-format.patch
  patch -Np1 -i "${srcdir}"/0015-Suppress-a-warning-from-SetWindowLongPtr.all.patch
  patch -Np1 -i "${srcdir}"/0035-enable-rgba-w32-windows.all.patch
  patch -Np1 -i "${srcdir}"/0037-W32-better-dwm-loading-and-support-dwm-detection.all.patch
  patch -Np1 -i "${srcdir}"/0053-dont-forget-to-bring-the-hwnd.all.patch
  patch -Np1 -i "${srcdir}"/0054-no-transparency-for-children.all.patch
  patch -Np1 -i "${srcdir}"/0055-opacity-for-non-toplevels-is-not-an-error.all.patch
  patch -Np1 -i "${srcdir}"/0056-dont-unregister-what-is-not-registered.all.patch
  patch -Np1 -i "${srcdir}"/0057-ensure-operation-is-around.all.patch
  patch -Np1 -i "${srcdir}"/0059-Make-sure-native-W32-print-dialog-uses-visual-styles.all.patch
  patch -Np1 -i "${srcdir}"/0060-missing-w32-theme.all.patch
  cp "${srcdir}/popover.ui" "./demos/gtk-demo"
  
  autoreconf -i
}

build() {
  export MSYS2_ARG_CONV_EXCL="-//OASIS"
  [[ -d "${srcdir}/${CHOST}" ]] && rm -rf "${srcdir}/${CHOST}"
  cp -rf ${srcdir}/gtk+-${pkgver} "${srcdir}/${CHOST}"
  cd "${srcdir}/${CHOST}"
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-win32-backend \
    --disable-gtk2-dependency \
    --enable-static \
    --enable-shared \
    --enable-introspection \
    --disable-cups
  make
}

package() {
  export MSYS2_ARG_CONV_EXCL="-//OASIS"
  cd "${srcdir}/${CHOST}"
  make -j1 DESTDIR="$pkgdir" install
  find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' -o -name '*.manifest' | xargs -rtl1 rm
  
  mv ${pkgdir}${MINGW_PREFIX}/bin/gtk-update-icon-cache{,-3.0}.exe
  mv ${pkgdir}${MINGW_PREFIX}/share/man/man1/gtk-update-icon-cache{,-3.0}.1
}
