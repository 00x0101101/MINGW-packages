# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_realname=qt-creator
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
_pre=rc1
_base_ver=3.1.0
pkgver=${_base_ver}${_pre}
pkgrel=1
pkgdesc='Cross-patform IDE'
url='http://qt-project.org'
arch=('any')
depends=("${_mingw_suff}-qt5" "${_mingw_suff}-gcc" "${_mingw_suff}-make")
options=('docs' '!strip' 'staticlibs')
optdepends=("${_mingw_suff}-gdb: for the debugger"
			"${_mingw_suff}-cmake: for cmake project support"
			"${_mingw_suff}-clang: for clang code model"
			#'openssh-askpass: for ssh support'
			#'git: for git support'
			#'mercurial: for mercurial support'
			#'bzr: for bazaar support'
			)
_pkgfqn="${_realname}-opensource-src-${_base_ver}-${_pre}"
source=(#http://download.qt-project.org/official_releases/qtcreator/${pkgver%.*}/${pkgver}/${_pkgfqn}.tar.gz
		http://download.qt-project.org/development_releases/qtcreator/${_base_ver%.*}/${_base_ver}-${_pre}/${_pkgfqn}.tar.gz
		qt-creator-3.0.0-path-quote.patch
		qt-creator-3.1.0-Allow-static-clang-via-LLVM_STATIC-env-var.patch)
noextract=(${_pkgfqn}.tar.gz)
sha256sums=('b2bda80fbbc4d901a793c57e33dd47ceacd348ca433684909c6e7ee7704e4ce9'
            '26f9f487cd609091900a6d900ab5067023b4e4349e06f4477c80debbbd32ec6f'
            '606e66eae324312f168c543fd0a3bbfe78b35f4b1f592ad7a11c5b1b7a6c83ae')

prepare() {
	cd $startdir/
	[ -d $srcdir/${_pkgfqn} ] || tar -xzvf ${_pkgfqn}.tar.gz -C $srcdir || true
	
	cd ${srcdir}/${_pkgfqn}
	patch -p0 -i ${srcdir}/qt-creator-3.0.0-path-quote.patch
	patch -p1 -i ${srcdir}/qt-creator-3.1.0-Allow-static-clang-via-LLVM_STATIC-env-var.patch
}

build() {
	mkdir ${srcdir}/build-${MINGW_CHOST}
	cd ${srcdir}/build-${MINGW_CHOST}
	export LLVM_INSTALL_DIR=${MINGW_PREFIX}
	export LLVM_STATIC=1
	${MINGW_PREFIX}/bin/qmake.exe ../${_pkgfqn}/qtcreator.pro CONFIG+=release
	make
	make docs
}

package() {
	cd ${srcdir}/build-${MINGW_CHOST}
	make INSTALL_ROOT="${pkgdir}${MINGW_PREFIX}" install
	make INSTALL_ROOT="${pkgdir}${MINGW_PREFIX}" install_docs
	
	install -Dm644 ${srcdir}/${_pkgfqn}/LGPL_EXCEPTION.TXT ${pkgdir}${MINGW_PREFIX}/share/licenses/qtcreator/LGPL_EXCEPTION.TXT
}

