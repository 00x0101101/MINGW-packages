# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=ogre3d
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-hg"
_rev=r6969.f40744b1106a
_base_ver=1.9.0
pkgver=${_base_ver}.${_rev}
pkgrel=1
pkgdesc="A cross-platform 3D game engine (mingw-w64)"
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
arch=('any')
url="http://www.ogre3d.org/"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config" "${MINGW_PACKAGE_PREFIX}-cmake" "mercurial")
depends=("${MINGW_PACKAGE_PREFIX}-boost"
        "${MINGW_PACKAGE_PREFIX}-cppunit"
        "${MINGW_PACKAGE_PREFIX}-FreeImage"
        "${MINGW_PACKAGE_PREFIX}-freetype"
        "${MINGW_PACKAGE_PREFIX}-glsl-optimizer"
        "${MINGW_PACKAGE_PREFIX}-hlsl2glsl"
        "${MINGW_PACKAGE_PREFIX}-intel-tbb"
        "${MINGW_PACKAGE_PREFIX}-nvidia-cg-toolkit"
        "${MINGW_PACKAGE_PREFIX}-ois"
        "${MINGW_PACKAGE_PREFIX}-tinyxml"
        "${MINGW_PACKAGE_PREFIX}-zlib"
        "${MINGW_PACKAGE_PREFIX}-zziplib"
        )
options=('staticlibs' '!strip') # '!buildflags'
source=("${_realname}-${pkgver}.tar.xz"{,.sig}
        001-remove-march-from-cflags.patch
        002-endlessworld-fix-cast.patch
        003-freeimage-link-with-shared.patch
        004-use-mingw-w64-directx.patch
        005-mingw-w64-redeclarations.patch
        006-cmake-wrong-command.patch
        007-fix-_WIN32-defines2.patch)
md5sums=('d6f82c8a9ef9b82947d0ce388be9ff74'
         'SKIP'
         'a2cbfc846906bbb80c7b05d2d60a2779'
         '3c00c5584a8edcb4dc07a57697a73d59'
         '166486655cd358be175f3b32b63e0ca5'
         'ee355ac6d4a271721407f030e34b5651'
         '56477180dc083746de83ea2aae3efd90'
         '31dbe883b2d9d03e767e2e2e7365795e'
         '9af326a7f9c7863e25ea265de4a2a223')

# source PKGBUILD && mksource
mksource() {
  [[ -x /usr/bin/hg ]] || (echo "hg not found. Install mercurial." && return 1)

  __hgroot='https://bitbucket.org/sinbad'
  __hgrepo='ogre'
  __hgbranch='default'
  __hgrev="v1-9"

  hg clone -u ${__hgrev} "${__hgroot}/${__hgrepo}/" ${__hgrepo}
  #hg clone -b ${__hgbranch} "${__hgroot}${__hgrepo}#${__hgrev}" ${__hgrepo}

  pushd ${__hgrepo}
    if (( $(hg id -n) < $(hg id -nr ${__hgbranch}) )); then
      printf 'You are not building the latest revision!\n'
      printf "Consider updating __hgrev to $(hg id -r ${__hgbranch}).\n"
    fi
    __revis="r$(hg identify -n).$(hg identify -i)"
  popd

  mv ${__hgrepo} ${_realname}-${_base_ver}.${__revis}
  find ${_realname}-${_base_ver}.${__revis} -depth -type d -name .hg -exec rm -rf {} \;
  rm ${_realname}-${_base_ver}.${__revis}/{.hgignore,.hgtags}
  tar -cJf ${_realname}-${_base_ver}.${__revis}.tar.xz ${_realname}-${_base_ver}.${__revis}/*
  rm -r ${_realname}-${_base_ver}.${__revis}

  gpg --detach-sign ${_realname}-${_base_ver}.${__revis}.tar.xz

  #scp ${pkgname}-${_base_ver}.tar.xz nym:/srv/ftp/other/vim/
  #scp ${pkgname}-${_base_ver}.tar.xz.sig nym:/srv/ftp/other/vim/
}

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/001-remove-march-from-cflags.patch
  patch -p0 -i ${srcdir}/002-endlessworld-fix-cast.patch
  patch -p1 -i ${srcdir}/003-freeimage-link-with-shared.patch
  patch -p1 -i ${srcdir}/004-use-mingw-w64-directx.patch
  patch -p1 -i ${srcdir}/005-mingw-w64-redeclarations.patch
  patch -p1 -i ${srcdir}/006-cmake-wrong-command.patch
  patch -p1 -i ${srcdir}/007-fix-_WIN32-defines2.patch
}

build() {
  local -a extra_config

  if check_option "debug" "y"; then
    extra_config+=( -DCMAKE_BUILD_TYPE=Debug )
  else
    extra_config+=( -DCMAKE_BUILD_TYPE=Release )
  fi

  CFLAGS+=" -O3 -fno-devirtualize"
  CXXFLAGS+=" -O3 -fno-devirtualize"
  export DXSDK_DIR=${MINGW_PREFIX}/${MINGW_CHOST}
  mkdir ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX:PATH=${pkgdir}${MINGW_PREFIX} \
    -DOGRE_DEPENDENCIES_DIR:PATH=${MINGW_PREFIX} \
    -DOGRE_INSTALL_TOOLS=ON \
    -DOGRE_COPY_DEPENDENCIES=OFF \
    -DOGRE_INSTALL_DEPENDENCIES=OFF \
    -DOGRE_BUILD_RENDERSYSTEM_D3D9=ON \
    -DOGRE_BUILD_RENDERSYSTEM_D3D11=ON \
    -DFREETYPE_FT2BUILD_INCLUDE_DIR=${MINGW_PREFIX}/include/freetype2 \
    "${extra_config[@]}" \
    ../${_realname}-${pkgver}

  make
}

package() {
  local -a _buildtype

  if check_option "debug" "y"; then
    _buildtype=Debug
  else
    _buildtype=Release
  fi
  
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 install #DESTDIR="$pkgdir"
  pushd ${pkgdir}${MINGW_PREFIX} > /dev/null

  mv bin/${_buildtype}/* bin/
  rm -r bin/${_buildtype}
  mv lib/${_buildtype}/* lib/
  rm -r lib/${_buildtype}

  sed -i "s|../../|../|g" \
    bin/plugins.cfg \
    bin/resources.cfg \
    bin/quakemap.cfg

  mkdir -p share/cmake-3.0/Modules
  mv CMake/* share/cmake-3.0/Modules/
  rm -r CMake
  popd > /dev/null
}
