# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Saul Ibarra Corretge <saghul@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_realname=p11-kit
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.20.3
pkgrel=3
pkgdesc="Library to work with PKCS#11 modules"
arch=('any')
url="http://p11-glue.freedesktop.org"
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-libtasn1"
          "${MINGW_PACKAGE_PREFIX}-libffi"
          "${MINGW_PACKAGE_PREFIX}-gettext"
          "${MINGW_PACKAGE_PREFIX}-libtasn1")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "gtk-doc")
options=('!debug' 'strip' 'staticlibs')
source=($url/releases/$_realname-$pkgver.tar.gz{,.sig}
        0000-no-proxy.mingw.patch
        0001-allow-gtkdocize.all.patch
        0002-disable-managed-test.all.patch
        0003-fix-test-path.all.patch
        0004-fix-abspath-check.all.patch
        0005-no-getuid-on-w32.all.patch
        0006-p11-kit-relocation.patch
        0007-add-debugging-to-mmap.patch
        0008-add-debugging-to-path.patch)
md5sums=('c0b057be50ab9721cfc15a7aae86ca3b'
         'SKIP'
         '70330b415eb5167e5a530c13bb96c212'
         'b7a706e19b5e29d96889ca2410a151df'
         'c11cba6726e4791ed8ff917978755596'
         'd4feddce2bb360ab466861dbbe4fdadd'
         '9a91b743f18126ff7d4d08e43c38d0a3'
         '8837624f5b0c588ce043f2774388632d'
         '07d3309cdd542206b2c6b83bc0ab085f'
         '0dcd0d7e10d5d898f32da8866580d640'
         '27793fc62cf1fbfe52964ba8099d2b44')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  rm -f common/pathtools.c common/pathtools.h > /dev/null 2>&1 || true
  patch -p1 -i ${srcdir}/0000-no-proxy.mingw.patch
  patch -p1 -i ${srcdir}/0001-allow-gtkdocize.all.patch
  patch -p1 -i ${srcdir}/0002-disable-managed-test.all.patch
  patch -p1 -i ${srcdir}/0003-fix-test-path.all.patch
  patch -p1 -i ${srcdir}/0004-fix-abspath-check.all.patch
  patch -p1 -i ${srcdir}/0005-no-getuid-on-w32.all.patch
  patch -p1 -i ${srcdir}/0006-p11-kit-relocation.patch
  # patch -p1 -i ${srcdir}/0007-add-debugging-to-mmap.patch
  # patch -p1 -i ${srcdir}/0008-add-debugging-to-path.patch

  autoreconf -vfi
  gtkdocize
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  "${srcdir}"/${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --disable-silent-rules \
    --sysconfdir=${MINGW_PREFIX}/etc \
    --localstatedir=${MINGW_PREFIX}/var \
    --with-trust-paths=${MINGW_PREFIX}/etc/pki/ca-trust/source:${MINGW_PREFIX}/share/pki/ca-trust-source
  MSYS2_ARG_CONV_EXCL="-DP11_SYSTEM_CONFIG_FILE=;-DP11_SYSTEM_CONFIG_MODULES=;-DP11_PACKAGE_CONFIG_MODULES=;-DP11_MODULE_PATH=;-DP11_BINDIR=;-DBINDIR=;-DDATADIR=;-DSYSCONFDIR=;-DPRIVATEDIR=" \
    make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  MSYS2_ARG_CONV_EXCL="-DP11_SYSTEM_CONFIG_FILE=;-DP11_SYSTEM_CONFIG_MODULES=;-DP11_PACKAGE_CONFIG_MODULES=;-DP11_MODULE_PATH=;-DP11_BINDIR=;-DBINDIR=;-DDATADIR=;-DSYSCONFDIR=;-DPRIVATEDIR=" \
    make DESTDIR="$pkgdir" install
}
