# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=openimageio
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=1.3.11
_commit=1548a68
pkgrel=2
pkgdesc="A library for reading and writing images, including classes, utilities, and applications (mingw-w64)"
arch=('any')
url="http://www.openimageio.org/"
license=("custom")
depends=("${_mingw_suff}-openexr"
		"${_mingw_suff}-boost"
		"${_mingw_suff}-jasper" 
		"${_mingw_suff}-glew"
		"${_mingw_suff}-libtiff"
		"${_mingw_suff}-opencolorio"
		#"${_mingw_suff}-intel-tbb"
		"${_mingw_suff}-libjpeg"
		"${_mingw_suff}-libpng"
		"${_mingw_suff}-giflib"
		"${_mingw_suff}-zlib"
		"${_mingw_suff}-freetype"
		"${_mingw_suff}-openjpeg"
		"${_mingw_suff}-libwebp")
makedepends=("${_mingw_suff}-cmake" "${_mingw_suff}-qt5" "${_mingw_suff}-python2" "${_mingw_suff}-boost")
optdepends=(#"${_mingw_suff}-qt4: iv image viewer"
            "${_mingw_suff}-python2: bindings support")
options=('strip' 'buildflags')
source=(https://github.com/OpenImageIO/oiio/tarball/Release-$pkgver
		fix-mingw-w64.patch
		workaround-ansidecl-h-PTR-define-conflict.patch)
md5sums=('e8594a15a3f370a36bcfdaaecbc6a19f'
         '78b0e6236eaef3086c2e32473c7e40fb'
         '6602e0502de60003e247c2e754e531fd')

prepare() {
	cd ${srcdir}/OpenImageIO-oiio-${_commit}
	patch -p1 -i ${srcdir}/fix-mingw-w64.patch
	patch -p1 -i ${srcdir}/workaround-ansidecl-h-PTR-define-conflict.patch
}

build() {
	mkdir -p ${pkgdir}${MINGW_PREFIX}
	pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
	export PREFIX_WIN=`pwd -W`
	popd > /dev/null
	
	pushd ${MINGW_PREFIX} > /dev/null
	export PREFIX_DEPS=`pwd -W`
	popd > /dev/null
	
	export OPENJPEG_HOME=${MINGW_PREFIX}
	cd OpenImageIO-oiio-${_commit}
	cd src
	[[ -d build ]] && rm -r build
	mkdir build && cd build
    ${MINGW_PREFIX}/bin/cmake.exe \
		-G"MSYS Makefiles" \
		-DCMAKE_INSTALL_PREFIX:PATH=${PREFIX_WIN} \
		-DPYLIB_INSTALL_DIR=lib/python2.7/site-packages \
		-DOPENJPEG_INCLUDE_DIR=${PREFIX_DEPS}/include/openjpeg-1.5 \
		-DOPENJPEG_OPENJPEG_LIBRARIES=${PREFIX_DEPS}/lib/libopenjpeg.dll.a \
		-DUSE_EXTERNAL_TBB=ON \
		-DCMAKE_SHARED_LINKER_FLAGS=" -Wl,--export-all-symbols -Wl,--enable-auto-import " \
		-DOIIO_BUILD_TESTS=ON \
      ../..
    make
}

package() {
  cd "$srcdir"/OpenImageIO-oiio-${_commit}/src/build

  make install
  rm -r ${pkgdir}${MINGW_PREFIX}/doc

  # license
  cd ../..
  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/$_realname/LICENSE"
}
