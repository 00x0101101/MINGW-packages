# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=gplugin
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.0.18
pkgrel=1
arch=('any')
pkgdesc="A GObject based library that implements a reusable plugin system (mingw-w64)"
license=("LGPL 2")
url="https://bitbucket.org/rw_grim/gplugin"
depends=("${MINGW_PACKAGE_PREFIX}-gtk3"
         "${MINGW_PACKAGE_PREFIX}-libxslt")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
             "gettext")
options=('strip' '!debug' 'staticlibs')
source=(https://bitbucket.org/rw_grim/${_realname}/get/v$pkgver.tar.bz2
        001-win.patch)
sha256sums=('fe0d6e450ff9f63e866bf75344cb561985dccae1e89e4162c20d07d961b71fc9'
            '583b969c8d67fd303cf219e5a6f475be8f4f535711324966efc57479fe031153')

prepare() {
  cd rw_grim-gplugin-9f4dea11d6eb

  patch -p1 -i ${srcdir}/001-win.patch
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ${MINGW_PREFIX}/bin/cmake \
    -DCMAKE_INSTALL_PREFIX=${pkgdir}${MINGW_PREFIX} \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS}" \
    -DCMAKE_C_FLAGS="${CFLAGS}" \
    -DBUILD_GJS="Off" \
    -DBUILD_LUA="Off" \
    -DBUILD_PYTHON="Off" \
    -DBUILD_SEED="Off" \
    -DTESTING_ENABLED="Off" \
    -G "MSYS Makefiles" \
   ../rw_grim-gplugin-9f4dea11d6eb
  make
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make install

  install -Dm644 "${srcdir}/rw_grim-gplugin-9f4dea11d6eb/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"

  mv "$pkgdir${MINGW_PREFIX}/lib/libgplugin.dll" "$pkgdir${MINGW_PREFIX}/bin"
  mv "$pkgdir${MINGW_PREFIX}/lib/libgplugin-gtk.dll" "$pkgdir${MINGW_PREFIX}/bin"

  MINGW_PREFIX_W=$(cygpath -m "$pkgdir${MINGW_PREFIX}")
  sed -i "s#${MINGW_PREFIX_W}#${MINGW_PREFIX}#g" "$pkgdir"${MINGW_PREFIX}/lib/pkgconfig/gplugin.pc
  sed -i "s#${MINGW_PREFIX_W}#${MINGW_PREFIX}#g" "$pkgdir"${MINGW_PREFIX}/lib/pkgconfig/gplugin-gtk.pc
}
