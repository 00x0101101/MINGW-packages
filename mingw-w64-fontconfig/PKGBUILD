# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=fontconfig
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=2.11.0
pkgrel=2
pkgdesc="A library for configuring and customizing font access (mingw-w64)"
arch=('any')
url="http://www.fontconfig.org/release"
license=("custom")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config")
depends=("${_mingw_suff}-gcc-libs"
		"${_mingw_suff}-expat>=2.1.0"
		"${_mingw_suff}-freetype>=2.3.11"
		"${_mingw_suff}-bzip2>=1.0.6"
		"${_mingw_suff}-libiconv")
options=('staticlibs' 'strip')
case $MINGW_CHOST in
	i686*)
		install=${_realname}32.install
	;;
	x86_64*)
		install=${_realname}64.install
	;;
esac
source=("http://www.fontconfig.org/release/fontconfig-$pkgver.tar.bz2"
		"fontconfig-2.11.0-symlinks-fix.patch")
sha256sums=('cb0e0ef6f03bc8568e95653840bb07d5859dc89d7ce2cade9d94fcccf8c1a467'
            '92541375e945261832d4876551f94596fcf540c4edaf789ac0f36de7592c5966')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/fontconfig-2.11.0-symlinks-fix.patch
}

build() {
    mkdir -p "${srcdir}/${_realname}-${pkgver}-build-${CARCH}"
    cd "${srcdir}/${_realname}-${pkgver}-build-${CARCH}"
    "${srcdir}"/${_realname}-${pkgver}/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --enable-iconv \
	  --disable-docs \
      --with-arch=${CARCH} \
	  as_ln_s="cp -pR"
    sed -i 's,all-am: Makefile $(PROGRAMS),all-am:,' test/Makefile
    make
}

package() {
    cd "${srcdir}/${_realname}-${pkgver}-build-${CARCH}"
    make -j1 DESTDIR="$pkgdir" install
    find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    #rm -r "${pkgdir}${MINGW_PREFIX}/"{etc,share}
}