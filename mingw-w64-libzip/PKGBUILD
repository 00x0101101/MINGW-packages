# Maintainer: Jianfeng Liu <liujianfeng1994@gmail.com>
# Contributor: Olivier Michel <Olivier.Michel@cyberbotics.com>

_realname=libzip
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.1.2
pkgrel=1
pkgdesc="A C library for reading, creating, and modifying zip archives (mingw-w64)"
url="http://www.nih.at/libzip/index.html"
license=('BSD')
arch=('any')
depends=("${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-dlfcn")
options=('staticlibs' '!libtool')
source=("http://www.nih.at/${_realname}/${_realname}-${pkgver}.tar.xz"
        'configure.patch')
sha256sums=('a921b45b5d840e998ff2544197eba4c3593dccb8ad0ee938630c2227c2c59fb3'
            '58fe10fe7867abdad71d2c632009a353b62076feaa7b174ee595498d977534d8')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  
  patch -p1 -i ${srcdir}/configure.patch
  
  autoreconf -fi
}

build() {
  mkdir -p "${srcdir}/build-${CARCH}"
  cd "${srcdir}/build-${CARCH}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-shared \
    --enable-static

  make
}

package() {
  cd "${srcdir}/build-${CARCH}"

  make DESTDIR="${pkgdir}" install

  # preserve old header path for compatibility
  # ln -s ${MINGW_PREFIX}/lib/libzip/include/zipconf.h "${pkgdir}${MINGW_PREFIX}/include/zipconf.h"
  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/LICENSE "${pkgdir}/${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
