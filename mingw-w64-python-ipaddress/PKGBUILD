# Contributor: Frederic Wang <fred.wang@free.fr>

_realname=ipaddress
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}" "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=1.0.15
pkgrel=1
pkgdesc='Port of the 3.3+ ipaddress module to 2.6, 2.7, 3.2 (mingw-w64)'
url='https://github.com/phihag/ipaddress'
license=('Python Software Foundation License')
arch=('any')
makedepends=("${MINGW_PACKAGE_PREFIX}-python2"
             "${MINGW_PACKAGE_PREFIX}-python3")
source=("https://pypi.python.org/packages/source/i/${_realname}/${_realname}-${pkgver}.tar.gz")
md5sums=('12915e923b738107e47827478d553ba1')

prepare() {
  cd ${srcdir}
  cp -r ${_realname}-${pkgver} build-python2
  cp -r ${_realname}-${pkgver} build-python3
}

package_python3-ipaddress() {
  cd ${srcdir}/build-python3
  ${MINGW_PREFIX}/bin/python3 setup.py build
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} --root="${pkgdir}"
}

package_python2-ipaddress() {
  cd ${srcdir}/build-python2
  ${MINGW_PREFIX}/bin/python2 setup.py build
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} --root="${pkgdir}"
}

package_mingw-w64-i686-python2-ipaddress() {
  package_python2-ipaddress
}

package_mingw-w64-i686-python3-ipaddress() {
  package_python3-ipaddress
}

package_mingw-w64-x86_64-python2-ipaddress() {
  package_python2-ipaddress
}

package_mingw-w64-x86_64-python3-ipaddress() {
  package_python3-ipaddress
}
