# Maintainer: Lubosz Sarnecki <lubosz@gmail.com>

_realname=clutter-gst
_tag=2.0.12
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.99.5.651.08ce0ee
pkgrel=1
pkgdesc="A GStreamer integration library for Clutter. (mingw-w64)"
arch=('any')
url="http://clutter-project.org/"
license=("LGPL")
makedepends=('automake' 'libtool' 'intltool' 'itstool' 'gtk-doc' 'gnome-doc-utils' 'yelp-tools')
depends=("${MINGW_PACKAGE_PREFIX}-gobject-introspection"
        "${MINGW_PACKAGE_PREFIX}-clutter"
        "${MINGW_PACKAGE_PREFIX}-gstreamer"
        )
options=(!libtool strip staticlibs)

source=(
        #"git://git.gnome.org/$_realname#tag=$_tag"
        "git://git.gnome.org/$_realname"
        #"0001-msys2-fix-gir-scanner.patch"
        "0001-msys2-fix-gir-build-on-master.patch"
        )
md5sums=("SKIP" 
         #"db504782f9b1e796d7faaa65ee376cac"
         "0774334a4dcdabefcbe551aeda983eab"
         )

pkgver() {
  cd $_realname
  major=$(grep "major_version," configure.ac | sed 's/m4_define(clutter_gst_major_version, //' | sed 's/)//')
  minor=$(grep "minor_version," configure.ac | sed 's/m4_define(clutter_gst_minor_version, //' | sed 's/)//')
  micro=$(grep "micro_version," configure.ac | head -1 | sed 's/m4_define(clutter_gst_micro_version, //' | sed 's/)//')
  hash=$(git log --pretty=format:'%h' -n 1)
  revision=$(git rev-list --count HEAD)
  echo $major.$minor.$micro.$revision.$hash  
}

prepare() {
  cd $_realname
  #git am ../0001-msys2-fix-gir-scanner.patch
  git am ../0001-msys2-fix-gir-build-on-master.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  cp -r "${srcdir}/${_realname}" "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --disable-static \
    --enable-shared \
    --enable-silent-rules \
    --disable-gtk-doc
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 DESTDIR="$pkgdir" install
}
