# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=tcl
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=8.6.1
pkgrel=1
pkgdesc="The Tcl scripting language (mingw-w64)"
arch=('any')
depends=("${_mingw_suff}-crt" "${_mingw_suff}-zlib")
makedepends=("${_mingw_suff}-gcc")
options=('staticlibs' 'strip' )
license=("custom")
url="http://tcl.sourceforge.net"
source=("http://downloads.sourceforge.net/sourceforge/tcl/tcl${pkgver}-src.tar.gz"
"tcl-8.5.14-autopath.patch"
"tcl-8.5.14-hidden.patch"
"tcl-8.6.1-mingwexcept.patch"
"tcl-mingw-w64-compatibility.patch")
sha256sums=('16ee769248e64ba1cae6b4834fcc4e4edd7470d881410e8d58f7dd1434343514'
            '3cb435f768052acabe5b1fbef5ae9a8d0967b0f86f7695fb4bae8953ee470357'
            'a809617939336a0068c5dd567f2c25489b2824ca25ad20f9cbf4e74c81396d2e'
            '77b7509f3677fe659a70994f664444c1f0e99899bd3e73cba71caa4ccb24ebd4'
            '866fe923b7d43339bea8a8484299ba71ebdb3a7213508809f2adae4eff60a382')

prepare() {
  cd "${srcdir}/tcl${pkgver}"
  patch -Np1 -i "${srcdir}/tcl-8.5.14-autopath.patch"
  patch -Np1 -i "${srcdir}/tcl-8.5.14-hidden.patch"
  patch -Np1 -i "${srcdir}/tcl-8.6.1-mingwexcept.patch"
  patch -Np0 -i "${srcdir}/tcl-mingw-w64-compatibility.patch"
}

build() {
    mkdir -p "${srcdir}/${_realname}-${pkgver}-build-${CARCH}"
    cp -r "$srcdir/tcl$pkgver/"* "${srcdir}/${_realname}-${pkgver}-build-${CARCH}/"
    cd "${srcdir}/${_realname}-${pkgver}-build-${CARCH}"
    [ "${MINGW_CHOST}" = "x86_64-w64-mingw32" ] && enable64bit='--enable-64bit'
    "${srcdir}"/${_realname}${pkgver}/win/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
	  $enable64bit
    make
}

package() {
    cd "${srcdir}/${_realname}-${pkgver}-build-${CARCH}"
	cp tclsh86.exe tclsh.exe
    PATH=.:$PATH make -j1 install INSTALL_ROOT="$pkgdir"
    rm "${pkgdir}${MINGW_PREFIX}/bin/zlib1.dll"
	cp "${pkgdir}${MINGW_PREFIX}/bin/tclsh86.exe" "${pkgdir}${MINGW_PREFIX}/bin/tclsh.exe"
    find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
	#find "${pkgdir}${MINGW_PREFIX}" -name '*.dll' | xargs -rtl1 strip --strip-unneeded
    #find "${pkgdir}${MINGW_PREFIX}" -name '*.a' -o -name '*.dll' | xargs -rtl1 strip -g
    find "${pkgdir}${MINGW_PREFIX}" -name '*.sh' -o -name '*.dll' -o -name '*.exe' | xargs -rtl1 chmod 755
    find "${pkgdir}${MINGW_PREFIX}" -name '*.sh' | xargs -rtl1 sed -i "s,libtcl86.a,libtcl86.dll.a,g"
    rm -rf "${pkgdir}${MINGW_PREFIX}/"{man,share}
    sed -e "s#${srcdir}/tcl${pkgver}/win#${MINGW_PREFIX}/lib#" \
      -e "s#${srcdir}/tcl${pkgver}#${MINGW_PREFIX}/include/tcl-private#" \
      -i "${pkgdir}${MINGW_PREFIX}/lib/tclConfig.sh"
		sed -e "s#${srcdir}/tcl${pkgver}/win/pkgs/tdbc1.0.0#${MINGW_PREFIX}/lib/tdbc1.0.0#" \
				-e "s#${srcdir}/tcl${pkgver}/pkgs/tdbc1.0.0/generic#${MINGW_PREFIX}/include#" \
				-e "s#${srcdir}/tcl${pkgver}/pkgs/tdbc1.0.0/library#${MINGW_PREFIX}/lib/tcl${pkgver%.*}#" \
				-e "s#${srcdir}/tcl${pkgver}/pkgs/tdbc1.0.0#${MINGW_PREFIX}/include#" \
				-i "${pkgdir}${MINGW_PREFIX}/lib/tdbc1.0.0/tdbcConfig.sh"
		sed -e "s#${srcdir}/tcl${pkgver}/win/pkgs/itcl4.0.0#${MINGW_PREFIX}/lib/itcl4.0.0#" \
				-e "s#${srcdir}/tcl${pkgver}/pkgs/itcl4.0.0/generic#${MINGW_PREFIX}/include/tcl-private#" \
				-e "s#${srcdir}/tcl${pkgver}/pkgs/itcl4.0.0#${MINGW_PREFIX}/include/tcl-private#" \
				-i "${pkgdir}${MINGW_PREFIX}/lib/itcl4.0.0/itclConfig.sh"
		mv "${pkgdir}${MINGW_PREFIX}/lib/libtcl86.a" "${pkgdir}${MINGW_PREFIX}/lib/libtcl86.dll.a"
		ln -s "${pkgdir}${MINGW_PREFIX}/lib/libtcl86.dll.a" "${pkgdir}${MINGW_PREFIX}/lib/libtcl.dll.a"
		ln -s "${pkgdir}${MINGW_PREFIX}/lib/tclConfig.sh" "${pkgdir}${MINGW_PREFIX}/lib/tcl${pkgver%.*.*}/tclConfig.sh"
    mkdir -p "${pkgdir}${MINGW_PREFIX}/include/tcl-private/"{generic,win}
    find generic win -name "*.h" -exec cp -p '{}' "${pkgdir}${MINGW_PREFIX}"/include/tcl-private/'{}' ';'
    ( cd "${pkgdir}${MINGW_PREFIX}/include"
      for i in *.h ; do
        [ -f "${pkgdir}${MINGW_PREFIX}/include/tcl-private/generic/$i" ] && ln -sf ../../$i "${pkgdir}${MINGW_PREFIX}/include/tcl-private/generic" ;
      done
    ) || true
    chmod a-x "${pkgdir}${MINGW_PREFIX}/lib/tcl${pkgver%.*}/encoding/"*.enc
    chmod a-x "${pkgdir}${MINGW_PREFIX}/lib/"*/pkgIndex.tcl
}
