# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libwpd
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=0.10.0
pkgrel=1
pkgdesc="Library for Importing WordPerfect (tm) Documents (mingw-w64)"
arch=('any')
url="http://libwpd.sourceforge.net/"
license=("LGPL-2.1+" "MPL-2.0+")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config")
depends=("${_mingw_suff}-gcc-libs" "${_mingw_suff}-librevenge" "${_mingw_suff}-zlib" "${_mingw_suff}-xz")
options=(staticlibs strip)
source=("http://downloads.sourceforge.net/project/${_realname}/${_realname}/${_realname}-${pkgver}/${_realname}-${pkgver}.tar.xz")
md5sums=('f7abb9b3ea96faf02ae019af68b1f07c')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
}

build() {
  export lt_cv_deplibs_check_method='pass_all'
  cp -r ${srcdir}/${_realname}-${pkgver} "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-shared \
    --enable-static \
    --without-docs \
    --disable-werror
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="$pkgdir" install
  find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
}
