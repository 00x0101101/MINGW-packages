# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=readline
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
_basever=6.2
_patchlevel=004
pkgver=$_basever.$_patchlevel
pkgrel=1
pkgdesc="MinGW port of readline for editing typed command lines (mingw-w64)"
arch=('any')
depends=("${_mingw_suff}-crt" "${_mingw_suff}-ncurses")
makedepends=("${_mingw_suff}-gcc")
options=('staticlibs' 'strip')
license=('GPL')
url="http://cnswww.cns.cwru.edu/php/chet/readline/rltop.html"
source=("http://ftp.gnu.org/gnu/readline/readline-${_basever}.tar.gz"{,.sig})
if [ $_patchlevel -gt 00 ]; then
    for (( p=1; p<=$((10#${_patchlevel})); p++ )); do
        source=(${source[@]} http://ftp.gnu.org/gnu/readline/readline-$_basever-patches/readline${_basever//./}-$(printf "%03d" $p){,.sig})
    done
fi

md5sums=('67948acb2ca081f23359d0256e9a271c'
         '928f7d248320a65e43c2dc427e99582b'
         '83287d52a482f790dfb30ec0a8746669'
         '8e6a51e2e0e6e45a82752e3692c111ac'
         '0665020ea118e8434bd145fb71f452cc'
         '285361ca6d48c51ae2428157e174e812'
         'c9d5d79718856e711667dede87cb7622'
         '4437205bb1462f5f32e4812b8292c675'
         'c08e787f50579ce301075c523fa660a4'
         '7e39cad1d349b8ae789e4fc33dbb235f')

prepare() {
  cd "${srcdir}/readline-${_basever}"

  for (( p=1; p<=$((10#${_patchlevel})); p++ )); do
    msg "applying patch readline${_basever//./}-$(printf "%03d" $p)"
    patch -Np0 -i $srcdir/readline${_basever//./}-$(printf "%03d" $p)
  done

  # Remove RPATH from shared objects (FS#14366)
  sed -i 's|-Wl,-rpath,$(libdir) ||g' support/shobj-conf
}

build() {
    mkdir -p ${srcdir}/build-${CARCH} && cd ${srcdir}/build-${CARCH}
	CFLAGS+=" -D__USE_MINGW_ANSI_STDIO=1"
    ../readline-${_basever}/configure \
      --prefix=${MINGW_PREFIX} \
      --host=${MINGW_CHOST} \
      --target=${MINGW_CHOST} \
      --build=${MINGW_CHOST} \
      --enable-shared \
	  --with-curses
    make
}

package() {
    cd "${srcdir}/build-${CARCH}"
    make install DESTDIR="${pkgdir}"
    rm -r "${pkgdir}${MINGW_PREFIX}"/share/
    #strip --strip-unneeded "${pkgdir}${MINGW_PREFIX}"/bin/*.dll
    #strip --strip-debug "${pkgdir}${MINGW_PREFIX}"/lib/*.a
}
