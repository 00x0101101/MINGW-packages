# Maintainer: Diego Sogari <diego.sogari@gmail.com>

_realname=pdfium
pkgbase=mingw-w64-${_realname}-git
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
_basever=2679
_commit=2b2cbebe77bc927febdf527c9e7857067873133e
pkgver=${_basever}.${_commit:0:7}
pkgrel=1
pkgdesc="Chromeâ€™s PDF rendering engine (mingw-w64)"
arch=('any')
url="https://pdfium.googlesource.com/pdfium/"
license=('BSD')
gtestdir=googletest-release-1.7.0
gmockdir=googlemock-release-1.7.0
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-cmake")
source=("${_realname}::git+https://pdfium.googlesource.com/pdfium.git#commit=${_commit}"
        "${gtestdir}.tar.gz::https://codeload.github.com/google/googletest/tar.gz/release-1.7.0"
        "${gmockdir}.tar.gz::https://codeload.github.com/google/googlemock/tar.gz/release-1.7.0"
        "${_realname}-${pkgver}.patch")
sha256sums=('SKIP'
            'f73a6546fdf9fce9ff93a5015e0333a8af3062a152a9ad6bcb772c96687016cc'
            '3f20b6acb37e5a98e8c4518165711e3e35d47deb6cdb5a4dd4566563b5efd232'
            '6a76651ff1550325007dbe4962147eb25f6bfa87ca57af25fa5027c1d09e1c83')

prepare() {
  cd "${srcdir}/${_realname}"
  cp -rf "../${gtestdir}" ./testing/gtest
  cp -rf "../${gmockdir}" ./testing/gmock
  patch -Np1 -i "../${_realname}-${pkgver}.patch"
}

build() {
  rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  
  export GYP_GENERATORS=cmake
  export GYP_DEFINES='pdf_enable_v8=0 pdf_enable_xfa=0'
  /usr/bin/python2 ../${_realname}/build/gyp_pdfium

  if [[ ${MINGW_CHOST} == *"x86_64"* ]]; then
    cmake_dir=out/Release_x64
  else
    cmake_dir=out/Release
  fi

  sed -e 's/-Werror//g' \
      -e 's/".so"/".dll"/g' \
      -e 's/libpdfium.so/libpdfium.dll/g' \
      -i "../${_realname}/${cmake_dir}/CMakeLists.txt"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    "../${_realname}/${cmake_dir}"

  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  cp -f ./pdfium_embeddertests.exe ./pdfium_unittests.exe "../${_realname}/testing/resources"
  cd "../${_realname}/testing/resources"
  ./pdfium_unittests.exe
  ./pdfium_embeddertests.exe
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  install -D ./libpdfium.dll "${pkgdir}${MINGW_PREFIX}/bin/libpdfium.dll"
  install -D ./libpdfium.dll.a "${pkgdir}${MINGW_PREFIX}/lib/libpdfium.dll.a"
  install -D ./obj.target/libpdfium_static.a "${pkgdir}${MINGW_PREFIX}/lib/libpdfium_static.a"
  install -d "${pkgdir}${MINGW_PREFIX}/include"
  install "../${_realname}/public/"*.h "${pkgdir}${MINGW_PREFIX}/include"
}
