# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname='firebird'
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgver=r43988.06f0675
pkgrel=1
pkgdesc="Cross-platform relational database offering many ANSI SQL standard features (mingw-w64)"
url="http://www.firebirdsql.org/"
license=('custom')
arch=('any')
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
depends=(
         "${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-icu"
         "${MINGW_PACKAGE_PREFIX}-libtommath" # For Firebird 3.x
        )
makedepends=(#"${MINGW_PACKAGE_PREFIX}-btyacc" # Maybe add check in firebird presence of system btyacc
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-libatomic_ops"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "git"
            )
options=('debug' '!strip')
source=("${_realname}"::"git+https://github.com/Alexpux/firebird.git#branch=mingw-w64-2_5"
        #"${_realname}"::"git+file:///v/Sources/GNU/firebird#branch=mingw-w64-2_5"
        )
sha256sums=('SKIP')

pkgver() {
  cd "${_realname}"
  printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${_realname}"
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  #mkdir -p "build-${MINGW_CHOST}"
  #cd "build-${MINGW_CHOST}"
  local _debug_conf=
  if check_option "debug" "y"; then
    _debug_conf="--enable-debug"
  fi

  cd ${srcdir}/${_realname}
  
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-static \
    --enable-shared \
    ${_debug_conf} \
    --disable-rpath \
    --disable-superserver \
    --with-fbdoc=${MINGW_PREFIX}/share/${_realname}/doc \
    --with-system-icu

  CC=${MINGW_PREFIX}/bin/gcc \
  make -j1
}

package() {
  cd ${srcdir}/${_realname}
  
  mkdir -p ${pkgdir}
  cp -rf gen/firebird ${pkgdir}${MINGW_PREFIX}
  cp -rf ${srcdir}/${_realname}/doc ${pkgdir}${MINGW_PREFIX}/
}
