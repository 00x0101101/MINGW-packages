# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=librocket

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgver=r243.bd47356
pkgrel=1
pkgdesc="The HTML/CSS User Interface Library (mingw-w64)"
arch=('any')
url="http://http://librocket.com/"
license=("MIT")
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
depends=("${MINGW_PACKAGE_PREFIX}-freetype"
         "${MINGW_PACKAGE_PREFIX}-python2"
         "${MINGW_PACKAGE_PREFIX}-boost")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "make")

options=('staticlibs' 'strip')

source=("${_realname}"::"git+https://github.com/libRocket/libRocket.git"
        "find-mingw-python.patch"
        "dll-exports.patch")

md5sums=('SKIP'
         '4cbe833d6edde34de35f5d284a20ecc0'
         'e768c35e89e5f567b08ce4264d064701')

pkgver() {
  cd "$srcdir/$_realname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/$_realname"
  patch -p1 -i "$srcdir"/find-mingw-python.patch
  patch -p1 -i ${srcdir}/dll-exports.patch
}

build() {
  [ -d build-${CARCH} ] && rm -rf build-${CARCH}
  mkdir -p "${srcdir}/build-${CARCH}"
  cd "${srcdir}/build-${CARCH}"

  ${MINGW_PREFIX}/bin/cmake \
  -G"MSYS Makefiles" \
  -DCMAKE_INSTALL_PREFIX=${pkgdir}${MINGW_PREFIX} \
  -DBUILD_PYTHON_BINDINGS=ON \
  -DBUILD_SHARED_LIBS=ON \
  -DPYTHON_EXECUTABLE=${MINGW_PREFIX}/bin/python2.exe \
  -DBUILD_SAMPLES=ON \
  -DCMAKE_GNUtoMS=0 \
  ../$_realname/Build
  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make install
}
