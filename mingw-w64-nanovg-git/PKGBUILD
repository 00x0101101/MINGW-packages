# Maintainer: Martell Malone <martellmalone@gmail.com>

_realname=nanovg
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgver=r252.90862ce
pkgrel=1
pkgdesc="Small antialiased vector graphics rendering library for OpenGL"
arch=('any')
url="https://github.com/memononen/nanovg"
license=('ZLIB')
makedepends=("git" "${MINGW_PACKAGE_PREFIX}-premake")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")

source=("$pkgname"::'git://github.com/memononen/nanovg.git')
md5sums=('SKIP')
options=('staticlibs')

pkgver() {
  cd "$srcdir/$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$pkgname"
  premake4 gmake
  cd build
  make nanovg
}

package() {
  install -v -Dm644 "$srcdir/$pkgname/build/libnanovg.a" "${pkgdir}${MINGW_PREFIX}/lib/libnanovg.a" || exit 1
  install -v -Dm644 "$srcdir/$pkgname/LICENSE.txt" "${pkgdir}${MINGW_PREFIX}/share/licenses/$_realname/LICENSE.txt" || exit 1
  install -v -Dm644 "$srcdir/$pkgname/src/nanovg.h" "${pkgdir}${MINGW_PREFIX}/include/nanovg/nanovg.h" || exit 1
  install -v -Dm644 "$srcdir/$pkgname/src/nanovg_gl.h" "${pkgdir}${MINGW_PREFIX}/include/nanovg/nanovg_gl.h" || exit 1
  install -v -Dm644 "$srcdir/$pkgname/src/nanovg_gl_utils.h" "${pkgdir}${MINGW_PREFIX}/include/nanovg/nanovg_gl_utils.h" || exit 1
  install -v -Dm644 "$srcdir/$pkgname/src/fontstash.h" "${pkgdir}${MINGW_PREFIX}/include/nanovg/fontstash.h" || exit 1
}