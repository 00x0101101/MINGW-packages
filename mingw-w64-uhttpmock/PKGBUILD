# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=uhttpmock
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.3.0
pkgrel=1
arch=('any')
pkgdesc="uhttpmock is a HTTP web service mocking project for projects which use libsoup (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-glib2"
         "${MINGW_PACKAGE_PREFIX}-libsoup")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
            "pkg-config"
            "make"
            "libtool"
            "automake-wrapper"
            "autoconf"
            "patch")
options=('strip' 'staticlibs')
license=("LGPL 2.1")
url="http://tecnocode.co.uk/"
source=(http://tecnocode.co.uk/downloads/${_realname}/${_realname}-$pkgver.tar.xz
        wsock.patch)
sha256sums=('d33cdf6c6852975162a9d758870edea17449db5ca9dc0e4e0a5831a47fbe8bfe'
            '255c33830186382f1a2cf442ebf2ab8a797875fd11220eaf8cf82401b3955a23')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/wsock.patch
  
  autoreconf -fi
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib
  make
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make DESTDIR=$pkgdir install
}
