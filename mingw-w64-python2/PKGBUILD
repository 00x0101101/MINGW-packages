# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=python2
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=2.7.6
pkgrel=2
_pybasever=2.7
pkgdesc="A high-level scripting language (mingw-w64)"
arch=('any')
license=('PSF')
url="http://www.python.org/"
depends=("${_mingw_suff}-crt" 
		"${_mingw_suff}-expat"
		"${_mingw_suff}-bzip2"
		"${_mingw_suff}-readline"
		"${_mingw_suff}-openssl"
		"${_mingw_suff}-libffi"
		"${_mingw_suff}-zlib")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config" "${_mingw_suff}-ncurses")
options=('!makeflags' 'staticlibs' 'strip')
source=("http://www.python.org/ftp/python/${pkgver%rc?}/Python-${pkgver}.tar.xz"
		0005-MINGW.patch
		0006-mingw-removal-of-libffi-patch.patch
		0007-mingw-system-libffi.patch
		0010-mingw-osdefs-DELIM.patch
		0015-mingw-use-posix-getpath.patch
		0020-mingw-w64-test-for-REPARSE_DATA_BUFFER.patch
		0025-mingw-regen-with-stddef-instead.patch
		0030-mingw-add-libraries-for-_msi.patch
		0035-MSYS-add-MSYSVPATH-AC_ARG.patch
		0040-mingw-cygwinccompiler-use-CC-envvars-and-ld-from-print-prog-name.patch
		0045-cross-darwin.patch
		0050-mingw-sysconfig-like-posix.patch
		0055-mingw-pdcurses_ISPAD.patch
		0060-mingw-static-tcltk.patch
		0065-mingw-x86_64-size_t-format-specifier-pid_t.patch
		0070-python-disable-dbm.patch
		0075-add-python-config-sh.patch
		0080-mingw-nt-threads-vs-pthreads.patch
		0085-cross-dont-add-multiarch-paths-if.patch
		0090-mingw-reorder-bininstall-ln-symlink-creation.patch
		0095-mingw-use-backslashes-in-compileall-py.patch
		0100-mingw-distutils-MSYS-convert_path-fix-and-root-hack.patch
		0105-mingw-MSYS-no-usr-lib-or-usr-include.patch
		0110-mingw-_PyNode_SizeOf-decl-fix.patch
		0115-mingw-cross-includes-lower-case.patch
		0500-mingw-install-LDLIBRARY-to-LIBPL-dir.patch
		0505-add-build-sysroot-config-option.patch
		0510-cross-PYTHON_FOR_BUILD-gteq-275-and-fullpath-it.patch
		0515-mingw-add-GetModuleFileName-path-to-PATH.patch
		0520-Add-interp-Python-DESTSHARED-to-PYTHONPATH-b4-pybuilddir-txt-dir.patch
		0525-msys-monkeypatch-os-system-via-sh-exe.patch)
sha1sums=('8321636af2acbeaa68fc635d7dda7369ed446a80'  # 2.7.6
		  '8553bbf96560f04c0cc85d8418c1f6b818feee3b'
          'bb71f24b42a80a5f3f260e553c9afa433fa4243e'
		  '46fffbb6fd41ce615cb8cd1b2e13994bef6c473d'
          '74abdb55e43f27e6c42b8c73a51bda74da21c742'
          '0e2b7be0bb1cb97c9047e806916a2d41c5830803'
          '5cae3234f71ae9eccdfb0aaaaee211604b4c428a'
          '5ce21c8e7db57cfaf001985230585e84db079d9b'
          'dea45873221c2cec00d82956085570d6028e094a'
          'ec555577e117511217a8040e072e332498d7792d'
          'e5ce9b07bef3a10472ede90a5c6f7c48e59cee90'
          '4d4c4c44ce324a34e3c91de8e1c982b2a4405c0e'
          'ce4a1c0aca20d3f38c6ab7ba5dfac307fe759c1c'
          '996af9c6d216257f054ca045b27436a8b70064c3'
          '7ea2e8be856818f119bd4300cc51e4364b2d3e46'
          'e2ea748ce60bfc0c6860f1486d5ad98810d18045'
          '49cffa94a7366387ac25a35e42f66c2284deeafa'
          '8045fd8ab9cc2b8eb3b7cf49800954bc35a1d650'
          '20867037dd91e4693a33005ffe320780a277b1d0'
          'cf2c5b2ce3fdbbb872c9e46c1671706031528df1'
          'd99db43152709de6aef6366413762985158ee4ef'
          '46b76dd6714f1d5974dd5ba97ab2354dc5480651'
          '940c4d6d51f7359262970356bb5ef32bfa98bf99'
          '3835ed1571384657d115a0cbd1551c2dd3bd52dd'
          '59648afbb6fd1259545de19f370112dd6bf0d50c'
          'de31e6cb8e77d65cf664d0d4aba95995a140abc0'
          '2f3ae8256f9c97e5f410ad976771c31ee9d0bb3c'
		  '7dac10e6c4b37cb583634a06ce98198c3ada0106'
          '060e1f3e3555c89719bdd54c00806fc4870686bc'
          '9ca35d09d769abc6c49b01b0c1da8be7b1e69265'
          '0b373dc92b0dddb13f9503c31214eca74381e7ca'
          'bbf1ebeaa9a2f7b8b810c6b62143c851810759fb')

prepare() {
  cd "${srcdir}/Python-${pkgver}"
  rm -f Modules/_ctypes/libffi_msvc/win32.S Python/fileblocks.c Misc/python-config.sh.in
  patch -Np1 -i "${srcdir}"/0005-MINGW.patch
  patch -Np1 -i "${srcdir}"/0006-mingw-removal-of-libffi-patch.patch
  patch -Np1 -i "${srcdir}"/0007-mingw-system-libffi.patch
  patch -Np1 -i "${srcdir}"/0010-mingw-osdefs-DELIM.patch
  patch -Np1 -i "${srcdir}"/0015-mingw-use-posix-getpath.patch
  patch -Np1 -i "${srcdir}"/0020-mingw-w64-test-for-REPARSE_DATA_BUFFER.patch
  patch -Np1 -i "${srcdir}"/0025-mingw-regen-with-stddef-instead.patch
  patch -Np1 -i "${srcdir}"/0030-mingw-add-libraries-for-_msi.patch
  patch -Np1 -i "${srcdir}"/0035-MSYS-add-MSYSVPATH-AC_ARG.patch
  patch -Np1 -i "${srcdir}"/0040-mingw-cygwinccompiler-use-CC-envvars-and-ld-from-print-prog-name.patch
  patch -Np1 -i "${srcdir}"/0045-cross-darwin.patch
  patch -Np1 -i "${srcdir}"/0050-mingw-sysconfig-like-posix.patch
  patch -Np1 -i "${srcdir}"/0055-mingw-pdcurses_ISPAD.patch
  patch -Np1 -i "${srcdir}"/0060-mingw-static-tcltk.patch
  patch -Np1 -i "${srcdir}"/0065-mingw-x86_64-size_t-format-specifier-pid_t.patch
  patch -Np1 -i "${srcdir}"/0070-python-disable-dbm.patch
  patch -Np1 -i "${srcdir}"/0075-add-python-config-sh.patch
  patch -Np1 -i "${srcdir}"/0080-mingw-nt-threads-vs-pthreads.patch 
  #patch -Np1 -i "${srcdir}"/0085-cross-dont-add-multiarch-paths-if.patch 
  patch -Np1 -i "${srcdir}"/0090-mingw-reorder-bininstall-ln-symlink-creation.patch
  patch -Np1 -i "${srcdir}"/0095-mingw-use-backslashes-in-compileall-py.patch
  patch -Np1 -i "${srcdir}"/0100-mingw-distutils-MSYS-convert_path-fix-and-root-hack.patch
  patch -Np1 -i "${srcdir}"/0105-mingw-MSYS-no-usr-lib-or-usr-include.patch
  patch -Np1 -i "${srcdir}"/0110-mingw-_PyNode_SizeOf-decl-fix.patch
  patch -Np1 -i "${srcdir}"/0115-mingw-cross-includes-lower-case.patch
  patch -Np1 -i "${srcdir}"/0500-mingw-install-LDLIBRARY-to-LIBPL-dir.patch
  patch -Np1 -i "${srcdir}"/0505-add-build-sysroot-config-option.patch
  patch -Np1 -i "${srcdir}"/0510-cross-PYTHON_FOR_BUILD-gteq-275-and-fullpath-it.patch
  patch -Np1 -i "${srcdir}"/0515-mingw-add-GetModuleFileName-path-to-PATH.patch
  patch -Np1 -i "${srcdir}"/0520-Add-interp-Python-DESTSHARED-to-PYTHONPATH-b4-pybuilddir-txt-dir.patch
  patch -Np1 -i "${srcdir}"/0525-msys-monkeypatch-os-system-via-sh-exe.patch

  autoreconf -vfi
  
  # Temporary workaround for FS#22322
  # See http://bugs.python.org/issue10835 for upstream report
  sed -i "/progname =/s/python/python${_pybasever}/" Python/pythonrun.c

  # Enable built-in SQLite module to load extensions (fix FS#22122)
  sed -i "/SQLITE_OMIT_LOAD_EXTENSION/d" setup.py

  # FS#23997
  sed -i -e "s|^#.* /usr/local/bin/python|#!/usr/bin/python2|" Lib/cgi.py

  sed -i "s/python2.3/python2/g" Lib/distutils/tests/test_build_scripts.py \
     Lib/distutils/tests/test_install_scripts.py Tools/scripts/gprof2html.py
  
  touch Include/graminit.h
  touch Python/graminit.c
  touch Parser/Python.asdl
  touch Parser/asdl.py
  touch Parser/asdl_c.py
  touch Include/Python-ast.h
  touch Python/Python-ast.c
  echo \"\" > Parser/pgen.stamp
		
  # Ensure that we are using the system copy of various libraries (expat, zlib and libffi),
  # rather than copies shipped in the tarball
  rm -r Modules/expat
  rm -r Modules/zlib
  rm -r Modules/_ctypes/{darwin,libffi}*
  
}

build() {
  pushd ${MINGW_PREFIX} > /dev/null
  export PREFIX_WIN=`pwd -W`
  popd > /dev/null

  CFLAGS+="-O2 -pipe -fwrapv -DNDEBUG -D__USE_MINGW_ANSI_STDIO=1 -I$PREFIX_WIN/${MINGW_CHOST}/include"
  CXXFLAGS+="-O2 -pipe -fwrapv -DNDEBUG -D__USE_MINGW_ANSI_STDIO=1 -I$PREFIX_WIN/include -I$PREFIX_WIN/include/ncursesw -I$MINGWHOME_WIN/$HOST/include"
  CPPFLAGS+="-O2 -pipe -I$PREFIX_WIN/include -I$PREFIX_WIN/include/ncursesw -I$PREFIX_WIN/${MINGW_CHOST}/include"
  LDFLAGS="-pipe -s -L$PREFIX_WIN/${MINGW_CHOST}/lib -L$PREFIX_WIN/lib"
  
  LIBFFI_INC=`${MINGW_PREFIX}/bin/pkg-config libffi --cflags-only-I | sed "s|\-I||g"`
  pushd ${LIBFFI_INC} > /dev/null
  export LIBFFI_INCLUDEDIR=`pwd -W`
  popd > /dev/null
  
  # Workaround for conftest error on 64-bit builds
  export ac_cv_working_tzset=no
	
  mkdir -p "${srcdir}/build-${CARCH}" && cd "${srcdir}/build-${CARCH}"
  MSYSTEM=MINGW ../Python-${pkgver}/configure \
      --prefix=${pkgdir}${MINGW_PREFIX} \
      --host=${MINGW_CHOST} \
      --build=${MINGW_CHOST} \
      --enable-shared \
      --with-threads \
      --with-system-expat \
      --with-system-ffi \
	  OPT="" \
	  CXX=g++
      # --with-dbmliborder=gdbm:ndbm
    make
}

package() {
    cd "${srcdir}/build-${CARCH}"
    make install # DESTDIR="$pkgdir"
    mv "${pkgdir}${MINGW_PREFIX}"/bin/smtpd.py "${pkgdir}${MINGW_PREFIX}"/lib/python${_pybasever}/

	cp -f "${pkgdir}${MINGW_PREFIX}"/lib/python${_pybasever}/config/libpython${_pybasever}.dll.a "${pkgdir}${MINGW_PREFIX}"/lib/libpython${_pybasever}.dll.a

	# some useful "stuff"
	install -dm755 "${pkgdir}${MINGW_PREFIX}"/lib/python${_pybasever}/Tools/{i18n,scripts}
	install -m755 "${srcdir}/Python-${pkgver}"/Tools/i18n/{msgfmt,pygettext}.py "${pkgdir}${MINGW_PREFIX}"/lib/python${_pybasever}/Tools/i18n/
	install -m755 "${srcdir}/Python-${pkgver}"/Tools/scripts/{README,*py} "${pkgdir}${MINGW_PREFIX}"/lib/python${_pybasever}/Tools/scripts/

	# clean up #!s
	find "${pkgdir}${MINGW_PREFIX}"/lib/python${_pybasever}/ -name '*.py' | \
		xargs sed -i "s|#[ ]*![ ]*/usr/bin/env python$|#!/usr/bin/env python2|"

	# clean-up reference to build directory
	sed -i "s#${srcdir}/Python-${pkgver}:##" "${pkgdir}${MINGW_PREFIX}"/lib/python${_pybasever}/config/Makefile
	
	#sed -i -e "s|^#.* /usr/local/bin/python|#!/usr/bin/python2|"
	sed -i "s|#!${pkgdir}${MINGW_PREFIX}/bin/python${_pybasever}.exe|#!python2|" "${pkgdir}${MINGW_PREFIX}"/bin/python${_pybasever}-config
	sed -i "s|#!${pkgdir}${MINGW_PREFIX}/bin/python${_pybasever}.exe|#!python2|" "${pkgdir}${MINGW_PREFIX}"/bin/python2-config
	sed -i "s|#!${pkgdir}${MINGW_PREFIX}/bin/python${_pybasever}.exe|#!python2|" "${pkgdir}${MINGW_PREFIX}"/bin/python-config
	
	# fix permissons
	find ${pkgdir}${MINGW_PREFIX} -type f \( -name *.dll -o -name *.exe \) | xargs chmod 0755
	
	# replace paths in sysconfig
	sed -i "s|${pkgdir}${MINGW_PREFIX}|${MINGW_PREFIX}|g" \
		"${pkgdir}${MINGW_PREFIX}"/lib/python${_pybasever}/_sysconfigdata.py \
		"${pkgdir}${MINGW_PREFIX}"/lib/python${_pybasever}/smtpd.py
		
}
