# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libguess

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.1
pkgrel=1
pkgdesc="High-speed character set detection library (mingw-w64)"
arch=('any')
url="http://atheme.org/projects/libguess.html"
license=('custom')
depends=("${MINGW_PACKAGE_PREFIX}-libmowgli")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
options=('staticlibs' 'strip')
source=("http://distfiles.atheme.org/libguess-${pkgver}.tar.gz")
md5sums=('efc2d53c30e47ffccae7148e53433396')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  
  sed 's/libmowgli >= 0.9.50/libmowgli-2/g' -i configure
}

build() {
  cd ${srcdir}/${_realname}-${pkgver}
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST}
  make
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}
  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
