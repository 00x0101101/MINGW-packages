# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gdk-pixbuf2
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=2.30.5
pkgrel=1
pkgdesc="An image loading library (mingw-w64)"
arch=('any')
url="http://www.gtk.org"
license=("LGPL")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config" "${_mingw_suff}-gobject-introspection")
depends=("${_mingw_suff}-gcc-libs"
		"${_mingw_suff}-glib2>=2.37.2"
		"${_mingw_suff}-jasper"
		"${_mingw_suff}-libpng"
		"${_mingw_suff}-libjpeg-turbo"
		"${_mingw_suff}-libtiff")
options=('strip' 'staticlibs')
source=("http://download.gnome.org/sources/gdk-pixbuf/${pkgver%.*}/gdk-pixbuf-$pkgver.tar.xz"
		0004-give-cc-to-gir-scanner.all.patch)
sha256sums=('43e71d50c4511d7d72fa75b1ac7ad3e8e04bdc97908d003449e78171683bb136'
            'bce33b7f4b156ef256285381d28bcf910a8437c4fd668d90b25e5901cc1dcff3')

prepare() {
	cd ${srcdir}/gdk-pixbuf-${pkgver}
	patch -p1 -i ${srcdir}/0004-give-cc-to-gir-scanner.all.patch
	
	autoreconf -fi
}

build() {
    CFLAGS+=" -mms-bitfields"
    CXXFLAGS+=" -mms-bitfields"
    mkdir -p "${srcdir}/build-${MINGW_CHOST}"
    cd "${srcdir}/build-${MINGW_CHOST}"
    ${srcdir}/gdk-pixbuf-${pkgver}/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --enable-static \
      --enable-shared \
	  --enable-introspection \
      --with-included-loaders=wbmp,png,pnm,ras,ani,xpm,xbm,tga,icns,pcx,qtif,gdip-ico,gdip-wmf,gdip-emf,gdip-bmp,gdip-gif,gdip-jpeg,gdip-tiff
    make
}

package() {
    cd "${srcdir}/build-${MINGW_CHOST}"
    make DESTDIR="$pkgdir" install
    find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    #rm -r "${pkgdir}${MINGW_PREFIX}/share"
}
