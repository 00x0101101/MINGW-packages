# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gdk-pixbuf2

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.31.1
pkgrel=1
pkgdesc="An image loading library (mingw-w64)"
arch=('any')
url="http://www.gtk.org"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config" "${MINGW_PACKAGE_PREFIX}-gobject-introspection")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
        "${MINGW_PACKAGE_PREFIX}-glib2>=2.37.2"
        "${MINGW_PACKAGE_PREFIX}-jasper"
        "${MINGW_PACKAGE_PREFIX}-libpng"
        "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
        "${MINGW_PACKAGE_PREFIX}-libtiff")
options=('strip' 'staticlibs')
install=${_realname}-${CARCH}.install
source=("http://download.gnome.org/sources/gdk-pixbuf/${pkgver%.*}/gdk-pixbuf-$pkgver.tar.xz"
        0001-Use-a-regex-to-properly-export-the-symbols.patch)
sha256sums=('25a75e3c61dac11e6ff6416ad846951ccafac6486b1c6a1bfb0b213b99db52cd'
            'e8d278e30c44e973e14e3c61e8ab195621d6a9a402e0da557db4616955ca4543')

prepare() {
  cd ${srcdir}/gdk-pixbuf-${pkgver}
  patch -p1 -i ${srcdir}/0001-Use-a-regex-to-properly-export-the-symbols.patch

  autoreconf -fi
}

build() {
  CFLAGS+=" -mms-bitfields"
  CXXFLAGS+=" -mms-bitfields"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ${srcdir}/gdk-pixbuf-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-static \
    --enable-shared \
    --enable-introspection \
    --with-included-loaders
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="$pkgdir" install
  find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
}
