# Maintainer: Martell Malone <martellmalone@gmail.com>
# Maintainer: Ray Donnelly <mingw.android@gmail.com>
# Contributor: David Macek <david.macek.0@gmail.com>
# Contributor: Mateusz Miku≈Ça <mati865@gmail.com>

_realname=nodejs
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=6.5.0
pkgrel=1
pkgdesc="Evented I/O for V8 javascript (mingw-w64)"
arch=('any')
url="https://nodejs.org/"
license=("MIT")
makedepends=("python2" "make" "gyp")
# If can switch to shared dependencies instead of
# static, bundled then here is the full list. For
# for now, reluctantly the only shared dependency
# is OpenSSL.
# depends=("${MINGW_PACKAGE_PREFIX}-c-ares"
#          "${MINGW_PACKAGE_PREFIX}-http-parser"
#          "${MINGW_PACKAGE_PREFIX}-v8"
#          "${MINGW_PACKAGE_PREFIX}-libuv"
#          "${MINGW_PACKAGE_PREFIX}-openssl"
#          "${MINGW_PACKAGE_PREFIX}-zlib")
depends=("${MINGW_PACKAGE_PREFIX}-c-ares"
         "${MINGW_PACKAGE_PREFIX}-http-parser"
         "${MINGW_PACKAGE_PREFIX}-libuv"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "winpty")
#options=('!emptydirs' '!strip') # 'debug')

source=("https://nodejs.org/dist/v${pkgver}/node-v${pkgver}.tar.gz"
        "0001-fix-python-2-shebang.patch"
        "0002-Mat_Sutcliffe-node_mingw64-without-MSYS-path-convers.patch"
        "0003-include-win32-headers.h-for-v8.patch"
        "0004-mingw-w64-defines-localtime_s.patch"
        "0005-skip-unneeded-includes.patch"
        "0006-fix-winnt-version.patch"
        "0007-MinGW-w64-add-platform-wintoolset-as-msvc-or-mingw-w.patch"
        "0008-Use-shell-wrapper-script-for-npm-on-MSYS2-MinGW-w64.patch"
        "0009-Revert-to-FHS-installation-paths-on-Windows-tests-no.patch"
        "0010-cygpath-conversion-in-npm-shell-wrapper.patch"
        "0011-fix-building-with-with-intl-system-icu.patch"
        "0012-fixes-for-node-gyp.patch"
        "0104-Use-system-gyp.patch"
        'node-gyp'
        'node-gyp.cmd')

sha256sums=('d7742558bb3331e41510d6e6f1f7b13c0527aecc00a63c3e05fcfd44427ff778'
            '55928e5adb59ceebd0b5b8f5015ddc8328acc0d09be1e19ba83b18b8097c1d83'
            '5d36af40fdf4f7927b27d66800560b23772674a8eb3350ce10b333e544b8f689'
            'f0fa1c7d0953cd330481d3f605359ab8f837aa3f8845bb5a61ad5fb599cdfcda'
            '82adf6790da5366044815f74f8455d7bf989dffc3d2f9c330916c613b92cbf97'
            '94ad1f6c0b6487a2e53fae024ac0cca518fccb5ad4817a6a186860e3daccf237'
            '49e2f9dc5e25d247f06d6c767005b8a31093e5476afe995d5e83a3ad8bf76283'
            '2d8b1b10db5ea34174a4c31e8ecf5925d22b647a1cc44cfcd9e3ab3f94846a84'
            '27973fd058b6f9e45c5a8dd848725099498e0fdd0ebad7754a25129e270ed076'
            '95d6c6a1b6415fbf3900a735a784fef31f9b7476b2aad4dd28dd41dca5c54079'
            '2e7befaaf995cf42dd7a3a91309d56ed9f49fb24b25b95efae9bc505b1361bc2'
            '8f4e93c13904b0c32ed58c63465fc01edc6bf2e98d52ed071b8a27800d20bc29'
            'c662ed7e1917d14cb19d71f1ec0c1dd0320123a8b769cdea8eb576d313f978a2'
            'da4d21c363fbb5f2ae4bf8d948024a37b143f453885fb262c013d87b677510c9'
            '8f00d54bc60c62cfc6c63657fca0a522ee485ab2f30de721bd09a0f868e81257'
            '55294eca72a62b2730470a4586b5caab2ec3a43d5412566770541c9fc59e4135')

prepare() {
  cd ${srcdir}/node-v${pkgver}

  # git init

  # Using perfctrs makes Vista the minimum OS. MinGW-w64's perflib.h is missing lots
  # of stuff too, so the patches aren't enough.
  patch -p1 -i "${srcdir}"/0001-fix-python-2-shebang.patch
  patch -p1 -i "${srcdir}"/0002-Mat_Sutcliffe-node_mingw64-without-MSYS-path-convers.patch
  patch -p1 -i "${srcdir}"/0003-include-win32-headers.h-for-v8.patch
  patch -p1 -i "${srcdir}"/0004-mingw-w64-defines-localtime_s.patch
  patch -p1 -i "${srcdir}"/0005-skip-unneeded-includes.patch
  patch -p1 -i "${srcdir}"/0006-fix-winnt-version.patch
  patch -p1 -i "${srcdir}"/0007-MinGW-w64-add-platform-wintoolset-as-msvc-or-mingw-w.patch
  patch -p1 -i "${srcdir}"/0008-Use-shell-wrapper-script-for-npm-on-MSYS2-MinGW-w64.patch
  patch -p1 -i "${srcdir}"/0009-Revert-to-FHS-installation-paths-on-Windows-tests-no.patch
  patch -p1 -i "${srcdir}"/0010-cygpath-conversion-in-npm-shell-wrapper.patch
  patch -p1 -i "${srcdir}"/0011-fix-building-with-with-intl-system-icu.patch
  patch -p1 -i "${srcdir}"/0012-fixes-for-node-gyp.patch
  patch -p1 -i "${srcdir}"/0104-Use-system-gyp.patch

  # taken form Arch with small improvement, can be used for recreating python patch
  # msg 'Fixing for python2 name'
  # find -type f ! \( -name "*.dat" -o -name "*.png" -o -name "*.gz" \
    # -o -name "*.tgz" -o -name "*.pdf" -o -name "*.md" -o -name "*.bat" \) -exec sed \
      # -e 's_^#!/usr/bin/env python$_&2_' \
      # -e 's_^\(#!/usr/bin/python2\).[45]$_\1_' \
      # -e 's_^#!/usr/bin/python$_&2_' \
      # -e 's_^\( *exec \+\)python\( \+.*\)$_\1python2\2_'\
      # -e 's_^\(.*\)python\( \+-c \+.*\)$_\1python2\2_'\
      # -e "s_'python'_'python2'_" -i {} \;
  # find test/ -type f -exec sed 's_python _python2 _' -i {} \;
  # sed "s_/usr/bin/env python2_/usr/bin/python2_" -i {configure,tools/gyp_node.py}
}

build() {

  local -a extra_config

  cd ${srcdir}
  sleep 5

  [[ -d ${CARCH} ]] && rm -rf ${CARCH}
  mv node-v${pkgver} ${CARCH}
  pushd ${CARCH}

# node.js uses a patched version of v8 5.1.281.81. If/when they upgrade
# we might be able to use this:
#    --shared-v8 \
#    --shared-v8-includes=$(pkgconf --variable=includedir v8) \
#    --shared-v8-libpath=$(pkgconf --variable=libdir v8) \
#    --shared-v8-libname=v8.$(pkgconf --modversion v8) \

# --output-dir= and 0004-Add-output-dir-option-to-configure-node_gyp-py.patch
# attempts to allow using the bundled (hence static) OpenSSL. The problem is
# that the link command line ends up being ~76k characters. This patch helps
# but it gets us down to ~45k. To further reduce this, the build folders of
# "Release/out.target" would be the next target.
#  OUTDIR=$(cd ../../..; echo $PWD)
#      --output-dir=/e/m2/t \
#
# --without-perfctr because MinGW-w64's perflib.h is missing a needed define
# (PERF_COUNTERSET_MULTI_AGGREGATE) and the win32 perfcnt stuff uses __out,
# __in, and __in_opt arg decorators.

  export CC=gcc
  export CXX=g++
  export LINK=g++

  if check_option "debug" "y"; then
    extra_config+=( --debug )
  fi

  # --without-etw because it must be compiled with mc (message compiler) which is a part of Windows SDK
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --dest-os=win \
    --shared-openssl \
    --shared-zlib \
    --shared-libuv \
    --shared-cares \
    --with-intl=system-icu \
    --shared-http-parser \
    --without-perfctr \
    --without-etw \
    "${extra_config[@]}"

  make
}

# check() {
  # cd ${CARCH}
  # make check -k || true
# }

package() {
  cd "${srcdir}/${CARCH}"
  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}"/${MINGW_PREFIX}/share/doc/nodejs
  cp -r doc/api/{*.html,assets} \
    "${pkgdir}"/${MINGW_PREFIX}/share/doc/nodejs

  install -D -m644 LICENSE \
    "${pkgdir}"/${MINGW_PREFIX}/share/licenses/nodejs/LICENSE

  install -m755 "${srcdir}/node-gyp" "${pkgdir}"${MINGW_PREFIX}/bin/node-gyp
  install -m755 "${srcdir}/node-gyp.cmd" "${pkgdir}"${MINGW_PREFIX}/bin/node-gyp.cmd
  install -m755 "${pkgdir}${MINGW_PREFIX}/lib/node_modules/npm/bin/npm.cmd" "${pkgdir}${MINGW_PREFIX}/bin/npm.cmd"

  # Use winpty to wrap the exe when executed from bash. Please don't move this into a patch as hopefully one day we won't need this hack.
  local _exename="node"
  mv "${pkgdir}"${MINGW_PREFIX}/bin/${_exename} "${pkgdir}"${MINGW_PREFIX}/bin/${_exename}_exe
  echo "#!/usr/bin/env bash" > "${pkgdir}${MINGW_PREFIX}/bin/${_exename}"
  echo '/usr/bin/winpty "$( dirname ${BASH_SOURCE[0]} )/'${_exename}'.exe" "$@"' >> "${pkgdir}${MINGW_PREFIX}/bin/${_exename}"
  mv "${pkgdir}"${MINGW_PREFIX}/bin/${_exename}_exe "${pkgdir}"${MINGW_PREFIX}/bin/${_exename}.exe

  # copy missing manpages
  cp -r "${pkgdir}${MINGW_PREFIX}/lib/node_modules/npm/man" "${pkgdir}${MINGW_PREFIX}/share"

  # /mingw64/lib/node_modules/npm/lib/npm.js is ridden with tests for win32, so that should probably be changed someday.
}
