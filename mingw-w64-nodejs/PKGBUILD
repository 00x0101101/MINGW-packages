# Maintainer: Martell Malone <martellmalone@gmail.com>
# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=nodejs

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.10.29
pkgrel=2
pkgdesc="Evented I/O for V8 javascript (mingw-w64)"
arch=('any')
url="http://nodejs.org/"
license=("MIT")
makedepends=("python2" "make" "gyp-svn")
# If can switch to shared dependencies instead of
# static, bundled then here is the full list. For
# for now, reluctantly the only shared dependency
# is OpenSSL.
# depends=("${MINGW_PACKAGE_PREFIX}-c-ares"
#          "${MINGW_PACKAGE_PREFIX}-http-parser"
#          "${MINGW_PACKAGE_PREFIX}-v8"
#          "${MINGW_PACKAGE_PREFIX}-libuv"
#          "${MINGW_PACKAGE_PREFIX}-openssl"
#          "${MINGW_PACKAGE_PREFIX}-zlib")
depends=("${MINGW_PACKAGE_PREFIX}-openssl" "winpty-git")
#options=('!emptydirs' '!strip') # 'debug')

source=("http://nodejs.org/dist/v${pkgver}/node-v${pkgver}.tar.gz"
        "0000-Fix-NODE_NET_SOCKET_READ-and-WRITE-win32-macros.patch"
        "0001-Fix-NODE_COUNT_GC_PERCENTTIME-macro.patch"
        "0002-Define-_WIN32_WINNT_0x0600-when-node_use_perfctr.patch"
        "0003-Win64-Fix--Define-and-use-INTPTR-in-win32-etw.h"
        "0004-Determine-and-use-EXEEXT-in-Makefile-and-install-py.patch"
        "0005-Make-makefiles-if-sys-platform-is-msys.patch"
        "0006-Add-output-dir-option-to-configure-node_gyp-py.patch"

        "0101-Fixing-for-python2-name.patch"
        "0102-Mat_Sutcliffe-node_mingw64-without-MSYS-path-conversion.patch"
        "0103-Mat_Sutcliffe-node_mingw64-MSYS-path-conversion.patch"
        "0104-Use-system-gyp.patch"
        "0105-Add-gyp-debug-option-to-configure.patch"

        "0200-v8-3.26.33-Disable-USING_V8_SHARED-for-v8_snapshot-static-lib.patch"
        "0201-v8-3.26.33-Include-win32-headers-h-then-undef-MemoryBarrier.patch"
        "0203-v8-3.26.33-Use-soname_version.dll-not-so.soname_version-on-Windows.patch"
        "0300-uv-Use-4-arg-swprintf-on-MinGW-w64.patch")

md5sums=('e14ea9f46f9beecdf4e9423fb626c70b'
         '7cc10915495698824f6f4548c0ce1d38'
         '0aebb004e42a515b73f65f0dfe91436b'
         'd06f47617aa37a625f68ef72c1cdcad8'
         '22fa77e95d622c9df075c11c75c92691'
         '114a57cba50444d1ad197a60e95f68a5'
         'a8f768f90eae43fd10a280017baea670'
         '5f0f5b7140a49f99c2d1ecabc61733c4'
         'cd5870bc8911fe4113d83407257ced4c'
         '4d6edb608a46c9abcd11bc845dba85da'
         '64bcb6ddfdd7863f10a302dedbe13d85'
         'c79481c9c5010e4bcd7b4389cf44b7f9'
         '63013c0eeaee90e0985266fba9023a3e'
         'dd82d12b16a416470bf73b56f0684163'
         '9828f204d7068faa656b83c738b67f5e'
         '0be71da4648801fba9aef222919cf611'
         '1f389190a61075b31675090fa4a60f53')

prepare() {

  cd ${srcdir}/node-v${pkgver}

  patch -p1 -i "${srcdir}"/0000-Fix-NODE_NET_SOCKET_READ-and-WRITE-win32-macros.patch

  # Using perfctrs makes Vista the minimum OS. MinGW-w64's perflib.h is missing lots
  # of stuff too, so the patches aren't enough.
  patch -p1 -i "${srcdir}"/0001-Fix-NODE_COUNT_GC_PERCENTTIME-macro.patch
  patch -p1 -i "${srcdir}"/0002-Define-_WIN32_WINNT_0x0600-when-node_use_perfctr.patch
  patch -p1 -i "${srcdir}"/0003-Win64-Fix--Define-and-use-INTPTR-in-win32-etw.h
  patch -p1 -i "${srcdir}"/0004-Determine-and-use-EXEEXT-in-Makefile-and-install-py.patch
  patch -p1 -i "${srcdir}"/0005-Make-makefiles-if-sys-platform-is-msys.patch
  patch -p1 -i "${srcdir}"/0006-Add-output-dir-option-to-configure-node_gyp-py.patch

  patch -p1 -i "${srcdir}"/0101-Fixing-for-python2-name.patch
  patch -p1 -i "${srcdir}"/0102-Mat_Sutcliffe-node_mingw64-without-MSYS-path-conversion.patch
# We don't want this patch, someone might though. May need fixing for MSYS2 anyway.
#  patch -p1 -i "${srcdir}"/0103-Mat_Sutcliffe-node_mingw64-MSYS-path-conversion.patch
  patch -p1 -i "${srcdir}"/0104-Use-system-gyp.patch
  patch -p1 -i "${srcdir}"/0105-Add-gyp-debug-option-to-configure.patch

  pushd deps/v8
  patch -p1 -i "${srcdir}"/0200-v8-3.26.33-Disable-USING_V8_SHARED-for-v8_snapshot-static-lib.patch
  patch -p1 -i "${srcdir}"/0201-v8-3.26.33-Include-win32-headers-h-then-undef-MemoryBarrier.patch
  patch -p1 -i "${srcdir}"/0203-v8-3.26.33-Use-soname_version.dll-not-so.soname_version-on-Windows.patch
  popd

  pushd deps/uv
  patch -p1 -i "${srcdir}"/0300-uv-Use-4-arg-swprintf-on-MinGW-w64.patch
  popd
}

build() {

  local -a extra_config

  cd ${srcdir}/node-v${pkgver}

# node.js uses a patched version of v8 3.26.33. If/when they upgrade
# we might be able to use this:
#    --shared-v8 \
#    --shared-v8-includes=$(pkgconf --variable=includedir v8) \
#    --shared-v8-libpath=$(pkgconf --variable=libdir v8) \
#    --shared-v8-libname=v8.$(pkgconf --modversion v8) \

#    --shared-cares-libpath=${MINGW_PREFIX}/lib \
#    --shared-cares-includes=${MINGW_PREFIX}/include \

#    --shared-cares \
#    --shared-libuv \
#    --shared-zlib \
#    --shared-http-parser \

# --output-dir= and 0004-Add-output-dir-option-to-configure-node_gyp-py.patch
# attempts to allow using the bundled (hence static) OpenSSL. The problem is
# that the link command line ends up being ~76k characters. This patch helps
# but it gets us down to ~45k. To further reduce this, the build folders of 
# "Release/out.target" would be the next target.
#  OUTDIR=$(cd ../../..; echo $PWD)
#      --output-dir=/e/m2/t \
#
# --without-perfctr because MinGW-w64's perflib.h is missing a needed define
# (PERF_COUNTERSET_MULTI_AGGREGATE) and the win32 perfcnt stuff uses __out,
# __in, and __in_opt arg decorators.

  export CC=gcc
  export CXX=g++
  export LINK=g++

  if check_option "debug" "y"; then
    extra_config+=( --debug )
  fi

  ./configure \
    --prefix=${MINGW_PREFIX} \
    --dest-os=win \
    --shared-openssl \
    --shared-openssl-includes=${MINGW_PREFIX}/include \
    --shared-openssl-libpath=${MINGW_PREFIX}/lib \
    --without-perfctr \
    --gyp-debug=all \
    "${extra_config[@]}"

  make
}

package() {

  # winpty-git script to invoke console
  mkdir -p "${pkgdir}${MINGW_PREFIX}/bin"
  _exename="node"
  echo "#!/usr/bin/env bash" > "${pkgdir}${MINGW_PREFIX}/bin/${_exename}"
  echo '/usr/bin/console "$( cd "$( dirname "${BASH_SOURCE[0]}" )" && pwd )/'${_exename}'.exe" "$@"' >> "${pkgdir}${MINGW_PREFIX}/bin/${_exename}"

  cd "${srcdir}/node-v${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -d "$pkgdir"/${MINGW_PREFIX}/share/doc/nodejs
  cp -r doc/api/{*.html,assets} \
    "$pkgdir"/${MINGW_PREFIX}/share/doc/nodejs

  install -D -m644 LICENSE \
    "$pkgdir"/${MINGW_PREFIX}/share/licenses/nodejs/LICENSE

  cp "$pkgdir"/${MINGW_PREFIX}/lib/node_modules/npm/node_modules/node-gyp/bin/node-gyp.js "$pkgdir"/${MINGW_PREFIX}/bin/node-gyp
}
