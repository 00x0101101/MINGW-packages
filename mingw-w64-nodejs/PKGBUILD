# Maintainer: Martell Malone <martellmalone@gmail.com>
# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=nodejs

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.10.29
pkgrel=1
pkgdesc="Evented I/O for V8 javascript (mingw-w64)"
arch=('any')
url="http://nodejs.org/"
license=("MIT")
makedepends=("python2" "make" "gyp-svn")
depends=("${MINGW_PACKAGE_PREFIX}-c-ares" "${MINGW_PACKAGE_PREFIX}-http-parser" "${MINGW_PACKAGE_PREFIX}-v8" "${MINGW_PACKAGE_PREFIX}-libuv" "${MINGW_PACKAGE_PREFIX}-openssl" "${MINGW_PACKAGE_PREFIX}-zlib")
options=('!emptydirs !strip')
source=("http://nodejs.org/dist/v${pkgver}/node-v${pkgver}.tar.gz"
        "0001-Fixing-for-python2-name.patch"
        "0002-node_mingw64.patch"
        "0003-Add-forced-flavor-configure-option.patch")
md5sums=('e14ea9f46f9beecdf4e9423fb626c70b'
         '6da4c2629f909468da7537febf1754a2'
         'bf767503ce3effcbfaac35c8f2025a31'
         '494782836c1499466eddfebf1136b5a7')

prepare() {
  cd ${srcdir}/node-v${pkgver}
  patch -p1 -i ${srcdir}/0001-Fixing-for-python2-name.patch
  patch -p1 -i ${srcdir}/0002-node_mingw64.patch
  patch -p1 -i ${srcdir}/0003-Add-forced-flavor-configure-option.patch
}

build() {
  # todo: out of source build doesn't work atm.
  # export PATH="${srcdir}/node-v${pkgver}:$PATH"
  # mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  # cd "${srcdir}/build-${MINGW_CHOST}"
  # dirty hack to find 'tools/*.py' in out of source build with love from martell
  # cp -r ${srcdir}/node-v${pkgver}/tools ${srcdir}/build-${MINGW_CHOST}
  # cp -r ${srcdir}/node-v${pkgver}/node.gyp ${srcdir}/build-${MINGW_CHOST}

  cd ${srcdir}/node-v${pkgver}

  ./configure \
    --shared-v8 \
    --shared-v8-includes=$(pkgconf --variable=includedir v8) \
    --shared-v8-libpath=$(pkgconf --variable=libdir v8) \
    --shared-v8-libname=v8.$(pkgconf --modversion v8) \
    --prefix=${MINGW_PREFIX} \
    --shared-http-parser \
    --shared-cares \
    --shared-openssl \
    --shared-libuv \
    --shared-zlib \
    --forced-flavor=linux

  make
}

package() {
  cd "${srcdir}/node-v${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -d "$pkgdir"/${MINGW_PREFIX}/share/doc/nodejs
  cp -r doc/api/{*.html,assets} \
    "$pkgdir"/${MINGW_PREFIX}/share/doc/nodejs

  install -D -m644 LICENSE \
    "$pkgdir"/${MINGW_PREFIX}/share/licenses/nodejs/LICENSE

  # Why the hell wasn't this installed?! FS#40221
  ln -s /${MINGW_PREFIX}/lib/node_modules/npm/node_modules/node-gyp/bin/node-gyp.js "$pkgdir"/${MINGW_PREFIX}/bin/node-gyp
}
