# Maintainer:  Marty Plummer (The_NetZ) <ntzrmtthihu777 (at) gmail (dot) com>
# Contributor: carstene1ns <url/mail: arch carsten-teibes de>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

_mingw_suff=mingw-w64-${CARCH}
_realname=SDL_sound
pkgname=${_mingw_suff}-${_realname}2-hg
pkgver=r.
pkgrel=1
pkgdesc="A library to decode several popular sound file formats, patched for SDL2 (dev version)"
arch=('any')
url="http://icculus.org/${_realname}/"
license=('LGPL')
depends=("${_mingw_suff}-SDL2"
         "${_mingw_suff}-libmodplug"
         "${_mingw_suff}-libvorbis"
         "${_mingw_suff}-flac"
         "${_mingw_suff}-speex"
         "${_mingw_suff}-physfs")
conflicts=("${_mingw_suff}-${_realname}")
provides=("${_mingw_suff}-${_realname}")
source=($_realname::"hg+http://hg.icculus.org/icculus/${_realname}"
        'pkgconfig.patch'
        'physfs-renamed-export.patch'
        'no-undefined.patch')
sha256sums=('SKIP'
            'c2ebc5487c678ca4d77f5530a01c14602b9794861f4f75a1097fe47f9a8bd7ff'
            'd7bd96390d9bc877c0204922c7c4666cadfdccc5e6c0cfcf9477d113377f5d10'
            '87a2872d6248be64086c3eb7d4b03e6a36d69243971844abe136a3d41e3b91bc')

pkgver() {
  cd ${_pkgbase}
  printf "r%s.%s" "$(hg identify -n)" "$(hg identify -i)"
}

prepare() {
  cd ${srcdir}/${_realname}
  # fix deprecated physfs declaration
  patch -Np1 -i ${srcdir}/physfs-renamed-export.patch
  # patch for SDL2 and pkgconfig support
  patch -Np1 -i ${srcdir}/pkgconfig.patch
  patch -Np1 -i ${srcdir}/no-undefined.patch
  ./bootstrap
}

build() {
  export lt_cv_deplibs_check_method='pass_all'
  mkdir -p ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}
  ${srcdir}/${_realname}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --disable-static \
    --disable-mikmod
  make
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}

  make DESTDIR="$pkgdir/" install
}
