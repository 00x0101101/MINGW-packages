# Maintainer: Zach Bacon <11doctorwhocanada@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_realname=midori
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.5.11
pkgrel=1
pkgdesc="midori web browser (mingw-w64)"
arch=('any')
url='http://www.midori-browser.org/'
license=('LGPL2.1')
source=("http://www.midori-browser.org/downloads/${_realname}_${pkgver}_all_.tar.bz2"
        "0001-Use-file-GLOB-var-RELATIVE.patch")
sha1sums=('64c86935028feb5f89d799c2acacaad67764da6f'
          '8620d10f32d07dfdde1c3c01dec355792cdca854')

prepare() {
  cd $srcdir/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0001-Use-file-GLOB-var-RELATIVE.patch
}

build() {
  cd "$srcdir"/${_realname}-${pkgver}
  [ -d "${srcdir}"/build-${CARCH} ] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DUSE_GTK3=1 \
    ../${_realname}-${pkgver} --trace-expand --debug-output
  make VERBOSE=1
}

check() {
  cd "${srcdir}"/build-${CARCH}
  make check
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make install DESTDIR="${pkgdir}"
}
