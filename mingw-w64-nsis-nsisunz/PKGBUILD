# Maintainer: Renato Silva <br.renatosilva@gmail.com>

_realname='nsis-nsisunz'
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.0
pkgrel=1
url='http://nsis.sourceforge.net/Nsisunz_plug-in'
pkgdesc='NSIS plugin which allows you to extract files from ZIP archives (mingw-w64)'
license=(custom:'ZLIB-like')
arch=(any)
depends=(${MINGW_PACKAGE_PREFIX}-nsis)
makedepends=(${MINGW_PACKAGE_PREFIX}-gcc ${MINGW_PACKAGE_PREFIX}-zlib)
source=('http://nsis.sourceforge.net/mediawiki/images/5/5a/NSISunzU.zip'
        001-use-system-libraries.patch
        LICENSE)
md5sums=('e29dbd0976d87db65a01f6e0b02d2a1a'
         '8af00464d3a8de9e7bade2e62f270fe5'
         '3d9a2ee0b5662c41a626b8acc29d8f34')

prepare() {
    cd "${srcdir}/NSISunzU"
    patch -p1 < "${startdir}"/001-use-system-libraries.patch
}

build() {
    cd "${srcdir}/NSISunzU/Source"
    local plugin_api="${MINGW_PREFIX}/share/doc/nsis/Examples/Plugin"

    # Unicode version
    gcc -c nsisunz.cpp -o nsisunz-unicode.o -I${plugin_api} -DUNICODE -D_UNICODE
    gcc -shared -o nsisunz-unicode.dll nsisunz-unicode.o --static $(pkg-config --static --libs minizip) ${plugin_api}/nsis/libpluginapi-unicode.a

    # ANSI version for x86
    if [[ "${CARCH}" = i686 ]]; then
        gcc -c nsisunz.cpp -o nsisunz-ansi.o -I${plugin_api}
        gcc -shared -o nsisunz-ansi.dll nsisunz-ansi.o --static $(pkg-config --static --libs minizip) ${plugin_api}/nsis/libpluginapi-ansi.a
    fi
}

package() {
    cd "${srcdir}/NSISunzU/Source"
    install -Dm644 "${srcdir}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
    install -Dm755 nsisunz-unicode.dll "${pkgdir}${MINGW_PREFIX}/share/nsis/Plugins/unicode/nsisunz.dll"
    if [[ "${CARCH}" = i686 ]]; then
        install -Dm755 nsisunz-ansi.dll "${pkgdir}${MINGW_PREFIX}/share/nsis/Plugins/ansi/nsisunz.dll"
    fi
}
