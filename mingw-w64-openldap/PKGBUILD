# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=openldap
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.4.39
pkgrel=1
arch=('any')
pkgdesc="OpenLDAP Software is an open source implementation of the Lightweight Directory Access Protocol (only client) (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-gnutls"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-db")
makedepends=("groff"
            "${MINGW_PACKAGE_PREFIX}-gcc"
            "libintl"
            "libtool"
            "make")
options=('strip' 'staticlibs')
license=("The OpenLDAP Public License")
url="http://www.openldap.org/"
source=(ftp://ftp.openldap.org/pub/OpenLDAP/openldap-release/${_realname}-$pkgver.tgz
        001-backmdb.patch)
sha256sums=('8267c87347103fef56b783b24877c0feda1063d3cb85d070e503d076584bf8a7'
            'b462bfab9362cf5cab61d67cd24902a1800fb498a152b681ee182c713b79146c')

prepare() {
  cd ${_realname}-${pkgver}

  patch -p1 -i ${srcdir}/001-backmdb.patch
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  unset CPPFLAGS
  ../${_realname}-${pkgver}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib
  sed -i "s/#define socklen_t int/\/*#define socklen_t int*\//" include/portable.h
  make depend
  make
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make DESTDIR=$pkgdir install
}
