# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=codelite

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=6.0.1.156.gb82f8e0
pkgrel=1
pkgdesc="Open-source, cross platform IDE for the C/C++ programming languages (mingw-w64)"
arch=('any')
license=('GPL')
url="http://www.codelite.org"
depends=("${MINGW_PACKAGE_PREFIX}-clang"
        "${MINGW_PACKAGE_PREFIX}-hunspell"
        "${MINGW_PACKAGE_PREFIX}-libssh"
        "${MINGW_PACKAGE_PREFIX}-wxWidgets"
        "${MINGW_PACKAGE_PREFIX}-clang"
        )
makedepends=("${MINGW_PACKAGE_PREFIX}-boost" "${MINGW_PACKAGE_PREFIX}-webkitgtk2" "${MINGW_PACKAGE_PREFIX}-cmake")
options=(!strip staticlibs !buildflags)
source=("${_realname}::git+https://github.com/eranif/codelite.git"
        codelite-sftp-createfilew.patch
        codelite-clang-deprecated.patch)
md5sums=('SKIP'
         'bf9749652078c87cc1b78b3b0b48f9f7'
         '4bec2ac130687bd4b947b98a0d5d3fd4')

pkgver() {
  cd "$_realname"
  git describe --tags | sed 's|-|.|g'
}

prepare() {
  cd "$srcdir/$_realname"
  patch -p1 -i ${srcdir}/codelite-sftp-createfilew.patch
  patch -p1 -i ${srcdir}/codelite-clang-deprecated.patch
}

build() {
  
  pushd ${MINGW_PREFIX} > /dev/null
  export PREFIX_DEPS=`pwd -W`
  popd > /dev/null
  
  cd $srcdir/${_realname}

  CPPFLAGS+=" -fno-devirtualize"
  CFLAGS+=" -fno-lto -fno-devirtualize"
  CXXFLAGS+=" -fno-lto -fno-devirtualize"
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DPREFIX=${pkgdir}${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_CLANG=1 \
    -DwxWidgets_CONFIG_EXECUTABLE=${MINGW_PREFIX}/bin/wx-config \
    -DwxWidgets_wxrc_EXECUTABLE=${MINGW_PREFIX}/bin/wxrc-3.0 \
    .

  make V=1 # -j1 VERBOSE=1
}

package() {  
  cd "$srcdir/${_realname}"
  make install
}

