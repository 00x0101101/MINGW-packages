# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=codelite

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=6.0.1.123.gda1ca7e
pkgrel=1
pkgdesc="Open-source, cross platform IDE for the C/C++ programming languages (mingw-w64)"
arch=('any')
license=('GPL')
url="http://www.codelite.org"
depends=("${MINGW_PACKAGE_PREFIX}-clang"
        "${MINGW_PACKAGE_PREFIX}-hunspell"
        "${MINGW_PACKAGE_PREFIX}-libssh"
        "${MINGW_PACKAGE_PREFIX}-wxWidgets"
        )
makedepends=("${MINGW_PACKAGE_PREFIX}-boost" "${MINGW_PACKAGE_PREFIX}-webkitgtk2" "${MINGW_PACKAGE_PREFIX}-cmake")
options=(!strip staticlibs !buildflags)
source=("${_realname}::git+https://github.com/eranif/codelite.git"
        codelite-no-rpath-windows.patch
        codelite-cmake-mingw.patch
        codelite-sftp-createfilew.patch
        codelite-find-hunspell.patch
        codelite-mingw-no-fpic.patch
        codelite-astyle-mingw-w64.patch
        codelite-mingw-not-monolithic-wxwidgets.patch
        codelite-mingw-install.patch
        codelite-mingw-windres.patch)
md5sums=('SKIP'
         '08799707bcd285decb0cb76d1e1439bd'
         '09e373db2099467b66fafdbea234b747'
         'bf9749652078c87cc1b78b3b0b48f9f7'
         '848abb8dcd91c1bfa2f127862131ce3d'
         '48a012c2c9e9a624660a79c44d93dee4'
         '68a493c90f2bbbfa0c224746ab934d16'
         '86c5febc39095744a7819be10362837e'
         '5ec9bf4334d1d40c041fd44d3323cf95'
         '7e478a51c6952e47d72b8ff30a9e8c81')

pkgver() {
  cd "$_realname"
  git describe --tags | sed 's|-|.|g'
}

prepare() {
  cd "$srcdir/$_realname"
  patch -p1 -i ${srcdir}/codelite-no-rpath-windows.patch
  patch -p1 -i ${srcdir}/codelite-cmake-mingw.patch
  patch -p1 -i ${srcdir}/codelite-sftp-createfilew.patch
  patch -p1 -i ${srcdir}/codelite-find-hunspell.patch
  patch -p1 -i ${srcdir}/codelite-mingw-no-fpic.patch
  patch -p1 -i ${srcdir}/codelite-astyle-mingw-w64.patch
  patch -p1 -i ${srcdir}/codelite-mingw-not-monolithic-wxwidgets.patch
  patch -p1 -i ${srcdir}/codelite-mingw-install.patch
  patch -p1 -i ${srcdir}/codelite-mingw-windres.patch
}

build() {
  
  pushd ${MINGW_PREFIX} > /dev/null
  export PREFIX_DEPS=`pwd -W`
  popd > /dev/null
  
  cd $srcdir/${_realname}

  CPPFLAGS+=" -fno-devirtualize"
  CFLAGS+=" -fno-lto -fno-devirtualize"
  CXXFLAGS+=" -fno-lto -fno-devirtualize"
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DPREFIX=${pkgdir}${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_CLANG=0 \
    -DwxWidgets_CONFIG_EXECUTABLE=${MINGW_PREFIX}/bin/wx-config \
    -DwxWidgets_wxrc_EXECUTABLE=${MINGW_PREFIX}/bin/wxrc-3.0 \
    .

  make V=1 # -j1 VERBOSE=1
}

package() {  
  cd "$srcdir/${_realname}"
  make install
}

