# $Id$
# Maintainer (ArchLinux): Andrea Scarpino <andrea@archlinux.org>
# Maintainer (MSYS2): Ray Donnelly <mingw.android@gmail.com>

_realname=extra-cmake-modules
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.1.0
_pkgver=5.1.0
pkgrel=1
pkgdesc='Extra CMake modules (mingw-w64)'
arch=('any')
url='https://projects.kde.org/projects/kdesupport/extra-cmake-modules'
license=('LGPL')
depends=("${MINGW_PACKAGE_PREFIX}-cmake")
# makedepends=('python-sphinx') # qt5-tools for QtHelp pages
source=("http://download.kde.org/stable/frameworks/${_pkgver}/${_realname}-${pkgver}.tar.xz")
md5sums=('005e28bb881718e3c381f8731fb70ea7')

prepare() {
  mkdir -p build-${CARCH}
}

build() {
  cd build-${CARCH}
  MSYS2_ARG_CONV_EXCL=-DCMAKE_INSTALL_PREFIX= \
    cmake ../${_realname}-${pkgver} \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DCMAKE_BUILD_TYPE=Release \
      -DBUILD_HTML_DOCS=OFF \
      -G'MSYS Makefiles'
  make
}

package() {
  cd build-${CARCH}
  make DESTDIR="${pkgdir}" install
}

