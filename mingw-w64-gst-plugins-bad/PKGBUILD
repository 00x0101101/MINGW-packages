# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gst-plugins-bad
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.3.3
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Bad Plugins (mingw-w64)"
arch=('any')
url="http://gstreamer.freedesktop.org/"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
            "${MINGW_PACKAGE_PREFIX}-pkg-config"
            "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
            )
depends=("${MINGW_PACKAGE_PREFIX}-curl"
        "${MINGW_PACKAGE_PREFIX}-celt"
        "${MINGW_PACKAGE_PREFIX}-daala"
        "${MINGW_PACKAGE_PREFIX}-faad2"
        #"${MINGW_PACKAGE_PREFIX}-faac"
        "${MINGW_PACKAGE_PREFIX}-fluidsynth"
        "${MINGW_PACKAGE_PREFIX}-gnutls"
        "${MINGW_PACKAGE_PREFIX}-gst-plugins-base"
        "${MINGW_PACKAGE_PREFIX}-gtk3"
        "${MINGW_PACKAGE_PREFIX}-libdca"
        "${MINGW_PACKAGE_PREFIX}-libdvdnav"
        "${MINGW_PACKAGE_PREFIX}-libexif"
        "${MINGW_PACKAGE_PREFIX}-libjpeg"
        "${MINGW_PACKAGE_PREFIX}-libmimic"
        "${MINGW_PACKAGE_PREFIX}-libmodplug"
        "${MINGW_PACKAGE_PREFIX}-libmpeg2"
        "${MINGW_PACKAGE_PREFIX}-libpng"
        "${MINGW_PACKAGE_PREFIX}-librsvg"
        "${MINGW_PACKAGE_PREFIX}-libwebp"
        "${MINGW_PACKAGE_PREFIX}-mpg123"
        "${MINGW_PACKAGE_PREFIX}-openal"
        "${MINGW_PACKAGE_PREFIX}-opencv"
        "${MINGW_PACKAGE_PREFIX}-openexr"
        "${MINGW_PACKAGE_PREFIX}-openjpeg"
        "${MINGW_PACKAGE_PREFIX}-opus"
        "${MINGW_PACKAGE_PREFIX}-schroedinger"
        "${MINGW_PACKAGE_PREFIX}-soundtouch"
        )
options=(!libtool strip staticlibs)
source=(${url}/src/${_realname}/${_realname}-${pkgver}.tar.xz
        0001-give-cc-to-gir-scanner.mingw.patch
        0002-fix-ldflags.patch
        0004-fix-X-detection.mingw.patch
        0005-fix-cast-errors.patch
        0006-win32-winsock.patch
        0007-motioncels-use-no-net.patch
        0008-examples-gtk3.patch)
md5sums=('e45cd6f389ab9f5be88e731ba94c4fd5'
         '9b40bf4167ca872b69429f04a67b2d02'
         '8ae9c566838d6a5b5dbf42e5760e2793'
         'eb0a27e74755e4ee1c5259c8c4ab40e8'
         '8ed893b5ae43eb215e51d8c8f3e55edd'
         '85fc9c14873fea868263c9ee077957d6'
         '8c6242d24ada30e88bb102afc4dc2901'
         'fcd5ce41047d2c318bbcd3dfb17d5a22')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0001-give-cc-to-gir-scanner.mingw.patch
  patch -p1 -i ${srcdir}/0002-fix-ldflags.patch
  patch -p0 -i ${srcdir}/0004-fix-X-detection.mingw.patch
  patch -p1 -i ${srcdir}/0005-fix-cast-errors.patch
  patch -p1 -i ${srcdir}/0006-win32-winsock.patch
  patch -p1 -i ${srcdir}/0007-motioncels-use-no-net.patch
  patch -p1 -i ${srcdir}/0008-examples-gtk3.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --disable-static \
    --enable-shared \
    --enable-silent-rules \
    --enable-experimental \
    --disable-gtk-doc \
    --with-gtk=3.0
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 DESTDIR="$pkgdir" install
  find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
  rm -f ${pkgdir}${MINGW_PREFIX}/lib/gstreamer*/*.a
}
