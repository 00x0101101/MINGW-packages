# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gst-plugins-bad
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=1.3.3
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Bad Plugins (mingw-w64)"
arch=('any')
url="http://gstreamer.freedesktop.org/"
license=("LGPL")
makedepends=("${_mingw_suff}-gcc"
            "${_mingw_suff}-pkg-config"
            "${_mingw_suff}-gobject-introspection"
            )
depends=("${_mingw_suff}-curl"
        "${_mingw_suff}-gst-plugins-base"
        "${_mingw_suff}-gtk3"
        "${_mingw_suff}-faad2"
        #"${_mingw_suff}-faac"
        "${_mingw_suff}-celt"
        #"${_mingw_suff}-libdca"
        "${_mingw_suff}-libmodplug"
        "${_mingw_suff}-libdvdnav"
        "${_mingw_suff}-opus"
        "${_mingw_suff}-fluidsynth"
        "${_mingw_suff}-openjpeg"
        "${_mingw_suff}-libjpeg"
        "${_mingw_suff}-libpng"
        "${_mingw_suff}-libwebp"
        "${_mingw_suff}-gnutls"
        "${_mingw_suff}-schroedinger"
        "${_mingw_suff}-libexif"
        "${_mingw_suff}-libmpeg2"
        "${_mingw_suff}-librsvg"
        "${_mingw_suff}-opencv"
        "${_mingw_suff}-openexr"
        "${_mingw_suff}-openal"
        )
options=(!libtool strip staticlibs)
source=(${url}/src/${_realname}/${_realname}-${pkgver}.tar.xz
        0001-give-cc-to-gir-scanner.mingw.patch
        0002-fix-ldflags.patch
        0004-fix-X-detection.mingw.patch
        0005-fix-cast-errors.patch
        0006-win32-winsock.patch
        0007-motioncels-use-no-net.patch)
md5sums=('e45cd6f389ab9f5be88e731ba94c4fd5'
         '9b40bf4167ca872b69429f04a67b2d02'
         '8ae9c566838d6a5b5dbf42e5760e2793'
         'eb0a27e74755e4ee1c5259c8c4ab40e8'
         '8ed893b5ae43eb215e51d8c8f3e55edd'
         '85fc9c14873fea868263c9ee077957d6'
         '8c6242d24ada30e88bb102afc4dc2901')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0001-give-cc-to-gir-scanner.mingw.patch
  patch -p1 -i ${srcdir}/0002-fix-ldflags.patch
  patch -p0 -i ${srcdir}/0004-fix-X-detection.mingw.patch
  patch -p1 -i ${srcdir}/0005-fix-cast-errors.patch
  patch -p1 -i ${srcdir}/0006-win32-winsock.patch
  patch -p1 -i ${srcdir}/0007-motioncels-use-no-net.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  cp -r "${srcdir}/${_realname}-${pkgver}" "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --disable-static \
    --enable-shared \
    --enable-silent-rules \
    --enable-experimental \
    --disable-gtk-doc \
    --with-gtk=3.0
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 DESTDIR="$pkgdir" install
  find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
  rm -f ${pkgdir}${MINGW_PREFIX}/lib/gstreamer*/*.a
}
