# Contributor: Xin Sun <sun.simpson@gmail.com>

_realname=szip
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=2.1
pkgrel=1
pkgdesc="Extended-Rice lossless compression algorithm implementation"
arch=('any')
url="http://www.hdfgroup.org/doc_resource/SZIP/"
source=(
  "http://www.hdfgroup.org/ftp/lib-external/szip/${pkgver}/src/szip-${pkgver}.tar.gz"
  "szip-hdf5.patch"
  )
md5sums=(
  "902f831bcefb69c6b635374424acbead"
  "2ac1108913387e1f7b4749a18cc353a3"
  )
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-cmake")
options=('staticlibs' 'strip')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p0 -i ${srcdir}/szip-hdf5.patch
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX:PATH=${pkgdir}${MINGW_PREFIX} \
    -DBUILD_SHARED_LIBS=ON \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    ../${_realname}-${pkgver}
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 install
  
  # clear README, licenses, etc. or shall we move them to another place?
  for f in ${pkgdir}${MINGW_PREFIX}/share/*
  do
    [[ -f $f ]] && rm -f $f
  done
  
  mv ${pkgdir}${MINGW_PREFIX}/lib/szip.lib ${pkgdir}${MINGW_PREFIX}/lib/libszip.dll.a
  sed -s "s|szip\.lib|libszip\.dll\.a|g" -i ${pkgdir}${MINGW_PREFIX}/share/cmake/SZIP/SZIP-targets-release.cmake
  sed -s 's|"${_IMPORT_PREFIX}/include"|"${_IMPORT_PREFIX}/include" PATH|g' -i ${pkgdir}${MINGW_PREFIX}/share/cmake/SZIP/SZIP-config.cmake
}
