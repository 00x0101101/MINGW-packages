# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gc
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=7.2.d
pkgrel=2
pkgdesc="A garbage collector for C and C++ (mingw-w64)"
arch=('any')
url="http://www.hpl.hp.com/personal/Hans_Boehm/gc"
license=("GPL")
makedepends=(${_mingw_suff}-gcc ${_mingw_suff}-pkg-config)
depends=(${_mingw_suff}-crt)
options=('staticlibs' 'strip') #!buildflags
source=("$url/gc_source/${_realname}-${pkgver%.*}d.tar.gz"
		0001-winpthreads.mingw.patch
		0002-export.mingw.patch
		0003-move-docs.all.patch)
md5sums=('91340b28c61753a789eb6077675d87d2'
         '97223c965b5e0c61d9bbf2710b44ddb6'
         '48a4d5cca9f3ad3218cbaea59a86bee8'
         'bbd985cd4a16d9b110cfccf4ecd2700b')


prepare() {
	cd "${srcdir}"/${_realname}-${pkgver%.*}
	patch -p1 -i ${srcdir}/0001-winpthreads.mingw.patch
	patch -p1 -i ${srcdir}/0002-export.mingw.patch
	patch -p1 -i ${srcdir}/0003-move-docs.all.patch
}

build() {
	mkdir -p "${srcdir}/build-${MINGW_CHOST}"
	cd "${srcdir}/build-${MINGW_CHOST}"
	"${srcdir}"/${_realname}-${pkgver%.*}/configure \
		--prefix=${MINGW_PREFIX} \
		--build=${MINGW_CHOST} \
		--host=${MINGW_CHOST} \
		--target=${MINGW_CHOST} \
		--enable-threads=posix \
		--disable-dependency-tracking \
		--enable-cplusplus
	make
}

package() {
	cd "${srcdir}/build-${MINGW_CHOST}"
	make DESTDIR="$pkgdir" install
	rm -r "${pkgdir}${MINGW_PREFIX}/share"
}
