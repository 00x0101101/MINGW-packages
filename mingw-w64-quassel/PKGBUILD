# $Id$
# Maintainer (ArchLinux): Vesa Kaihlavirta <vegai@iki.fi>
# Maintainer (ArchLinux): Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Maintainer (MSYS2): Ray Donnelly <mingw.android@gmail.com>

pkgbase=quassel
pkgname=("${MINGW_PACKAGE_PREFIX}-${pkgbase}")
pkgver=0.12.2
pkgrel=1
pkgdesc="Next-generation distributed IRC client (mingw-w64)"
arch=('any')
url="http://quassel-irc.org/"
license=('GPL')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake")
depends=("${MINGW_PACKAGE_PREFIX}-qt5"
         #"${MINGW_PACKAGE_PREFIX}-extra-cmake-modules" #TODO: only needed with WITH_KDE=ON
         "${MINGW_PACKAGE_PREFIX}-Snorenotify" )
source=(http://quassel-irc.org/pub/$pkgbase-$pkgver.tar.bz2
        "0001-MinGW-w64-Adopt-FHS-Layout.patch")
sha256sums=("6bd6f79ecb88fb857bea7e89c767a3bd0f413ff01bae9298dd2e563478947897"
            "f05f9e502581e7ec632f89b969e3934022f8ff7846fd23e48d2eebf246d815f9")

prepare() {
  cd "${srcdir}"/quassel-${pkgver}
  patch -p1 -i "${srcdir}"/0001-MinGW-w64-Adopt-FHS-Layout.patch
}

build() {
  cd "${srcdir}"
  [ -d build-${CARCH} ] && rm -rf build-${CARCH}
  mkdir build-${CARCH}

  # -DWIN32_LEAN_AND_MEAN=1 because "interface" is used in:
  # /mingw64/include/QtDBus/qdbusconnection.h
  cd "${srcdir}"/build-${CARCH}
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DCMAKE_CXX_FLAGS="${CFLAGS} -DWIN32_LEAN_AND_MEAN=1" \
      -DUSE_QT5=ON \
      -DWITH_KDE=OFF \
      -DCMAKE_BUILD_TYPE="Release" \
      -DWANT_CORE=ON \
      -DWANT_QTCLIENT=ON \
      -DWANT_MONO=ON \
      ${srcdir}/quassel-${pkgver}/ \
      -Wno-dev \
      -G'MSYS Makefiles'
  make
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make DESTDIR="${pkgdir}" install
}
