# Maintainer: Diego Sogari <diego.sogari@falker.com.br>

_realname=libkml
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.3.0
pkgrel=1
pkgdesc="Reference implementation of OGC KML 2.2 (mingw-w64)"
arch=('any')
url="https://github.com/google/libkml/"
license=('New BSD License')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "automake" "autoconf"
             "${MINGW_PACKAGE_PREFIX}-python2")
depends=("${MINGW_PACKAGE_PREFIX}-uriparser" "${MINGW_PACKAGE_PREFIX}-zlib")
checkdepends=("${MINGW_PACKAGE_PREFIX}-gtest")
commit=8609edf7c8d13ae2ddb6eac2bca7c8e49c67a5f8
source=("https://github.com/google/libkml/archive/${commit}.zip"
        "${_realname}-${pkgver}.patch"
        "${_realname}-${pkgver}-complement.tar"
        "minizipext-1.0.tar")
sha256sums=('7f92545c1af1070fcce95da335e88dfbab5bb098784b1eded247cc31d90b5f6b'
            '58104e9fb50afbfbd9fbd25911c6e0fe348dbac9514071de4281ec4c6bdd61c1'
            'd9fee26d33c7ac22ffd389ca47c63a2032bb21709aa28850be8a47725e5e84dc'
            'a8960519ca90c51952be0a925e5eb88859a7e69d11f940a9a95232df2dd7a719')

prepare() {
  cd "${srcdir}/${_realname}-${commit}"
  patch -Np1 -i "../${_realname}-${pkgver}.patch"
  cp -rf ../minizipext-1.0 third_party
  autoreconf -fvi
}

build() {
  rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  
  ../${_realname}-${commit}/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX}

  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make check
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
