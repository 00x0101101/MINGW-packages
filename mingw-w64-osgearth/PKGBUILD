# Maintainer: James Duley <jagduley gmail>

_realname=osgearth
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.7
pkgrel=1
pkgdesc="A terrain rendering toolkit for OpenSceneGraph"
arch=('any')
license=('LGPL')
url="http://osgearth.org"
depends=("${MINGW_PACKAGE_PREFIX}-gdal" "${MINGW_PACKAGE_PREFIX}-OpenSceneGraph")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake")
source=("https://github.com/gwaldron/osgearth/archive/${_realname}-${pkgver}.tar.gz"
        "0001-Fix-mingw-debug-build.patch"
        "0002-Forced-include-headers-for-Qt5-moc-on-files-without-.patch"
        "0003-Build-fix-against-OSG-3.4.0-and-OSG-3.5.0.patch")
md5sums=('aad15a3ee27a34dcabc9b8f4922a1e96'
         '3beb4bfcdb4be556705ded174ae41c89'
         '4b682376141baae7628e9651a7fe578c'
         '12fb399f93e0b92d295685d946c984b2')

prepare() {
  cd ${srcdir}/${_realname}-${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0001-Fix-mingw-debug-build.patch
  patch -p1 -i ${srcdir}/0002-Forced-include-headers-for-Qt5-moc-on-files-without-.patch
  patch -p1 -i ${srcdir}/0003-Build-fix-against-OSG-3.4.0-and-OSG-3.5.0.patch
}

build() {
  _pkgver=${_realname}-${pkgver} # How it's tagged in git

  [[ -d ${srcdir}/build ]] && rm -rf ${srcdir}/build
  mkdir -p ${srcdir}/build && cd ${srcdir}/build

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    ../${_realname}-${_pkgver}

  make
}

package() {
  cd ${srcdir}/build

  make install DESTDIR="${pkgdir}"
}
