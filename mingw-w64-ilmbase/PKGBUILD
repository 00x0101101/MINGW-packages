# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=ilmbase
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=2.1.0
pkgrel=6
pkgdesc="Base libraries from ILM for OpenEXR (mingw-w64)"
arch=('any')
url="http://http://www.openexr.com/"
license=("custom")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config" "libtool")
depends=("${_mingw_suff}-gcc-libs")
options=('staticlibs' 'strip')
source=("http://download.savannah.nongnu.org/releases/openexr/${_realname}-${pkgver}.tar.gz"
        ilmthread-mingw-win32.patch
        ilmthread-mingw-pthreads.patch
        ilmbase-2.1.0_obsolete-macros.patch
        cmake-soversion.patch
        cmake-install-binaries.patch)
md5sums=('8ba2f608191ad020e50277d8a3ba0850'
         '3a34cf48b52c2af0584ef3e8cf50499a'
         '8b9d9c6e4ed88dc9dfb80ce4f637460a'
         '1c9a320c8ab09c98abe9ecb80eb24e2e'
         '3af90921f715f52012d0b8d3a631bc91'
         'baaa4fa9467969a611c70e62efa1fe6b')
prepare(){
  cd "$srcdir/${_realname}-$pkgver"
  # Use either one or the other of the ilmthread patches
  # depending on the sort of threads that you want.
  # patch -Np1 -i "$srcdir/ilmthread-mingw-win32.patch"
  patch -Np1 -i "$srcdir/ilmthread-mingw-pthreads.patch"
  patch -Np1 -i "$srcdir/ilmbase-2.1.0_obsolete-macros.patch"
  patch -Np1 -i "$srcdir/cmake-soversion.patch"
  patch -Np1 -i "$srcdir/cmake-install-binaries.patch"
  #./bootstrap
}

build() {
  mkdir build-${CARCH}
  cd build-${CARCH}
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${pkgdir}${MINGW_PREFIX} \
    -DBUILD_SHARED_LIBS=ON \
    ../${_realname}-${pkgver}

  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make install

  pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
  export PREFIX_WIN=`pwd -W`
  popd > /dev/null
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/IlmBase.pc
}
