# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=kicad
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgver=r6681.7c747c1
pkgrel=1
pkgdesc="Software for the creation of electronic schematic diagrams and printed circuit board artworkrs (mingw-w64)"
arch=('any')
url="https://github.com/imageworks/OpenShadingLanguage/"
license=("custom")
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
depends=("${MINGW_PACKAGE_PREFIX}-boost"
        "${MINGW_PACKAGE_PREFIX}-cairo" 
        "${MINGW_PACKAGE_PREFIX}-glew"
        "${MINGW_PACKAGE_PREFIX}-openssl"
        "${MINGW_PACKAGE_PREFIX}-swig"
        "${MINGW_PACKAGE_PREFIX}-wxPython"
        "${MINGW_PACKAGE_PREFIX}-wxWidgets")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-python2"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "doxygen")
source=("${_realname}"::"git+https://github.com/KiCad/kicad-source-mirror.git"
        001-use-system-openssl.patch
        002-fix-python-module-install.patch
        003-unix-files-layout.patch)
md5sums=('SKIP'
         '679b2e817d70182b631b631d54e1fef9'
         '7e84409ccbafcc1137471655132f945e'
         '17becd7e91e77fde1cb73999c8f8b151')

pkgver() {
  cd "$srcdir/$_realname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${srcdir}/$_realname
  patch -p1 -i ${srcdir}/001-use-system-openssl.patch
  patch -p1 -i ${srcdir}/002-fix-python-module-install.patch
  patch -p1 -i ${srcdir}/003-unix-files-layout.patch
  
  # Use modules from cmake
  rm -f CMakeModules/FindwxWidgets.cmake
  rm -f CMakeModules/FindPythonLibs.cmake
}

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -r build-${MINGW_CHOST}
  mkdir build-${MINGW_CHOST} && cd build-${MINGW_CHOST}
  export OPENSSL_ROOT_DIR=${MINGW_PREFIX}
  export PYTHON_ROOT_DIR=${MINGW_PREFIX}
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${pkgdir}${MINGW_PREFIX} \
    -DKICAD_SKIP_BOOST=ON \
    -DKICAD_SCRIPTING=ON \
    -DKICAD_SCRIPTING_MODULES=ON \
    -DKICAD_SCRIPTING_WXPYTHON=ON \
    -DPYTHON_EXECUTABLE=${MINGW_PREFIX}/bin/python2.exe \
    ../$_realname
    #-DVERBOSE=1
    
  make
}

package() {
  cd build-${MINGW_CHOST}

  make install
}
