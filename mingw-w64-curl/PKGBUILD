# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=curl
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=7.34.0
pkgrel=2
pkgdesc="An URL retrival utility and library. (mingw-w64)"
arch=('any')
url="http://curl.haxx.se"
license=("MIT")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config")
depends=("${_mingw_suff}-winpthreads"
		"${_mingw_suff}-gnutls"
		"${_mingw_suff}-libidn"
		"${_mingw_suff}-libssh2"
		"${_mingw_suff}-openssl"
		"${_mingw_suff}-zlib")
options=('staticlibs' 'strip')
source=("$url/download/${_realname}-$pkgver.tar.bz2"{,.asc}
		0001-connect-Try-all-addresses-in-first-connection-attemp.patch
		0001-FILE-don-t-wait-due-to-CURLOPT_MAX_RECV_SPEED_LARGE.patch)
md5sums=('88491df2bb32e9146e776ae6ac2f8327'
         'SKIP'
         'ee755b0e886207fc1e8154e426c46ca7'
         'ba766acdb7568aac0b23d479ebecd591')

prepare() {
	cd "${_realname}-$pkgver"

	patch -Np1 <"$srcdir"/0001-connect-Try-all-addresses-in-first-connection-attemp.patch
	patch -Np1 <"$srcdir"/0001-FILE-don-t-wait-due-to-CURLOPT_MAX_RECV_SPEED_LARGE.patch
}

build() {
    mkdir -p "${srcdir}/build-${MINGW_CHOST}"
    cd "${srcdir}/build-${MINGW_CHOST}"
    "${srcdir}"/${_realname}-${pkgver}/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
	  --target=${MINGW_CHOST} \
      --without-ca-bundle \
      --without-random
    make
}

package() {
	mkdir -p "${pkgdir}${MINGW_PREFIX}/bin"
    cd "${srcdir}/build-${MINGW_CHOST}"
    make DESTDIR="$pkgdir" install
    rm -r "${pkgdir}${MINGW_PREFIX}/share"
}
