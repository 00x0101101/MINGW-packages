# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=clang
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}-svn"
pkgver=r197209
pkgrel=1
pkgdesc="LC language family frontend for LLVM (mingw-w64)"
arch=('any')
url="http://llvm.org"
license=("custom:University of Illinois/NCSA Open Source License")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config" "lndir")
depends=("${_mingw_suff}-gcc")
provides=("${_mingw_suff}-${_realname}")
conflicts=("${_mingw_suff}-${_realname}")
options=('staticlibs' 'strip')
source=(#"llvm"::"svn+http://llvm.org/svn/llvm-project/llvm/trunk"
		#"clang"::"svn+http://llvm.org/svn/llvm-project/cfe/trunk"
		#"rt"::"svn+http://llvm.org/svn/llvm-project/compiler-rt/trunk"
		#"testsuite"::"svn+http://llvm.org/svn/llvm-project/test-suite/trunk"
		"llvm"::"git+http://llvm.org/git/llvm.git"
		"clang"::"git+http://llvm.org/git/clang.git"
		"rt"::"git+http://llvm.org/git/compiler-rt.git"
		"testsuite"::"git+http://llvm.org/git/test-suite.git"
		llvm-seh-fix.patch)
md5sums=('SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'e9f2e53818f5d27c214c589f82abb01f')

pkgver() {
  cd "$srcdir/clang"
  local ver="$(git rev-parse HEAD)"
  printf "r%s" "$(git svn find-rev ${ver})"
  #printf "r.%s" "${ver//[[:alpha:]]}"
  #printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${srcdir}/llvm
  patch -p0 -i ${srcdir}/llvm-seh-fix.patch
  
  cd ${srcdir}
  mkdir source
  lndir ${srcdir}/llvm ${srcdir}/source
  mkdir source/tools/clang
  lndir ${srcdir}/clang ${srcdir}/source/tools/clang
  mkdir source/projects/{compiler-rt,test-suite}
  lndir ${srcdir}/rt ${srcdir}/source/projects/compiler-rt
  lndir ${srcdir}/testsuite ${srcdir}/source/projects/test-suite
}

build() {
    mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}
	echo 'ac_cv_have_decl_strerror_s=${ac_cv_have_decl_strerror_s=no}' > config.cache

	# Include location of libffi headers in CPPFLAGS
	CPPFLAGS+=" $(pkg-config --cflags libffi)"
	
    ../source/configure \
      --prefix=${MINGW_PREFIX} \
	  --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
	  --with-sysroot=${MINGW_PREFIX} \
	  --enable-targets=x86,x86_64 \
	  --enable-optimized \
	  --disable-assertions \
	  --disable-pthreads \
	  --with-python=${MINGW_PREFIX}/bin/python2 \
	  --enable-libffi \
	  --enable-docs
	#--enable-shared
	#--enable-embed-stdcxx
	#--enable-libcpp
	#--enable-cxx11
	#
	#--enable-libffi
	#--enable-ltdl-install
	#
	#--with-c-include-dir
	#--with-gcc-toolchain
	#--with-default-sysroot
	#--with-binutils-include
	#--with-bug-report-url
    make -j5
}

package() {
    cd "${srcdir}/build-${MINGW_CHOST}"
    make -j1 DESTDIR="$pkgdir" install
}

