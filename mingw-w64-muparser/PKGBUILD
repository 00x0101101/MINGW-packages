# Maintainer: RÃ©mi BERTHO <remi.bertho@openmailbox.org>

_realname=muparser
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.2.4
pkgrel=1
pkgdesc="A fast math parser library"
arch=('i686' 'x86_64')
url="http://muparser.sourceforge.net/"
license=('custom')
options=('!makeflags')
source=("${_realname}-${pkgver}.zip::https://drive.google.com/uc?export=download&id=0BzuB-ydOOoduejdwdTQwcF9JLTA")
sha1sums=('78db09680cafd5db4fd08a5d472afd2fa65f9274')

build() {
  cd $srcdir/${_realname}_v${pkgver//./_}
  ./configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
	--enable-shared=yes \
	--enable-samples=no \
	--enable-debug=no
    
  mingw32-make
}

package() {
  cd $srcdir/${_realname}_v${pkgver//./_}
  mingw32-make DESTDIR=${pkgdir} install
  
  mkdir ${pkgdir}${MINGW_PREFIX}/bin
  mv ${pkgdir}${MINGW_PREFIX}/lib/muparser.dll ${pkgdir}${MINGW_PREFIX}/bin

  # license
  install -D -m644 License.txt \
	$pkgdir${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}

