# Maintainer: Saul Ibarra Corretge <saghul@gmail.com>

_realname=pyqt5

pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}-common" "${MINGW_PACKAGE_PREFIX}-python2-${_realname}" "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=5.3.1
pkgrel=1
pkgdesc="Qt5 bindings for Python (mingw-w64)"
arch=('any')
license=('GPL')
url="http://www.riverbankcomputing.co.uk/software/pyqt"
makedepends=("${MINGW_PACKAGE_PREFIX}-python2-sip"
         "${MINGW_PACKAGE_PREFIX}-python3-sip"
         "${MINGW_PACKAGE_PREFIX}-qt5")
source=("http://downloads.sourceforge.net/project/pyqt/PyQt5/PyQt-$pkgver/PyQt-gpl-$pkgver.tar.gz"
        001-mingw-python.patch)
md5sums=('c24b1a4040292fcfdbf128a9a5a38e66'
         '33cdfbab6b7ad3419d2bdde18e389bb6')

prepare() {
 cd "${srcdir}"/PyQt-gpl-$pkgver

  #Apply patches
  patch -p1 -i "${srcdir}"/001-mingw-python.patch
}

build() {
  for builddir in python{2,3}-${MINGW_CHOST}; do
    [[ -d $builddir ]] && rm -rf $builddir
    cp -r PyQt-gpl-${pkgver} $builddir
    pushd $builddir
    mkdir release
    ${MINGW_PREFIX}/bin/${builddir%-${MINGW_CHOST}} configure.py \
      --confirm-license \
      --qsci-api \
      -q ${MINGW_PREFIX}/bin/qmake.exe \
      --verbose
    make V=1
    popd
  done
}

package_pyqt5-common(){
  pkgdesc="Common PyQt files shared between python3-pyqt5 and python2-pyqt5"
  depends=("${MINGW_PACKAGE_PREFIX}-qt5")

  cd python2-${MINGW_CHOST}
  make -C pyrcc DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install
  make -C pylupdate DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install

  install -Dm644 PyQt5.api "${pkgdir}${MINGW_PREFIX}"/share/qt5/qsci/api/python/PyQt5.api
}

package_python3-pyqt5(){
  pkgdesc="A set of Python 3.x bindings for the Qt5 toolkit"
  depends=("${MINGW_PACKAGE_PREFIX}-python3-sip" "${MINGW_PACKAGE_PREFIX}-pyqt5-common" "${MINGW_PACKAGE_PREFIX}-python3")
  #optdepends=('python-opengl: enable OpenGL 3D graphics in PyQt applications'
  #            'python-dbus: for python-dbus mainloop support')

  cd python3-${MINGW_CHOST}
  # INSTALL_ROOT is needed for the QtDesigner module, the other Makefiles use DESTDIR
  make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install

  # Provided by pyqt-common
  rm "${pkgdir}${MINGW_PREFIX}"/bin/{pylupdate5,pyrcc5}
  rm "${pkgdir}${MINGW_PREFIX}"/share/qt5/qsci/api/python/PyQt5.api
}

package_python2-pyqt5(){
  pkgdesc="A set of Python 2.x bindings for the Qt5 toolkit"
  depends=("${MINGW_PACKAGE_PREFIX}-python2-sip" "${MINGW_PACKAGE_PREFIX}-pyqt5-common" "${MINGW_PACKAGE_PREFIX}-python2")
  #optdepends=('python2-opengl: enable OpenGL 3D graphics in PyQt applications'
  #            'python-dbus: for python-dbus mainloop support')

  cd python2-${MINGW_CHOST}
  # INSTALL_ROOT is needed for the QtDesigner module, the other Makefiles use DESTDIR
  make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install

  # Fix conflicts with python-pyqt5
  mv "${pkgdir}${MINGW_PREFIX}"/bin/{,python2-}pyuic5

  rm "${pkgdir}${MINGW_PREFIX}"/bin/{pylupdate5,pyrcc5}
  rm "${pkgdir}${MINGW_PREFIX}"/share/qt5/plugins/designer/libpyqt5.dll
  rm "${pkgdir}${MINGW_PREFIX}"/share/qt5/plugins/PyQt5/libpyqt5qmlplugin.dll
  rm "${pkgdir}${MINGW_PREFIX}"/share/qt5/qsci/api/python/PyQt5.api
}

# Wrappers
package_mingw-w64-i686-pyqt5-common() {
  package_pyqt5-common
}

package_mingw-w64-i686-python2-pyqt5() {
  package_python2-pyqt5
}

package_mingw-w64-i686-python3-pyqt5() {
  package_python3-pyqt5
}

package_mingw-w64-x86_64-pyqt5-common() {
  package_pyqt5-common
}

package_mingw-w64-x86_64-python2-pyqt5() {
  package_python2-pyqt5
}

package_mingw-w64-x86_64-python3-pyqt5() {
  package_python3-pyqt5
}
