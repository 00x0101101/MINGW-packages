# Maintainer: Saul Ibarra Corretge <saghul@gmail.com>

_realname=python2-pyqt4

pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=4.11.1
pkgrel=2
pkgdesc="Qt4 bindings for Python (mingw-w64)"
arch=('any')
license=('GPL')
url="http://www.riverbankcomputing.co.uk/software/pyqt"
depends=("${MINGW_PACKAGE_PREFIX}-python2"
         "${MINGW_PACKAGE_PREFIX}-python2-sip4"
         "${MINGW_PACKAGE_PREFIX}-qt4")
source=("http://downloads.sourceforge.net/project/pyqt/PyQt4/PyQt-$pkgver/PyQt-win-gpl-$pkgver.zip"
        0001-fix-configure.patch)
md5sums=('b97e2e0bb2c1c9d785b4dd76924be63c'
         'bd1f4d8baecc7016aa21912e03323625')

prepare() {
  cd "${srcdir}"/PyQt-win-gpl-$pkgver

  # Apply patches
  patch -p0 -i "${srcdir}"/0001-fix-configure.patch
}

build() {
  cd "${srcdir}"/PyQt-win-gpl-$pkgver

  ${MINGW_PREFIX}/bin/python2 configure.py \
    --bindir "${pkgdir}${MINGW_PREFIX}"/bin \
    --destdir "${pkgdir}${MINGW_PREFIX}"/lib/python2.7/site-packages \
    --sipdir "${pkgdir}${MINGW_PREFIX}"/sip \
    --confirm-license \
    --no-designer-plugin \
    --no-sip-files \
    --concatenate \
    -p win32-g++

  # Fix Makefiles
  sed -i 's/@if not exist \(.*\) mkdir /mkdir -p /' Makefile */Makefile
  sed -i 's@copy /y@cp -r@' Makefile */Makefile

  # Build
  make
}

package() {
  cd "${srcdir}"/PyQt-win-gpl-$pkgver
  make install
  install -D -m644 LICENSE-MERGED "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
  rm -rf "${pkgdir}${MINGW_PREFIX}"/lib/python2.7/site-packages/PyQt4/uic/port_v3
}
