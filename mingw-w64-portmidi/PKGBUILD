# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=portmidi
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=217
pkgrel=1
pkgdesc="Platform independent library for real-time MIDI input/output (mingw-w64)"
arch=('any')
url="http://portmedia.sourceforge.net/"
license=("custom")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
source=("http://downloads.sourceforge.net/portmedia/portmidi/${pkgver}/portmidi-src-${pkgver}.zip"
        enable-install-on-windows.patch)
md5sums=('03f46fd3947e2ef4c8c465baaf832241'
         'dbff55c9f23f13002dd2ec6452641352')

prepare() {
  cd portmidi
  patch -p1 -i "${srcdir}/enable-install-on-windows.patch"
}

build() {
  # we don't have Java yet, so we fake it

  mkdir -p "build-${MINGW_CHOST}"
  cd "build-${MINGW_CHOST}"
  cmake ../portmidi \
    -G "MSYS Makefiles" \
    -DCMAKE_BUILD_TYPE=Release \
    -DJAVA_INCLUDE_PATH= -DJAVA_INCLUDE_PATH2= -DJAVA_JVM_LIBRARY= \
    -DCMAKE_INSTALL_PREFIX:PATH=${MINGW_PREFIX}

  make -t pmjni
  make
}

package() {
  cd "build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install

  # fix install destination
  mv "${pkgdir}/usr/local" "${pkgdir}${MINGW_PREFIX}"
  rm -r "${pkgdir}/usr"

  # remove unnecessary difference in libname
  mv "${pkgdir}${MINGW_PREFIX}/lib"/{libportmidi_s.a,libportmidi.a}

  # install missing DLL
  install -Dm0755 libportmidi.dll "${pkgdir}${MINGW_PREFIX}/bin/libportmidi.dll"

  # install executables
  for exe in latency.exe midiclock.exe midithread.exe midithru.exe mm.exe qtest.exe sysex.exe test.exe; do
    install -Dm0755 "pm_test/${exe}" "${pkgdir}${MINGW_PREFIX}/bin/pm-${exe}"
  done

  # install license
  install -Dm0755 "${srcdir}/portmidi/license.txt" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/license.txt"
}
