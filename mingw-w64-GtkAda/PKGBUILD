# Maintainer: JÃ¼rgen Pfeifer <juergen@familiepfeifer.de>

_realname=GtkAda
_mingw_suff=mingw-w64-${CARCH}
pkgname=${_mingw_suff}-${_realname}-svn
pkgrel=1
basever=3.8
pkgver=3.8_r227065
pkgdesc="A complete GTK+ based graphical toolkit for Ada"
arch=('any')
provides=("${_mingw_suff}-${_realname}")
url="http://libre.adacore.com/tools/gtkada/"
options=('!staticlibs' 'strip')
license=('GPL3' 'custom')
groups=("${_mingw_suff}")
makedepends=("subversion"
    "${_mingw_suff}-gcc-ada"
    "${_mingw_suff}-gtk3")
depends=("${_mingw_suff}-gcc-ada" 
    "${_mingw_suff}-gtk3")
source=("$_realname::svn+http://svn.eu.adacore.com/anonsvn/Dev/trunk/$_realname/"
	"${_realname}-mingw.patch")
md5sums=('SKIP' 
	 'efb086e34d464c0aab8bf4ec1214a2c80ee1ab8a')

pkgver() {
  cd "$srcdir/$_realname"
  local ver="$(svnversion)"
  printf "%s_r%s" "$basever" "${ver//[[:alpha:]]}"
}

prepare()
{
  cd ${srcdir}/$_realname
  patch -p1 -i ${srcdir}/${_realname}-mingw.patch
  autoreconf -fvi
}

build() {
  cd ${srcdir}/$_realname
  ./configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --enable-shared \
    --disable-static \
    --with-GL=no
    
    make -C src prep
    make
}

package() {
  cd ${srcdir}/$_realname
  make DESTDIR="$pkgdir" install
  rm -rf ${pkgdir}${MINGW_PREFIX}/share/examples
  rm -rf ${pkgdir}${MINGW_PREFIX}/share/doc

  # Copy License Files
  mkdir -p ${pkgdir}${MINGW_PREFIX}/share/licenses/$_realname  
  cp -pf ${srcdir}/${_realname}/COPYING* \
    ${pkgdir}${MINGW_PREFIX}/share/licenses/$_realname
}
