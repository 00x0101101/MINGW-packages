# Maintainer: Lubosz Sarnecki <lubosz@gmail.com>

_realname=gst-python
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}-git")
pkgver=1.4.0
pkgrel=1
pkgdesc="GStreamer GObject Introspection overrides for Python 3 (mingw-w64)"
arch=('any')
license=('LGPL')
depends=("${MINGW_PACKAGE_PREFIX}-gstreamer"
         "${MINGW_PACKAGE_PREFIX}-python3-gobject"
         "${MINGW_PACKAGE_PREFIX}-gobject-introspection")
makedepends=('git')
url='http://gstreamer.freedesktop.org/modules/gst-python.html'
options=(!libtool strip staticlibs)

source=(http://gstreamer.freedesktop.org/src/gst-python/${_realname}-${pkgver}.tar.xz
        '0001-msys2-python3-config-returns-1-so-use-which.patch'
        '0002-msys2-fix-linking-errors.patch'
        '0004-add-overrides.patch'
        '0005-unix-override-dir.patch'
        '0006-plugin-libtool.patch')
md5sums=('72e3ba811db671366162bbf75a01ed37'
         'eb52a3ef8270c2d263c79799bcaf377a'
         '14ee538deb2796a53ff4177fd7caeadd'
         '9241cc5807a4ef87548b6d5405141bf1'
         'cf4576d2775e01c2147801d5614fa1b2'
         'f1dc65f40ae023dd499a5dbe3b754418')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0001-msys2-python3-config-returns-1-so-use-which.patch
  patch -p1 -i ${srcdir}/0002-msys2-fix-linking-errors.patch
  patch -p1 -i ${srcdir}/0004-add-overrides.patch
  patch -p1 -i ${srcdir}/0005-unix-override-dir.patch
  patch -p1 -i ${srcdir}/0006-plugin-libtool.patch

  WANT_AUTOMAKE=latest autoreconf -fiv
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p build-${MINGW_CHOST}
  cd "${srcdir}/build-${MINGW_CHOST}"

  PYTHON=${MINGW_PREFIX}/bin/python3 \
  ../${_realname}-${pkgver}/configure \
      --prefix=${MINGW_PREFIX} \
      --bindir=${MINGW_PREFIX}/bin \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --target=${MINGW_CHOST} \
      --disable-silent-rules

  make
}

package() {
  cd build-${MINGW_CHOST}
  make DESTDIR="${pkgdir}" install
}
