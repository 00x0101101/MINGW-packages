# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=openjpeg
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=1.5.1
pkgrel=2
pkgdesc="An open source JPEG 2000 codec (mingw-w64)"
arch=('any')
url="http://www.openjpeg.org"
license=("BSD")
depends=("${_mingw_suff}-crt" "${_mingw_suff}-lcms2" "${_mingw_suff}-libtiff" "${_mingw_suff}-libpng" "${_mingw_suff}-zlib")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config")
options=('staticlibs' 'strip')
source=("http://openjpeg.googlecode.com/files/openjpeg-${pkgver}.tar.gz"
        "cdecl.patch"
        "openjpeg-1.5.1_tiff-pkgconfig.patch"
        "openjpeg-1.5-r2029.patch"
        "openjpeg-1.5-r2031.patch"
        "openjpeg-1.5-r2032.patch"
		'rhbz1036491_CVE-2013-6052.patch'
		'rhbz1036493_CVE-2013-6053.patch'
		'rhbz1036495_CVE-2013-6045.patch'
		'rhbz1037945_CVE-2013-1447.patch'
		'rhbz1037948_CVE-2013-6887.patch')
        
md5sums=('b5f74cec2688fb918331bb014061be6f'
         '673edf457f9b4f3fdc9a083e1857a76a'
         '7d4244a290c6f8d6777a7bd68706a7ef'
         '3f7235e2e18f3a9953778d6e9d25cb81'
         '4154f1f9fcc3c8a706d3741cda24295f'
         'e2dabc34959eacc5f8122aa521ece4e1'
         '22b49c6689de2f4a5719681ff9854275'
         '09f29dda2175f360dfc6e6e594a0d195'
         '6224029ab05d5dc701878f5ad04f9ba9'
         'f091bff93751c58f9aee306a848fb961'
         '5210916d5278a7391bc0e1d2f109f396')

prepare() {
  cd "$srcdir/openjpeg-$pkgver"
  # Fix CRLF line endings
  dos2unix libopenjpeg/opj_malloc.h
  #rm -r thirdparty
  patch -Np1 -i "$srcdir"/cdecl.patch
  patch -Np1 -i "$srcdir"/openjpeg-1.5.1_tiff-pkgconfig.patch
  patch -Np1 -i "$srcdir"/rhbz1036491_CVE-2013-6052.patch
  patch -Np1 -i "$srcdir"/rhbz1036493_CVE-2013-6053.patch
  patch -Np1 -i "$srcdir"/rhbz1036495_CVE-2013-6045.patch
  patch -Np1 -i "$srcdir"/rhbz1037945_CVE-2013-1447.patch
  patch -Np1 -i "$srcdir"/rhbz1037948_CVE-2013-6887.patch
  patch -Np0 -i "$srcdir"/openjpeg-1.5-r2029.patch
  patch -Np0 -i "$srcdir"/openjpeg-1.5-r2031.patch
  patch -Np0 -i "$srcdir"/openjpeg-1.5-r2032.patch
  
  autoreconf -i
}

build() {
    mkdir -p $srcdir/build-${MINGW_CHOST} && cd $srcdir/build-${MINGW_CHOST}
    ../${_realname}-${pkgver}/configure \
      --prefix=${MINGW_PREFIX} \
	  --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --disable-doc \
      --enable-jpwl
    make
}

package() {
    cd "$srcdir/build-${MINGW_CHOST}"
    make DESTDIR="$pkgdir" install
    rm -r "${pkgdir}${MINGW_PREFIX}"/share
    #rm "$pkgdir"/usr/${_arch}/bin/*.exe
}
