# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=openjpeg
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.5.2
pkgrel=5
pkgdesc="An open source JPEG 2000 codec (mingw-w64)"
arch=('any')
url="http://www.openjpeg.org"
license=("BSD")
depends=("${MINGW_PACKAGE_PREFIX}-lcms2"
         "${MINGW_PACKAGE_PREFIX}-libtiff"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("dos2unix"
            "${MINGW_PACKAGE_PREFIX}-cmake"
            "${MINGW_PACKAGE_PREFIX}-gcc"
            "${MINGW_PACKAGE_PREFIX}-pkg-config")
options=('staticlibs' 'strip')
source=(${_realname}-${pkgver}.tar.gz::https://github.com/uclouvain/openjpeg/archive/version.${pkgver}.tar.gz
        "openjpeg-1.5.1_tiff-pkgconfig.patch"
        "mingw-install-pkgconfig-files.patch"
        "versioned-dlls-mingw.patch"
        "openjpeg-1.5.2-stdcall-for-all-win.patch")

md5sums=('545f98923430369a6b046ef3632ef95c'
         '7d4244a290c6f8d6777a7bd68706a7ef'
         '5f1a6d346dad00f919723dbc00546ae1'
         'e68263246b76758903bfcc2c9de3601f'
         '5dacc2bb99e7547160e968b4ccad009d')

prepare() {
  cd "${srcdir}/openjpeg-version.${pkgver}"
  # Fix CRLF line endings
  dos2unix libopenjpeg/opj_malloc.h
  #rm -r thirdparty
  patch -Np1 -i "${srcdir}"/openjpeg-1.5.1_tiff-pkgconfig.patch
  patch -Np1 -i "${srcdir}"/mingw-install-pkgconfig-files.patch
  patch -Np1 -i "${srcdir}"/versioned-dlls-mingw.patch
  patch -Np1 -i "${srcdir}"/openjpeg-1.5.2-stdcall-for-all-win.patch

  #autoreconf -i
}

build() {
  msg "Build shared version..."
  [[ -d ${srcdir}/build-shared-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-shared-${MINGW_CHOST}
  mkdir ${srcdir}/build-shared-${MINGW_CHOST}
  cd ${srcdir}/build-shared-${MINGW_CHOST}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=ON \
    -DCMAKE_SYSTEM_PREFIX_PATH=${MINGW_PREFIX} \
    -DOPENJPEG_INSTALL_SUBDIR="openjpeg/${ver}" \
    -DBUILD_MJ2=ON \
    -DBUILD_JPWL=ON \
    -DBUILD_JPIP=ON \
    -DBUILD_SHARED_LIBS=ON \
    ../${_realname}-version.${pkgver}
  make

  msg "Build static version..."
  [[ -d $srcdir/build-static-${MINGW_CHOST} ]] && rm -rf $srcdir/build-static-${MINGW_CHOST}
  mkdir $srcdir/build-static-${MINGW_CHOST}
  cd $srcdir/build-static-${MINGW_CHOST}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_SYSTEM_PREFIX_PATH=${MINGW_PREFIX} \
    -DOPENJPEG_INSTALL_SUBDIR="openjpeg/${ver}" \
    -DBUILD_MJ2=ON \
    -DBUILD_JPWL=ON \
    -DBUILD_JPIP=OFF \
    -DBUILD_SHARED_LIBS=OFF \
    ../${_realname}-version.${pkgver}
  make
}

package() {
  cd "${srcdir}/build-static-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install

  cd "${srcdir}/build-shared-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install

  # Fix conflict with openjpeg2
  mv ${pkgdir}${MINGW_PREFIX}/bin/opj_dec_server{,1}.exe
}
