# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=installer-framework
pkgname="${MINGW_PACKAGE_PREFIX}-qt-${_realname}-git"
pkgdesc="The Qt Installer Framework used for the Qt SDK installer"
pkgver=r2826.6277c86
pkgrel=1
arch=('any')
url='https://www.qt.io/developers/'
license=('GPL3' 'LGPL' 'FDL' 'custom')
provides=("${MINGW_PACKAGE_PREFIX}-qt-${_realname}")
makedepends=("${MINGW_PACKAGE_PREFIX}-qt5-static" "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-make" "${MINGW_PACKAGE_PREFIX}-libwebp"
             "${MINGW_PACKAGE_PREFIX}-jasper" "git")
options=('strip' 'staticlibs')
#options=('!strip' 'debug' 'staticlibs')
source=("${_realname}"::"git+https://github.com/qtproject/${_realname}.git"
        "0002-add-installation-stuff-to-pro-files.patch"
        "0003-add-recursive-option-to-rmdir-operation.patch"
        "0004-add-dirExists-fuction.patch"
        "0005-Fix-RunProgram-variable-substitution.patch"
        "0006-Don-t-remove-files-that-live-outside-of-path.patch"
        "0007-Don-t-prevent-qmake-linking-qmltooling-plugin.patch"
        "0008-Use-QTPLUGIN-qminimal-on-Windows-too.patch"
        "0009-Add-explicit-platform-commandline-option.patch")
md5sums=('SKIP'
         '304efa470525878962e5984ecaa058dd'
         'b93ec46aba54098cb4784e80df746bb3'
         'ab6316be304c163f32cd5573e9518091'
         '08fe7f42c351efef3b058bc96c157d0e'
         '39a712cb0269666f85eacfbc82492069'
         'aade45ecda5856b21e6f5a2a451be27a'
         'a32a417d89c690df14128f768c0fd972'
         'bd7fd58d89cb0b07c1f5346cdbef57b5')

pkgver() {
  cd "$srcdir"/${_realname}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}"/${_realname}
  git am "${srcdir}"/0002-add-installation-stuff-to-pro-files.patch
  git am "${srcdir}"/0003-add-recursive-option-to-rmdir-operation.patch
  git am "${srcdir}"/0004-add-dirExists-fuction.patch
  # Apply patch from https://bugreports.qt-project.org/browse/QTIFW-397
  git am "${srcdir}"/0005-Fix-RunProgram-variable-substitution.patch
  # This requires ../mingw-w64-qt5-static/0044-qt-5.4.1-Revert-Revert-fix-NTFS-mount-points.patch
  # to fix the problem for junction points. Together they fix:
  # https://github.com/msys2/msys2.github.io/issues/4
  git am "${srcdir}"/0006-Don-t-remove-files-that-live-outside-of-path.patch
  git am "${srcdir}"/0007-Don-t-prevent-qmake-linking-qmltooling-plugin.patch
  git am "${srcdir}"/0008-Use-QTPLUGIN-qminimal-on-Windows-too.patch
  git am "${srcdir}"/0009-Add-explicit-platform-commandline-option.patch
}

build() {
  local _config
  cd "${srcdir}"
  [[ -d build-${CARCH} ]] && rm -rf build-${CARCH}
  mkdir build-${CARCH}
  if check_option "debug" "n"; then
    _config="release"
  else
    _config="debug"
  fi
  cd build-${CARCH}
  ${MINGW_PREFIX}/qt5-static/bin/qmake.exe ../${_realname}/installerfw.pro CONFIG+="${_config}" CONFIG+=no_testcase_installs
  make
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make INSTALL_ROOT="${pkgdir}${MINGW_PREFIX}" install
}
