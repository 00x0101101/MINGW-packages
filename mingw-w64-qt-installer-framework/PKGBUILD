# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=installer-framework
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-qt-${_realname}-git"
pkgdesc="The Qt Installer Framework used for the Qt SDK installer"
pkgver=r2098.be32157
pkgrel=2
arch=('any')
url='http://qt-project.org/'
license=('GPL3' 'LGPL' 'FDL' 'custom')
provides=("${_mingw_suff}-qt-${_realname}")
makedepends=("${_mingw_suff}-qt5-static" "${_mingw_suff}-gcc"
      "${_mingw_suff}-make")
#options=('strip' 'staticlibs')
options=('!strip' 'debug' 'staticlibs')
source=("${_realname}"::"git://gitorious.org/${_realname}/${_realname}.git"
        "0001-add-installation-stuff-to-pro-files.patch"
        "0002-add-recursive-option-to-rmdir-operation.patch")
md5sums=('SKIP'
         'e4b94f1a8ad6991852fd93ea304102db'
         'af86471d4b2d81c5b5bdb63a351f54cb')

pkgver() {
  cd "$srcdir/${_realname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${srcdir}/${_realname}
  git am ${srcdir}/0001-add-installation-stuff-to-pro-files.patch
  git am ${srcdir}/0002-add-recursive-option-to-rmdir-operation.patch
}

build() {
  local _config
  cd ${srcdir}/${_realname}
  if check_option "debug" "n"; then
    _config="release"
  else
    _config="debug"
  fi
  ${MINGW_PREFIX}/qt5-static/bin/qmake.exe installerfw.pro CONFIG+="${_config}"
  make
}

package() {
  cd ${srcdir}/${_realname}
  make INSTALL_ROOT="${pkgdir}${MINGW_PREFIX}" install
}
