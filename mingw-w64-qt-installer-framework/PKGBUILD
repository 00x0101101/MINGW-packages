# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=installer-framework

pkgname="${MINGW_PACKAGE_PREFIX}-qt-${_realname}-git"
pkgdesc="The Qt Installer Framework used for the Qt SDK installer"
pkgver=r2146.ab724c0
pkgrel=1
arch=('any')
url='http://qt-project.org/'
license=('GPL3' 'LGPL' 'FDL' 'custom')
provides=("${MINGW_PACKAGE_PREFIX}-qt-${_realname}")
makedepends=("${MINGW_PACKAGE_PREFIX}-qt5-static" "${MINGW_PACKAGE_PREFIX}-gcc"
      "${MINGW_PACKAGE_PREFIX}-make")
options=('strip' 'staticlibs')
#options=('!strip' 'debug' 'staticlibs')
source=("${_realname}"::"git://gitorious.org/${_realname}/${_realname}.git"
        "0001-add-installation-stuff-to-pro-files.patch"
        "0002-add-recursive-option-to-rmdir-operation.patch"
        "0003-add-dirExists-fuction.patch")
md5sums=('SKIP'
         'eb26b6d9a49f888e04334549bf9c823d'
         'a3399b0f925a2165643f7f702b3ef2bc'
         'dfd2183cc5d668f514087ee9991b64d7')

pkgver() {
  cd "$srcdir/${_realname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${srcdir}/${_realname}
  git am ${srcdir}/0001-add-installation-stuff-to-pro-files.patch
  git am ${srcdir}/0002-add-recursive-option-to-rmdir-operation.patch
  git am ${srcdir}/0003-add-dirExists-fuction.patch
}

build() {
  local _config
  cd ${srcdir}/${_realname}
  if check_option "debug" "n"; then
    _config="release"
  else
    _config="debug"
  fi
  ${MINGW_PREFIX}/qt5-static/bin/qmake.exe installerfw.pro CONFIG+="${_config}"
  make
}

package() {
  cd ${srcdir}/${_realname}
  make INSTALL_ROOT="${pkgdir}${MINGW_PREFIX}" install
}
