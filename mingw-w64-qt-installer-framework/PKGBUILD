# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=installer-framework

pkgname="${MINGW_PACKAGE_PREFIX}-qt-${_realname}-git"
pkgdesc="The Qt Installer Framework used for the Qt SDK installer"
pkgver=r2149.12b0bad
pkgrel=1
arch=('any')
url='http://qt-project.org/'
license=('GPL3' 'LGPL' 'FDL' 'custom')
provides=("${MINGW_PACKAGE_PREFIX}-qt-${_realname}")
makedepends=("${MINGW_PACKAGE_PREFIX}-qt5-static" "${MINGW_PACKAGE_PREFIX}-gcc"
      "${MINGW_PACKAGE_PREFIX}-make")
options=('strip' 'staticlibs')
#options=('!strip' 'debug' 'staticlibs')
source=("${_realname}"::"git://gitorious.org/${_realname}/${_realname}.git"
        "0001-add-installation-stuff-to-pro-files.patch"
        "0002-add-recursive-option-to-rmdir-operation.patch"
        "0003-add-dirExists-fuction.patch"
        "0004-Fix-RunProgram-variable-substitution.patch")
md5sums=('SKIP'
         '65800cef9ca6e2d185666dbe1176edc5'
         '0b03111f4351103d349f4feadb34e74c'
         '8cd8fd15e2a8f451dd054b2f5df057f3'
         'eb8478725a2dd6dfa324a092c76bde69')

pkgver() {
  cd "$srcdir"/${_realname}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}"/${_realname}
  git am "${srcdir}"/0001-add-installation-stuff-to-pro-files.patch
  git am "${srcdir}"/0002-add-recursive-option-to-rmdir-operation.patch
  git am "${srcdir}"/0003-add-dirExists-fuction.patch
  # Apply patch from https://bugreports.qt-project.org/browse/QTIFW-397
  git am "${srcdir}"/0004-Fix-RunProgram-variable-substitution.patch
}

build() {
  local _config
  cd "${srcdir}"/${_realname}
  if check_option "debug" "n"; then
    _config="release"
  else
    _config="debug"
  fi
  ${MINGW_PREFIX}/qt5-static/bin/qmake.exe installerfw.pro CONFIG+="${_config}"
  make
}

package() {
  cd "${srcdir}"/${_realname}
  make INSTALL_ROOT="${pkgdir}${MINGW_PREFIX}" install
}
