# Maintainer: Ignacio Casal Quinteiro <icq@gnome.org>

_realname=libgit2-glib
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.0.20
pkgrel=1
arch=('any')
pkgdesc="A glib wrapper library around the libgit2 git access library (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-libgit2"
         "${MINGW_PACKAGE_PREFIX}-glib2")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-vala"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection")
options=('strip' 'staticlibs')
license=("LGPL")
url="https://wiki.gnome.org/Projects/Libgit2-glib"
source=("http://ftp.gnome.org/pub/gnome/sources/${_realname}/${pkgver%.*}/${_realname}-$pkgver.tar.xz"
        0001-Do-not-build-clone-example.patch)
sha256sums=('f8a10c8d3fcad14eed080dff6b8db0c72866c11a05b9731af31cb7258bcc8d95'
            '1427cf43838508a6e9edbf0dd2b6b0a7b210a8d81f9f1f950af054d9bfe0ed0a')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -Np1 -i "${srcdir}/0001-Do-not-build-clone-example.patch"
  autoreconf -f -i
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  export PYTHON=${MINGW_PREFIX}/bin/python3
  ../${_realname}-${pkgver}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib \
    --enable-python
  make V=1 -j1
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make DESTDIR=$pkgdir install
}
