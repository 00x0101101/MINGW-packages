# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=xvidcore
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=1.3.2
pkgrel=1
pkgdesc="XviD is an open source MPEG-4 video codec (mingw-w64)"
arch=('any')
license=('GPL')
url="http://www.xvid.org/"
depends=("${_mingw_suff}-crt")
options=('strip' '!libtool' 'staticlibs')
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config" "${_mingw_suff}-yasm")
source=(http://downloads.xvid.org/downloads/${_realname}-${pkgver}.tar.bz2
		mingw.patch)
md5sums=('34389e980d4f849688f8b1e168f4e2f9'
         '0f9ee289a5b90d85b9778d601113c4cc')

prepare() {
	cd ${srcdir}/${_realname}
	patch -p1 -i ${srcdir}/mingw.patch
}

build() {
	cp -rf ${srcdir}/${_realname} ${srcdir}/build-${MINGW_CHOST}
	cd ${srcdir}/build-${MINGW_CHOST}/build/generic
    ./configure \
	  --prefix=${MINGW_PREFIX} \
	  --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST}

	make
}

package() {
    cd ${srcdir}/build-${MINGW_CHOST}/build/generic
    make DESTDIR="${pkgdir}" install
	
	#Fix dynamic libraries
	mkdir -p "${pkgdir}${MINGW_PREFIX}/bin"
	mv "${pkgdir}${MINGW_PREFIX}/"lib/*.dll "${pkgdir}${MINGW_PREFIX}/bin/"
	#cd "${pkgdir}${MINGW_PREFIX}/bin"
	#_mylib=$(basename xvidcore.dll)
	#ln -sf ${_mylib} libxvidcore.dll

    cp "${srcdir}/build-${MINGW_CHOST}"/build/generic/=build/*.dll.a "${pkgdir}${MINGW_PREFIX}/lib/"
}
