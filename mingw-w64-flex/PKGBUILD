# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=flex
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.5.39
pkgrel=1
pkgdesc="A tool for generating text-scanning programs"
arch=('any')
url="http://flex.sourceforge.net"
license=('custom')
depends=("${MINGW_PACKAGE_PREFIX}-m4" "${MINGW_PACKAGE_PREFIX}-libiconv" "${MINGW_PACKAGE_PREFIX}-gettext")
options=('staticlibs' 'debug' '!strip')
source=(http://downloads.sourceforge.net/sourceforge/flex/${_realname}-$pkgver.tar.bz2
        0001-2.5.37-vpath.patch
        0002-dont-wait-on-child-since-there-is-no-fork.patch
        0003-define-htonl-htons-for_WIN32.patch
        0004-check-for-regex-library-for-MinGW-w64.patch
        0005-filter-c-hack-out-fork-and-pipe.patch)
md5sums=('77d44c6bb8c0705e0017ab9a84a1502b'
         '68f74b4a02893b4382e4fd42e1f0176a'
         '1335914681e4cbdf81300f0f4fadad54'
         '13cf10d90a617f508d315ac47da895ed'
         '14d935f8b06a80d06702f30ab4411cf4'
         'b7df5389abcf6d9dff24b4355a0073e6')

prepare() {
  cd $srcdir/${_realname}-$pkgver

  #patch -p2 -i "${srcdir}"/0001-2.5.37-vpath.patch
  patch -p1 -i "${srcdir}"/0002-dont-wait-on-child-since-there-is-no-fork.patch
  patch -p1 -i "${srcdir}"/0003-define-htonl-htons-for_WIN32.patch
  patch -p1 -i "${srcdir}"/0004-check-for-regex-library-for-MinGW-w64.patch
  patch -p1 -i "${srcdir}"/0005-filter-c-hack-out-fork-and-pipe.patch

  ./autogen.sh
}

build() {
  cd $srcdir/${_realname}-$pkgver
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --without-libiconv-prefix \
    --without-libintl-prefix \
    --enable-static \
    --disable-shared
  make
}

check() {
  cd $srcdir/${_realname}-$pkgver

  # these tests used features removed in bison-2.6
  sed -i -e '/test-bison-yylloc/d' -e '/test-bison-yylval/d' tests/Makefile.in

  make check
}

package() {
  cd $srcdir/${_realname}-$pkgver

  make DESTDIR=$pkgdir install
  cp -f ${pkgdir}${MINGW_PREFIX}/bin/flex.exe ${pkgdir}${MINGW_PREFIX}/bin/flex++.exe
  cp -f ${pkgdir}${MINGW_PREFIX}/bin/flex.exe ${pkgdir}${MINGW_PREFIX}/bin/lex.exe

  install -Dm644 COPYING \
    ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/license.txt
}

# Previous flex ports:
# http://optimate.dl.sourceforge.net/project/flex/flex/2.5.4.a/flex-2.5.4a.tar.bz2
# http://dfn.dl.sourceforge.net/project/gnuwin32/flex/2.5.4a-1/flex-2.5.4a-1-src.zip
