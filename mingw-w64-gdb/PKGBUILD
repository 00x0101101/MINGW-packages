# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gdb
_mingw_suff=mingw-w64-${CARCH}
_gcc_ver=4.8.2
pkgname=("${_mingw_suff}-${_realname}")
pkgver=7.6.2
pkgrel=3
pkgdesc="GNU Debugger"
arch=('any')
url="http://www.gnu.org/software/gdb/"
license=('GPL')
groups=("${_mingw_suff}-toolchain")
depends=("${_mingw_suff}-libiconv" "${_mingw_suff}-zlib" "${_mingw_suff}-expat" "${_mingw_suff}-libiconv" "${_mingw_suff}-python2")
checkdepends=('dejagnu' 'bc')
makedepends=("${_mingw_suff}-libiconv" "${_mingw_suff}-zlib" "${_mingw_suff}-ncurses" "${_mingw_suff}-xz")
options=('staticlibs' '!distcc' '!ccache')
source=("ftp://mirrors.kernel.org/sources.redhat.com/gdb/releases/gdb-${pkgver}.tar.bz2"
		'gdbinit'
		gdb-fix-display-tabs-on-mingw.patch
		gdb-mingw-gcc-4.7.patch
		gdb-perfomance.patch
		gdb-python-fix-crash.patch)
md5sums=('496399e96654fc0f899a5c964bc1f0f8'
         'dec2439447cb1accf037457b459093f7'
         'e48292d553379a9765c82e80e618ffd2'
         '7cc5df72d958ef3e4b1ffa9f7b069e6f'
         'e9b7c252fedfa950998f5f3b32536461'
         '012dc4a13cc4171c3253a1bc94e32952')

prepare() {
  cd ${srcdir}/gdb-${pkgver}
  
  # https://sourceware.org/ml/gdb-patches/2013-11/msg00224.html
  patch -p1 -i ${srcdir}/gdb-fix-display-tabs-on-mingw.patch
  # https://sourceware.org/bugzilla/show_bug.cgi?id=15559
  patch -p1 -i ${srcdir}/gdb-mingw-gcc-4.7.patch
  # http://sourceware.org/bugzilla/show_bug.cgi?id=15412
  patch -p1 -i ${srcdir}/gdb-perfomance.patch
  # https://sourceware.org/bugzilla/show_bug.cgi?id=12127
  patch -p1 -i ${srcdir}/gdb-python-fix-crash.patch

  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

  mkdir ${srcdir}/gdb-build-${CARCH}
}

build() {
  cd ${srcdir}/gdb-build-${CARCH}

  CFLAGS+=" -D__USE_MINGW_ANSI_STDIO=1"
  CXXFLAGS+=" -D__USE_MINGW_ANSI_STDIO=1"
  export LDFLAGS=""
	
  ${srcdir}/gdb-${pkgver}/configure \
    --build=${MINGW_CHOST} \
	--host=${MINGW_CHOST} \
	--target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --enable-targets="i686-w64-mingw32,x86_64-w64-mingw32" \
	--enable-64-bit-bfd \
	--disable-nls \
	--disable-werror \
	--disable-win32-registry \
	--disable-rpath \
	--with-system-gdbinit=${MINGW_PREFIX}/etc/gdbinit \
	--with-python=${MINGW_PREFIX}/bin/python-config.sh \
	--with-tcl=${MINGW_PREFIX}/lib \
	--with-tk=${MINGW_PREFIX}/lib \
	--with-expat \
	--with-libiconv-prefix=${MINGW_PREFIX} \
	--with-zlib \
	--disable-tui

  make
}

package() {
  cd ${srcdir}/gdb-build-${CARCH}
  make DESTDIR=${pkgdir} install
  
  # Remove unwanted files
  rm -rf ${pkgdir}${MINGW_PREFIX}/share/{man,info}
  
  # install "custom" system gdbinit
  install -D -m644 ${srcdir}/gdbinit ${pkgdir}${MINGW_PREFIX}/etc/gdbinit
  sed -i 's|%GCC_NAME%|gcc-'${_gcc_ver}'|g' ${pkgdir}${MINGW_PREFIX}/etc/gdbinit
  
  mkdir -p ${pkgdir}${MINGW_PREFIX}/include/gdb
  mv ${pkgdir}${MINGW_PREFIX}/include/*.h ${pkgdir}${MINGW_PREFIX}/include/gdb/
  mkdir -p ${pkgdir}${MINGW_PREFIX}/lib/gdb
  mv ${pkgdir}${MINGW_PREFIX}/lib/*.a ${pkgdir}${MINGW_PREFIX}/lib/gdb/
}
