# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gettext
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=0.18.3.1
pkgrel=1
arch=('any')
groups=("${_mingw_suff}")
pkgdesc="GNU internationalization library (mingw-w64)"
depends=("${_mingw_suff}-crt" "${_mingw_suff}-libiconv") # "${_mingw_suff}-termcap"
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-ncurses")
options=('strip' 'staticlibs')
license=('GPL' 'LGPL')
url="http://www.gnu.org/software/gettext/"
source=("http://ftp.gnu.org/pub/gnu/gettext/gettext-$pkgver.tar.gz"{,.sig})
md5sums=('3fc808f7d25487fc72b5759df7419e02'
         'SKIP')

build()
{
  cd "$srcdir/gettext-$pkgver"

  CFLAGS+=" -fexceptions --param=ssp-buffer-size=4"
  CXXFLAGS+=" -fexceptions --param=ssp-buffer-size=4"
  
  mkdir -p $srcdir/build-${CARCH} && cd $srcdir/build-${CARCH}
  # export lt_cv_deplibs_check_method='pass_all'
  ../gettext-$pkgver/configure \
      --prefix=${MINGW_PREFIX} \
      --host=${MINGW_CHOST} \
      --target=${MINGW_CHOST} \
      --build=${MINGW_CHOST} \
      --disable-java \
      --disable-native-java \
      --disable-csharp \
      --enable-static \
      --enable-threads=win32 \
      --without-emacs \
      --disable-openmp \
	  --without-cvs \
	  --without-git
  make
}

package() {
    cd $srcdir/build-${CARCH}
    make DESTDIR="$pkgdir" install

    #${_arch}-strip "$pkgdir/usr/${_arch}/bin/"*.exe
    #${_arch}-strip -x -g "$pkgdir/usr/${_arch}/bin/"*.dll
    #${_arch}-strip -g "$pkgdir/usr/${_arch}/lib/"*.a

    # Drop some useless tools & files
    rm -rf "${pkgdir}${MINGW_PREFIX}/lib/gettext"
    rm -rf "${pkgdir}${MINGW_PREFIX}/share/man"
	rm -rf "${pkgdir}${MINGW_PREFIX}/share/info"
}
