# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=giflib
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=5.0.5
pkgrel=1
pkgdesc="A library for reading and writing gif images (mingw-w64)"
arch=('any')
url="http://sourceforge.net/projects/giflib"
license=("MIT")
makedepends=("${_mingw_suff}-gcc" "xmlto")
depends=("${_mingw_suff}-crt")
options=('staticlibs' 'strip')
source=("http://downloads.sourceforge.net/sourceforge/giflib/${_realname}-${pkgver}.tar.bz2"
"giflib-5.0.4-include-stddef.patch")
md5sums=('c3262ba0a3dad31ba876fb5ba1d71a02'
         'e39b98822334f2bd2009469e5dea65c5')

prepare() {
  cd "$srcdir/giflib-$pkgver"
  patch -Np1 -i "$srcdir/giflib-5.0.4-include-stddef.patch"
}

build() {
    mkdir -p "${srcdir}/${_realname}-${pkgver}-build-${CARCH}"
    cd "${srcdir}/${_realname}-${pkgver}-build-${CARCH}"

    "${srcdir}"/${_realname}-${pkgver}/configure \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --target=${MINGW_CHOST} \
      --prefix=${MINGW_PREFIX}
    make bin_PROGRAMS=''
}

package() {
    cd "${srcdir}/${_realname}-${pkgver}-build-${CARCH}"
    make DESTDIR="$pkgdir" bin_PROGRAMS='' install
    rm -r "${pkgdir}${MINGW_PREFIX}/share"
}