# Maintainer: Martell Malone <martell malone at g mail dot com>

_realname=chipmunk

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=6.2.1
pkgrel=1
pkgdesc="A high-performance 2D rigid body physics library (mingw-w64)"
arch=('any')
url="http://chipmunk-physics.net/"
license=('MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-cmake")
source=(http://files.slembcke.net/chipmunk/release/Chipmunk-6.x/Chipmunk-${pkgver}.tgz
        fix-install-locations.patch)
md5sums=('1cc6ff6a1f1cfcc6e167841fb24bf3c6'
         'ae50c9ed773b2858435ca7c6dbb8d524')
options=(staticlibs !buildflags !strip)

prepare() {
  cd ${srcdir}/Chipmunk-$pkgver
  patch -p1 -i ${srcdir}/fix-install-locations.patch
}

build() {

  mkdir -p "${srcdir}/build-${CARCH}"
  cd "${srcdir}/build-${CARCH}"
  #unset LDFLAGS CFLAGS CXXFLAGS
  
  ${MINGW_PREFIX}/bin/cmake -G"MSYS Makefiles" \
  -DCMAKE_BUILD_TYPE=Release \
  -DCMAKE_INSTALL_PREFIX="${pkgdir}${MINGW_PREFIX}" \
  -DBUILD_DEMOS=OFF \
  -DBUILD_SHARED=ON \
  -DINSTALL_STATIC=ON \
    ../Chipmunk-$pkgver
  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make install
}
