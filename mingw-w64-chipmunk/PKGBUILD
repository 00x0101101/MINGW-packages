# Maintainer: Martell Malone <martell malone at g mail dot com>

_realname=chipmunk
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=7.0.1
pkgrel=1
pkgdesc="A high-performance 2D rigid body physics library (mingw-w64)"
arch=('any')
url="http://chipmunk-physics.net/"
license=('MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-cmake")
source=(http://files.slembcke.net/chipmunk/release/Chipmunk-7.x/Chipmunk-${pkgver}.tgz
        001-build-fix.patch)
md5sums=('3c7651a4231b5c31c8679473f297ae03'
         '457ee3f8fb9a4779d8f7eaaab7eb2697')
options=(staticlibs !buildflags !strip)

prepare() {
  cd ${srcdir}/Chipmunk-${pkgver}
  patch -p1 -i ${srcdir}/001-build-fix.patch
}

build() {

  mkdir -p "${srcdir}/build-${CARCH}"
  cd "${srcdir}/build-${CARCH}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DBUILD_DEMOS=OFF \
    -DBUILD_SHARED=ON \
    -DINSTALL_STATIC=ON \
    ../Chipmunk-${pkgver}

  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR=${pkgdir} install
}
