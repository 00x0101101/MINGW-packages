# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=pkg-config
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.28
pkgrel=2
pkgdesc="A system for managing library compile/link flags (mingw-w64)"
arch=('any')
url="http://pkgconfig.freedesktop.org/wiki/"
license=('GPL')
depends=("${MINGW_PACKAGE_PREFIX}-glib2")
conflicts=("${MINGW_PACKAGE_PREFIX}-pkgconf")
replaces=("${MINGW_PACKAGE_PREFIX}-pkgconf")
source=(http://pkgconfig.freedesktop.org/releases/${_realname}-${pkgver}.tar.gz
        0002-check-for-host.mingw.patch
        0003-flush.mingw.patch
        0004-expected-path-for.mingw.patch
        0005-mangle.mingw.patch
        0006-path-for-tests.mingw.patch
        0007-sub-path-for-tests.mingw.patch
        0008-check-for-wine.mingw.patch
        0009-use-wine-if-present.mingw.patch
        0010-expand-paths.mingw.patch
        0011-platform-dependent-separator-and-adjustable-prefix.mingw.patch
        0099-no-prefix-on.mingw.patch)
md5sums=('aa3c86e67551adc3ac865160e34a2a0d'
         'd4cf66f456cc80466c553ccd96c0c0a1'
         '986d72727b4899a18106bd581f2cce34'
         '0620261f077b3c0dc1622ff3a6e3b87d'
         'f2332f2111ea6fc6c096249ccad297b1'
         '8c3fafdb507b0f01bfb2f13dd7747752'
         '58b709c3d9bc6285b9496ee449084b02'
         '079c1a68d297a8062fe620c302b9781f'
         '432aaf4ccaf70b093bc90cabd8c995e0'
         '43deff1cc78f45f7e3d6b9fee96de0e3'
         'd6b6acdb90f93c35378f53e34e7cd6fa'
         'd61554e8456fa9f67fbf90c8219dd2bf')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0002-check-for-host.mingw.patch
  patch -p1 -i ${srcdir}/0003-flush.mingw.patch
  patch -p1 -i ${srcdir}/0004-expected-path-for.mingw.patch
  patch -p1 -i ${srcdir}/0005-mangle.mingw.patch
  patch -p1 -i ${srcdir}/0006-path-for-tests.mingw.patch
  patch -p1 -i ${srcdir}/0007-sub-path-for-tests.mingw.patch
  patch -p1 -i ${srcdir}/0008-check-for-wine.mingw.patch
  patch -p1 -i ${srcdir}/0009-use-wine-if-present.mingw.patch
  patch -p1 -i ${srcdir}/0010-expand-paths.mingw.patch
  patch -p1 -i ${srcdir}/0011-platform-dependent-separator-and-adjustable-prefix.mingw.patch
  #patch -p1 -i ${srcdir}/0099-no-prefix-on.mingw.patch
  autoreconf -fi
}

build() {
  [ -d ${srcdir}/build-${MINGW_CHOST} ] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --with-internal-glib \
    --disable-indirect-deps
  make
}

check() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make check
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install
}
