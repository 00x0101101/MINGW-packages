# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=blender

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.70a
pkgrel=2
pkgdesc="A fully integrated 3D graphics creation suite (mingw-w64)"
arch=('any')
license=('GPL')
url="http://www.blender.org"
depends=("${MINGW_PACKAGE_PREFIX}-boost"
        "${MINGW_PACKAGE_PREFIX}-glew"
        "${MINGW_PACKAGE_PREFIX}-ffmpeg"
        "${MINGW_PACKAGE_PREFIX}-fftw"
        "${MINGW_PACKAGE_PREFIX}-freetype"
        "${MINGW_PACKAGE_PREFIX}-libpng"
        "${MINGW_PACKAGE_PREFIX}-libsndfile"
        "${MINGW_PACKAGE_PREFIX}-libtiff"
        "${MINGW_PACKAGE_PREFIX}-openexr"
        "${MINGW_PACKAGE_PREFIX}-openal"
        "${MINGW_PACKAGE_PREFIX}-opencollada"
        "${MINGW_PACKAGE_PREFIX}-opencolorio"
        "${MINGW_PACKAGE_PREFIX}-openimageio"
        "${MINGW_PACKAGE_PREFIX}-openshadinglanguage"
        "${MINGW_PACKAGE_PREFIX}-python3"
        "${MINGW_PACKAGE_PREFIX}-python3-numpy"
        "${MINGW_PACKAGE_PREFIX}-wintab-sdk")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake" "${MINGW_PACKAGE_PREFIX}-boost" "${MINGW_PACKAGE_PREFIX}-llvm")
#[[ $CARCH == x86_64 ]] && makedepends+=('cuda')
#optdepends=('cuda: cycles renderer cuda support')
options=(!strip staticlibs)
#install=blender.install
source=(http://download.blender.org/source/$_realname-$pkgver.tar.gz
        "mingw-w64-cmake.patch"
        "fix-mingw-w64-definitions.patch"
        "mingw-dont-install-3rdparty.patch"
        "fix-osl.patch")
md5sums=('12858464ee9347d9883982973b1689d9'
         'd89ba38e140bddb546c9975c4c76dd8e'
         '57bce4d96f2724d44d5bbeedaa36d392'
         '1969914e11d0a4f74dbfeddad742c9da'
         'cd59e2ed1d0d4318e6c200436b3277e5')

prepare() {
  cd "$srcdir/$_realname-v$pkgver"
  patch -p1 -i ${srcdir}/mingw-w64-cmake.patch
  patch -p1 -i ${srcdir}/fix-mingw-w64-definitions.patch
  patch -p1 -i ${srcdir}/mingw-dont-install-3rdparty.patch
  patch -p1 -i ${srcdir}/fix-osl.patch
}

build() {
  pushd ${MINGW_PREFIX} > /dev/null
  export PREFIX_DEPS=`pwd -W`
  popd > /dev/null

  mkdir $srcdir/build-${MINGW_CHOST}
  cd $srcdir/build-${MINGW_CHOST}

  [[ $CARCH == i686 ]] && ENABLESSE2="-DSUPPORT_SSE2_BUILD=OFF"
  [[ $CARCH == i686 ]] && BUILDCUDA="OFF" || BUILDCUDA="ON"
  [[ $CARCH == i686 ]] && PMARCH="i686" || PMARCH="x86-64"
  CFLAGS="-O2 -pipe -march=${PMARCH} -mtune=generic -D__USE_MINGW_ANSI_STDIO=1"
  CXXFLAGS="$CFLAGS"
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DMINGW_LIBS=${PREFIX_DEPS} \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_SYSTEM_GLEW=ON \
    -DWITH_SYSTEM_OPENJPEG=ON \
    -DWITH_INSTALL_PORTABLE=OFF \
    -DWITH_OPENCOLLADA=ON \
    -DWITH_GAMEENGINE=ON \
    -DWITH_RAYOPTIMIZATION=ON \
    -DWITH_JACK=OFF \
    -DWITH_PLAYER=ON \
    -DWITH_CODEC_FFMPEG=ON \
    -DWITH_CODEC_SNDFILE=ON \
    -DWITH_CYCLES=ON \
    -DWITH_CYCLES_CUDA_BINARIES=OFF \
    -DWITH_CYCLES_OSL=ON \
    -DWITH_FFTW3=ON \
    -DWITH_MOD_OCEANSIM=ON \
    -DPYTHON_VERSION=3.4 \
    -DPYTHON_LIBPATH=${PREFIX_DEPS}/lib \
    -DPYTHON_LIBRARY=python3.4m \
    -DPYTHON_INCLUDE_DIRS=${PREFIX_DEPS}/include/python3.4m \
    -DWITH_PYTHON_INSTALL=OFF \
    -DWITH_PYTHON_INSTALL_NUMPY=OFF \
    -DPYTHON_NUMPY_PATH=${PREFIX_DEPS}/lib/python3.4/site-packages \
    -DCYCLES_OSL=${PREFIX_DEPS} \
    -DWITH_IMAGE_REDCODE=ON \
    -DWITH_OPENMP=ON \
    ../${_realname}-v${pkgver}

  # Remove the openjpeg line once blender accepts my patch
  make # -j1 VERBOSE=1
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make install
  mkdir -p ${pkgdir}${MINGW_PREFIX}/lib
  cp -r bin ${pkgdir}${MINGW_PREFIX}/
  rm ${pkgdir}${MINGW_PREFIX}/bin/msgfmt.exe
  mv ${pkgdir}${MINGW_PREFIX}/bin/*.a ${pkgdir}${MINGW_PREFIX}/lib/

  ${MINGW_PREFIX}/bin/python3 -m compileall "${pkgdir}${MINGW_PREFIX}/bin/${pkgver}"
  ${MINGW_PREFIX}/bin/python3 -m compileall \
  ${pkgdir}${MINGW_PREFIX}/bin/${pkgver}/scripts/startup \
  ${pkgdir}${MINGW_PREFIX}/bin/${pkgver}/scripts/modules \
  ${pkgdir}${MINGW_PREFIX}/bin/${pkgver}/scripts/addons
}
