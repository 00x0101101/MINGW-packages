# Maintainer: RÃ©mi BERTHO <remi.bertho@openmailbox.org>

_realname=podofo
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.9.3
pkgrel=1
pkgdesc="A C++ library to work with the PDF file format"
arch=('i686' 'x86_64')
url="http://podofo.sourceforge.net"
license=('GPL')
depends=("${MINGW_PACKAGE_PREFIX}-lua51" "${MINGW_PACKAGE_PREFIX}-openssl" "${MINGW_PACKAGE_PREFIX}-fontconfig" "${MINGW_PACKAGE_PREFIX}-libtiff" "libidn" "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake")
source=("http://downloads.sourceforge.net/${_realname}/${_realname}-${pkgver}.tar.gz" "win.patch")
md5sums=('268ca111826757ca7e3f5c4be3547e7a' '4a0006884bd9fd3ad73f3ec3ed0cd886')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i "$srcdir/win.patch"
}

build() {
  cd ${_realname}-${pkgver}

  mkdir -p build
  cd build
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe -G"MSYS Makefiles" \
		-Wno-dev \
		-DCMAKE_C_COMPILER=${MINGW_PREFIX}/bin/gcc.exe \
		-DCMAKE_CXX_COMPILER=${MINGW_PREFIX}/bin/g++.exe \
		-DCMAKE_MAKE_PROGRAM=mingw32-make \
		-DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} .. \
        -DFREETYPE_INCLUDE_DIR=/mingw64/include/freetype2 \
		-DCMAKE_BUILD_TYPE=Release \
        -DPODOFO_BUILD_SHARED=1 \
        -DPODOFO_HAVE_JPEG_LIB=1 \
        -DPODOFO_HAVE_PNG_LIB=1 \
        -DPODOFO_HAVE_TIFF_LIB=1
  mingw32-make
}

package() {
  cd ${_realname}-${pkgver}/build
  mingw32-make DESTDIR=$pkgdir install
}
