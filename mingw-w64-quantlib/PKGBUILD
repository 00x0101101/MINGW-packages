# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=QuantLib
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.6.2
pkgrel=1
pkgdesc="QuantLib - A free/open-source library for quantitative finance"
arch=('any')
url='http://quantlib.org'
license=('BSD')
makedepends=("${MINGW_PACKAGE_PREFIX}-boost" "automake" "autoconf" "libtool")
options=('staticlibs' 'strip')
source=(https://downloads.sourceforge.net/project/quantlib/QuantLib/${pkgver}/QuantLib-${pkgver}.tar.gz)
md5sums=('e1831b8fc7fbf155659ec7aa8351b57a')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  cp -rf ${_realname}-${pkgver} build-${MINGW_CHOST}
  cd "${srcdir}/build-${MINGW_CHOST}"

  ./configure \
      --disable-dependency-tracking \
      --enable-static \
      --enable-shared \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST}
  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make check
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/LICENSE.TXT "${pkgdir}${MINGW_PREFIX}"/share/licenses/${_realname}/LICENSE
}
