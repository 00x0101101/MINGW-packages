# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libgcrypt
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=1.5.3
pkgrel=1
pkgdesc="General purpose cryptographic library based on the code from GnuPG (mingw-w64)"
arch=(any)
url="http://www.gnupg.org"
license=('LGPL')
depends=("${_mingw_suff}-crt" "${_mingw_suff}-libgpg-error")
groups=("${_mingw_suff}")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-libgpg-error") # "transfig" "ghostscript"
options=('staticlibs' 'strip')
source=("ftp://ftp.gnupg.org/gcrypt/libgcrypt/libgcrypt-${pkgver}.tar.bz2"
        'libgcrypt-mingw64-use-correct-datatype-for-GetProcessWorkingSet.patch'
        'libgcrypt-use-correct-def-file.patch'
        'libgcrypt-use-correct-asm-code-for-win64.patch')
md5sums=('993159b2924ae7b0e4eaff0743c2db35'
         '7bb247584a66a83677c618aec812ca9b'
         '531e089caca74b5daf130b7173c2a5c5'
         '8ea7a618d222f1301b0eaf596954bb51')

prepare() {
  cd "${srcdir}/libgcrypt-${pkgver}"
  patch -p0 -i "$srcdir"/libgcrypt-mingw64-use-correct-datatype-for-GetProcessWorkingSet.patch
  patch -p0 -i "$srcdir"/libgcrypt-use-correct-def-file.patch
  patch -p0 -i "$srcdir"/libgcrypt-use-correct-asm-code-for-win64.patch
}

build() {
    cp -r "$srcdir/libgcrypt-${pkgver}" "$srcdir/build-${CARCH}"
    cd "$srcdir/build-${CARCH}"
    if test "${MINGW_CHOST}" = "x86_64-w64-mingw32"
    then
      configure_args="ac_cv_sys_symbol_underscore=no"
    else
      configure_args=""
    fi
    ./configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
	  --host=${MINGW_CHOST} \
	  --target=${MINGW_CHOST} \
      --enable-static \
      --enable-pubkey-ciphers='dsa elgamal rsa' \
      --with-gpg-error-prefix=${MINGW_PREFIX} \
	  $configure_args
    make
}

package() {
    cd "$srcdir/build-${CARCH}"
    make DESTDIR="$pkgdir" install -j1 
    rm -r "${pkgdir}${MINGW_PREFIX}"/share
    # rm "${pkgdir}${MINGW_PREFIX}"/bin/*.exe
    # strip --strip-unneeded "${pkgdir}${MINGW_PREFIX}"/bin/*.dll
    # strip -g "${pkgdir}${MINGW_PREFIX}"/lib/*.a
}

