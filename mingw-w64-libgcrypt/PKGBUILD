# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libgcrypt
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=1.6.0
pkgrel=1
pkgdesc="General purpose cryptographic library based on the code from GnuPG (mingw-w64)"
arch=(any)
url="http://www.gnupg.org"
license=('LGPL')
depends=("${_mingw_suff}-crt" "${_mingw_suff}-libgpg-error")
groups=("${_mingw_suff}")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-libgpg-error") # "transfig" "ghostscript"
options=('staticlibs' 'strip')
source=("ftp://ftp.gnupg.org/gcrypt/libgcrypt/libgcrypt-${pkgver}.tar.bz2"
        'libgcrypt-mingw64-use-correct-datatype-for-GetProcessWorkingSet.patch'
        'libgcrypt-use-correct-def-file.patch'
        'libgcrypt-use-correct-asm-code-for-win64.patch'
		'Smarter-fig2dev-detection.all.patch')
md5sums=('984079ff16fb644dd80d4dddc5fc1a73'
         '7bb247584a66a83677c618aec812ca9b'
         '531e089caca74b5daf130b7173c2a5c5'
         '8ea7a618d222f1301b0eaf596954bb51'
         'e5d71838a1c1630990ebb475a2b3117d')

prepare() {
  cd "${srcdir}/libgcrypt-${pkgver}"
  patch -p0 -i "$srcdir"/libgcrypt-mingw64-use-correct-datatype-for-GetProcessWorkingSet.patch
  patch -p0 -i "$srcdir"/libgcrypt-use-correct-def-file.patch
  #patch -p0 -i "$srcdir"/libgcrypt-use-correct-asm-code-for-win64.patch
  patch -p1 -i "$srcdir"/Smarter-fig2dev-detection.all.patch
  
  autoreconf -fi
}

build() {
    cp -r "$srcdir/libgcrypt-${pkgver}" "$srcdir/build-${CARCH}"
    cd "$srcdir/build-${CARCH}"
    #if test "${MINGW_CHOST}" = "x86_64-w64-mingw32"
    #then
    #  configure_args="ac_cv_sys_symbol_underscore=no"
    #else
    #  configure_args=""
    #fi
	[[ $CARCH == x86_64 ]] && BUILD_ASM="--disable-asm" || BUILD_ASM=""
    ./configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
	  --host=${MINGW_CHOST} \
	  --target=${MINGW_CHOST} \
      --enable-shared \
	  --disable-static \
	  $BUILD_ASM \
	  --disable-padlock-support \
      --with-gpg-error-prefix=${MINGW_PREFIX}
    make -j1
}

package() {
    cd "$srcdir/build-${CARCH}"
    make DESTDIR="$pkgdir" install -j1 
    rm -r "${pkgdir}${MINGW_PREFIX}"/share
    find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
}

