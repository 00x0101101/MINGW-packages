# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_realname=qbs

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.2.2
pkgrel=2
_pkgfqn="${_realname}-${pkgver}.src"
pkgdesc='Qt Build Suite'
url='http://qt-project.org'
arch=('any')
depends=("${MINGW_PACKAGE_PREFIX}-qt5")
options=('docs' 'staticlibs') # '!strip' 'debug')
source=("http://download.qt-project.org/official_releases/qbs/${pkgver}/${_pkgfqn}.tar.gz"
        "manifest"
        "0001-win32-fix-debug-QBSQTPROFILELIB-var-expansion.patch"
        "0002-WIP-Xcode-generator.patch"
        "0003-add-generator-header-installs.patch")
sha256sums=('26d46cf6bf580397810b50ee4eea94880148b8a64deab06ea3a4c868db18d162'
            '838098b25a8044176b3139b4003594570c62a8d64f5470fbbd769f3bf44e0855'
            'd6580fbe7d2892cf269385ab8bb44194219a8067da6d4ebf1fe703b208e54440'
            '817d5c5ffe7ef51a4dd73b6eb970349b0ddf940aece355ad07ec50c8de84c541'
            'fa45188b2a2a488445893839bb59ffce438d0e1f79384598ccdfbbf803c46193')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 < "${srcdir}"/0001-win32-fix-debug-QBSQTPROFILELIB-var-expansion.patch
  patch -p1 < "${srcdir}"/0002-WIP-Xcode-generator.patch || true
  patch -p1 < "${srcdir}"/0003-add-generator-header-installs.patch
}

build() {
local _config
if check_option "debug" "n"; then
  _config="release"
else
  _config="debug"
fi
  cd ${srcdir}/${_realname}-${pkgver}
  [ -d build-${CARCH} ] && rm -rf build-${CARCH}
  mkdir build-${CARCH}
  cd build-${CARCH}
  ${MINGW_PREFIX}/bin/qmake.exe ../qbs.pro CONFIG+=${_config}
  make
  make docs
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}/build-${CARCH}
  make INSTALL_ROOT=${pkgdir}${MINGW_PREFIX} install
  make INSTALL_ROOT=${pkgdir}${MINGW_PREFIX} install_docs

  rm -f ${pkgdir}${MINGW_PREFIX}/lib/*.dll

  for file in qbs-setup-madde-platforms qbs-setup-qt; do
    cp ${srcdir}/manifest ${pkgdir}${MINGW_PREFIX}/bin/${file}.exe.manifest
  done
}

