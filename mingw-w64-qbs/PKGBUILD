# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=qbs

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.2.2
pkgrel=1
_pkgfqn="${_realname}-${pkgver}.src"
pkgdesc='Qt Build Suite'
url='http://qt-project.org'
arch=('any')
depends=("${MINGW_PACKAGE_PREFIX}-qt5")
options=('docs' '!strip') # 'staticlibs' 'debug')
source=("http://download.qt-project.org/official_releases/qbs/${pkgver}/${_pkgfqn}.tar.gz"
        "0001-win32-fix-debug-QBSQTPROFILELIB-var-expansion.patch"
        "manifest")
sha256sums=('26d46cf6bf580397810b50ee4eea94880148b8a64deab06ea3a4c868db18d162'
            'd6580fbe7d2892cf269385ab8bb44194219a8067da6d4ebf1fe703b208e54440'
            '838098b25a8044176b3139b4003594570c62a8d64f5470fbbd769f3bf44e0855')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 < "${srcdir}/0001-win32-fix-debug-QBSQTPROFILELIB-var-expansion.patch"
}

build() {
  local _config
  if check_option "debug" "n"; then
    _config="release"
  else
    _config="debug"
  fi
  mkdir -p ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}
  
  ${MINGW_PREFIX}/bin/qmake.exe \
    ../${_realname}-${pkgver}/qbs.pro \
    CONFIG+=${_config}

  make
  make docs
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make INSTALL_ROOT=${pkgdir}${MINGW_PREFIX} install
  make INSTALL_ROOT=${pkgdir}${MINGW_PREFIX} install_docs
  
  rm -f ${pkgdir}${MINGW_PREFIX}/lib/*.dll
  
  for file in qbs-setup-madde-platforms qbs-setup-qt; do
    cp ${srcdir}/manifest ${pkgdir}${MINGW_PREFIX}/bin/${file}.exe.manifest
  done
}

