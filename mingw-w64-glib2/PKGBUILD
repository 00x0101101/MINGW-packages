# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=glib2
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=2.38.2
pkgrel=1
arch=('any')
pkgdesc="Common C routines used by GTK+ 2.4 and other libs (mingw-w64)"
depends=("${_mingw_suff}-winpthreads"
		"${_mingw_suff}-gettext"
		"${_mingw_suff}-zlib"
		"${_mingw_suff}-libffi")
makedepends=("${_mingw_suff}-gcc" "python2")
license=('LGPL')
options=('strip' 'staticlibs')
url="http://www.gtk.org/"
source=("http://ftp.gnome.org/pub/GNOME/sources/glib/${pkgver%.*}/glib-$pkgver.tar.xz"
        "0001-Use-CreateFile-on-Win32-to-make-sure-g_unlink-always.patch"
        "glib-send-log-messages-to-correct-stdout-and-stderr.patch"
        "glib-prefer-constructors-over-DllMain.patch")
md5sums=('26d1d08e478fc48c181ca8be44f5b69f'
         'c08c41118f5318d10f326c7c30e1a33a'
         '7b065f9bc154bf996523974a698b24de'
         '46969e815f3ab19582ba33667a52048b')

prepare() {
  cd "$srcdir/glib-$pkgver"

  patch -Np1 -i "$srcdir/0001-Use-CreateFile-on-Win32-to-make-sure-g_unlink-always.patch"
  patch -Np0 -i "$srcdir/glib-send-log-messages-to-correct-stdout-and-stderr.patch"
  patch -Np1 -i "$srcdir/glib-prefer-constructors-over-DllMain.patch"
}

build()
{
  cd "$srcdir/glib-$pkgver"

  export CFLAGS+=" -fexceptions --param=ssp-buffer-size=4"
  export CXXFLAGS+=" -fexceptions --param=ssp-buffer-size=4"

  mkdir $srcdir/build-${CARCH}-static && cd $srcdir/build-${CARCH}-static
    ../glib-$pkgver/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --target=${MINGW_CHOST} \
      --disable-shared --enable-static
  make

  mkdir $srcdir/build-${CARCH}-shared && cd $srcdir/build-${CARCH}-shared
    ../glib-$pkgver/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --target=${MINGW_CHOST} \
      --disable-static
  make
}

package() {
    cd "$srcdir/build-${CARCH}-shared"
    make DESTDIR="$pkgdir" install

	cd "$srcdir/build-${CARCH}-static"
    make DESTDIR="$pkgdir/static" install
	
	cd "$srcdir/build-${CARCH}-shared"
    mv "$pkgdir/static/${MINGW_PREFIX}/lib/"*.a "$pkgdir/${MINGW_PREFIX}/lib/"

    rm -rf "${pkgdir}/static"

    rm -rf "${pkgdir}${MINGW_PREFIX}"/{share/{gtk-doc,man,bash-completion,gdb},lib/charset.alias,etc}
    rm "${pkgdir}${MINGW_PREFIX}/lib/"*.def

    rm -f "${pkgdir}${MINGW_PREFIX}/bin/gdbus-codegen"
    rm -rf "${pkgdir}${MINGW_PREFIX}/lib/gdbus-2.0"
}
