# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libuv
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.11.29
pkgrel=1
pkgdesc="A new platform layer for Node.JS (mingw-w64)"
arch=('any')
url="https://github.com/joyent/libuv"
license=("custom")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
options=('staticlibs' 'strip')
source=("https://github.com/joyent/libuv/archive/v$pkgver.tar.gz")
sha256sums=('fac45e72c2df30ddc9cbdf5002fc1363281d0e433831e3d43acb1036c2cabfc6')

prepare() {
  cd "$srcdir/${_realname}-$pkgver"
  ./autogen.sh
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  "${srcdir}"/${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST}
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="$pkgdir" install

  install -D -m644 "${srcdir}"/${_realname}-${pkgver}/LICENSE "${pkgdir}${MINGW_PREFIX}"/share/licenses/${_realname}/LICENSE
}
