# Maintainer: Renato Silva <br.renatosilva@gmail.com>

_realname=libmongoose
pkgbase="mingw-w64-${_realname}"
pkgname=(${MINGW_PACKAGE_PREFIX}-${_realname})
pkgver=6.2
pkgrel=1
url="https://github.com/cesanta/mongoose"
pkgdesc='Embedded web server for C/C++ (mingw-w64)'
license=('GPL2')
arch=('any')
depends=(${MINGW_PACKAGE_PREFIX}-gcc-libs)
makedepends=(${MINGW_PACKAGE_PREFIX}-gcc)
source=(${_realname}-${pkgver}.tar.gz::"https://github.com/cesanta/mongoose/archive/${pkgver}.tar.gz"
        mongoose.pc
        Makefile)
sha1sums=('6aee69d07a1b674de9b11c3cab6a878f0d43b727'
          '92c82813e7c1b277a0078c0e67addc7e159aec81'
          '344bfa94b2a032dcf47adbaaa190100445fa0181')

prepare() {
  cp Makefile mongoose-${pkgver}/
  cp mongoose.pc mongoose-${pkgver}/
}

build() {
  cd "${srcdir}/mongoose-${pkgver}"
  make PREFIX=${MINGW_PREFIX} CC=gcc AR=ar -j1
}

package() {
  cd "${srcdir}/mongoose-${pkgver}"
  make PREFIX=${MINGW_PREFIX} DESTDIR=${pkgdir} install
  install -D -m644 mongoose.pc "${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/mongoose.pc"
  install -D -m644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"

  sed -e "s|@@PREFIX@@|${MINGW_PREFIX}|g" \
      -e "s|@@VERSION@@|5.6|g" \
      -i "${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/mongoose.pc"
}
