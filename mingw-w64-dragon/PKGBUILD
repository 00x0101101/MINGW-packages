# Maintainer: Bjoern Lemke <lemke@lemke-it.com>

_realname=dragon
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.4.0
pkgrel=1
pkgdesc="Dragon parser generator (mingw-w64)"
arch=('any')
url='http://www.lemke-it.com/'
license=('GPL3')

depends=("${MINGW_PACKAGE_PREFIX}-lfcbase")

source=("http://www.lemke-it.com/${_realname}-${pkgver}.tar.gz")

sha256sums=("a2354e3b34fe10b7a7ba94e3fcb34c621409fd89e9d2599f883b2aade92f2277")

prepare() {
  cd $srcdir/${_realname}-${pkgver}
}

build() {
  cd "$srcdir"/${_realname}-${pkgver}
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-static \
    --enable-shared

  make
}

check() {
  cd "${srcdir}"/build-${CARCH}
  make check
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make install DESTDIR="${pkgdir}"
}
