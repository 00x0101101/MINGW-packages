# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=atk
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=2.10.0
pkgrel=1
pkgdesc="A library providing a set of interfaces for accessibility (mingw-w64)"
arch=('any')
url="http://www.gtk.org"
license=("LGPL")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config")
depends=("${_mingw_suff}-crt" "${_mingw_suff}-glib2>=2.38.0")
options=('strip' 'staticlibs')
source=("http://ftp.gnome.org/pub/gnome/sources/atk/${pkgver%.*}/${_realname}-$pkgver.tar.xz")
sha256sums=('636917a5036bc851d8491194645d284798ec118919a828be5e713b6ecc5b50b0')

build() {
    CFLAGS+=" -mms-bitfields"
    mkdir -p "${srcdir}/build-${MINGW_CHOST}"
    cd "${srcdir}/build-${MINGW_CHOST}"
    "${srcdir}"/${_realname}-${pkgver}/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --enable-static \
      --enable-shared \
      --disable-glibtest
    make
}

package() {
    cd "${srcdir}/build-${MINGW_CHOST}"
    make -j1 DESTDIR="$pkgdir" install
    find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    rm -r "${pkgdir}${MINGW_PREFIX}/share"
}