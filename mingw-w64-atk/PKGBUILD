# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=atk
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=2.11.6
pkgrel=1
pkgdesc="A library providing a set of interfaces for accessibility (mingw-w64)"
arch=('any')
url="http://www.gtk.org"
license=("LGPL")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config")
depends=("${_mingw_suff}-gcc-libs" "${_mingw_suff}-glib2>=2.38.0")
options=('strip' 'staticlibs')
install=atk-${CARCH}.install
source=("http://ftp.gnome.org/pub/gnome/sources/atk/${pkgver%.*}/${_realname}-$pkgver.tar.xz"
		0001-create-libdir.mingw.patch
		0002-fix-gtkdoc.all.patch
		0003-give-cc-to-git-scanner.mingw.patch)
sha256sums=('9035451580efcae3731af02b5e30e44467349574bee90eb361f825377b9bbe42'
            'fc363b01679e8be850086da082ffae8e9cb1ceb0d08c54a06a3a14276f065000'
            '33f728e1f38e1cd98806cb127bfdd0d7daed4ba301bc81256fbf8e2032a88753'
            '0569d3792e6354707ef804e2e9251b09ac406ae6428712291cf387de8f21f927')

prepare() {
	cd "${srcdir}"/${_realname}-${pkgver}
	patch -p1 -i ${srcdir}/0001-create-libdir.mingw.patch
	patch -p1 -i ${srcdir}/0002-fix-gtkdoc.all.patch
	patch -p1 -i ${srcdir}/0003-give-cc-to-git-scanner.mingw.patch
	
	autoreconf -fi
}

build() {
    CFLAGS+=" -mms-bitfields"
    mkdir -p "${srcdir}/build-${MINGW_CHOST}"
    cd "${srcdir}/build-${MINGW_CHOST}"
    "${srcdir}"/${_realname}-${pkgver}/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --enable-static \
      --enable-shared \
      --disable-glibtest
    make
}

package() {
    cd "${srcdir}/build-${MINGW_CHOST}"
    make -j1 DESTDIR="$pkgdir" install
    find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    rm -r "${pkgdir}${MINGW_PREFIX}/share"
}
