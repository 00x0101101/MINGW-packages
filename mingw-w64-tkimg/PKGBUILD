# Based on the Arch package

_pkgname=tkimg
pkgname=${MINGW_PACKAGE_PREFIX}-$_pkgname
_pkgver=1.4
pkgver=${_pkgver}.2
pkgrel=1
pkgdesc="Adds support to Tk for many other Image formats: BMP, XBM, XPM, GIF, PNG, JPEG, TIFF and postscript."
url="http://tkimg.sourceforge.net"
arch=('any')
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-zlib" "${MINGW_PACKAGE_PREFIX}-libjpeg"
	 "${MINGW_PACKAGE_PREFIX}-libpng" "${MINGW_PACKAGE_PREFIX}-libtiff"
	 "${MINGW_PACKAGE_PREFIX}-tk")
# Uses dtplite for generating the man pages:
makedepends=("${MINGW_PACKAGE_PREFIX}-tcllib")
source=("http://downloads.sourceforge.net/tkimg/$_pkgname$pkgver.tar.gz"
	001-compile-tif_jbig.patch
	002-dtplite_tcl.patch)
md5sums=('c42b46692a55c402c97e8f2a896bc2d4'
	 '9582ee32e4e7c9c0002dcc8127c5f7fd'
	 'c8aa6d8c1b182826d794a214fea18e6e')

prepare() {
  cd "${srcdir}/${_pkgname}${_pkgver}"
  patch -Np1 -i "${srcdir}/001-compile-tif_jbig.patch"
  patch -Np1 -i "${srcdir}/002-dtplite_tcl.patch"
}

build() {
  if check_option "debug" "y"; then
    extra_config+=( --enable-symbols )
  fi

  mkdir -p "${srcdir}/build-${CARCH}"
  cd "${srcdir}/build-${CARCH}"

  ../${_pkgname}${_pkgver}/configure --prefix=${MINGW_PREFIX} \
     "${extra_config[@]}"
  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make INSTALL_ROOT="$pkgdir" install
  rm -f ${pkgdir}/${MINGW_PREFIX}/lib/*.sh
}
