# Maintainer: Lubosz Sarnecki <lubosz@gmail.com>

_name=clutter-gtk
_tag=1.5.2
pkgname="${MINGW_PACKAGE_PREFIX}-${_name}"
pkgver=1.5.2.446.ef32dff
pkgrel=1
pkgdesc="Clutter integration with GTK+. (mingw-w64)"
arch=('any')
url="http://clutter-project.org/"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gobject-introspection")
depends=("${MINGW_PACKAGE_PREFIX}-gtk3"
        "${MINGW_PACKAGE_PREFIX}-clutter"
        )
options=(!libtool strip staticlibs)

source=("git://git.gnome.org/$_name#tag=$_tag"
        "0001-msys-add-no-undefined-to-libtool-flags.patch"
        "0002-msys-fix-introspection-build.patch"
        "0003-msys-update-clutter-win32-header-location.patch")

md5sums=("SKIP"
         "317a8e3259f818c2e1147697d623e920"
         "4b892afaf561ac2503962d238770f967"
         "48afee140d22c548a85691d1ca2097a3")

pkgver() {
  cd $_name
  major=$(grep "clutter_gtk_major]," configure.ac | sed 's/m4_define(\[clutter_gtk_major\], \[//' | sed 's/\])//')
  minor=$(grep "clutter_gtk_minor]," configure.ac | sed 's/m4_define(\[clutter_gtk_minor\], \[//' | sed 's/\])//')
  micro=$(grep "clutter_gtk_micro]," configure.ac | head -1 | sed 's/m4_define(\[clutter_gtk_micro\], \[//' | sed 's/\])//')
  hash=$(git log --pretty=format:'%h' -n 1)
  revision=$(git rev-list --count HEAD)
  echo $major.$minor.$micro.$revision.$hash  
}

prepare() {
  cd $_name
  git am ../0001-msys-add-no-undefined-to-libtool-flags.patch
  git am ../0002-msys-fix-introspection-build.patch
  git am ../0003-msys-update-clutter-win32-header-location.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  cp -r "${srcdir}/${_name}" "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --disable-static \
    --enable-shared \
    --enable-silent-rules \
    --disable-gtk-doc
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 DESTDIR="$pkgdir" install
}
