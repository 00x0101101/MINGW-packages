# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libnice

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.1.7
pkgrel=1
pkgdesc="An implementation of the IETF's draft ICE (for p2p UDP data streams) (mingw-w64)"
arch=('any')
url="http://nice.freedesktop.org"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-gstreamer")
depends=("${MINGW_PACKAGE_PREFIX}-glib2")
optdepends=("${MINGW_PACKAGE_PREFIX}-gstreamer")
options=('staticlibs' 'strip')
source=("http://nice.freedesktop.org/releases/${_realname}-$pkgver.tar.gz"
        0001-fix-redefinition-mingw-w64.patch
        0002-no-undefined.patch)
md5sums=('ed74abea19b3f049baf095f137388f2a'
         'b817c8a7c4cbb405341de75b9824204f'
         '5c937f7c3a5857038f27a009d33b0757')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0001-fix-redefinition-mingw-w64.patch
  patch -p1 -i ${srcdir}/0002-no-undefined.patch
  
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-shared \
    --enable-static

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="$pkgdir" install
  rm -f ${pkgdir}${MINGW_PREFIX}/lib/gstreamer*/*.a
}
