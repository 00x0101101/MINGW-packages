# Maintainer: Renato Silva <br.renatosilva@gmail.com>

_realname='libmongoose'
pkgname=(${MINGW_PACKAGE_PREFIX}-${_realname}-git)
provides=(${MINGW_PACKAGE_PREFIX}-${_realname})
conflicts=(${MINGW_PACKAGE_PREFIX}-${_realname})
pkgver=r1793.41b405d
pkgrel=2
url='https://github.com/cesanta/mongoose'
pkgdesc='Embedded web server for C/C++ (mingw-w64)'
license=('GPL2')
arch=('any')
depends=(${MINGW_PACKAGE_PREFIX}-gcc-libs) #${MINGW_PACKAGE_PREFIX}-openssl
makedepends=(${MINGW_PACKAGE_PREFIX}-gcc)
source=(${_realname}::'git+https://github.com/cesanta/mongoose'
        mongoose.pc
        Makefile)
sha1sums=('SKIP'
          '92c82813e7c1b277a0078c0e67addc7e159aec81'
          'c4a55e3cc984dba00258d81e2ab09e8e91ff4419')

pkgver() {
  cd "${srcdir}/${_realname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cp Makefile ${_realname}/
  cp mongoose.pc ${_realname}/
}

build() {
  cd "${srcdir}/${_realname}"
  make PREFIX=${MINGW_PREFIX} CC=gcc -j1
}

package() {
  cd "${srcdir}/${_realname}"
  make PREFIX=${MINGW_PREFIX} DESTDIR=${pkgdir} install
  install -D -m644 mongoose.pc "${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/mongoose.pc"
  install -D -m644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
  
  sed -e "s|@@PREFIX@@|${MINGW_PREFIX}|g" \
      -e "s|@@VERSION@@|5.6|g" \
      -i "${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/mongoose.pc"
}
