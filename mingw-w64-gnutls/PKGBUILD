# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gnutls
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=3.2.7
pkgrel=1
pkgdesc="A library which provides a secure layer over a reliable transport layer"
arch=('any')
license=('GPL3' 'LGPL2.1')
url="http://www.gnutls.org/"
options=('!zipman')
depends=("${_mingw_suff}-gcc"
		"${_mingw_suff}-libtasn1"
		"${_mingw_suff}-libgcrypt"
		"${_mingw_suff}-gmp"
		"${_mingw_suff}-zlib"
		"${_mingw_suff}-nettle"
		"${_mingw_suff}-p11-kit"
		"${_mingw_suff}-libgnurx"
		"${_mingw_suff}-libgpg-error")
makedepends=("${_mingw_suff}-gcc")
source=(ftp://ftp.gnutls.org/gcrypt/gnutls/v3.2/${_realname}-${pkgver}.tar.xz{,.sig}
		0004-fix-gtkdoc.all.patch
		0005-fix-strtok-conflict.mingw.patch)
md5sums=('c27d14fe6ad9dc10deddc7e561e0eb63'
         'SKIP'
         '86cfab6e7c2ccfcfd1dad9d5024627e3'
         '77287dc835afbb8cc4e9c4aa6b7adb7b')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/0004-fix-gtkdoc.all.patch
  patch -p1 -i ${srcdir}/0005-fix-strtok-conflict.mingw.patch
  
  autoreconf -fi
}

build() {
  mkdir -p "${srcdir}/${_realname}-${pkgver}-build-${CARCH}"
  cd "${srcdir}/${_realname}-${pkgver}-build-${CARCH}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
	--target=${MINGW_CHOST} \
	--with-zlib \
	--enable-shared --disable-static \
    --enable-cxx \
	--enable-nls \
	--disable-rpath \
    --disable-gtk-doc \
    --with-libiconv-prefix=${MINGW_PREFIX} \
    --with-libregex-libs=-lgnurx \
	gl_cv_double_slash_root=yes
  make
}

check() {
  cd ${_realname}-${pkgver}-build-${CARCH}
  make -k check || /bin/true
}

package() {
  cd ${_realname}-${pkgver}-build-${CARCH}
  make DESTDIR="${pkgdir}" install
  
  # lots of .png files are put into infodir and are gzipped by makepkg! this may need to be fixed by using !zipman
  # gzip -9 all files in infodir and manpages manually
  #find "$pkgdir/usr/share/info" -name '*.info*' -exec gzip -9 {} \;
  #find "$pkgdir/usr/share/man" -exec gzip -9 {} \;
  rm -r ${pkgdir}${MINGW_PREFIX}/share/{info,man}
}
