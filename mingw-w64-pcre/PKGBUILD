# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=pcre
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=8.34
pkgrel=1
pkgdesc="A library that implements Perl 5-style regular expressions (mingw-w64)"
arch=('any')
url="http://pcre.org"
license=("BSD")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config")
depends=("${_mingw_suff}-crt" "${_mingw_suff}-zlib" "${_mingw_suff}-bzip2") # "${_mingw_suff}-readline"
options=('staticlibs' 'strip')
source=("http://downloads.sourceforge.net/pcre/pcre-$pkgver.tar.bz2")
md5sums=('5439e321351bddd5533551bbce128d07')

build() {
    mkdir -p "${srcdir}/${_realname}-${pkgver}-build-${CARCH}"
    cd "${srcdir}/${_realname}-${pkgver}-build-${CARCH}"
	CPPFLAGS+=" -I${MINGW_PREFIX}/include"
    "${srcdir}"/${_realname}-${pkgver}/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
	  --target=${MINGW_CHOST} \
      --enable-unicode-properties \
      --enable-utf \
	  --enable-pcre8 \
      --enable-pcre16 \
      --enable-pcre32 \
	  --enable-pcregrep-libz \
	  --enable-pcregrep-libbz2
    make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}-build-${CARCH}"
  make check
}

package() {
    cd "${srcdir}/${_realname}-${pkgver}-build-${CARCH}"
    make DESTDIR="$pkgdir" install
    #find "${pkgdir}${MINGW_PREFIX}" -name '*.exe' | xargs -rtl1 rm
    #find "${pkgdir}${MINGW_PREFIX}" -name '*.dll' | xargs -rtl1 strip --strip-unneeded
    #find "${pkgdir}${MINGW_PREFIX}" -name '*.a' -o -name '*.dll' | xargs -rtl1 strip -g
    rm -r "${pkgdir}${MINGW_PREFIX}/share"
}
