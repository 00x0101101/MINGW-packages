# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=spice-gtk
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.26
pkgrel=2
arch=('any')
pkgdesc="GTK3 widget for SPICE clients (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-gtk3"
         "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
         "${MINGW_PACKAGE_PREFIX}-python2"
         "${MINGW_PACKAGE_PREFIX}-pixman"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-cyrus-sasl"
         "${MINGW_PACKAGE_PREFIX}-vala"
         "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
         "${MINGW_PACKAGE_PREFIX}-usbredir"
         "${MINGW_PACKAGE_PREFIX}-dbus-glib"
         "${MINGW_PACKAGE_PREFIX}-phodav")
makedepends=("${MINGW_PACKAGE_PREFIX}-spice-protocol")
options=('strip' 'staticlibs')
license=("GPL 2")
url="http://www.spice-space.org"
source=(http://www.spice-space.org/download/gtk/${_realname}-$pkgver.tar.bz2
        001-win.patch)
sha256sums=('d61cabeb4ae03afb5bb921139491d1088ca0cdf77c7e70b8039fe62c2246e3f9'
            'fa47a600c6d152ab49f794305bba752fa80a644ef94d2007a1cccc00e244655a')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}

  patch -p1 -i ${srcdir}/001-win.patch

  autoreconf -isf
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --enable-introspection \
    --with-gtk=3.0 \
    --with-audio=no

  make
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make DESTDIR=$pkgdir install
}
