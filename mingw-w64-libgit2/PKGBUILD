# Maintainer: Martell Malone <martellmalone@gmail.com>

_realname=libgit2
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.21.0
pkgrel=1
pkgdesc='A linkable library for Git (mingw-w64)'
arch=('any')
url='http://www.libgit2.github.com/'
license=('GPL2' 'custom')
_pkgfqn=v${pkgver}.tar.gz
source=("https://github.com/libgit2/libgit2/archive/${_pkgfqn}")
md5sums=('06b598bf21b0536dcbf454dc63af2313')
options=('strip')
depends=("${MINGW_PACKAGE_PREFIX}-http-parser"
        "${MINGW_PACKAGE_PREFIX}-libssh2"
        "${MINGW_PACKAGE_PREFIX}-openssl"
        "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake" 'p7zip')

noextract=(${_pkgfqn})

prepare() {
  cd ${srcdir}
  msg "Extracting ${_pkgfqn} due to git folder tag"
  7z x "$startdir/${_pkgfqn}" -so | 7z x -aoa -si -ttar -o"${srcdir}"
}

build() {
  mkdir -p ${srcdir}/build-${MINGW_CHOST} 
  cd ${srcdir}/build-${MINGW_CHOST}
  
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DTHREADSAFE:BOOL=1 \
    -DCMAKE_INSTALL_PREFIX:PATH=${pkgdir}${MINGW_PREFIX} \
    ${srcdir}/${_realname}-${pkgver}

  make
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make install
  
  pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
  export PREFIX_WIN=`pwd -W`
  popd > /dev/null
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/libgit2.pc
}
