# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=farstream
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.2.6
pkgrel=1
pkgdesc="Farstream (formerly Farsight) - Audio/Video Communications Framework"
arch=('any')
url="http://www.freedesktop.org/wiki/Software/Farstream"
groups=("${MINGW_PACKAGE_PREFIX}")
license=('LGPL')
options=('staticlibs' 'strip')
depends=("${MINGW_PACKAGE_PREFIX}-gst-plugins-base" "${MINGW_PACKAGE_PREFIX}-libnice")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "gtk-doc")
source=(http://freedesktop.org/software/${_realname}/releases/${_realname}/${_realname}-$pkgver.tar.gz{,.asc}
        0001-farstream-0.2.4.patch
        0002-give-cc-to-gir-scanner.all.patch)
md5sums=('63cecbb912e7a8b96d4470420327e2cc'
         'SKIP'
         '617b1a8fdbc5508ca03abc271f62a73b'
         '8a758676c230c58a687ff74784dd3ae5')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/0001-farstream-0.2.4.patch"
  patch -p1 -i "${srcdir}/0002-give-cc-to-gir-scanner.all.patch"

  WANT_AUTOMAKE=latest autoreconf --force --install
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-shared \
    --enable-static \
    --enable-introspection

  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make check
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install

  rm -f ${pkgdir}${MINGW_PREFIX}/lib/gstreamer*/*.a
  rm -f ${pkgdir}${MINGW_PREFIX}/lib/farstream*/*.a
}
