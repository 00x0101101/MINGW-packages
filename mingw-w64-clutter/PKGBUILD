# Maintainer: Lubosz Sarnecki <lubosz@gmail.com>

_realname=clutter
_tag=1.19.6
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.19.6.8694.1e07fd7
pkgrel=1
pkgdesc="A GObject based library for creating fast, visually rich graphical user interfaces. (mingw-w64)"
arch=('any')
url="http://clutter-project.org/"
license=("LGPL")
makedepends=('automake' 'libtool' 'intltool' 'itstool' 'gtk-doc' 'gnome-doc-utils' 'yelp-tools')
depends=("${MINGW_PACKAGE_PREFIX}-gobject-introspection"
        "${MINGW_PACKAGE_PREFIX}-atk"
        "${MINGW_PACKAGE_PREFIX}-json-glib"
        "${MINGW_PACKAGE_PREFIX}-cogl"
        )
options=(!libtool strip staticlibs)

source=("git://git.gnome.org/$_realname#tag=$_tag"
        "0001-msys2-add-header-for-BOOL-defintion.patch"
        "0002-msys2-add-CC-flags-to-gir-scanner.patch")

md5sums=("SKIP" 
         "3a92db8341129b0db058d47f97062f65"
         "993eabbeb6fa31af855a593956727e6f")

pkgver() {
  cd $_realname
  major=$(grep "major_version]," configure.ac | sed 's/m4_define(\[clutter_major_version\], \[//' | sed 's/\])//')
  minor=$(grep "minor_version]," configure.ac | sed 's/m4_define(\[clutter_minor_version\], \[//' | sed 's/\])//')
  micro=$(grep "micro_version]," configure.ac | head -1 | sed 's/m4_define(\[clutter_micro_version\], \[//' | sed 's/\])//')
  hash=$(git log --pretty=format:'%h' -n 1)
  revision=$(git rev-list --count HEAD)
  echo $major.$minor.$micro.$revision.$hash  
}

prepare() {
  cd $_realname
  git am ../0001-msys2-add-header-for-BOOL-defintion.patch
  git am ../0002-msys2-add-CC-flags-to-gir-scanner.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  cp -r "${srcdir}/${_realname}" "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --disable-static \
    --enable-shared \
    --enable-silent-rules \
    --disable-gtk-doc
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 DESTDIR="$pkgdir" install
}
