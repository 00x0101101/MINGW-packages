# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=gtk-doc
_mingw_suff=mingw-w64-${CARCH}
pkgname=("${_mingw_suff}-${_realname}")
pkgver=1.19
pkgrel=1
pkgdesc="Documentation tool for public library API (mingw-w64)"
arch=('any')
url="http://www.gtk.org/gtk-doc/"
license=("GPL" "FDL")
options=(staticlibs strip)
depends=("${_mingw_suff}-docbook-xsl"
		"${_mingw_suff}-gnome-doc-utils"
		"perl"
		"vim"
		#"source-highlight"
		)
makedepends=(
		#'jade'
		"${_mingw_suff}-libxml2"
		${_mingw_suff}-"python2"
		'itstool'
		#'dblatex'
		)
optdepends=(#"${_mingw_suff}-jade: SGML support"
            #"${_mingw_suff}-dblatex: PDF support"
			)
url="http://www.gtk.org/gtk-doc/"
source=(http://ftp.gnome.org/pub/gnome/sources/${_realname}/${pkgver}/${_realname}-${pkgver}.tar.xz
		0001-fix-xmlcatalog-defaulting.all.patch
		0002-extra-config-options.mingw.patch
		0003-force-exe-on.mingw.patch
		0004-fix-parallel-builds.all.patch
		0005-fudge-pkgconfig-invocation.mingw.patch)
md5sums=('1d1b5c074598415454a4be88228cc8f2'
         '1aa464e2167bcbe05e5e0047145076f7'
         '09170063492adfde1b431249d7d4377c'
         '10cd62576e2aa443e168883e48a98988'
         'ba6252d764cc1c8fd0809f5a81378abe'
         '453bc194dc011a8e70874c7f7425bfa3')

prepare() {
	cd "${srcdir}/${_realname}-${pkgver}"
	patch -p1 -i ${srcdir}/0001-fix-xmlcatalog-defaulting.all.patch
	patch -p1 -i ${srcdir}/0002-extra-config-options.mingw.patch
	patch -p1 -i ${srcdir}/0003-force-exe-on.mingw.patch
	#patch -p1 -i ${srcdir}/0004-fix-parallel-builds.all.patch
	#patch -p1 -i ${srcdir}/0005-fudge-pkgconfig-invocation.mingw.patch

	libtoolize --force --copy
	autoreconf -fi
}

build() {
	export MSYS2_ARG_CONV_EXCL="-//OASIS"
	mkdir -p "${srcdir}/build-${MINGW_CHOST}"
	cd "${srcdir}/build-${MINGW_CHOST}"
	export PYTHON=${MINGW_PREFIX}/bin/python2
	#export XMLCATALOG=/usr/bin/xmlcatalog
	../${_realname}-${pkgver}/configure \
		--prefix=${MINGW_PREFIX} \
		--build=${MINGW_CHOST} \
		--host=${MINGW_CHOST} \
		--enable-shared \
		--enable-static \
		--with-xml-catalog=${MINGW_PREFIX}/etc/xml/catalog
	make
}

package() {
	cd "${srcdir}/build-${MINGW_CHOST}"
	make DESTDIR="$pkgdir" install
	mkdir -p ${pkgdir}${MINGW_PREFIX}/lib
	mv ${pkgdir}${MINGW_PREFIX}/share/pkgconfig ${pkgdir}${MINGW_PREFIX}/lib/
}
