# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=octopi

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
#_qmake=${MINGW_PREFIX}/bin/qmake
_qmake=${MINGW_PREFIX}/qt5-static/bin/qmake
pkgver=r670.a897f15
pkgrel=1
pkgdesc="a powerful Pacman frontend using Qt libs"
arch=('any')
url="https://github.com/aarnt/octopi"
license=('GPL2')
makedepends=('git' "${MINGW_PACKAGE_PREFIX}-qt5-static" "${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-make")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}" "${MINGW_PACKAGE_PREFIX}-${_realname}-git")
_gitroot="git://github.com/aarnt/octopi.git"
_gitname="octopi"
#options=('debug' '!strip')
source=("${_gitname}"::"${_gitroot}"
        "0001-Use-Q_PID-instead-of-int-reducing-QProcess-repititio.patch"
        "0002-Use-a-shared-QProcessEnvironment.patch"
        "0003-Use-instead-of-QDir-separator.patch"
        "0004-Append-to-instead-of-overwrite-m_readAllStandardOutp.patch"
        "0005-Define-QT_WA.patch"
        "0006-MSYS2-Add-ectn_MSYS2-as-a-LinuxDistro.patch"
        "0007-MSYS2-Add-getMSYS2Root.patch"
        "0008-MSYS2-hasPacmanDatabase-getMSYS2Root-changes.patch"
        "0009-MSYS2-openRootTerminal-getSUCommand-isRootRunning.patch"
        "0010-MSYS2-Process-id-changes-WIP.patch"
        "0011-MSYS2-Prepend-getMSYS2Root-to-file-checks.patch")
md5sums=('SKIP'
         '47a1e70b80cd2b87840b4cafc5bfb4d3'
         '09aed9b3ce3c3806241937f92a5734db'
         'fb205748f4d70359650de16370edf7e3'
         '434c963796603b6d26d9f18454356dfb'
         'fc9c20f9658bbbda2a546a96d418dacf'
         'a56acf001acf84ec4b1f5d965cc24fbd'
         '58d788294c1a3d97d31338243b93822d'
         'a5a4725fa4ca818b5f6f669c3b3608cf'
         'c9b2ebb32eaf92719e67988f56b813e3'
         '3e5846ca295710a8c2835dcdb521916a'
         '60194fd23c428f677ffeb4c80801b435')

pkgver() {
    cd "$srcdir/$_realname"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/$_gitname"

  git am "${srcdir}"/0001-Use-Q_PID-instead-of-int-reducing-QProcess-repititio.patch
  git am "${srcdir}"/0002-Use-a-shared-QProcessEnvironment.patch
  git am "${srcdir}"/0003-Use-instead-of-QDir-separator.patch
  git am "${srcdir}"/0004-Append-to-instead-of-overwrite-m_readAllStandardOutp.patch
  git am "${srcdir}"/0005-Define-QT_WA.patch
  git am "${srcdir}"/0006-MSYS2-Add-ectn_MSYS2-as-a-LinuxDistro.patch
  git am "${srcdir}"/0007-MSYS2-Add-getMSYS2Root.patch
  git am "${srcdir}"/0008-MSYS2-hasPacmanDatabase-getMSYS2Root-changes.patch
  git am "${srcdir}"/0009-MSYS2-openRootTerminal-getSUCommand-isRootRunning.patch
  git am "${srcdir}"/0010-MSYS2-Process-id-changes-WIP.patch
  git am "${srcdir}"/0011-MSYS2-Prepend-getMSYS2Root-to-file-checks.patch
}

build() {
  local _config
  cd ${srcdir}
  if check_option "debug" "n"; then
    _config="release"
  else
    _config="debug"
  fi

  rm -rf build-${MINGW_CHOST}
  mkdir build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  msg "Building Octopi..."
  ${_qmake} ../$_gitname/$_gitname.pro CONFIG+="c++11 ${_config}"
  mingw32-make ${_config}

  mkdir -p notifier/pacmanhelper
  pushd notifier/pacmanhelper
  msg "Building pacmanhelper..."
  ${_qmake} ../../../$_gitname/notifier/pacmanhelper/pacmanhelper.pro CONFIG+="${_config}"
  mingw32-make ${_config}
  popd

  mkdir -p notifier/$_gitname-notifier
  pushd notifier/$_gitname-notifier
  msg "Building $_gitname-notifier..."
  ${_qmake} ../../../$_gitname/notifier/$_gitname-notifier/$_gitname-notifier.pro CONFIG+="release"
  mingw32-make ${_config}
  popd
}

package() {
  mkdir -p ${pkgdir}${MINGW_PREFIX}
  pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
  PREFIX_WIN=$(pwd -W)
  popd > /dev/null

  cd "$srcdir/build-${MINGW_CHOST}"

  install -D -m755 bin/$_gitname                                          ${pkgdir}${MINGW_PREFIX}/bin/$_gitname
  install -D -m755 notifier/bin/$_gitname-notifier                        ${pkgdir}${MINGW_PREFIX}/bin/$_gitname-notifier
  install -D -m644 $srcdir/$_gitname/resources/images/${_gitname}_red.png ${pkgdir}${MINGW_PREFIX}/share/icons/$_gitname.png

  #services
  install -D -m755 notifier/bin/pacmanhelper                                    ${pkgdir}${MINGW_PREFIX}/lib/octopi/pacmanhelper

  install -D -m644 $srcdir/$_gitname/notifier/pacmanhelper/polkit/org.octopi.pacman.policy        ${pkgdir}${MINGW_PREFIX}/share/polkit-1/actions/org.octopi.pacman.policy
  install -D -m644 $srcdir/$_gitname/notifier/pacmanhelper/polkit/org.octopi.pacmanhelper.conf    ${pkgdir}${MINGW_PREFIX}/etc/dbus-1/system.d/org.octopi.pacmanhelper.conf
  install -D -m644 $srcdir/$_gitname/notifier/pacmanhelper/polkit/org.octopi.pacmanhelper.xml     ${pkgdir}${MINGW_PREFIX}/share/dbus-1/interfaces/org.octopi.pacmanhelper.xml
  install -D -m644 $srcdir/$_gitname/notifier/pacmanhelper/polkit/org.octopi.pacmanhelper.service ${pkgdir}${MINGW_PREFIX}/share/dbus-1/system-services/org.octopi.pacmanhelper.service
}
