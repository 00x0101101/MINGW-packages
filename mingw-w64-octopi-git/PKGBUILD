# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=octopi

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
#_qmake=${MINGW_PREFIX}/bin/qmake
_qmake=${MINGW_PREFIX}/qt5-static/bin/qmake
pkgver=r669.3a5a95b
pkgrel=1
pkgdesc="a powerful Pacman frontend using Qt libs"
arch=('any')
url="https://github.com/aarnt/octopi"
license=('GPL2')
makedepends=('git' "${MINGW_PACKAGE_PREFIX}-qt5-static" "${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-make")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}" "${MINGW_PACKAGE_PREFIX}-${_realname}-git")
_gitroot="git://github.com/aarnt/octopi.git"
_gitname="octopi"
options=('!debug' 'strip')
source=("${_gitname}"::"${_gitroot}"
        "0001-Use-Q_PID-instead-of-int-reducing-QProcess-repititio.patch"
        "0002-Use-a-shared-QProcessEnvironment.patch"
        "0003-Define-QT_WA.patch"
        "0004-MSYS2-Add-ectn_MSYS2-as-a-LinuxDistro.patch"
        "0005-MSYS2-Add-getMSYS2Root.patch"
        "0006-MSYS2-hasPacmanDatabase-getMSYS2Root-changes.patch"
        "0007-MSYS2-openRootTerminal-getSUCommand-isRootRunning.patch"
        "0008-MSYS2-Process-id-changes-WIP.patch"
        "0009-MSYS2-Hack-around-crash-in-refreshTabFiles.patch")
md5sums=('SKIP'
         'fcdd32a9572ffced35f57f7f46db1820'
         'd262cdaa522b0e59691cd0a525530c8b'
         '11e8baa5f61075d38917fc01f42ecb8c'
         '98d19f39fee5e12a8991e22ecfc3c4b1'
         'aa445f3a99c7b18ce94c258439e517c5'
         '7a1d2a340764ecec0b0cbc1fe68cf6b2'
         '92ad3ec27964331c7b89283a30e65736'
         '173b6b8d21151f3f31b850ef38bc6658'
         '9b6e01ebbabc7158f91cdbeed7112faf')

pkgver() {
    cd "$srcdir/$_realname"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/$_gitname"

  git am "${srcdir}"/0001-Use-Q_PID-instead-of-int-reducing-QProcess-repititio.patch
  git am "${srcdir}"/0002-Use-a-shared-QProcessEnvironment.patch
  git am "${srcdir}"/0003-Define-QT_WA.patch
  git am "${srcdir}"/0004-MSYS2-Add-ectn_MSYS2-as-a-LinuxDistro.patch
  git am "${srcdir}"/0005-MSYS2-Add-getMSYS2Root.patch
  git am "${srcdir}"/0006-MSYS2-hasPacmanDatabase-getMSYS2Root-changes.patch
  git am "${srcdir}"/0007-MSYS2-openRootTerminal-getSUCommand-isRootRunning.patch
  git am "${srcdir}"/0008-MSYS2-Process-id-changes-WIP.patch
  git am "${srcdir}"/0009-MSYS2-Hack-around-crash-in-refreshTabFiles.patch
#  git am "${srcdir}"/0010-Debugging.patch
}

build() {
  local _config
  cd ${srcdir}
  if check_option "debug" "n"; then
    _config="release"
  else
    _config="debug"
  fi

  rm -rf build-${MINGW_CHOST}
  mkdir build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  msg "Building Octopi..."
  ${_qmake} ../$_gitname/$_gitname.pro CONFIG+="c++11 ${_config}"
  mingw32-make ${_config}

  mkdir -p notifier/pacmanhelper
  pushd notifier/pacmanhelper
  msg "Building pacmanhelper..."
  ${_qmake} ../../../$_gitname/notifier/pacmanhelper/pacmanhelper.pro CONFIG+="${_config}"
  mingw32-make ${_config}
  popd

  mkdir -p notifier/$_gitname-notifier
  pushd notifier/$_gitname-notifier
  msg "Building $_gitname-notifier..."
  ${_qmake} ../../../$_gitname/notifier/$_gitname-notifier/$_gitname-notifier.pro CONFIG+="release"
  mingw32-make ${_config}
  popd
}

package() {
  mkdir -p ${pkgdir}${MINGW_PREFIX}
  pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
  PREFIX_WIN=$(pwd -W)
  popd > /dev/null

  cd "$srcdir/build-${MINGW_CHOST}"

  install -D -m755 bin/$_gitname                                          ${pkgdir}${MINGW_PREFIX}/bin/$_gitname
  install -D -m755 notifier/bin/$_gitname-notifier                        ${pkgdir}${MINGW_PREFIX}/bin/$_gitname-notifier
  install -D -m644 $srcdir/$_gitname/resources/images/${_gitname}_red.png ${pkgdir}${MINGW_PREFIX}/share/icons/$_gitname.png

  #services
  install -D -m755 notifier/bin/pacmanhelper                                    ${pkgdir}${MINGW_PREFIX}/lib/octopi/pacmanhelper

  install -D -m644 $srcdir/$_gitname/notifier/pacmanhelper/polkit/org.octopi.pacman.policy        ${pkgdir}${MINGW_PREFIX}/share/polkit-1/actions/org.octopi.pacman.policy
  install -D -m644 $srcdir/$_gitname/notifier/pacmanhelper/polkit/org.octopi.pacmanhelper.conf    ${pkgdir}${MINGW_PREFIX}/etc/dbus-1/system.d/org.octopi.pacmanhelper.conf
  install -D -m644 $srcdir/$_gitname/notifier/pacmanhelper/polkit/org.octopi.pacmanhelper.xml     ${pkgdir}${MINGW_PREFIX}/share/dbus-1/interfaces/org.octopi.pacmanhelper.xml
  install -D -m644 $srcdir/$_gitname/notifier/pacmanhelper/polkit/org.octopi.pacmanhelper.service ${pkgdir}${MINGW_PREFIX}/share/dbus-1/system-services/org.octopi.pacmanhelper.service
}
