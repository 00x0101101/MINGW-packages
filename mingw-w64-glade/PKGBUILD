# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=glade
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=3.16.1
pkgrel=2
pkgdesc="User interface builder for GTK+ and GNOME (mingw-w64)"
arch=('any')
url="http://glade.gnome.org"
license=("GPL" "LGPL")
options=('strip' 'staticlibs')
depends=(
		"${_mingw_suff}-gtk3"
		"${_mingw_suff}-libxml2"
		"${_mingw_suff}-hicolor-icon-theme"
		)
makedepends=("${_mingw_suff}-gcc"
		"${_mingw_suff}-pkg-config"
		'intltool'
		"${_mingw_suff}-gtk-doc"
		"${_mingw_suff}-gobject-introspection"
		"${_mingw_suff}-python2-gobject2"
		'itstool' 
		'docbook-xsl')
optdepends=("${_mingw_suff}-python2: Python widgets support"
            #'devhelp: help browser'
			)
url="http://glade.gnome.org/"
install=glade-${CARCH}.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${_realname}/${pkgver%.*}/${_realname}-$pkgver.tar.xz
		0004-give-cc-to-gir-scanner.all.patch
		0005-give-gtk-cflags-to-gir.patch
		0006-disable-gnome-help.patch)
md5sums=('1b60003e91a19cc0f1acae24d30269fc'
         'fb4d5a33bbfea33b6437c85a9ac567c8'
         '294197385457bdc58905a32b6d0e9e1e'
         '3ebefeb0d48ff89f7992e5046ce9f09c')

prepare() {
	cd "${srcdir}/${_realname}-${pkgver}"
	patch -p1 -i ${srcdir}/0004-give-cc-to-gir-scanner.all.patch
	patch -p1 -i ${srcdir}/0005-give-gtk-cflags-to-gir.patch
	patch -p1 -i ${srcdir}/0006-disable-gnome-help.patch
	autoreconf -fi
}

build() {
	cd "${srcdir}/${_realname}-${pkgver}"
	export PYTHON=${MINGW_PREFIX}/bin/python2
	./configure \
		--prefix=${MINGW_PREFIX} \
		--build=${MINGW_CHOST} \
		--host=${MINGW_CHOST} \
		--disable-static \
		--enable-introspection \
		--enable-shared \
		--disable-gtk-doc \
		--enable-gladeui \
		--disable-man-pages

	make VERBOSE=1
}

package() {
	cd "${srcdir}/${_realname}-${pkgver}"
	make -j1 DESTDIR="$pkgdir" install
}
