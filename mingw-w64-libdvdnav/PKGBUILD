# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libdvdnav

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.2.1
pkgrel=1
pkgdesc="The library for xine-dvdnav plugin (mingw-w64)"
arch=('any')
url="http://dvdnav.mplayerhq.hu/"
license=("GPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-libdvdread")
options=('staticlibs' 'strip')
source=("http://dvdnav.mplayerhq.hu/releases/${_realname}-${pkgver}.tar.xz"
        0001-fix-dvdread-dependency-for-mini.mingw.patch)
sha256sums=('7fca272ecc3241b6de41bbbf7ac9a303ba25cb9e0c82aa23901d3104887f2372'
            '828181a341b2ca4a873788a4c2d9b9120d2f13371790d03b03339511804c5c51')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0001-fix-dvdread-dependency-for-mini.mingw.patch
  
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "${srcdir}/${_realname}-$pkgver"
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-shared \
    --enable-static
  make
}

package() {
  cd "${srcdir}/${_realname}-$pkgver"
  make DESTDIR="$pkgdir" install
}
