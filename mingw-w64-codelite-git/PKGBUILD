# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=codelite
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=6.1.1.34.g5bc1fb4
pkgrel=1
pkgdesc="Open-source, cross platform IDE for the C/C++ programming languages (mingw-w64)"
arch=('any')
license=('GPL')
url="http://www.codelite.org"
depends=("${MINGW_PACKAGE_PREFIX}-hunspell"
        "${MINGW_PACKAGE_PREFIX}-libssh"
        "${MINGW_PACKAGE_PREFIX}-wxWidgets"
        #"${MINGW_PACKAGE_PREFIX}-drmingw"
        "${MINGW_PACKAGE_PREFIX}-gcc-libs")
optdepends=("${MINGW_PACKAGE_PREFIX}-clang")
makedepends=("${MINGW_PACKAGE_PREFIX}-boost"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-clang")
options=(strip staticlibs !debug)
source=("${_realname}::git+https://github.com/eranif/codelite.git"
        codelite-clang-deprecated.patch
        codelite-mingw-relocate.patch
        resource-compile.patch
        typo-fix.patch)
md5sums=('SKIP'
         '4bec2ac130687bd4b947b98a0d5d3fd4'
         'd7a7fbb9d6115650cb8898f8007c8ca3'
         '88962296163068819cf3418c23ce39e8'
         'd122425a7c7859d0c9061896502bfad3')

pkgver() {
  cd "$_realname"
  git describe --tags | sed 's|-|.|g'
}

prepare() {
  cd "$srcdir/$_realname"
  patch -p1 -i ${srcdir}/codelite-clang-deprecated.patch
  patch -p1 -i ${srcdir}/codelite-mingw-relocate.patch
  patch -p1 -i ${srcdir}/resource-compile.patch
  patch -p1 -i ${srcdir}/typo-fix.patch
}

build() {
  local -a extra_config

  if check_option "debug" "y"; then
    extra_config+=( -DCMAKE_BUILD_TYPE=Debug )
  else
    extra_config+=( -DCMAKE_BUILD_TYPE=Release )
  fi
  
  cd $srcdir/${_realname}

  CPPFLAGS+=" -fno-devirtualize"
  CFLAGS+=" -fno-devirtualize"
  CXXFLAGS+=" -fno-devirtualize"
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DPREFIX=${pkgdir}${MINGW_PREFIX} \
    -DENABLE_CLANG=1 \
    -DWITH_WEBVIEW=1 \
    -DwxWidgets_CONFIG_EXECUTABLE=${MINGW_PREFIX}/bin/wx-config \
    -DwxWidgets_wxrc_EXECUTABLE=${MINGW_PREFIX}/bin/wxrc-3.0 \
    "${extra_config[@]}" \
    .

  make V=1 # -j1 VERBOSE=1
}

package() {  
  cd "$srcdir/${_realname}"
  make install
  
  rm -f ${pkgdir}${MINGW_PREFIX}/lib/codelite/*.a
  rm -f ${pkgdir}${MINGW_PREFIX}/lib/*.a
}

