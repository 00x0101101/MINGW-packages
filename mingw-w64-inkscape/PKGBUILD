# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=inkscape
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=0.48.4
pkgrel=1
pkgdesc="Vector graphics editor using the SVG file format (mingw-w64)"
arch=('any')
url="http://inkscape.sourceforge.net/"
license=("GPL" "LGPL")
makedepends=("${_mingw_suff}-gcc"
		"${_mingw_suff}-pkg-config"
		"${_mingw_suff}-boost"
		"intltool")
depends=(
		"${_mingw_suff}-gc"
		"${_mingw_suff}-gsl"
		"${_mingw_suff}-gtkmm"
		#"${_mingw_suff}-gtkspell"
		"${_mingw_suff}-imagemagick"
		"${_mingw_suff}-libxslt"
		"${_mingw_suff}-poppler"
		"${_mingw_suff}-python2"
		"${_mingw_suff}-popt"
		"${_mingw_suff}-libwpg"
		#"${_mingw_suff}-desktop-file-utils"
		#"${_mingw_suff}-hicolor-icon-theme"
		)
optdepends=(#"${_mingw_suff}-pstoedit: latex formulas"
            #"${_mingw_suff}-texlive-core: latex formulas"
            #"${_mingw_suff}-python2-numpy: some extensions"
            #"${_mingw_suff}-python2-lxml: some extensions and filters"
            #"${_mingw_suff}-uniconvertor: reading/writing to some proprietary formats"
			)
options=('staticlibs' 'strip')
source=("http://downloads.sourceforge.net/project/${_realname}/${_realname}/${pkgver}/${_realname}-${pkgver}.tar.bz2"
		'spuriouscomma.patch'
		'freetype.patch'
		'inkscape-0.48.4-gc74.patch'
		'headers-fix.patch')
sha1sums=('5f26f6ad191d1e7c2a9fb69a438722beb172224c'
          '7d1d5a6d1d2b0926721a994d5889c52890fc57c1'
          '3b08c1f3b53d1ca421d29fb886a75fb7a3563510'
          '9d39d8bcc1d0c574f211169c24da16e796ca380b'
          '12fddb0a0b34b4308de1979d13b26d05a6ec8f77')

prepare() {
	cd "${srcdir}/${_realname}-${pkgver}"
	patch -p0 -i ${srcdir}/freetype.patch # FS#37956
	patch -p0 -i ${srcdir}/spuriouscomma.patch
	patch -p1 -i ${srcdir}/inkscape-0.48.4-gc74.patch
	patch -p1 -i ${srcdir}/headers-fix.patch
	
	sed -i 's|/usr/bin/python\>|/usr/bin/python2|g' cxxtest/*.py
	sed -i 's|/usr/bin/env python\>|/usr/bin/env python2|g' share/*/{test/,}*.py
	sed -i 's|"python" },|"python2" },|g' src/extension/implementation/script.cpp
	sed -i 's|python -c|python2 -c|g' configure share/extensions/uniconv*.py
	sed -i 's|"python"|"python2"|g' src/main.cpp
	sed -i '/^#include <g.kmm/i #include <glibmm.h>' src/*{,/*{,/*{,/*}}}.{h,cpp}
}

build() {
	# export lt_cv_deplibs_check_method='pass_all'
	cd "${srcdir}/${_realname}-${pkgver}"
	mkdir -p build-${MINGW_CHOST} && cd build-${MINGW_CHOST}	

	CFLAGS+=" -D__USE_MINGW_ANSI_STDIO=1"
	CXXFLAGS+=" -D__USE_MINGW_ANSI_STDIO=1"
	../configure \
		--prefix=${MINGW_PREFIX} \
		--build=${MINGW_CHOST} \
		--host=${MINGW_CHOST} \
		--with-python \
		--without-perl \
		--enable-lcms \
		--enable-poppler-cairo \
		--disable-dependency-tracking
	make #-j1 VERBOSE=1
}

package() {
	cd ${_realname}-${pkgver}/build-${MINGW_CHOST}
	make DESTDIR="${pkgdir}" install
	#rm -r "${pkgdir}${MINGW_PREFIX}/share"
}
