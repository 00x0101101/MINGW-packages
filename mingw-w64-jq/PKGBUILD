# Maintainer: Gore Liu <goreliu@126.com>

_realname=jq
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.5
pkgrel=1
pkgdesc="Command-line JSON processor (mingw-w64)"
arch=('any')
url='https://stedolan.github.io/jq/'
license=('MIT')
makedepends=("autoconf" "automake" "bison" "flex" "python2")
source=("https://github.com/stedolan/${_realname}/releases/download/${_realname}-1.5/${_realname}-${pkgver}.tar.gz")
sha256sums=('c4d2bfec6436341113419debf479d833692cc5cdab7eb0326b5a4d4fbe9f493c')

build() {
  cd "$srcdir"/${_realname}-${pkgver}
  [ -d "${srcdir}"/build-${CARCH} ] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-static \
    --enable-shared

  make
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make install DESTDIR="${pkgdir}"
}
