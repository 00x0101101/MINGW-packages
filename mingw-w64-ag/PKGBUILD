# Maintainer: Martell Malone <martellmalone@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_realname=ag
_longname=the_silver_searcher
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.29.1
pkgrel=2
pkgdesc="The Silver Searcher: An attempt to make something better than ack, which itself is better than grep (mingw-w64)"
arch=('any')
url="http://geoff.greer.fm/ag"
license=("Apache")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-pcre"
         "${MINGW_PACKAGE_PREFIX}-xz"
         "${MINGW_PACKAGE_PREFIX}-zlib")
options=('staticlibs') # '!strip' 'debug')
source=(http://geoff.greer.fm/ag/releases/$_longname-$pkgver.tar.gz
        "0001-lang-allow-full-filenames-as-well-as-extensions.patch"
        "0002-lang-autotools-new.patch"
        "0003-lang-make-add-Makefile-Makefile-Debug-Makefile-Release.patch"
        "0004-mingw-build.patch"
        "0005-lang-makepkg-new.patch")
md5sums=('be0cb0f64582e758206f5bd7386d4373'
         '42bb985a2e8e6b7bea407e44269cc4d8'
         '70b62fa84b9caeb025f8e4a57bf96f65'
         '40aa7b0c03472ee141cf5afe2343ef04'
         '0dbfdf4ddba43446de0f4941844dfcfd'
         'e246c39f073615bedb4ec7d79db6d82b')

prepare() {
  cd "${srcdir}"/${_longname}-${pkgver}
  patch -p1 -i "${srcdir}"/0001-lang-allow-full-filenames-as-well-as-extensions.patch
  patch -p1 -i "${srcdir}"/0002-lang-autotools-new.patch
  patch -p1 -i "${srcdir}"/0003-lang-make-add-Makefile-Makefile-Debug-Makefile-Release.patch
  patch -p1 -i "${srcdir}"/0004-mingw-build.patch
  patch -p1 -i "${srcdir}"/0005-lang-makepkg-new.patch

  # configure.ac forces -O2, so force it to -O0 if debugging.
  if check_option "debug" "y"; then
    sed -i "s#-O2#-O0#g" configure.ac
  fi

  autoreconf -fi
}

build() {

  mkdir -p "${srcdir}/build-${CARCH}"
  cd "${srcdir}/build-${CARCH}"

  "${srcdir}"/${_longname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --{build,host}=${MINGW_CHOST}

  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR="${pkgdir}" install
}
