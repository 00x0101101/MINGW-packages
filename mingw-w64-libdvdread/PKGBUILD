# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libdvdread

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.9.9
pkgrel=1
pkgdesc="Provides a simple foundation for reading DVD video disks (mingw-w64)"
arch=('any')
url="http://dvdnav.mplayerhq.hu"
license=("GPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-libdvdcss")
options=('staticlibs' 'strip')
source=("http://dvdnav.mplayerhq.hu/releases/${_realname}-${pkgver}.tar.xz"
        0001-no-undefined-on.all.patch)
sha256sums=('d91275471ef69d488b05cf15c60e1cd65e17648bfc692b405787419f47ca424a'
            '0a9664cce3a2e001445d150edfe5d8f06f3ca0d1affb91b50b1b5bde90c2713e')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0001-no-undefined-on.all.patch
  
  autoreconf -fi
}

build() {
  cd "${srcdir}/${_realname}-$pkgver"
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-shared \
    --enable-static \
    --disable-dlfcn \
    --with-libdvdcss
  make
}

package() {
  cd "${srcdir}/${_realname}-$pkgver"
  make DESTDIR="$pkgdir" install
}
