# Maintainer: Lubosz Sarnecki <lubosz@gmail.com>

_realname=gst-editing-services
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.3.0.1.1910.7e5742e
pkgrel=1
pkgdesc="GStreamer Editing Services (mingw-w64)"
arch=('any')
url="http://gstreamer.freedesktop.org/"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-python2")
depends=("${MINGW_PACKAGE_PREFIX}-gst-plugins-base"
         "${MINGW_PACKAGE_PREFIX}-gstreamer")
options=(!libtool strip staticlibs)
source=("git://anongit.freedesktop.org/gstreamer/$_realname"
		0001-make-mingw-find-gir-scanner.patch
		0002-add-cflags-for-gir.patch
		)
md5sums=("SKIP" 
		 "38b10c073e9ccd3dfbeab28e151fdc9a"
		 "650a069277d5b0e85f8be15948e2d03a")

pkgver() {
  cd $_realname
  version=$(grep AC_INIT configure.ac | sed 's/AC_INIT(GStreamer Editing Services, //' | sed 's/,//')
  hash=$(git log --pretty=format:'%h' -n 1)
  revision=$(git rev-list --count HEAD)
  echo $version.$revision.$hash  
}

prepare() {
  cd $_realname
  git am ../0001-make-mingw-find-gir-scanner.patch
  git am ../0002-add-cflags-for-gir.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  cp -r "${srcdir}/${_realname}" "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --disable-static \
    --enable-shared \
    --enable-silent-rules \
    --enable-experimental \
    --disable-gtk-doc
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 DESTDIR="$pkgdir" install
  find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
  rm -f ${pkgdir}${MINGW_PREFIX}/lib/gstreamer*/*.a
}
