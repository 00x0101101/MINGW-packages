# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gtk2
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=2.24.22
pkgrel=1
pkgdesc="GTK+ is a multi-platform toolkit (v2) (mingw-w64)"
arch=('any')
url="http://www.gtk.org"
license=("LGPL")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config") # gtk-update-icon-cache
depends=(
		"${_mingw_suff}-crt"
		"${_mingw_suff}-atk>=1.29.2"
		"${_mingw_suff}-pango>=1.20"
		"${_mingw_suff}-glib2>=2.28.0"
		"${_mingw_suff}-cairo>=1.6"
		"${_mingw_suff}-gdk-pixbuf2>=2.21.0"
)
options=(!libtool strip staticlibs)
source=(
"http://ftp.gnome.org/pub/gnome/sources/gtk+/${pkgver%.*}/gtk+-${pkgver}.tar.xz")

md5sums=("5fbbfb7637bbd571a572a2dae0e736d2")

build() {
	cd "${srcdir}/gtk+-${pkgver}"
	CFLAGS+=" -mms-bitfields"
	CXXFLAGS+=" -mms-bitfields"
	mkdir -p "${srcdir}/build-${MINGW_CHOST}"
	cd "${srcdir}/build-${MINGW_CHOST}"
	rm "${srcdir}/gtk+-${pkgver}/gtk/gtk.def"
	../gtk+-${pkgver}/configure \
		--prefix=${MINGW_PREFIX} \
		--build=${MINGW_CHOST} \
		--host=${MINGW_CHOST} \
		--with-gdktarget=win32 \
		--disable-modules \
		--disable-cups \
		--disable-papi \
		--enable-static \
		--enable-shared \
		--disable-glibtest
	make
}

package() {
	cd "${srcdir}/build-${MINGW_CHOST}"
	make -j1 DESTDIR="$pkgdir" install
	find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
	rm -r "${pkgdir}${MINGW_PREFIX}/etc"
	rm -r "${pkgdir}${MINGW_PREFIX}/share/gtk-2.0"
}
