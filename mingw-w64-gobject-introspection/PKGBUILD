# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gobject-introspection
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=1.39.90
pkgrel=1
pkgdesc="GObject Introspection can scan C header and source files in order to generate introspection typelib files (mingw-w64)"
arch=('any')
url="https://live.gnome.org/GObjectIntrospection"
license=("LGPL")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config" "${_mingw_suff}-python2")
depends=(
		"${_mingw_suff}-gcc-libs"
		"${_mingw_suff}-glib2"
		"${_mingw_suff}-cairo"
		"${_mingw_suff}-libffi"
		)
options=('strip' 'staticlibs')

source=(http://ftp.gnome.org/pub/GNOME/sources/gobject-introspection/${pkgver%.*}/${_realname}-${pkgver}.tar.xz
		0003-fix-exeext.mingw.patch
		0005-quote-path.mingw.patch
		0006-w32-compat.mingw.patch
		0007-w32-subproc.mingw.patch
		0008-more.mingw.patch
		0009-make-libs.mingw.patch
		0010-fix-gcc-options.mingw.patch
		0011-exefix.mingw.patch
		0012-fix-runtime-prefix.mingw.patch
		0014-asm-volatile.mingw.patch
		0015-inline.mingw.patch
		0016-restrict.mingw.patch
		0017-correct-exeext.mingw.patch
		0018-debug-rmtree-errors.mingw.patch
		0020-fix-shlib-resolution.mingw.patch
		0021-scanner-Make-shlib-Resolution-Work-Also-With-Visual-.all.patch
		0022-tmp.mingw.patch
		0050-dont-load-msvcrt.patch
		0055-fix-python-detection.patch
		)

md5sums=('029ff2fe9cfc33b2d93f64481bd5fb80'
         '4b883aa4b536dc3eb3de9615da956831'
         '0ae62e46cad38440a75d6b7a29e3a34b'
         '41c6afff2fdac05215d42c1277ac0f54'
         '9c77e42cca10ef062d85d1ad556b6d8e'
         '7caaf026566ec311cc41e3c5199cdb18'
         '3468d4122f969699cd1284295e0d562b'
         '71d8873063d48d04b16194ebe82bd0f6'
         '771572e7761b1c7442fa39eed385fa79'
         'f164bfa9b3caa66dc664131a729b3a70'
         'b75603196af084e453491a7945fbd98f'
         'b00342ad289d8ae739d4676da9783c0d'
         'bcad49d111ac0d9c62598505ad271b9c'
         '89588f3ca415081ae2ad71b19c92e3b3'
         'f0ecde986ebf1f0e28b752f9c36fb6c1'
         '4405ff31670a9bae1c7d59176b765ad4'
         'c43416070a6d0d0fd1e5436b0eb50aa8'
         '5e1f1929aa782a33a7d75d1027e1ba7b'
         'e3598d539258678eef8dde2216419faf'
         '6d809bf266e42a20b31a7acbaf0384d1')

prepare() {
	cd ${srcdir}/${_realname}-${pkgver}
	patch -p1 -i ${srcdir}/0003-fix-exeext.mingw.patch
	patch -p1 -i ${srcdir}/0005-quote-path.mingw.patch
	patch -p1 -i ${srcdir}/0006-w32-compat.mingw.patch
	patch -p1 -i ${srcdir}/0007-w32-subproc.mingw.patch
	patch -p1 -i ${srcdir}/0008-more.mingw.patch
	patch -p1 -i ${srcdir}/0009-make-libs.mingw.patch
	patch -p1 -i ${srcdir}/0010-fix-gcc-options.mingw.patch
	patch -p1 -i ${srcdir}/0011-exefix.mingw.patch
	patch -p1 -i ${srcdir}/0012-fix-runtime-prefix.mingw.patch
	patch -p1 -i ${srcdir}/0014-asm-volatile.mingw.patch
	patch -p1 -i ${srcdir}/0015-inline.mingw.patch
	patch -p1 -i ${srcdir}/0016-restrict.mingw.patch
	patch -p1 -i ${srcdir}/0017-correct-exeext.mingw.patch
	patch -p1 -i ${srcdir}/0018-debug-rmtree-errors.mingw.patch
	patch -p1 -i ${srcdir}/0020-fix-shlib-resolution.mingw.patch
	patch -p1 -i ${srcdir}/0021-scanner-Make-shlib-Resolution-Work-Also-With-Visual-.all.patch
	patch -p1 -i ${srcdir}/0022-tmp.mingw.patch
	patch -p1 -i ${srcdir}/0050-dont-load-msvcrt.patch
	patch -p1 -i ${srcdir}/0055-fix-python-detection.patch

	autoreconf -fi
}

build() {
	export PYTHON=${MINGW_PREFIX}/bin/python2
	
	pushd ${MINGW_PREFIX} > /dev/null
	export PREFIX_DEPS=`pwd -W`
	popd > /dev/null

	CFLAGS+=" -mms-bitfields"
	CXXFLAGS+=" -mms-bitfields"
	mkdir -p "${srcdir}/build-${MINGW_CHOST}"
	cd "${srcdir}/build-${MINGW_CHOST}"
	../${_realname}-${pkgver}/configure \
		--prefix=${MINGW_PREFIX} \
		--build=${MINGW_CHOST} \
		--host=${MINGW_CHOST} \
		--enable-silent-rules
	make -j1
}

package() {
	cd "${srcdir}/build-${MINGW_CHOST}"
	make -j1 DESTDIR="$pkgdir" install
	find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' -o -name '*.manifest' | xargs -rtl1 rm
	#rm -r "${pkgdir}${MINGW_PREFIX}/share/man"
}
