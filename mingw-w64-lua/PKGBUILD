# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=lua
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=5.2.4
pkgrel=1
pkgdesc="A powerful light-weight programming language designed for extending applications. (mingw-w64)"
arch=('any')
url="http://www.lua.org/"
license=('MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
options=('staticlibs' 'strip' 'emptydirs')
source=("$url/ftp/lua-$pkgver.tar.gz"
        'lua.pc'
        'implib.patch')
md5sums=('913fdb32207046b273fdb17aad70be13'
         'e7ba6c2b695b0b84a5ea0cbff5fc9067'
         '39ed0d1e9eece585f7c0f2c49136dd01')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  cp "${srcdir}/lua.pc" .
  patch -p0 -i "${srcdir}/implib.patch"

  mkdir -p "${srcdir}/build-${CARCH}"
  cp -r * "${srcdir}/build-${CARCH}"
}

build() {
  cd "${srcdir}/build-${CARCH}"
  sed -e "s|%VER%|${pkgver%.*}|g;s|%REL%|$pkgver|g" \
    -e "s|/usr|${MINGW_PREFIX}|g" \
    -i lua.pc

  make -j1 \
    AR="ar rcu" \
    RANLIB="ranlib" \
    STRIP="strip" \
    CC="${MINGW_CHOST}-gcc" \
    mingw
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make \
    TO_BIN="lua.exe luac.exe lua52.dll" \
    TO_LIB="liblua.a liblua.dll.a" \
    INSTALL_TOP="${pkgdir}${MINGW_PREFIX}" \
    INSTALL_MAN="${pkgdir}${MINGW_PREFIX}"/share/man/man1 \
    install
  install -Dm644 lua.pc "${pkgdir}${MINGW_PREFIX}"/lib/pkgconfig/lua.pc
  install -Dm644 lua.pc "${pkgdir}${MINGW_PREFIX}"/lib/pkgconfig/lua5.2.pc

  # Install the documentation
  install -d "${pkgdir}${MINGW_PREFIX}"/share/doc/lua
  install -m644 doc/*.{gif,png,css,html} "${pkgdir}${MINGW_PREFIX}"/share/doc/lua
}
