# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=aria2
MINGW_PACKAGE_PREFIX="mingw-w64-${CARCH}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.18.8
pkgrel=2
pkgdesc="A multi-protocol & multi-source, cross platform download utility"
arch=('any')
url="http://aria2.sourceforge.net/"
license=('GPL2')
makedepends=(
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-bison"
             "${MINGW_PACKAGE_PREFIX}-gcc")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-gettext"
         "${MINGW_PACKAGE_PREFIX}-c-ares"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-sqlite3"
         "${MINGW_PACKAGE_PREFIX}-gmp"
         "${MINGW_PACKAGE_PREFIX}-nettle"
         "${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-libgcrypt"
         "${MINGW_PACKAGE_PREFIX}-libiconv"
         "${MINGW_PACKAGE_PREFIX}-expat"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-libuv")
source=("http://sourceforge.net/projects/aria2/files/stable/$_realname-$pkgver/$_realname-$pkgver.tar.xz"
        '0001-libintl-vprintf.patch')
md5sums=('3e03b8a6faf36fd76e6ffb58b5d6f6a5'
         '19f241142f8019d52719d2f405d27452')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p0 -i "${srcdir}/0001-libintl-vprintf.patch"
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # add missing library
  export LIBS=-lintl

  # disabling gnutls and wintls so we will fall back to openssl
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --enable-static \
    --enable-shared \
    --without-gnutls \
    --without-wintls \
    --enable-libaria2
  make
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
