# Maintainer: AlexWMF <alexxwmf@gmail.com>
 
_realname=libvmime
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=r997.03a0e36
pkgrel=1
pkgdesc="An open source solution for working with MIME messages and Internet messaging services like IMAP, POP or SMTP."
url="http://www.vmime.org/"
arch=('any')
license=('GPL3')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-cmake")
depends=("${MINGW_PACKAGE_PREFIX}-boost"
        "${MINGW_PACKAGE_PREFIX}-icu"
        "${MINGW_PACKAGE_PREFIX}-gnutls"
        "${MINGW_PACKAGE_PREFIX}-gsasl"
        "${MINGW_PACKAGE_PREFIX}-gtk3"
        "${MINGW_PACKAGE_PREFIX}-libiconv"
        "${MINGW_PACKAGE_PREFIX}-openssl")
source=("${_realname}"::"git+https://github.com/kisli/vmime.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_realname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${_realname}"
}
 
build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${pkgdir}${MINGW_PREFIX} \
    -DVMIME_HAVE_MESSAGING_PROTO_SENDMAIL:BOOL=OFF \
    ../${_realname}
 
  make
}
 
package() {
  cd build-${MINGW_CHOST}
  make install
}
