# Maintainer: Saul Ibarra Corretge <saghul@gmail.com>

_realname=python2-sip4

pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=4.16.2
pkgrel=2
pkgdesc="Tool to create Python bindings for C and C++ libraries"
arch=('any')
license=('GPL')
url="http://www.riverbankcomputing.co.uk/software/sip"
depends=("${MINGW_PACKAGE_PREFIX}-python2"
         "${MINGW_PACKAGE_PREFIX}-qt4")

source=("http://downloads.sourceforge.net/project/pyqt/sip/sip-$pkgver/sip-$pkgver.zip"
        "0001-fix-libpython-name.patch")
md5sums=('8e651c3216d247444967189e5553e750'
         '114b8eb3be19ed90fded76531454eb3a')

prepare() {
  cd "${srcdir}"/sip-$pkgver

  # Apply patches
  patch -p0 -i "${srcdir}"/0001-fix-libpython-name.patch
}

build() {
  cd "${srcdir}"/sip-$pkgver
  mkdir -p "${pkgdir}"

  ${MINGW_PREFIX}/bin/python2 configure.py \
    --bindir "${pkgdir}${MINGW_PREFIX}"/bin \
    --destdir "${pkgdir}${MINGW_PREFIX}"/lib/python2.7/site-packages \
    --incdir "${pkgdir}${MINGW_PREFIX}"/include/python2.7 \
    --sipdir "${pkgdir}${MINGW_PREFIX}"/sip \
    -p win32-g++

  # Fix makefiles
  sed -i 's/@if not exist \(.*\) mkdir /mkdir -p /' Makefile */Makefile
  sed -i 's@copy /y@cp -r@' Makefile */Makefile

  # Build
  make
}

package() {
  cd "${srcdir}"/sip-$pkgver
  make install
  install -D -m644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
