# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=ghostscript
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=9.10
pkgrel=1
arch=('any')
groups=("${_mingw_suff}")
license=('AGPL' 'custom')
pkgdesc="An interpreter for the PostScript language (mingw-w64)"
depends=("${_mingw_suff}-gtk3"
		"${_mingw_suff}-freetype"
		"${_mingw_suff}-fontconfig"
		"${_mingw_suff}-jasper"
		"${_mingw_suff}-zlib"
		"${_mingw_suff}-libpng>=1.5.7"
		"${_mingw_suff}-libjpeg"
		"${_mingw_suff}-libtiff>=4.0.0"
		"${_mingw_suff}-lcms2"
		"${_mingw_suff}-dbus"
		"${_mingw_suff}-openjpeg"
		"${_mingw_suff}-gdk-pixbuf2"
		)
makedepends=("${_mingw_suff}-gtk3" "${_mingw_suff}-gnutls")
optdepends=("${_mingw_suff}-gtk3:          needed for gsx")
url="http://www.ghostscript.com/"
source=(http://downloads.ghostscript.com/public/ghostscript-${pkgver}.tar.bz2
		mingw-build.patch)
options=('staticlibs' 'strip')
md5sums=('7179bb1ed4f6f453147e6f7e1f210ce8'
         '3e2e91f9bea8f115b9b64b7f7db0fc46')

prepare() {
	cd "$srcdir/${_realname}-$pkgver"
	patch -p1 -i ${srcdir}/mingw-build.patch

	sed -e "s|/usr|${MINGW_PREFIX}/${MINGW_CHOST}|g" -i base/all-arch.mak base/unix-aux.mak
	
	# force it to use system-libs
	rm -rf freetype jpeg libpng lcms lcms2 openjpeg tiff zlib
	
	#cd base
	libtoolize --force --copy --install
	autoconf -f -i
}

build()
{
	cd ${srcdir}/${_realname}-$pkgver
	./configure \
		--prefix=${MINGW_PREFIX} \
		--host=${MINGW_CHOST} \
		--target=${MINGW_CHOST} \
		--build=${MINGW_CHOST} \
		--with-drivers=ALL \
		--disable-cups \
		--with-system-libtiff \
		--disable-contrib
		
	make so
}

package() {
    cd ${srcdir}/${_realname}-$pkgver
    make DESTDIR="$pkgdir" soinstall

	# install missing doc files # http://bugs.archlinux.org/task/18023
	install -m 644 "${srcdir}"/${_realname}-${pkgver}/doc/{Ps2ps2.htm,gs-vms.hlp,gsdoc.el,pscet_status.txt} "${pkgdir}${MINGW_PREFIX}"/share/${_realname}/$pkgver/doc/

	install -D -m644 "${srcdir}"/${_realname}-${pkgver}/LICENSE "${pkgdir}${MINGW_PREFIX}"/share/licenses/${_realname}/LICENSE

	# remove unwanted localized man-pages
	rm -rf "${pkgdir}${MINGW_PREFIX}"/share/man/[^man1]*
}
