# Contributor: Xin Sun <sun.simpson@gmail.com>

_realname=hdf5
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=1.8.12
pkgrel=1
pkgdesc="General purpose library and file format for storing scientific data"
arch=('any')
url="http://www.hdfgroup.org/HDF5/"
source=(
  "http://www.hdfgroup.org/ftp/HDF5/releases/hdf5-${pkgver}/src/hdf5-${pkgver}.tar.bz2"
  "hdf5-ws2_32.patch"
  "hdf5-install.patch"
  )
md5sums=(
  "03ad766d225f5e872eb3e5ce95524a08"
  "c1aa1d0306fdefe435f35bc3dc7f860b"
  "50e4241f7c977e180da866cd43e571c3"
)
depends=("${_mingw_suff}-gcc-libs" "${_mingw_suff}-zlib" "${_mingw_suff}-szip")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-cmake" "patch")
options=('staticlibs' 'strip')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p0 -i ${srcdir}/hdf5-ws2_32.patch
  patch -p0 -i ${srcdir}/hdf5-install.patch
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX:PATH=${pkgdir}${MINGW_PREFIX} \
    -DBUILD_SHARED_LIBS=ON \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DHDF5_BUILD_HL_LIB=ON \
    -DHDF5_BUILD_CPP_LIB=ON \
    -DHDF5_BUILD_FORTRAN=OFF \
    -DHDF5_ENABLE_DEPRECATED_SYMBOLS=OFF \
    -DHDF5_ENABLE_SZIP_SUPPORT=ON \
    -DHDF5_ENABLE_Z_LIB_SUPPORT=ON \
    ../${_realname}-${pkgver}
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 install
  
  # clear README, licenses, etc. or shall we move them to another place?
  for f in ${pkgdir}${MINGW_PREFIX}/share/*
  do
    [[ -f $f ]] && rm -f $f
  done
  
  mv ${pkgdir}${MINGW_PREFIX}/lib/hdf5.lib ${pkgdir}${MINGW_PREFIX}/lib/libhdf5.dll.a
  mv ${pkgdir}${MINGW_PREFIX}/lib/hdf5_hl.lib ${pkgdir}${MINGW_PREFIX}/lib/libhdf5_hl.dll.a
  mv ${pkgdir}${MINGW_PREFIX}/lib/hdf5_cpp.lib ${pkgdir}${MINGW_PREFIX}/lib/libhdf5_cpp.dll.a
  mv ${pkgdir}${MINGW_PREFIX}/lib/hdf5_hl_cpp.lib ${pkgdir}${MINGW_PREFIX}/lib/libhdf5_hl_cpp.dll.a
  sed -s "s|hdf5.lib|libhdf5.dll.a|g" -i ${pkgdir}${MINGW_PREFIX}/share/cmake/hdf5/hdf5-targets-release.cmake
  sed -s "s|hdf5_cpp.lib|libhdf5_cpp.dll.a|g" -i ${pkgdir}${MINGW_PREFIX}/share/cmake/hdf5/hdf5-targets-release.cmake
  sed -s "s|hdf5_hl.lib|libhdf5_hl.dll.a|g" -i ${pkgdir}${MINGW_PREFIX}/share/cmake/hdf5/hdf5-targets-release.cmake
  sed -s "s|hdf5_hl_cpp.lib|libhdf5_hl_cpp.dll.a|g" -i ${pkgdir}${MINGW_PREFIX}/share/cmake/hdf5/hdf5-targets-release.cmake
}
