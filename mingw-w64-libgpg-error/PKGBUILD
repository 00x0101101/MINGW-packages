# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libgpg-error
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=1.12
pkgrel=1
pkgdesc="Support library for libgcrypt (mingw-w64)"
arch=('any')
url="http://www.gnupg.org"
license=("LGPL")
makedepends=("${_mingw_suff}-gcc")
depends=("${_mingw_suff}-crt" "${_mingw_suff}-gettext" "${_mingw_suff}-winpthreads")
groups=("${_mingw_suff}")
options=('staticlibs' 'strip')
source=("ftp://ftp.gnupg.org/gcrypt/libgpg-error/libgpg-error-${pkgver}.tar.bz2"
		"01-mingw32-fix-potomo.mingw.patch")
md5sums=('8f0eb41a344d19ac2aa9bd101dfb9ce6'
         '22eccdf0147d4e5e4e6900c506ea4444')

prepare() {
  cd "$srcdir/libgpg-error-$pkgver"
  patch -p1 -i ${srcdir}/01-mingw32-fix-potomo.mingw.patch
}

build() {
  cd "$srcdir/libgpg-error-$pkgver"
  sed -i "s,EXPORTS,," "src/gpg-error.def.in"
  mkdir -p $srcdir/build-${CARCH} && cd $srcdir/build-${CARCH}
    ../libgpg-error-$pkgver/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
	  --host=${MINGW_CHOST} \
	  --target=${MINGW_CHOST} \
      --enable-static
  make
}

package() {
    cd "${srcdir}/build-${CARCH}"
    make DESTDIR="$pkgdir" install
    # rm "${pkgdir}${MINGW_PREFIX}"/bin/*.exe
    # strip --strip-unneeded "${pkgdir}${MINGW_PREFIX}"/bin/*.dll
    # strip -g "${pkgdir}${MINGW_PREFIX}"/lib/*.a
    rm -r "${pkgdir}${MINGW_PREFIX}/share"
}
