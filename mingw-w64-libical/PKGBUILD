# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=libical
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.0.0
pkgrel=2
arch=('any')
pkgdesc="An Open Source implementation of the iCalendar protocols and protocol data units (mingw-w64)"
license=("LGPL 2.1")
url="https://github.com/libical/libical"
depends=()
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
            "make"
            "libtool"
            "automake-wrapper"
            "autoconf"
            "patch")
options=('strip' '!debug' 'staticlibs')
source=(https://github.com/${_realname}/${_realname}/archive/v$pkgver.tar.gz
        001-win.patch)
sha256sums=('0072e83834092315772e6719b85fc8b11530b1ff53f4d108315fb38cddbce8c2'
            '42f818a7d1304252b49c331a13ba58f681c834b00799fd3771c5eb9bfb95427b')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-win.patch
  
  autoreconf -fi
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib \
    --enable-shared \
    --disable-static \
    --with-devel
  make
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make DESTDIR=$pkgdir install
}
