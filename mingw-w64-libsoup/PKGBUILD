# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libsoup
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=2.42.2
pkgrel=1
pkgdesc="HTTP client/server library (mingw-w64)"
arch=(any)
url="http://ftp.gnome.org/pub/GNOME/sources/libsoup"
license=("LGPL")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config" "intltool")
depends=("${_mingw_suff}-gcc-libs"
		"${_mingw_suff}-glib2"
		"${_mingw_suff}-libxml2"
		"${_mingw_suff}-sqlite3"
		"${_mingw_suff}-glib-networking")
options=('staticlibs' 'strip')
source=("http://ftp.gnome.org/pub/gnome/sources/${_realname}/${pkgver%.*}/${_realname}-$pkgver.tar.xz"
		libsoup-2.42.2-gcc48.patch)
sha256sums=('1f4f9cc55ba483dc8defea0c3f97cc507dc48384c5529179e29c1e6d05630dbf'
            '9aa35493a1209c574c36fe3d7bbb8bf611b336d36538aa060c7d42726abc0475')

prepare() {
	cd "$srcdir/${_realname}-$pkgver"
	patch -Np1 -i "$srcdir/libsoup-2.42.2-gcc48.patch"
}

build() {
    mkdir -p "${srcdir}/build-${MINGW_CHOST}"
    cd "${srcdir}/build-${MINGW_CHOST}"
    "${srcdir}"/${_realname}-$pkgver/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --without-apache-httpd \
	  --without-apache-module-dir \
	  --with-gnome
    make
}

package() {
    cd "${srcdir}/build-${MINGW_CHOST}"
    make DESTDIR="$pkgdir" install
	mkdir -p "${pkgdir}${MINGW_PREFIX}/share"
    mv "${pkgdir}${MINGW_PREFIX}/lib/locale" "${pkgdir}${MINGW_PREFIX}/share/"
}
