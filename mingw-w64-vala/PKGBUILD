# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=vala
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.25.3
pkgrel=1
pkgdesc="Compiler for the GObject type system (mingw-w64)"
arch=('any')
url="http://live.gnome.org/Vala"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-libxslt")
depends=("${MINGW_PACKAGE_PREFIX}-glib2")
source=(http://ftp.gnome.org/pub/gnome/sources/${_realname}/${pkgver:0:4}/${_realname}-$pkgver.tar.xz)
sha256sums=('bdc318480edb176b31e80bf792db0a4d3bb76af846c4379dc285da6318f74ecf')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  autoreconf -ivf
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  "${srcdir}"/${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-shared
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 DESTDIR="$pkgdir" install

  mv -f ${pkgdir}${MINGW_PREFIX}/share/pkgconfig/* ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/
  rm -rf ${pkgdir}${MINGW_PREFIX}/share/pkgconfig
}
