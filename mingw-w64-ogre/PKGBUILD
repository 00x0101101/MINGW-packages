# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=ogre3d
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=1.9.0
pkgrel=1
pkgdesc="A low-level cryptographic library (mingw-w64)"
arch=('any')
url="http://www.ogre3d.org/"
license=("LGPL")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config" "${_mingw_suff}-cmake")
depends=("${_mingw_suff}-crt"
		"${_mingw_suff}-winpthreads"
		"${_mingw_suff}-tinyxml"
		"${_mingw_suff}-boost"
		"${_mingw_suff}-nvidia-cg-toolkit"
		"${_mingw_suff}-freetype"
		"${_mingw_suff}-zlib"
		"${_mingw_suff}-ois"
		"${_mingw_suff}-zziplib"
		#"${_mingw_suff}-intel-tbb"
		"${_mingw_suff}-FreeImage")
options=('staticlibs' '!strip' '!buildflags')
source=("${_realname}-${pkgver}.tar.xz"
ogre-1.9.0-fix-cflags.patch
ogre-endlessworld-fix.patch
ogre-freeimage-fix.patch)
		#"${_realname}"::"hg+https://bitbucket.org/sinbad/ogre/src/4578da5bf5b00fdf023b87e98099d647c5cb92ab?at=v1-9-0"
md5sums=('ed0b64499ea6535db081d0132adcc63b'
         'a2cbfc846906bbb80c7b05d2d60a2779'
         '3c00c5584a8edcb4dc07a57697a73d59'
         '166486655cd358be175f3b32b63e0ca5')

# source PKGBUILD && mksource
mksource() {
  [[ -x /usr/bin/hg ]] || (echo "hg not found. Install mercurial." && return 1)

  __hgroot='https://bitbucket.org/sinbad'
  __hgrepo='ogre'
  __hgbranch='default'
  __hgrev="v1-9"

  hg clone -u ${__hgrev} "${__hgroot}/${__hgrepo}/" ${__hgrepo}
  #hg clone -b ${__hgbranch} "${__hgroot}${__hgrepo}#${__hgrev}" ${__hgrepo}

  pushd ${__hgrepo}
  if (( $(hg id -n) < $(hg id -nr ${__hgbranch}) )); then
    printf 'You are not building the latest revision!\n'
    printf "Consider updating __hgrev to $(hg id -r ${__hgbranch}).\n"
  fi
  popd

  mv ${__hgrepo} ${_realname}-${pkgver}
  find ${_realname}-${pkgver} -depth -type d -name .hg -exec rm -rf {} \;
  rm ${_realname}-${pkgver}/{.hgignore,.hgtags}
  tar -cJf ${_realname}-${pkgver}.tar.xz ${_realname}-${pkgver}/*
  rm -r ${_realname}-${pkgver}

  gpg --detach-sign ${_realname}-${pkgver}.tar.xz

  #scp ${pkgname}-${pkgver}.tar.xz nym:/srv/ftp/other/vim/
  #scp ${pkgname}-${pkgver}.tar.xz.sig nym:/srv/ftp/other/vim/
}

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/ogre-1.9.0-fix-cflags.patch
  patch -p0 -i ${srcdir}/ogre-endlessworld-fix.patch
  patch -p1 -i ${srcdir}/ogre-freeimage-fix.patch
}

build() {
	mkdir -p ${pkgdir}${MINGW_PREFIX}
	pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
	export PREFIX_WIN=`pwd -W`
	popd > /dev/null
	
	pushd ${MINGW_PREFIX} > /dev/null
	export PREFIX_DEPS=`pwd -W`
	popd > /dev/null
	
	case ${MINGW_CHOST} in
		i686*)
			local _march=pentium4 #i686
			local _tune=generic #generic
		;;
		x86_64*)
			local _march=core2
			local _tune=nocona
		;;
	esac
	export CFLAGS="-O2 -pipe -march=${_march} -mtune=${_tune}"
	export CXXFLAGS="-O2 -pipe -march=${_march} -mtune=${_tune}"
    cd "${srcdir}/${_realname}-${pkgver}"
    ${MINGW_PREFIX}/bin/cmake.exe \
	  -G"MSYS Makefiles" \
	  -DCMAKE_INSTALL_PREFIX:PATH=${PREFIX_WIN} \
	  -DOGRE_DEPENDENCIES_DIR:PATH=${PREFIX_DEPS} \
	  -DOGRE_INSTALL_TOOLS=ON \
	  -DOGRE_COPY_DEPENDENCIES=OFF \
	  -DOGRE_INSTALL_DEPENDENCIES=OFF \
	  -DCMAKE_BUILD_TYPE:STRING=RelWithDebInfo \
	  -DFREETYPE_FT2BUILD_INCLUDE_DIR=${PREFIX_DEPS}/include/freetype2
    make
}

package() {
    cd "${srcdir}/${_realname}-${pkgver}"
    make -j1 install #DESTDIR="$pkgdir"
	pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
      mv bin/RelWithDebInfo/* bin/
	  rm -r bin/RelWithDebInfo
	  mv lib/RelWithDebInfo/* lib/
	  rm -r lib/RelWithDebInfo
	popd > /dev/null
}
