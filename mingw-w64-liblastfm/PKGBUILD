# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: AlexWMF <alexxwmf@gmail.com>

_realname=liblastfm

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"

pkgver=1.0.3
pkgrel=1
pkgdesc="A Qt C++ library for the Last.fm webservices http://www.last.fm"
arch=('any')
license=('Boost')
url="http://www.rasterbar.com/products/libtorrent"
depends=("${MINGW_PACKAGE_PREFIX}-qt4"
        "${MINGW_PACKAGE_PREFIX}-fftw"
        "${MINGW_PACKAGE_PREFIX}-libsamplerate")

makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
            "${MINGW_PACKAGE_PREFIX}-cmake")

options=(!strip staticlibs !buildflags)

source=(${_realname}-${pkgver}.tar.gz::"https://github.com/eartle/${_realname}/archive/${pkgver}.tar.gz")
md5sums=('84bb35038408b4087356641d381650a1')

build() {
  mkdir -p ${pkgdir}${MINGW_PREFIX}
  pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
  export PREFIX_WIN=`pwd -W`
  popd > /dev/null

  mkdir ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}

  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX:PATH=${PREFIX_WIN} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DBUILD_TESTS=False \
    ${srcdir}/${_realname}-${pkgver}/

  make ${MAKEFLAGS} # VERBOSE=1 
}

package() {  
  cd ${srcdir}/build-${MINGW_CHOST}
  make -j1 install
}
