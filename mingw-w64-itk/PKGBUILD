# Maintainer: Sergey Popov <popov.sa@gmail.com>

_realname=itk
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.7.2
pkgrel=1
pkgdesc='An open-source C++ toolkit for medical image processing (mingw-w64)'
arch=('any')
url='http://www.itk.org/'
license=('Apache')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-expat"
         "${MINGW_PACKAGE_PREFIX}-fftw"
         "${MINGW_PACKAGE_PREFIX}-hdf5"
         "${MINGW_PACKAGE_PREFIX}-libjpeg"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-libtiff"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-opencv"
             "${MINGW_PACKAGE_PREFIX}-vtk")
optdepends=("${MINGW_PACKAGE_PREFIX}-opencv: ITK-OpenCV bridge"
            "${MINGW_PACKAGE_PREFIX}-vtk: ITK-VTK bridge")
source=("https://github.com/InsightSoftwareConsortium/ITK/archive/v${pkgver}.tar.gz")
sha1sums=('1926161605b015f53bb21e3ee3cf132a8b07991b')
_src="${_realname}-${pkgver}"

build() {
  CFLAGS+=" ${CPPFLAGS}"
  CXXFLAGS+=" ${CPPFLAGS} -std=gnu++11"

  local -a _extra_config
  if [[ "$CARCH" = 'i686' ]]; then
    _extra_config=('-DITK_USE_64BITS_IDS=OFF'
                   '-DVNL_CONFIG_ENABLE_SSE2_ROUNDING=OFF')
  fi
  if [[ "$CARCH" = 'x86_64' ]]; then
    _extra_config=('-DITK_USE_64BITS_IDS=ON'
                   '-DVNL_CONFIG_ENABLE_SSE2_ROUNDING=ON')
  fi

  [[ -d "${srcdir}/build-${CARCH}" ]] && rm -rf "${srcdir}/build-${CARCH}"
  mkdir -p "${srcdir}/build-${CARCH}" && cd "${srcdir}/build-${CARCH}"

  "${MINGW_PREFIX}/bin/cmake.exe" \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX="${pkgdir}${MINGW_PREFIX}" \
    -DBUILD_DOCUMENTATION=OFF \
    -DBUILD_EXAMPLES=OFF \
    -DBUILD_TESTING=OFF \
    -DITK_LEGACY_REMOVE=OFF \
    -DVNL_CONFIG_THREAD_SAFE=ON \
    -DModule_ITKVideoBridgeOpenCV=ON \
    -DModule_ITKVtkGlue=ON \
    -DITK_USE_GPU=OFF \
    -DITK_USE_SYSTEM_EXPAT=ON \
    -DITK_USE_SYSTEM_FFTW=ON \
    -DUSE_FFTWD=ON \
    -DUSE_FFTWF=ON \
    -DITK_USE_SYSTEM_GDCM=OFF \
    -DITK_USE_SYSTEM_HDF5=ON \
    -DITK_USE_SYSTEM_JPEG=ON \
    -DITK_USE_SYSTEM_PNG=ON \
    -DITK_USE_SYSTEM_TIFF=ON \
    -DITK_USE_SYSTEM_VXL=OFF \
    -DITK_USE_SYSTEM_ZLIB=ON \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=ON \
    "${_extra_config[@]}" \
    "../${_src}"

  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make -j1 install

  pushd "${pkgdir}${MINGW_PREFIX}" > /dev/null
  local _MINGW_PREFIX_WIN="$(cygpath -m ${MINGW_PREFIX})"
  sed -s "s|${_MINGW_PREFIX_WIN}|\${_IMPORT_PREFIX}|g" \
    -i "lib/cmake/${_realname}-${pkgver%.*}/ITKTargets-release.cmake"
  find "lib/cmake/${_realname}-${pkgver%.*}" -name '*.cmake' -exec \
    sed -s "s|${_MINGW_PREFIX_WIN}|\${ITK_INSTALL_PREFIX}|g" -i {} \;
  install -Dm644 "${srcdir}/${_src}/LICENSE" "share/licenses/${_realname}/LICENSE"
  install -Dm644 "${srcdir}/${_src}/NOTICE" "share/licenses/${_realname}/NOTICE"
  install -Dm644 "${srcdir}/${_src}/Modules/ThirdParty/KWSys/src/KWSys/Copyright.txt" \
    "share/licenses/${_realname}/KWSys/Copyright.txt"
  popd > /dev/null
}
