# Maintainer: Andrew Chadwick <a.t.chadwick@gmail.com>

_realname=libmypaint
_realver=1.3.0-beta.0
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=(
    "mypaint<1.3.0alpha"
)
pkgver=${_realver/-/}
pkgrel=1
pkgdesc="Brush engine used by MyPaint (mingw-w64)"
arch=('any')
url="http://mypaint.org"
license=("ISC")
makedepends=(
    "${MINGW_PACKAGE_PREFIX}-gcc"
    "${MINGW_PACKAGE_PREFIX}-pkg-config"
    "${MINGW_PACKAGE_PREFIX}-json-c"
    "${MINGW_PACKAGE_PREFIX}-glib2"
)
makedepends+=("autoconf" "automake" "libtool" "git")
# autotools are required because several Makefile.am are modified
depends=(
    "${MINGW_PACKAGE_PREFIX}-gcc-libs"
    "${MINGW_PACKAGE_PREFIX}-glib2"
    "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
)
options=('strip' '!debug')
source=("https://github.com/mypaint/${_realname}/releases/download/v${_realver}/${_realname}-${_realver}.tar.xz")
sha256sums=('01a101c81da6ce4c9e080c210f001ba74c225c9e954f4b574aead9d01f9fcccf')

build() {
  #export GI_SCANNER_DEBUG="save-temps"
  cd "${srcdir}"
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  cp -rf ${_realname}-${_realver} build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  PKG_CONFIG_PATH="${PKG_CONFIG_PATH}:/usr/share/pkgconfig" \
  ./configure --prefix=${MINGW_PREFIX}
  make
}

package() {
  cd "${srcdir}"/build-${MINGW_CHOST}
  make -j1 DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}
