# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=FreeImage
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=3.15.4
pkgrel=3
pkgdesc="Library project for developers who would like to support popular graphics image formats (mingw-w64)."
arch=('any')
license=('GPL' 'custom:FIPL')
url="http://freeimage.sourceforge.net/"
depends=("${_mingw_suff}-crt")
makedepends=("${_mingw_suff}-gcc")
options=('strip' 'staticlibs' '!buildflags')
source=("http://downloads.sourceforge.net/sourceforge/freeimage/FreeImage${pkgver//./}.zip"
		'mingw64.patch'
		'Makefile.mingw-w64')
md5sums=('9f9a3b2c3c1b4fd24fe479e8aaee86b1'
         '1482fbf6046cf25feaba5df7c61dd509'
         'a396e939ac69acb5b94747718742683c')

prepare() {
  cd ${srcdir}/FreeImage

  patch -Np0 < ${srcdir}/mingw64.patch
  
  cp -f ${srcdir}/Makefile.mingw-w64 ${srcdir}/FreeImage/Makefile.mingw-w64
  #sed -i "s|IMPORTLIB = \$(TARGET).lib|IMPORTLIB = lib\$(TARGET).dll.a|g" Makefile.mingw
  #sed -i "s|-Wl,-soname,\$(SOLIBNAME)|-Wl,--out-implib,\$(IMPORTLIB)|g" Makefile.mingw
  #sed -i "s|--add-stdcall-underscore|--no-leading-underscore --kill-at|g" Makefile.mingw
}

build() {
  cd ${srcdir}/FreeImage
  
  mkdir -p ${srcdir}/build-{shared,static}
  lndir ${srcdir}/FreeImage ${srcdir}/build-shared
  lndir ${srcdir}/FreeImage ${srcdir}/build-static
  
  cd ${srcdir}/build-shared
  make \
     CC=${MINGW_CHOST}-gcc \
     CXX=${MINGW_CHOST}-g++ \
     LD=${MINGW_CHOST}-g++ \
     RC=windres \
     AR=ar \
     DLLTOOL=dlltool \
     FREEIMAGE_LIBRARY_TYPE="SHARED" \
     -f Makefile.mingw-w64
  
  cd ${srcdir}/build-static
  make \
    CC=${MINGW_CHOST}-gcc \
    CXX=${MINGW_CHOST}-g++ \
    LD=${MINGW_CHOST}-g++ \
    RC=windres \
    AR=ar \
    DLLTOOL=dlltool \
    FREEIMAGE_LIBRARY_TYPE="STATIC" \
    -f Makefile.mingw-w64

}

package() {
    install -d "${pkgdir}${MINGW_PREFIX}"/lib
    install -d "${pkgdir}${MINGW_PREFIX}"/bin
    install -d "${pkgdir}${MINGW_PREFIX}"/include
	
    cd ${srcdir}/build-shared
    install -m755 Dist/FreeImage.dll "${pkgdir}${MINGW_PREFIX}"/bin/
	install -m644 Dist/FreeImage.h   "${pkgdir}${MINGW_PREFIX}"/include/
	install -m644 libFreeImage.dll.a "${pkgdir}${MINGW_PREFIX}"/lib/
    
	cd ${srcdir}/build-static
	install -m644 libFreeImage.a "${pkgdir}${MINGW_PREFIX}"/lib/
}
