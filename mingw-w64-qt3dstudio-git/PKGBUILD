# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=qt3dstudio
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
_base_ver=1.0.0
pkgver=1.0.0.r201.2b75758
pkgrel=1
pkgdesc='3D tool for Qt ecosystem (mingw-w64)'
url='https://www.qt.io/'
arch=('any')
license=('LGPL')
depends=("${MINGW_PACKAGE_PREFIX}-qt5"
         "${MINGW_PACKAGE_PREFIX}-gcc"
         "${MINGW_PACKAGE_PREFIX}-make")
makedepends=("git" "perl")
options=('docs' 'staticlibs') # 'debug' '!strip')
source=(${_realname}::git+https://codereview.qt-project.org/qt3dstudio/qt3dstudio
        ${_realname}/src/3rdparty/ColladaDOM::git+https://codereview.qt-project.org/qt3dstudio/qt3dstudio-collada-dom
        ${_realname}/src/3rdparty/EASTL::git+https://codereview.qt-project.org/qt3dstudio/qt3dstudio-eastl
        ${_realname}/src/3rdparty/Lua::git+https://codereview.qt-project.org/qt3dstudio/qt3dstudio-lua
        ${_realname}/src/3rdparty/pcre::git+https://codereview.qt-project.org/qt3dstudio/qt3dstudio-pcre
        0001-Fix-building-Qt3DStudio-with-mingw.patch
        0002-Use-pragma-directives-only-for-MSVC-builds.patch
        0001-Fix-warnings-and-building-colladadom-for-mingw.patch)
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            '6cf6d9dfd71c0bbfaadf808688122e60dad95bec346866023ecc5680970c5c83'
            '9240fbd8d1f8de5fc732071910ced18102ae05f475e19645907a7bfb37d63cd8'
            'f49d42fa7cb727eee3246e7c65eaea5a424db96f4dae6f4af96973b951f29fca')

pkgver() {
  cd "${_realname}"

  local full_version=${_base_ver}.r$(git rev-list --count --first-parent HEAD ).$(git rev-parse --short HEAD )

  printf "%s" $full_version
}

prepare() {
  mv ${srcdir}/ColladaDOM ${srcdir}/${_realname}/src/3rdparty/
  mv ${srcdir}/EASTL ${srcdir}/${_realname}/src/3rdparty/
  mv ${srcdir}/Lua ${srcdir}/${_realname}/src/3rdparty/
  mv ${srcdir}/pcre ${srcdir}/${_realname}/src/3rdparty/

  cd ${srcdir}/${_realname}
  patch -p1 -i ${srcdir}/0001-Fix-building-Qt3DStudio-with-mingw.patch
  patch -p1 -i ${srcdir}/0002-Use-pragma-directives-only-for-MSVC-builds.patch
  pushd src/3rdparty/ColladaDOM
    patch -p1 -i ${srcdir}/0001-Fix-warnings-and-building-colladadom-for-mingw.patch
  popd
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir ${srcdir}/build-${MINGW_CHOST}
  
  local absSrcDir=$(cygpath -u ${srcdir})
  cd ${absSrcDir}/build-${MINGW_CHOST}
  if check_option "debug" "y"; then
    _config_variant=debug
  else
    _config_variant=release
  fi
  ${MINGW_PREFIX}/bin/qmake.exe ${absSrcDir}/${_realname}/qt3dstudio.pro CONFIG+=${_config_variant}
  make ${_config_variant}
  make docs
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make INSTALL_ROOT="${pkgdir}${MINGW_PREFIX}" install
  make INSTALL_ROOT="${pkgdir}${MINGW_PREFIX}" install_docs
}
