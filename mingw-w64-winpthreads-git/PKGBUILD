# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=winpthreads
_mingw_suff=mingw-w64-${CARCH}
pkgname=("${_mingw_suff}-${_realname}-git" "${_mingw_suff}-libwinpthread-git")
_ver_base=4.0.0
pkgver=4.0.0.4044.502c720
pkgrel=1
pkgdesc="MinGW-w64 winpthreads library"
arch=('any')
url="http://mingw-w64.sourceforge.net"
license=('custom')
groups=("${_mingw_suff}-toolchain" "${_mingw_suff}")
makedepends=("subversion" "${_mingw_suff}-gcc" "${_mingw_suff}-binutils" "${_mingw_suff}-crt" "${_mingw_suff}-headers")
options=('strip' '!buildflags' 'staticlibs' '!emptydirs' '!debug')
source=("${_realname}"::"git://git.code.sf.net/p/mingw-w64/mingw-w64")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/${_realname}"
  printf "%s.%s.%s" "$_ver_base" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}
  

  declare -a extra_config
  if check_option "debug" "y"; then
    extra_config+=("CFLAGS=-O0 -g -DWINPTHREAD_DBG")
    extra_config+=("CXXFLAGS=-O0 -g -DWINPTHREAD_DBG")
    extra_config+=("CXXFLAGS=")
  fi

  ${srcdir}/${_realname}/mingw-w64-libraries/winpthreads/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-static \
    --enable-shared \
    "${extra_config[@]}"

  # I failed to figure out how to disable stripping, this
  # will have to do instead.
  if check_option "strip" "n"; then
    sed -i 's,^STRIP = .*$,STRIP = true,g'                Makefile
    sed -i 's,^STRIP=.*$,STRIP=true,g'                    libtool
    sed -i 's,old_striplib=.*$strip,old_striplib=true,g'  libtool
    sed -i 's,striplib=.*$,striplib=true,g'               libtool
  fi

  make
}

package_winpthreads() {
  depends=("${_mingw_suff}-crt-git" "${_mingw_suff}-libwinpthread-git=${pkgver}")
  replaces=("${_mingw_suff}-${_realname}-svn")
  provides=("${_mingw_suff}-${_realname}")
  conflicts=("${_mingw_suff}-${_realname}")

  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install
  
  mkdir -p ${pkgdir}${MINGW_PREFIX}/${MINGW_CHOST}
  mv ${pkgdir}${MINGW_PREFIX}/include ${pkgdir}${MINGW_PREFIX}/${MINGW_CHOST}/
  mv ${pkgdir}${MINGW_PREFIX}/lib ${pkgdir}${MINGW_PREFIX}/${MINGW_CHOST}/
  rm -rf ${pkgdir}${MINGW_PREFIX}/bin
  
  install -Dm644 ${srcdir}/${_realname}/mingw-w64-libraries/winpthreads/COPYING ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}

package_libwinpthread() {
  provides=("${_mingw_suff}-libwinpthread")
  conflicts=("${_mingw_suff}-libwinpthread")
  replaces=("${_mingw_suff}-libwinpthread-svn")

  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install
  
  rm -rf ${pkgdir}${MINGW_PREFIX}/include
  rm -rf ${pkgdir}${MINGW_PREFIX}/lib
}

# Wrappers for package functions

# 32-bit wrappers
package_mingw-w64-i686-winpthreads-git() {
  package_winpthreads
}

package_mingw-w64-i686-libwinpthread-git() {
  package_libwinpthread
}

# 64-bit wrappers
package_mingw-w64-x86_64-winpthreads-git() {
  package_winpthreads
}

package_mingw-w64-x86_64-libwinpthread-git() {
  package_libwinpthread
}
