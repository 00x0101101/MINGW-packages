# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libotr

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.0.0
pkgrel=1
pkgdesc="Off-the-Record Messaging Library and Toolkit (mingw-w64)"
arch=('any')
url="http://www.cypherpunks.ca/otr/'"
license=('GPL')
depends=("${MINGW_PACKAGE_PREFIX}-libgcrypt")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
options=('staticlibs' 'strip')
source=("http://www.cypherpunks.ca/otr/${_realname}-${pkgver}.tar.gz"
        'kopete.patch'
        'libotr-no-undefined.patch')
md5sums=('00979dca82d70383fcd1b01f3974363c'
         '6ef0928ce2946bd039c89d59fa28707a'
         '83a5e2e8cd03aab67143826025469a1d')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/kopete.patch
  patch -p1 -i ${srcdir}/libotr-no-undefined.patch

  autoreconf -fi
}

build() {
  mkdir -p ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}  
  ${srcdir}/${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-shared \
    --enable-static \
    --mandir=${MINGW_PREFIX}/share/man
  make
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}  
  make DESTDIR="$pkgdir" install
}
