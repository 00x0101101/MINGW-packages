# Maintainer: Johannes Schindelin <johannes.schindelin@gmx.de>

_realname=msmtp
pkgbase="mingw-w64-${_realname}"
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.6.2
tag=msmtp-${pkgver}
pkgrel=1
pkgdesc="An SMTP client (mingw-w64)"
arch=('any')
license=('GPL2+')
url="http://msmtp.sourceforge.net/"
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
  "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-gettext"
  "${MINGW_PACKAGE_PREFIX}-gnutls"
  "${MINGW_PACKAGE_PREFIX}-libffi"
  "${MINGW_PACKAGE_PREFIX}-libwinpthread-git")
options=('strip')
source=("git://git.code.sf.net/p/msmtp/code#tag=${tag}"
  '0001-include-winsock2.patch'
  '0002-dont-redefine-winver-constant.patch')
sha1sums=('SKIP'
  'dbca2cf777e5c591f021168c83cb128f703153fe'
  '295758aa2ad31540a4c0adde725d568210c2b356')

prepare () {
  cd ${srcdir}/code

  patch -p1 -i "${srcdir}/0001-include-winsock2.patch"
  patch -p1 -i "${srcdir}/0002-dont-redefine-winver-constant.patch"
  autoreconf -i
  ./configure --prefix=${MINGW_PREFIX}
  sed -i 's/-R\/mingw..\/lib//' src/Makefile
  touch src/*.c
}

build() {
  cd ${srcdir}/code

  make
}

package() {
  cd ${srcdir}/code

    make DESTDIR="${pkgdir}" install
}
