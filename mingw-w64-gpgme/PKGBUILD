# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_realname=gpgme
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.5.5
pkgrel=4
pkgdesc="A C wrapper library for GnuPG (mingw-w64)"
arch=('any')
url="http://www.gnupg.org/related_software/gpgme/"
license=('LGPL')
depends=("${MINGW_PACKAGE_PREFIX}-glib2"
         "${MINGW_PACKAGE_PREFIX}-gnupg"
         "${MINGW_PACKAGE_PREFIX}-libassuan"
         "${MINGW_PACKAGE_PREFIX}-libgpg-error"
         "${MINGW_PACKAGE_PREFIX}-npth"
         )
options=('!emptydirs') # '!strip' 'debug')
source=(ftp://ftp.gnupg.org/gcrypt/${_realname}/${_realname}-${pkgver}.tar.bz2{,.sig}
        gpgme-1.5.4-mingw-mkstemp.patch
        gpgme-find-gnupg.patch
        debugging-dont-DeleteFileA-tmpfiles.patch)
sha1sums=('88476d72cb099e179de4040760502886f7a54926'
          'SKIP'
          '8c7d7648772c29620b7fd23e867f60386ba92790'
          '6dbd6f52c918facbe8fee5c5521ebad724ec9653'
          '305408eb39c03d72aab15029f85bd8d629b87a2c')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -p1 -i "${srcdir}"/gpgme-1.5.4-mingw-mkstemp.patch
  patch -p1 -i "${srcdir}"/gpgme-find-gnupg.patch
  if check_option "debug" "y"; then
    patch -p1 -i "${srcdir}"/debugging-dont-DeleteFileA-tmpfiles.patch
  fi

  autoreconf -ivf
}

build() {
  [[ -d "${srcdir}"/build-${MINGW_CHOST} ]] && rm -rf "${srcdir}"/build-${MINGW_CHOST}
  mkdir -p "${srcdir}"/build-${MINGW_CHOST} && cd "${srcdir}"/build-${MINGW_CHOST}
  
  "${srcdir}"/${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --libexecdir=${MINGW_PREFIX}/bin \
    --disable-fd-passing \
    --enable-shared \
    --enable-static \
    --disable-gpgsm-test
  
  make
}

check() {
  cd "${srcdir}"/build-${MINGW_CHOST}
  #make check
}

package() {
  cd "${srcdir}"/build-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install
}
