# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=icu
_mingw_suff=mingw-w64-${CARCH}
pkgname=("${_mingw_suff}-${_realname}")
pkgver=52.1
pkgrel=3
arch=('any')
pkgdesc="International Components for Unicode library (mingw-w64)"
arch=('any')
url="http://www.icu-project.org/"
license=('custom:"icu"')
depends=("${_mingw_suff}-crt")
makedepends=("${_mingw_suff}-gcc")
options=('!buildflags' 'staticlibs' 'strip')
source=("http://download.icu-project.org/files/icu4c/${pkgver}/icu4c-${pkgver//./_}-src.tgz"
        'icu4c-4_9_1-mingw-w64-mkdir-compatibility.patch'
        'icu-config.patch'
        'icu-pkgconfig.patch'
        'mh-mingw.diff'
        'mh-mingw64.diff'
        'icu-config2.patch')

md5sums=('9e96ed4c1d99c0d14ac03c140f9f346c'
         'a7d1a3ab604904a5f109d60b5deb60ee'
         '5a330fc92017cba09441308692d15a18'
         '4e2272dc17e6cfd33f1dd1030f3f50ec'
         'adf7e568dde93ceccc7d5f201761d137'
         '73dbd2a6f94315c5fe35208942ebe858'
         'fe39795e123109423de12e5ae9c6987d')

prepare() {
  cd "$srcdir/icu"

  patch -p1 -i "$srcdir"/mh-mingw.diff
  patch -p1 -i "$srcdir"/mh-mingw64.diff
  patch -p1 -i "$srcdir"/icu4c-4_9_1-mingw-w64-mkdir-compatibility.patch
  patch -p1 -i "$srcdir"/icu-config.patch
  patch -p1 -i "$srcdir"/icu-pkgconfig.patch
  patch -p1 -i "$srcdir"/icu-config2.patch

  #cd source
  #autoreconf -vfi
}

build() {
  cd "$srcdir/icu/"
  [ -d build-${CARCH} ] && rm -rf build-${CARCH}
  mv source build-${CARCH}
  pushd build-${CARCH}
  # unset CFLAGS LDFLAGS
  #mkdir -p "build-${CARCH}" && pushd "build-${CARCH}"
  #./runConfigureICU MinGW --prefix=${MINGW_PREFIX}
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --disable-rpath \
    --enable-shared --enable-static
  make
}

package() {
  #mkdir -p "$pkgdir/usr/bin"
  cd "$srcdir/icu/build-${CARCH}"
  make install DESTDIR="$pkgdir"
  rm -rf "${pkgdir}${MINGW_PREFIX}"/share
  #rm "${pkgdir}${MINGW_PREFIX}"/bin/*.exe
  mv "${pkgdir}${MINGW_PREFIX}"/lib/*.dll "${pkgdir}${MINGW_PREFIX}"/bin
}
