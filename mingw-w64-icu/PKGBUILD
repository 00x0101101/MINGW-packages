# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=icu
_mingw_suff=mingw-w64-${CARCH}
pkgname=("${_mingw_suff}-${_realname}")
pkgver=52.1
pkgrel=4
arch=('any')
pkgdesc="International Components for Unicode library (mingw-w64)"
arch=('any')
url="http://www.icu-project.org/"
license=('custom:"icu"')
depends=("${_mingw_suff}-gcc-libs")
makedepends=("${_mingw_suff}-gcc")
options=('!buildflags' 'staticlibs' 'strip')
source=("http://download.icu-project.org/files/icu4c/${pkgver}/icu4c-${pkgver//./_}-src.tgz"
		0004-move-to-bin.mingw.patch
		0007-actually-move-to-bin.mingw.patch
		0008-data-install-dir.mingw.patch
		0009-fix-bindir-in-config.mingw.patch
		0010-msys-rules-for-makefiles.mingw.patch
		0011-sbin-dir.mingw.patch
		0012-libprefix.mingw.patch
		0013-dont-short-names.mingw.patch
		0014-mingwize-pkgdata.mingw.patch
		0015-debug.mingw.patch
		0016-icu-pkgconfig.patch
		)

md5sums=('9e96ed4c1d99c0d14ac03c140f9f346c'
         '4c8143786ed499184e64fc5a1a1d0fdf'
         'eb56b72df2ca0dc77261c4f2adc5c8ad'
         '9c63125f7bdd88871c89e13ee62ac02b'
         '93830d77021eab270e537d466653557e'
         'cce2427d95bcb4a364df5ab605c3e9a3'
         'ee93f131a132f7e65d11d15a467b7abc'
         'd95161f2041f2a602667b28b4525060c'
         'e42c5492037b58e88003539432235c4a'
         'e191e78207b55da374b6a8200f48da5b'
         '630eb32bd56cd97c258cb735c4a10ce0'
         '4e2272dc17e6cfd33f1dd1030f3f50ec')

prepare() {
	cd "$srcdir/icu"

	patch -p1 -i "$srcdir"/0004-move-to-bin.mingw.patch
	patch -p1 -i "$srcdir"/0007-actually-move-to-bin.mingw.patch
	patch -p1 -i "$srcdir"/0008-data-install-dir.mingw.patch
	patch -p1 -i "$srcdir"/0009-fix-bindir-in-config.mingw.patch
	patch -p1 -i "$srcdir"/0010-msys-rules-for-makefiles.mingw.patch
	patch -p1 -i "$srcdir"/0011-sbin-dir.mingw.patch
	patch -p1 -i "$srcdir"/0012-libprefix.mingw.patch
	patch -p1 -i "$srcdir"/0013-dont-short-names.mingw.patch
	patch -p1 -i "$srcdir"/0014-mingwize-pkgdata.mingw.patch
	patch -p1 -i "$srcdir"/0015-debug.mingw.patch
	patch -p1 -i "$srcdir"/0016-icu-pkgconfig.patch

	cd source
	autoreconf -vfi
}

build() {
	cd "$srcdir/icu/"
	[ -d build-${CARCH} ] && rm -rf build-${CARCH}
	cp -rf source build-${CARCH}
	pushd build-${CARCH}
	./configure \
		--prefix=${MINGW_PREFIX} \
		--build=${MINGW_CHOST} \
		--host=${MINGW_CHOST} \
		--target=${MINGW_CHOST} \
		--disable-rpath \
		--enable-shared \
		--enable-static
	make
}

check() {
	cd "$srcdir/icu/build-${CARCH}"
	PATH=$srcdir/icu/build-${CARCH}/bin:$srcdir/icu/build-${CARCH}/lib:$PATH \
	make V=1 -k check
}

package() {
	cd "$srcdir/icu/build-${CARCH}"
	make install DESTDIR="$pkgdir"
	mv "${pkgdir}${MINGW_PREFIX}"/lib/*.dll "${pkgdir}${MINGW_PREFIX}"/bin/
	mv "${pkgdir}${MINGW_PREFIX}"/bin/*.a "${pkgdir}${MINGW_PREFIX}"/lib/
}
