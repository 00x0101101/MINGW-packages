# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=opencolorio
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}-git"
pkgver=688.ebd6efc
pkgrel=1
pkgdesc="A color management framework for visual effects and animation (mingw-w64)"
arch=('any')
url="http://opencolorio.org"
license=('BSD')
provides=("${_mingw_suff}-${_realname}")
conflicts=("${_mingw_suff}-${_realname}")
depends=("${_mingw_suff}-python2" "${_mingw_suff}-tinyxml")
makedepends=("${_mingw_suff}-cmake"
			"${_mingw_suff}-yaml-cpp0.3")
options=('strip' 'buildflags')
source=("${_realname}"::"git+https://github.com/imageworks/OpenColorIO.git"
		#http://github.com/imageworks/OpenColorIO/tarball/v$pkgver
		mingw-w64.patch
		fix-redefinitions.patch)
sha1sums=('SKIP'
          'e9e1ba43ecd153ae095c33c231d452aab8b19972'
          '8e4eaca025b110ea18036ccf71e0f5e1e8e17f5b')

pkgver() {
  cd "$srcdir/$_realname"
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd $_realname
	patch -p1 -i ${srcdir}/mingw-w64.patch
	patch -p1 -i ${srcdir}/fix-redefinitions.patch
}

build() {
	mkdir -p ${pkgdir}${MINGW_PREFIX}
	pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
	export PREFIX_WIN=`pwd -W`
	popd > /dev/null
	
	pushd ${MINGW_PREFIX} > /dev/null
	export PREFIX_DEPS=`pwd -W`
	popd > /dev/null

	cd $_realname 
	${MINGW_PREFIX}/bin/cmake \
		-G"MSYS Makefiles" \
		-DCMAKE_INSTALL_PREFIX:PATH=${PREFIX_WIN} \
		-DGLEW_INCLUDES=${PREFIX_DEPS}/include \
		-DGLEW_LIBRARIES=${PREFIX_DEPS}/lib/libglew32.a \
		-DUSE_EXTERNAL_LCMS=ON \
		-DUSE_EXTERNAL_TINYXML=ON \
		-DUSE_EXTERNAL_YAML=ON \
		-DOCIO_USE_BOOST_PTR=ON \
		-DOCIO_BUILD_TRUELIGHT=OFF \
		.
	make 
}

package() {
  cd "$_realname"
  make install
  install -Dm644 LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
  
  cd ${pkgdir}${MINGW_PREFIX}
  export PREFIX_WIN=`pwd -W`
  mkdir -p bin
  mv lib/*.dll bin/
  for ff in lib/pkgconfig/*.pc; do
    sed -i "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" $ff
  done
}
