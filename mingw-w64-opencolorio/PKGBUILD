# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=opencolorio

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgver=694.64adcad
pkgrel=1
pkgdesc="A color management framework for visual effects and animation (mingw-w64)"
arch=('any')
url="http://opencolorio.org"
license=('BSD')
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
depends=("${MINGW_PACKAGE_PREFIX}-boost"
        "${MINGW_PACKAGE_PREFIX}-glew"
        "${MINGW_PACKAGE_PREFIX}-lcms2"
        "${MINGW_PACKAGE_PREFIX}-python2"
        "${MINGW_PACKAGE_PREFIX}-tinyxml")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
        "${MINGW_PACKAGE_PREFIX}-yaml-cpp0.3"
        "${MINGW_PACKAGE_PREFIX}-pkg-config")
options=('strip')
source=("${_realname}"::"git+https://github.com/imageworks/OpenColorIO.git"
        #http://github.com/imageworks/OpenColorIO/tarball/v$pkgver
        mingw-w64.patch
        fix-redefinitions.patch
        detect-mingw-python.patch)
sha1sums=('SKIP'
          '2f17db31e1d8ae96198d272c21b3c6e5bf46ec41'
          '8e4eaca025b110ea18036ccf71e0f5e1e8e17f5b'
          'f2041229152b331bb9da663c2e1e43f2ba0c0ac0')

pkgver() {
  cd "$srcdir/$_realname"
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd $_realname
  patch -p1 -i ${srcdir}/mingw-w64.patch
  patch -p1 -i ${srcdir}/fix-redefinitions.patch
  patch -p1 -i ${srcdir}/detect-mingw-python.patch
}

build() {
  pushd ${MINGW_PREFIX} > /dev/null
  export PREFIX_DEPS=`pwd -W`
  popd > /dev/null

  CPPFLAGS+=" -I${MINGW_PREFIX}/include/python2.7"
  CXXFLAGS+=" -I${MINGW_PREFIX}/include/python2.7"
  mkdir -p ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}

  export PYTHON=${MINGW_PREFIX}/bin/python2.exe
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${pkgdir}${MINGW_PREFIX} \
    -DGLEW_INCLUDES=${PREFIX_DEPS}/include \
    -DGLEW_LIBRARIES=${PREFIX_DEPS}/lib/libglew32.dll.a \
    -DUSE_EXTERNAL_TINYXML=ON \
    -DUSE_EXTERNAL_YAML=ON \
    -DUSE_EXTERNAL_LCMS=ON \
    -DOCIO_USE_BOOST_PTR=ON \
    -DOCIO_BUILD_PYGLUE=ON \
    -DOCIO_PYGLUE_LINK=ON \
    -DPYTHON=${MINGW_PREFIX}/bin/python2.exe \
    -DOCIO_BUILD_TRUELIGHT=OFF \
    ../$_realname
    
    #-DPYTHON_LIBRARIES=${PREFIX_DEPS}/lib/libpython2.7.dll.a \
    #-DPYTHON_HEADER=${PREFIX_DEPS}/include/python2.7/Python.h \
    #-DPYTHON_INCLUDE_DIRS=${PREFIX_DEPS}/include/python2.7 \
  make 
}

package() {
  cd build-${MINGW_CHOST}
  make install
  install -Dm644 ${srcdir}/${_realname}/LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE

  cd ${pkgdir}${MINGW_PREFIX}
  export PREFIX_WIN=`pwd -W`
  mkdir -p bin
  mv lib/*.dll bin/
  for ff in lib/pkgconfig/*.pc; do
    sed -i "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" $ff
  done
}
