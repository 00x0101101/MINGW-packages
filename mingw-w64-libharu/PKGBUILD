# Maintainer: RÃ©mi BERTHO <remi.bertho@openmailbox.org>

_realname=libharu
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.3.0
pkgrel=1
pkgdesc="C library for generating PDF documents"
url="https://github.com/libharu/libharu"
arch=('i686' 'x86_64')
license=('custom:ZLIB')
depends=("${MINGW_PACKAGE_PREFIX}-libpng")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake")
source=("https://github.com/libharu/libharu/archive/RELEASE_${pkgver//./_}.tar.gz" "win_various_install_files.patch" "win_lib.patch")
md5sums=('4f916aa49c3069b3a10850013c507460' 'cde4ba6d18174097801aba1ac65d3efa' '20267023a191192ddb313d625bdf8037')

prepare() {
  cd "$srcdir/$_realname-RELEASE_${pkgver//./_}"
  patch -p1 -i "$srcdir/win_various_install_files.patch"
  patch -p1 -i "$srcdir/win_lib.patch"
}

build() {
  cd "$srcdir/$_realname-RELEASE_${pkgver//./_}"
  mkdir -p build
  cd build
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe -G"MSYS Makefiles" \
    -DCMAKE_C_COMPILER=${MINGW_PREFIX}/bin/gcc.exe \
    -DCMAKE_MAKE_PROGRAM=${MINGW_PREFIX}/bin/mingw32-make.exe \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_STATIC_LIBS=ON \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} ..
  mingw32-make
}

package() {
  cd "$srcdir/$_realname-RELEASE_${pkgver//./_}/build"
  mingw32-make DESTDIR=$pkgdir install
}

