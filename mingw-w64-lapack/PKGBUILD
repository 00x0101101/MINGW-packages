# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=lapack
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=3.5.0
pkgrel=1
arch=('any')
pkgdesc="Linear Algebra PACKage (mingw-w64)"
depends=("${_mingw_suff}-crt" "${_mingw_suff}-gcc-fortran")
makedepends=("${_mingw_suff}-cmake" "${_mingw_suff}-gcc-fortran")
options=('strip' 'staticlibs')
license=('LGPL')
url="http://www.netlib.org/lapack"
source=("http://www.netlib.org/${_realname}/${_realname}-${pkgver}.tgz")
sha1sums=('5870081889bf5d15fd977993daab29cf3c5ea970')

build()
{
	mkdir -p ${pkgdir}${MINGW_PREFIX}
	pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
	export PREFIX_WIN=`pwd -W`
	popd > /dev/null

	pushd ${MINGW_PREFIX} > /dev/null
	export PREFIX_DEPS=`pwd -W`
	popd > /dev/null

	cd "$srcdir"
	mkdir -p build-${MINGW_CHOST}-static && pushd build-${MINGW_CHOST}-static
	${MINGW_PREFIX}/bin/cmake \
		-G"MSYS Makefiles" \
		-DCMAKE_INSTALL_PREFIX:PATH=${PREFIX_WIN} \
		-DCMAKE_Fortran_COMPILER:PATH=${PREFIX_DEPS}/bin/gfortran.exe \
		-DCMAKE_BUILD_TYPE=Release \
		-DBUILD_SHARED_LIBS=OFF \
		-DBUILD_TESTING=OFF \
		../${_realname}-${pkgver}
	make
    popd

	mkdir -p build-${MINGW_CHOST} && pushd build-${MINGW_CHOST}
	${MINGW_PREFIX}/bin/cmake \
		-G"MSYS Makefiles" \
		-DCMAKE_INSTALL_PREFIX:PATH=${PREFIX_WIN} \
		-DCMAKE_Fortran_COMPILER:PATH=${PREFIX_DEPS}/bin/gfortran.exe \
		-DCMAKE_BUILD_TYPE=Release \
		-DBUILD_SHARED_LIBS=ON \
		-DBUILD_TESTING=OFF \
		../${_realname}-${pkgver}
	make
	popd
}

package()
{
	cd "$srcdir/build-${MINGW_CHOST}-static"
	make install # DESTDIR="$pkgdir"
	cd "$srcdir/build-${MINGW_CHOST}"
	make install # DESTDIR="$pkgdir"
}
