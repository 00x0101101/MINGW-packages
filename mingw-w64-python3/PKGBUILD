# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=python3
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=3.3.3
pkgrel=1
_pybasever=3.3
pkgdesc="A high-level scripting language (mingw-w64)"
arch=('any')
license=('PSF')
url="http://www.python.org/"
depends=("${_mingw_suff}-crt" 
		"${_mingw_suff}-expat"
		"${_mingw_suff}-bzip2"
		"${_mingw_suff}-readline"
		"${_mingw_suff}-openssl"
		"${_mingw_suff}-libffi"
		"${_mingw_suff}-zlib"
		"${_mingw_suff}-xz")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config" "${_mingw_suff}-ncurses")
options=('!makeflags' 'staticlibs' 'strip')
source=("http://www.python.org/ftp/python/${pkgver%rc?}/Python-${pkgver}.tar.xz"
0000-add-python-config-sh.patch
0005-cross-fixes.patch
0010-cross-darwin-feature.patch
0030-py3k-20121004-MINGW.patch
0031-py3k-20121004-MINGW-removal-of-pthread-patch.patch
0032-py3k-20121004-MINGW-ntthreads.patch
0033-py3k-mingw-ntthreads-vs-pthreads.patch
0034-py3k-20121004-MINGW-removal-of-libffi-patch.patch
0035-mingw-system-libffi.patch
0045-mingw-use-posix-getpath.patch
0050-mingw-sysconfig-like-posix.patch
0055-mingw-_winapi_as_builtin_for_Popen_in_cygwinccompiler.patch
0060-mingw-x86_64-size_t-format-specifier-pid_t.patch
0065-cross-dont-add-multiarch-paths-if-cross-compiling.patch
0070-mingw-use-backslashes-in-compileall-py.patch
0075-msys-convert_path-fix-and-root-hack.patch
0080-mingw-hack-around-double-copy-scripts-issue.patch
0085-allow-static-tcltk.patch
0090-CROSS-avoid-ncursesw-include-path-hack.patch
0091-CROSS-properly-detect-WINDOW-_flags-for-different-nc.patch
0092-mingw-pdcurses_ISPAD.patch
0095-no-xxmodule-for-PYDEBUG.patch
0100-grammar-fixes.patch
0105-builddir-fixes.patch
0110-msys-monkeypatch-os-system-via-sh-exe.patch
0115-msys-replace-slashes-used-in-io-redirection.patch)
sha1sums=('af4e75a34bd538c79b9871227c2e7f56569ac107'
          '71af01b5159179913c6aa321f020f324bcae0611'
          'd1824e20668d34a451daf9e89daecac949941431'
          '5b19d137c226ee135439ee71fdcb3e7410777902'
          '19a1a08809db53856974e387b9a55dfda071dfc0'
          'c18f5ab7c2d11c1f8fc935d4d0661b3c2798e110'
          '8f408f3767db8a473fb3abd09cb6d179802cc4e8'
          'eb723ebc838d6d9e54c4595c9b353467d6bc96cf'
          'ea38c00a004d3c4ada149901176024e1dfcc59dd'
          'fbc221bf142d6b2f0fa90829d1afb13a766f48ec'
          '11eef09d4e2e017a9ca9162f3eee45033267de50'
          '6327d0fd2f19ea421cc9f1370f2b3ba121f61a69'
          '4a39da9f3ab9f30dc24277073d46c36ac3d0c06f'
          '9a83558c163712475242837974cace9b5d1e7902'
          'd065392ed68850195c60a34e03f471dc2378f7e2'
          'daf2004a0ae52ee1fdfe7fdc3508fa0069e9ecde'
          'b050e882c53262fb5bf9cb9d9be02f7e88081085'
          '1944c898edd643b7675dab7c90c0335766f04291'
          '09ceb69276c4f5ac358459f98e034b06e00a7cc5'
          '23b0a0cc2bd4c9d9489a17509261013f12e484ae'
          '8666b22adfea2b53e5d8084149efbf1520fcfac1'
          '78b73373f5c85468c1d5de15e03e20acd74fa773'
          'd881a39da3fec120ff87c7e0d4d9e607557f0f22'
          '51e578a5f9c14aff74dc2807f2e3831a903101ff'
          '75afc61712c282d7f5a3a455cb200e4f27c020af'
          '2041d60c111fcbe825696bab84887f5fa175ed7c'
          'c49cddcea19349379990a775642fdf4051ec77fa')

prepare() {
  cd "${srcdir}/Python-${pkgver}"
  rm -f Modules/_ctypes/libffi_msvc/win32.S Python/fileblocks.c Misc/python-config.sh.in
  patch -Np1 -i "${srcdir}"/0000-add-python-config-sh.patch
  #patch -Np1 -i "${srcdir}"/0005-cross-fixes.patch
  patch -Np1 -i "${srcdir}"/0010-cross-darwin-feature.patch
  patch -Np1 -i "${srcdir}"/0030-py3k-20121004-MINGW.patch
  patch -Np1 -i "${srcdir}"/0031-py3k-20121004-MINGW-removal-of-pthread-patch.patch
  patch -Np1 -i "${srcdir}"/0032-py3k-20121004-MINGW-ntthreads.patch
  patch -Np1 -i "${srcdir}"/0033-py3k-mingw-ntthreads-vs-pthreads.patch
  patch -Np1 -i "${srcdir}"/0034-py3k-20121004-MINGW-removal-of-libffi-patch.patch
  patch -Np1 -i "${srcdir}"/0035-mingw-system-libffi.patch
  patch -Np1 -i "${srcdir}"/0045-mingw-use-posix-getpath.patch
  patch -Np1 -i "${srcdir}"/0050-mingw-sysconfig-like-posix.patch
  patch -Np1 -i "${srcdir}"/0055-mingw-_winapi_as_builtin_for_Popen_in_cygwinccompiler.patch
  patch -Np1 -i "${srcdir}"/0060-mingw-x86_64-size_t-format-specifier-pid_t.patch
  patch -Np1 -i "${srcdir}"/0065-cross-dont-add-multiarch-paths-if-cross-compiling.patch
  patch -Np1 -i "${srcdir}"/0070-mingw-use-backslashes-in-compileall-py.patch
  patch -Np1 -i "${srcdir}"/0075-msys-convert_path-fix-and-root-hack.patch
  patch -Np1 -i "${srcdir}"/0080-mingw-hack-around-double-copy-scripts-issue.patch
  patch -Np1 -i "${srcdir}"/0085-allow-static-tcltk.patch
  patch -Np1 -i "${srcdir}"/0090-CROSS-avoid-ncursesw-include-path-hack.patch
  patch -Np1 -i "${srcdir}"/0091-CROSS-properly-detect-WINDOW-_flags-for-different-nc.patch
  patch -Np1 -i "${srcdir}"/0092-mingw-pdcurses_ISPAD.patch
  #patch -Np1 -i "${srcdir}"/0095-no-xxmodule-for-PYDEBUG.patch
  patch -Np1 -i "${srcdir}"/0100-grammar-fixes.patch
  patch -Np1 -i "${srcdir}"/0105-builddir-fixes.patch
  patch -Np1 -i "${srcdir}"/0110-msys-monkeypatch-os-system-via-sh-exe.patch
  patch -Np1 -i "${srcdir}"/0115-msys-replace-slashes-used-in-io-redirection.patch

  autoreconf -vfi
  
  # Temporary workaround for FS#22322
  # See http://bugs.python.org/issue10835 for upstream report
  #sed -i "/progname =/s/python/python${_pybasever}/" Python/pythonrun.c
  
  touch Include/graminit.h
  touch Python/graminit.c
  touch Parser/Python.asdl
  touch Parser/asdl.py
  touch Parser/asdl_c.py
  touch Include/Python-ast.h
  touch Python/Python-ast.c
  echo \"\" > Parser/pgen.stamp
		
  # Ensure that we are using the system copy of various libraries (expat, zlib and libffi),
  # rather than copies shipped in the tarball
  rm -r Modules/expat
  rm -r Modules/zlib
  rm -r Modules/_ctypes/{darwin,libffi}*
  
}

build() {
  pushd ${MINGW_PREFIX} > /dev/null
  export PREFIX_WIN=`pwd -W`
  popd > /dev/null

  export CFLAGS="-O2 -pipe -fwrapv -DNDEBUG -D__USE_MINGW_ANSI_STDIO=1 -I$PREFIX_WIN/${MINGW_CHOST}/include"
  export CXXFLAGS="-O2 -pipe -fwrapv -DNDEBUG -D__USE_MINGW_ANSI_STDIO=1 -I$PREFIX_WIN/include -I$PREFIX_WIN/include/ncursesw -I$MINGWHOME_WIN/$HOST/include"
  export CPPFLAGS="-O2 -pipe -I$PREFIX_WIN/include -I$PREFIX_WIN/include/ncursesw -I$PREFIX_WIN/${MINGW_CHOST}/include"
  export LDFLAGS="-pipe -s -L$PREFIX_WIN/${MINGW_CHOST}/lib -L$PREFIX_WIN/lib"
  
  LIBFFI_INC=`${MINGW_PREFIX}/bin/pkg-config libffi --cflags-only-I | sed "s|\-I||g"`
  pushd ${LIBFFI_INC} > /dev/null
  export LIBFFI_INCLUDEDIR=`pwd -W`
  popd > /dev/null
  
  # Workaround for conftest error on 64-bit builds
  export ac_cv_working_tzset=no
	
  mkdir -p "${srcdir}/build-${CARCH}" && cd "${srcdir}/build-${CARCH}"
  MSYSTEM=MINGW ../Python-${pkgver}/configure \
      --prefix=${pkgdir}${MINGW_PREFIX} \
      --host=${MINGW_CHOST} \
      --build=${MINGW_CHOST} \
      --enable-shared \
      --with-threads \
      --with-system-expat \
      --with-system-ffi \
	  OPT="" \
	  CXX=g++
      # --with-dbmliborder=gdbm:ndbm
    make
}

package() {
    cd "${srcdir}/build-${CARCH}"
    make install # DESTDIR="$pkgdir"
    #mv "${pkgdir}${MINGW_PREFIX}"/bin/smtpd.py "${pkgdir}${MINGW_PREFIX}"/lib/python${_pybasever}/

	cp -f "${pkgdir}${MINGW_PREFIX}"/lib/python${_pybasever}/config-${_pybasever}/libpython${_pybasever}.dll.a "${pkgdir}${MINGW_PREFIX}"/lib/libpython${_pybasever}.dll.a

	# some useful "stuff"
	install -dm755 "${pkgdir}${MINGW_PREFIX}"/lib/python${_pybasever}/Tools/{i18n,scripts}
	install -m755 "${srcdir}/Python-${pkgver}"/Tools/i18n/{msgfmt,pygettext}.py "${pkgdir}${MINGW_PREFIX}"/lib/python${_pybasever}/Tools/i18n/
	install -m755 "${srcdir}/Python-${pkgver}"/Tools/scripts/{README,*py} "${pkgdir}${MINGW_PREFIX}"/lib/python${_pybasever}/Tools/scripts/

	# clean up #!s
	find "${pkgdir}${MINGW_PREFIX}"/lib/python${_pybasever}/ -name '*.py' | \
		xargs sed -i "s|#[ ]*![ ]*/usr/bin/env python$|#!/usr/bin/env python3|"

	# clean-up reference to build directory
	sed -i "s#${srcdir}/Python-${pkgver}:##" "${pkgdir}${MINGW_PREFIX}"/lib/python${_pybasever}/config-${_pybasever}/Makefile
	
	#sed -i -e "s|^#.* /usr/local/bin/python|#!/usr/bin/python3|"
	sed -i "s|#!${pkgdir}${MINGW_PREFIX}/bin/python${_pybasever}.exe|#!python3|" "${pkgdir}${MINGW_PREFIX}"/bin/python${_pybasever}-config
	sed -i "s|#!${pkgdir}${MINGW_PREFIX}/bin/python${_pybasever}.exe|#!python3|" "${pkgdir}${MINGW_PREFIX}"/bin/python3-config
	sed -i "s|#!${pkgdir}${MINGW_PREFIX}/bin/python${_pybasever}.exe|#!python3|" "${pkgdir}${MINGW_PREFIX}"/bin/python-config
	
	# fix permissons
	find ${pkgdir}${MINGW_PREFIX} -type f \( -name *.dll -o -name *.exe \) | xargs chmod 0755
	
	# replace paths in sysconfig
	sed -i "s|${pkgdir}${MINGW_PREFIX}|${MINGW_PREFIX}|g" \
		"${pkgdir}${MINGW_PREFIX}"/lib/python${_pybasever}/_sysconfigdata.py \
		"${pkgdir}${MINGW_PREFIX}"/lib/python${_pybasever}/smtpd.py
		
}
