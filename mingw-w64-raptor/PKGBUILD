# Maintainer: Jeroen Ooms <jeroenooms@gmail.com>

_realname=raptor2
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.0.15
pkgrel=1
pkgdesc="RDF parser toolkit (mingw-w64)"
arch=('any')
url='http://librdf.org/raptor/'
license=('LICENSE')
source=("http://download.librdf.org/source/${_realname}-${pkgver}.tar.gz")
sha1sums=("504231f87024df9aceb90eb957196b557b4b8e38")

depends=("${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-curl")

prepare() {
  cd $srcdir/${_realname}-${pkgver}
}

build() {
  cd "$srcdir"/${_realname}-${pkgver}
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}
  CFLAGS="-DNESTED_QSORT -DRAPTOR_STATIC -DIN_LIBXML" \
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --disable-debug \
    --disable-dependency-tracking \
    --with-www=none

  make
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make install DESTDIR="${pkgdir}"
}
