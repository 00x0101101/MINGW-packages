# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libidn
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=1.28
pkgrel=1
pkgdesc="Implementation of the Stringprep, Punycode and IDNA specifications (mingw-w64)"
arch=('any')
url="http://www.gnu.org/software/libidn"
license=("GPL3, LGPL")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config")
depends=("${_mingw_suff}-winpthreads" "${_mingw_suff}-gettext")
options=('staticlibs' 'strip')
source=("ftp://ftp.gnu.org/gnu/libidn/libidn-${pkgver}.tar.gz"
		0001-fix-gtkdoc.all.patch
		0002-fix-gdoc.all.patch)
sha1sums=('725587211b229c156e29fa2ad116b0ef71a7ca17'
          '42baea83d76aeaa99e50f551d98dade8a5394fe3'
          '730dfdfdf3c2878113224c104f9c6c213010d7f0')

prepare() {
  cd "${srcdir}"/libidn-${pkgver}
  patch -p1 -i ${srcdir}/0001-fix-gtkdoc.all.patch
  patch -p1 -i ${srcdir}/0002-fix-gdoc.all.patch
}

build() {
  mkdir -p "${srcdir}/${_realname}-${pkgver}-build-${CARCH}"
  cd "${srcdir}/${_realname}-${pkgver}-build-${CARCH}"
  "${srcdir}"/libidn-${pkgver}/configure \
      --prefix=${MINGW_PREFIX}\
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
	  --target=${MINGW_CHOST} \
      --disable-csharp \
      --disable-java \
      --enable-threads=win32
  make
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}-build-${CARCH}"
  make DESTDIR="$pkgdir" install
  #find "${pkgdir}${MINGW_PREFIX}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
  #find "${pkgdir}${MINGW_PREFIX}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
  #find "${pkgdir}${MINGW_PREFIX}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
  rm -r "${pkgdir}${MINGW_PREFIX}/share"
}
