# Maintainer: Martell Malone <martellmalone@gmail.com>
# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=git
# Marat Radchenko is trying to upstream mingw-w64 patches
# and Johannes Schindelin made a branch of msysgit for it
# Set _based_on_dscho_w64_msysgit to yes to build that.
# Patches that required re-basing for this repository are
# infixed with "-w64-msysgit".
_based_on_dscho_w64_msysgit=no
if [ "${_based_on_dscho_w64_msysgit}" = "yes" ]; then
  SRC_DIR=${_realname}-dscho
  GIT_URL="${SRC_DIR}::git+http://github.com/dscho/git.git#branch=w64-msysgit"
else
  SRC_DIR=${_realname}
  GIT_URL="${SRC_DIR}::git+http://github.com/msysgit/git.git"
fi

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
_ver_base=2.0.0
pkgver=2.0.0.47132.e31124d
pkgrel=1
pkgdesc="The fast distributed version control system (mingw-w64)"
arch=('any')
url="http://git-scm.com/"
license=('GPL2')

depends=("${MINGW_PACKAGE_PREFIX}-libiconv")

options=(!libtool strip staticlibs)

source=(${GIT_URL}
        "0001-Don-t-redefine-_ReadWriteBarrier-in-malloc.c.h.patch"
        "0002-Undefine-FORCEINLINE-on-MinGW-w64-in-malloc.c.h.patch"
        "0003-Fix-BASIC_LDFLAGS-and-COMPAT_CFLAGS-for-64bit-MinGW-.patch"
        "0004-Also-look-in-winsock2.h-for-sockaddr_storage-and-loo.patch"
        "0005-Don-t-redefine-some-things-MinGW-w64-provides.patch"
        "0006-Include-ntdef.h-on-MinGW-w64-for-REPARSE_DATA_BUFFER.patch"
        "0007-Include-winsock2.h-and-not-netdb.h-on-MinGW-w64.patch"
        "0008-fix-mingw64-compat-wide-char.patch"
        "0009-fix-fork-for-mingw64.patch"
        "0010-Use-GIT_CHECK_FUNC-for-socket-and-basename.patch"
        "0010-w64-msysgit-Use-GIT_CHECK_FUNC-for-socket-and-basename.patch"
        "0011-Use-GIT_CHECK_FUNC-for-regcomp-regex.patch"
        "0011-w64-msysgit-Use-GIT_CHECK_FUNC-for-regcomp-regex.patch"
        "0012-Use-system-dirent.h-on-MinGW-w64.patch"
        "0013-Remove-custom-pthread-layer.patch"
        "0014-Do-not-compile-dirent-on-mingw64.patch"
        "0015-Alter-fscache-for-mingw64.patch")

md5sums=('SKIP'
         'cd1574d3529aac3f0c9f4194939ac23f'
         '2383167c26c29fe7af9b3c73758a8b55'
         '04f800e290884b8d4659f5ebdc9456ca'
         '3e986d42aa12f0b54c59d02638aa4068'
         '14ff828b1d7c771d2da2977dbb018f09'
         'b2a58b49e186aa43758584b77a8b47e1'
         '970e1ee0021779dd646a81536d42bcef'
         '62b8a45fa280082bb73be75a82aa44c7'
         'fd874a1c65f2e2040d486054e46d252c'
         '43d160e878841cb5642683a060c39c1f'
         '69af98595eeabd892289de06d0cd009f'
         '157fb4de7db3b3b53025c103780da2f2'
         '6e59f1619c2a9d8575c53ca8a8ab7f6b'
         '1aacf732e02da4cb6604b4496ba24ff9'
         'c6a4066fb670e95ed102b543e7d67c01'
         'c594da89c1d029abdc2d85eb61fc6bea'
         '185c2096bf376b056445d57d92dbe3a0')

pkgver() {
  cd "${srcdir}"/${SRC_DIR}
  printf "%s.%s.%s" "$_ver_base" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  export PYTHON_PATH='/usr/bin/python2'
  cd "${srcdir}"/${SRC_DIR}

  if [ "${_based_on_dscho_w64_msysgit}" = "no" ]; then
    git am "${srcdir}"/0001-Don-t-redefine-_ReadWriteBarrier-in-malloc.c.h.patch
    git am "${srcdir}"/0002-Undefine-FORCEINLINE-on-MinGW-w64-in-malloc.c.h.patch
    git am "${srcdir}"/0003-Fix-BASIC_LDFLAGS-and-COMPAT_CFLAGS-for-64bit-MinGW-.patch
  fi
  git am "${srcdir}"/0004-Also-look-in-winsock2.h-for-sockaddr_storage-and-loo.patch
  if [ "${_based_on_dscho_w64_msysgit}" = "no" ]; then
    git am "${srcdir}"/0005-Don-t-redefine-some-things-MinGW-w64-provides.patch
    git am "${srcdir}"/0006-Include-ntdef.h-on-MinGW-w64-for-REPARSE_DATA_BUFFER.patch
  fi
  git am "${srcdir}"/0007-Include-winsock2.h-and-not-netdb.h-on-MinGW-w64.patch
  if [ "${_based_on_dscho_w64_msysgit}" = "no" ]; then
    git am "${srcdir}"/0008-fix-mingw64-compat-wide-char.patch
    git am "${srcdir}"/0009-fix-fork-for-mingw64.patch
    git am "${srcdir}"/0010-Use-GIT_CHECK_FUNC-for-socket-and-basename.patch
  fi
  if [ "${_based_on_dscho_w64_msysgit}" = "no" ]; then
    git am "${srcdir}"/0010-Use-GIT_CHECK_FUNC-for-socket-and-basename.patch
    git am "${srcdir}"/0011-Use-GIT_CHECK_FUNC-for-regcomp-regex.patch
  else
    git am "${srcdir}"/0010-w64-msysgit-Use-GIT_CHECK_FUNC-for-socket-and-basename.patch
    git am "${srcdir}"/0011-w64-msysgit-Use-GIT_CHECK_FUNC-for-regcomp-regex.patch
  fi
  if [ "${_based_on_dscho_w64_msysgit}" = "no" ]; then
    git am "${srcdir}"/0012-Use-system-dirent.h-on-MinGW-w64.patch
    git am "${srcdir}"/0013-Remove-custom-pthread-layer.patch
    git am "${srcdir}"/0014-Do-not-compile-dirent-on-mingw64.patch
    git am "${srcdir}"/0015-Alter-fscache-for-mingw64.patch
  fi

  autoreconf -fi
}

build() {

  export PYTHON_PATH=/usr/bin/python2
  cd "$srcdir"/${SRC_DIR}

  ./configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib \
    --with-editor=vim \
    --htmldir=${MINGW_PREFIX}/share/doc/git/html \
    --mandir=${MINGW_PREFIX}/share/man \
    --with-curl=${MINGW_PREFIX}

  make INSTALLDIRS=vendor -j1 all V=1
  make -C contrib/subtree prefix=/usr all V=1
}

package() {
  export PYTHON_PATH=/usr/bin/python2
  cd "$srcdir"/${SRC_DIR}
  make INSTALLDIRS=vendor DESTDIR="$pkgdir" install
}
