# Maintainer: Martell Malone <martellmalone@gmail.com>
# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=git

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
_ver_base=2.0.0
pkgver=2.0.0.46073.b31f626
pkgrel=1
pkgdesc="The fast distributed version control system (mingw-w64)"
arch=('any')
url="http://git-scm.com/"
license=('GPL2')

depends=("${MINGW_PACKAGE_PREFIX}-libiconv")

options=(!libtool strip staticlibs)

source=("${_realname}"::"git+http://github.com/msysgit/git.git"
        "0001-Don-t-redefine-_ReadWriteBarrier-in-malloc.c.h.patch"
        "0002-Undefine-FORCEINLINE-on-MinGW-w64-in-malloc.c.h.patch"
        "0003-Fix-BASIC_LDFLAGS-and-COMPAT_CFLAGS-for-64bit-MinGW-.patch"
        "0004-Also-look-in-winsock2.h-for-sockaddr_storage-and-loo.patch"
        "0005-Don-t-redefine-some-things-MinGW-w64-provides.patch"
        "0006-Include-ntdef.h-on-MinGW-w64-for-REPARSE_DATA_BUFFER.patch"
        "0007-Include-winsock2.h-and-not-netdb.h-on-MinGW-w64.patch"
        "0008-fix-mingw64-compat-wide-char.patch"
        "0009-fix-fork-for-mingw64.patch"
        "0010-Use-GIT_CHECK_FUNC-for-socket-and-basename.patch"
        "0011-Use-GIT_CHECK_FUNC-for-regcomp-regex.patch"
        "0012-Use-system-dirent.h-on-MinGW-w64.patch")

md5sums=('SKIP'
         '8901928a77a11c5e65e041d51e9ef3dc'
         '34dc7a8425a8fa10d7234afb4aa2c4ac'
         'ed7cc8023b73cfda67da2178c9269118'
         'eaf2703bfe4d82b73d571f5de95727ac'
         '4727b6317e6671835d84d56b4be96fd5'
         '72204f65ae61129de8d03dfeb66f6ed0'
         '362d7befa54ce9bf1db8bb778dc09fe2'
         'b378d4b96c440e2511fd061924679ff6'
         '7a14a263fd6b895ba5f2a4009687e9eb'
         'ae07c3d215cc6ebc89c17dc5eebfdbd8'
         '7e46452ae2ac4abdacc14bd2f826aadd')

pkgver() {
  cd "$srcdir/$_realname"
  printf "%s.%s.%s" "$_ver_base" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  export PYTHON_PATH='/usr/bin/python2'
  cd "$srcdir/$_realname"

  git am "${srcdir}"/0001-Don-t-redefine-_ReadWriteBarrier-in-malloc.c.h.patch
  git am "${srcdir}"/0002-Undefine-FORCEINLINE-on-MinGW-w64-in-malloc.c.h.patch
  git am "${srcdir}"/0003-Fix-BASIC_LDFLAGS-and-COMPAT_CFLAGS-for-64bit-MinGW-.patch
  git am "${srcdir}"/0004-Also-look-in-winsock2.h-for-sockaddr_storage-and-loo.patch
  git am "${srcdir}"/0005-Don-t-redefine-some-things-MinGW-w64-provides.patch
  git am "${srcdir}"/0006-Include-ntdef.h-on-MinGW-w64-for-REPARSE_DATA_BUFFER.patch
  git am "${srcdir}"/0007-Include-winsock2.h-and-not-netdb.h-on-MinGW-w64.patch
  git am "${srcdir}"/0008-fix-mingw64-compat-wide-char.patch
  git am "${srcdir}"/0009-fix-fork-for-mingw64.patch
  git am "${srcdir}"/0010-Use-GIT_CHECK_FUNC-for-socket-and-basename.patch
  git am "${srcdir}"/0011-Use-GIT_CHECK_FUNC-for-regcomp-regex.patch
  git am "${srcdir}"/0012-Use-system-dirent.h-on-MinGW-w64.patch

  autoreconf -fi
}

build() {

  export PYTHON_PATH='/usr/bin/python2'
  cd "$srcdir/$_realname"
  
  ./configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib \
    --with-editor=vim \
    --htmldir=${MINGW_PREFIX}/share/doc/git/html \
    --mandir=${MINGW_PREFIX}/share/man \
    --with-curl=${MINGW_PREFIX}

  make INSTALLDIRS=vendor -j1 all V=1
  make -C contrib/subtree prefix=/usr all V=1
}

package() {
  export PYTHON_PATH='/usr/bin/python2'
  cd "$srcdir/$_realname"
  make INSTALLDIRS=vendor DESTDIR="$pkgdir" install
}

# // 35 "E:/msys64/mingw64/x86_64-w64-mingw32/include/crtdefs.h" 3
# __extension__ typedef unsigned long long size_t;
# // 258 "E:/msys64/mingw64/x86_64-w64-mingw32/include/ws2tcpip.h" 3
#    typedef int size_t;
