# Maintainer: Martell Malone <martellmalone@gmail.com>

_realname=git

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
_ver_base=2.0.0
pkgver=2.0.0.46073.b31f626
pkgrel=1
pkgdesc="The fast distributed version control system (mingw-w64)"
arch=('any')
url="http://git-scm.com/"
license=('GPL2')

depends=("${MINGW_PACKAGE_PREFIX}-libiconv")

options=(!libtool strip staticlibs)

source=("${_realname}"::"git+http://github.com/msysgit/git.git"
        "0001-fix-mingw64-compat-wide-char.patch"
        "0002-fix-fork-for-mingw64.patch"
        "0003-fix-autoconf-socklen-mingw64.patch"
        "0004-fix-mingw64-32bit-time-temp.patch")

md5sums=('SKIP'
         '8c5f8e66329ed6c8680fc5d67fbf5f88'
         'fd2b671dc5ef615904ffea082045ca59'
         '85dd5d3d3c66731760cebb47d3e826bc'
         'f498d2eb285e4adb24e9526aa527fa56')

pkgver() {
  cd "$srcdir/$_realname"
  printf "%s.%s.%s" "$_ver_base" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  export PYTHON_PATH='/usr/bin/python2'
  cd "$srcdir/$_realname"
  git am $srcdir/0001-fix-mingw64-compat-wide-char.patch
  git am $srcdir/0002-fix-fork-for-mingw64.patch
  git am $srcdir/0003-fix-autoconf-socklen-mingw64.patch
  git am $srcdir/0004-fix-mingw64-32bit-time-temp.patch
  autoreconf -fi
}

build() {

  export PYTHON_PATH='/usr/bin/python2'
  cd "$srcdir/$_realname"
  
  ./configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib \
    --with-editor=vim \
    --htmldir=${MINGW_PREFIX}/share/doc/git/html \
    --mandir=${MINGW_PREFIX}/share/man

  make INSTALLDIRS=vendor V=1 -j1 all
  make -C contrib/subtree prefix=/usr all

}

package() {
  export PYTHON_PATH='/usr/bin/python2'
  cd "$srcdir/$_realname"
  make INSTALLDIRS=vendor DESTDIR="$pkgdir" install
}
