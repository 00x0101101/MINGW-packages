# Maintainer: Martell Malone <martellmalone@gmail.com>

_realname=git

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
_ver_base=2.0.0
pkgver=2.0.0.46073.b31f626
pkgrel=1
pkgdesc="The fast distributed version control system (mingw-w64)"
arch=('any')
url="http://git-scm.com/"
license=('GPL2')

depends=("${MINGW_PACKAGE_PREFIX}-libiconv")

options=(!libtool strip staticlibs)

source=("${_realname}"::"git+http://github.com/msysgit/git.git"
        "0001-fix-mingw64-compat-wide-char.patch"
        "0002-fix-fork-for-mingw64.patch"
        "0003-fix-autoconf-socklen-mingw64.patch"
        "0004-fix-mingw64-32bit-time-temp.patch"
        "0005-Also-look-in-winsock2.h-for-sockaddr_storage.patch")

md5sums=('SKIP'
         'a433d3a32a210d83a6c86d193fa4fefb'
         'bf230d8bb9528938e894aa052fd5f599'
         'b795b7c04019b28e8a7c5b0d7623e26b'
         'd0bbc5add2b3e3b04b493c0f46d2f9bf'
         'ca345e2e3ebbff3d9dc1a631d26c21da')

pkgver() {
  cd "$srcdir/$_realname"
  printf "%s.%s.%s" "$_ver_base" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  export PYTHON_PATH='/usr/bin/python2'
  cd "$srcdir/$_realname"
  git am $srcdir/0001-fix-mingw64-compat-wide-char.patch
  git am $srcdir/0002-fix-fork-for-mingw64.patch
  git am $srcdir/0003-fix-autoconf-socklen-mingw64.patch
  git am $srcdir/0004-fix-mingw64-32bit-time-temp.patch
  git am $srcdir/0005-Also-look-in-winsock2.h-for-sockaddr_storage.patch

  autoreconf -fi
}

build() {

  export PYTHON_PATH='/usr/bin/python2'
  cd "$srcdir/$_realname"
  
  ./configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib \
    --with-editor=vim \
    --htmldir=${MINGW_PREFIX}/share/doc/git/html \
    --mandir=${MINGW_PREFIX}/share/man

  make INSTALLDIRS=vendor -j1 all V=1
  make -C contrib/subtree prefix=/usr all V=1
}

package() {
  export PYTHON_PATH='/usr/bin/python2'
  cd "$srcdir/$_realname"
  make INSTALLDIRS=vendor DESTDIR="$pkgdir" install
}
