# Maintainer: Martell Malone <martellmalone@gmail.com>
# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=git

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
_ver_base=2.0.0
pkgver=2.0.0.46073.b31f626
pkgrel=1
pkgdesc="The fast distributed version control system (mingw-w64)"
arch=('any')
url="http://git-scm.com/"
license=('GPL2')

depends=("${MINGW_PACKAGE_PREFIX}-libiconv")

options=(!libtool strip staticlibs)

source=("${_realname}"::"git+http://github.com/msysgit/git.git"
        "0001-Don-t-redefine-_ReadWriteBarrier-in-malloc.c.h.patch"
        "0002-Undefine-FORCEINLINE-on-MinGW-w64-in-malloc.c.h.patch"
        "0003-Fix-BASIC_LDFLAGS-and-COMPAT_CFLAGS-for-64bit-MinGW-.patch"
        "0004-Also-look-in-winsock2.h-for-sockaddr_storage-and-loo.patch"
        "0005-Don-t-redefine-some-things-MinGW-w64-provides.patch"
        "0006-Include-ntdef.h-on-MinGW-w64-for-REPARSE_DATA_BUFFER.patch"
        "0007-Include-winsock2.h-and-not-netdb.h-on-MinGW-w64.patch"
        "0008-fix-mingw64-compat-wide-char.patch"
        "0009-fix-fork-for-mingw64.patch")

md5sums=('SKIP'
         'c34c63b5c08d309e641daeea6ec2d95b'
         '51ed5b9e80b790975c2e9df5c90b39bc'
         '876acef8a5fe156fc6be33e4eb45c0a7'
         '41be411c3cca60660c06f6094e79a521'
         '2a3e57970a56a0b554a997bd36776168'
         '65cdcfe68eeacbbf56d184dd9330a498'
         '22f51b519538454ee3d3086b296bb505'
         '74e5ff3dc965e1dddcdfdc1fb060b783'
         '2b0648efeccde7e8f9892a65d2111501')

pkgver() {
  cd "$srcdir/$_realname"
  printf "%s.%s.%s" "$_ver_base" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  export PYTHON_PATH='/usr/bin/python2'
  cd "$srcdir/$_realname"

  git am "${srcdir}"/0001-Don-t-redefine-_ReadWriteBarrier-in-malloc.c.h.patch
  git am "${srcdir}"/0002-Undefine-FORCEINLINE-on-MinGW-w64-in-malloc.c.h.patch
  git am "${srcdir}"/0003-Fix-BASIC_LDFLAGS-and-COMPAT_CFLAGS-for-64bit-MinGW-.patch
  git am "${srcdir}"/0004-Also-look-in-winsock2.h-for-sockaddr_storage-and-loo.patch
  git am "${srcdir}"/0005-Don-t-redefine-some-things-MinGW-w64-provides.patch
  git am "${srcdir}"/0006-Include-ntdef.h-on-MinGW-w64-for-REPARSE_DATA_BUFFER.patch
  git am "${srcdir}"/0007-Include-winsock2.h-and-not-netdb.h-on-MinGW-w64.patch
  git am "${srcdir}"/0008-fix-mingw64-compat-wide-char.patch
  git am "${srcdir}"/0009-fix-fork-for-mingw64.patch

  autoreconf -fi
}

build() {

  export PYTHON_PATH='/usr/bin/python2'
  cd "$srcdir/$_realname"
  
  ./configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib \
    --with-editor=vim \
    --htmldir=${MINGW_PREFIX}/share/doc/git/html \
    --mandir=${MINGW_PREFIX}/share/man

  make INSTALLDIRS=vendor -j1 all V=1
  make -C contrib/subtree prefix=/usr all V=1
}

package() {
  export PYTHON_PATH='/usr/bin/python2'
  cd "$srcdir/$_realname"
  make INSTALLDIRS=vendor DESTDIR="$pkgdir" install
}

# // 35 "E:/msys64/mingw64/x86_64-w64-mingw32/include/crtdefs.h" 3
# __extension__ typedef unsigned long long size_t;
# // 258 "E:/msys64/mingw64/x86_64-w64-mingw32/include/ws2tcpip.h" 3
#    typedef int size_t;
