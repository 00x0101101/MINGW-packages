# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=dnscrypt-proxy
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.4.0
pkgrel=1
pkgdesc="A tool for securing communications between a client and a DNS resolver (mingw-w64)"
arch=("any")
url="http://dnscrypt.org"
license=('custom:ISC')
depends=("${MINGW_PACKAGE_PREFIX}-libsodium")
source=("http://download.dnscrypt.org/${_realname}/${_realname}-$pkgver.tar.bz2")
md5sums=('40b5b73f5042330b86084460d7c839c6')

build() {
  cd ${_realname}-${pkgver}
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --sbindir=${MINGW_PREFIX}/bin \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-plugins \
    --with-included-ltdl
  make
}

check() {
  cd ${_realname}-${pkgver}
  make -k check
}

package() {
  cd ${_realname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
