# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=protobuf
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.6.0
pkgrel=1
pkgdesc="Protocol Buffers - Google's data interchange format (mingw-w64)"
arch=('any')
url="https://github.com/google/protobuf/"
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs" "${MINGW_PACKAGE_PREFIX}-zlib")
groups=("${MINGW_PACKAGE_PREFIX}")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-python2-setuptools")
options=('staticlibs' 'strip')
source=("https://protobuf.googlecode.com/svn/rc/${_realname}-$pkgver.tar.gz")
md5sums=('9959d86087e64524d7f91e7a5a6e4fd7')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
}

build() {
  [[ -d "$srcdir/build-${MINGW_CHOST}" ]] && rm -rf "$srcdir/build-${MINGW_CHOST}"
  cp -rf ${_realname}-${pkgver} build-${MINGW_CHOST}
  cd "$srcdir/build-${MINGW_CHOST}"

  ./configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST}
  make
}

check() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make check
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make DESTDIR="$pkgdir" install
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING.txt "${pkgdir}${MINGW_PREFIX}"/share/licenses/${_realname}/LICENSE
  
  #cd python
  #MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  #${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} --root="$pkgdir"
}
