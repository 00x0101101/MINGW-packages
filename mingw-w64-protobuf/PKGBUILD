# Maintainer: Mario Emmenlauer <memmenlauer@biodataanalysis.de>

_realname=protobuf
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
_pkgver=3.0.0-beta-2
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Protocol Buffers - Google's data interchange format (mingw-w64)"
arch=('any')
url='https://developers.google.com/protocol-buffers/'
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs" "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-cmake" "automake" "autoconf" "libtool")
options=('staticlibs' 'strip')
source=(${_realname}-${_pkgver}.tar.gz::https://github.com/google/protobuf/archive/v${_pkgver}.tar.gz
        googlemock-release-1.7.0.tar.gz::https://github.com/google/googlemock/archive/release-1.7.0.tar.gz
	googletest-release-1.7.0.tar.gz::https://github.com/google/googletest/archive/release-1.7.0.tar.gz)
sha256sums=('be224d07ce87f12e362cff3df02851107bf92a4e4604349b1d7a4b1f0c3bfd86'
            '3f20b6acb37e5a98e8c4518165711e3e35d47deb6cdb5a4dd4566563b5efd232'
	    'f73a6546fdf9fce9ff93a5015e0333a8af3062a152a9ad6bcb772c96687016cc')

prepare() {
  cd "${srcdir}/${_realname}-${_pkgver}"

  #mkdir -p ./gmock
  #tar --directory ./gmock --strip-components 1 -xzf ${srcdir}/googlemock-release-1.7.0.tar.gz
  #mkdir -p ./gtest
  #tar --directory ./gtest --strip-components 1 -xzf ${srcdir}/googletest-release-1.7.0.tar.gz
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  cp -rf "${_realname}-${_pkgver}" "build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  ./autogen.sh

  ./configure \
      --prefix="${MINGW_PREFIX}" \
      --build="${MINGW_CHOST}" \
      --host="${MINGW_CHOST}" \
      --enable-shared

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake ./cmake \
      -Wno-dev \
      -G"MSYS Makefiles" \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      -DCMAKE_BUILD_TYPE="Release" \
      -DCMAKE_SKIP_RPATH="ON" \
      -Dprotobuf_MSVC_STATIC_RUNTIME="ON" \
      -DBUILD_SHARED_LIBS="ON" \
      -Dprotobuf_BUILD_TESTS="ON" \
      -Dprotobuf_WITH_ZLIB="ON"

  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make check
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 ${srcdir}/${_realname}-${_pkgver}/LICENSE "${pkgdir}${MINGW_PREFIX}"/share/licenses/${_realname}/LICENSE
  install -Dm644 ${srcdir}/build-${MINGW_CHOST}/protobuf.pc "${pkgdir}${MINGW_PREFIX}"/lib/pkgconfig/protobuf.pc
  install -Dm644 ${srcdir}/build-${MINGW_CHOST}/protobuf-lite.pc "${pkgdir}${MINGW_PREFIX}"/lib/pkgconfig/protobuf-lite.pc
}
