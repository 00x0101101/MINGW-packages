# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gedit
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.12.2
pkgrel=1
arch=('any')
pkgdesc="A text editor for GNOME (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-gtksourceview3"
        "${MINGW_PACKAGE_PREFIX}-libpeas"
        "${MINGW_PACKAGE_PREFIX}-enchant"
        "${MINGW_PACKAGE_PREFIX}-iso-codes"
        "${MINGW_PACKAGE_PREFIX}-python3-gobject"
        "${MINGW_PACKAGE_PREFIX}-gsettings-desktop-schemas")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
            "${MINGW_PACKAGE_PREFIX}-pkg-config"
            "${MINGW_PACKAGE_PREFIX}-python3"
            "${MINGW_PACKAGE_PREFIX}-vala"
            "intltool"
            "itstool"
            "gettext-devel"
            "yelp-tools")
options=('strip' 'staticlibs')
license=("BSD")
url="http://www.gnome.org"
install=gedit-${CARCH}.install
source=(http://ftp.gnome.org/pub/gnome/sources/${_realname}/${pkgver:0:4}/${_realname}-$pkgver.tar.xz
        0001-Tentative-fix-for-win32-build-warning.patch
        0002-metadata-manager-use-gdatetime-and-guint64.patch
        0001-metadata-manager-retry-another-blind-fix.patch
        0001-Also-change-the-format.patch
        0001-Fix-the-windows-build.patch
        0001-Specify-INTROSPECTION_SCANNER_ENV-to-be-able-to-buil.patch
        0003-pass-gtk-cflags-to-scanner.patch)
md5sums=('a23644771605c4226059d0b92faf70d2'
         '924ee0c750ca9f4e88edff0bf828a5bc'
         '6880986c9eb5b67832888048d837114b'
         '11e8a503b5ef5d3f6756966e7b645afa'
         '061632885f8c9f5e4687f6153ed33fa1'
         '3513625f98e48292890b278d98959442'
         '905776dbf7dfc43064258835c9d96b4c'
         'f606ec2fa63eef8d1246f2042bd789d2')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0001-Tentative-fix-for-win32-build-warning.patch
  patch -p1 -i ${srcdir}/0002-metadata-manager-use-gdatetime-and-guint64.patch
  patch -p1 -i ${srcdir}/0001-metadata-manager-retry-another-blind-fix.patch
  patch -p1 -i ${srcdir}/0001-Also-change-the-format.patch
  patch -p1 -i ${srcdir}/0001-Fix-the-windows-build.patch
  patch -p1 -i ${srcdir}/0001-Specify-INTROSPECTION_SCANNER_ENV-to-be-able-to-buil.patch
  patch -p1 -i ${srcdir}/0003-pass-gtk-cflags-to-scanner.patch
  autopoint --force
  AUTOPOINT='intltoolize --automake --copy' autoreconf -f -i
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  export PYTHON=${MINGW_PREFIX}/bin/python3
  ../${_realname}-${pkgver}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib \
    --disable-updater \
    --disable-schemas-compile \
    --enable-python
  make
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make DESTDIR=$pkgdir install
}
