# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gedit
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.13.4
pkgrel=2
arch=('any')
pkgdesc="A text editor for GNOME (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-gtksourceview3"
        "${MINGW_PACKAGE_PREFIX}-libpeas"
        "${MINGW_PACKAGE_PREFIX}-enchant"
        "${MINGW_PACKAGE_PREFIX}-iso-codes"
        "${MINGW_PACKAGE_PREFIX}-python3-gobject"
        "${MINGW_PACKAGE_PREFIX}-gsettings-desktop-schemas")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
            "${MINGW_PACKAGE_PREFIX}-pkg-config"
            "${MINGW_PACKAGE_PREFIX}-python3"
            "${MINGW_PACKAGE_PREFIX}-vala"
            "intltool"
            "itstool"
            "gettext-devel"
            "yelp-tools")
options=('strip' 'staticlibs')
license=("BSD")
url="http://www.gnome.org"
install=gedit-${CARCH}.install
source=(http://ftp.gnome.org/pub/gnome/sources/${_realname}/${pkgver:0:4}/${_realname}-$pkgver.tar.xz
        gtk-utility.patch
        split-menus.patch)
sha256sums=('5091b329af74ec10a2a250c5f248dd570048af8f91d083da7d7c64961d7f0f6e'
            '49ffc0743738a6d60c1f98195c0f882b29f6aeb04a28d7ab25719c99eba03783'
            'd690093c6d87642734308903e59a9e6b93a1774044dacace180e19d2023455e0')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -Np1 -i "$srcdir/gtk-utility.patch"
  patch -Np1 -i "$srcdir/split-menus.patch"
  #autopoint --force
  AUTOPOINT='intltoolize --automake --copy' autoreconf -f -i
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  export PYTHON=${MINGW_PREFIX}/bin/python3
  ../${_realname}-${pkgver}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib \
    --disable-updater \
    --disable-schemas-compile \
    --enable-python
  make
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make DESTDIR=$pkgdir install
}
