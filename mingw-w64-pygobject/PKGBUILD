# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=pygobject
_mingw_suff=mingw-w64-${CARCH}
pkgname=("${_mingw_suff}-python2-gobject2" "${_mingw_suff}-python3-gobject2")
pkgver=3.11.5
pkgrel=1
pkgdesc="Allows you to load glade interface files in a program at runtime (mingw-w64)"
arch=(any)
url="http://www.pygtk.org"
license=('LGPL')
groups=("${_mingw_suff}")
makedepends=("${_mingw_suff}-python2" "${_mingw_suff}-python3")
options=('staticlibs' 'strip')
source=(http://ftp.gnome.org/pub/gnome/sources/pygobject/${pkgver%.*}/${_realname}-${pkgver}.tar.xz
		Werror-remove.patch)
md5sums=('f4129a9cd6f2fe3ae15d1e63c8c7818f'
         '97364096214dcf2c43f731d4fc3c1d1f')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/Werror-remove.patch
  autoreconf -fi
}

build() {
	cd "$srcdir"

	rm -rf python{2,3}-build
	CFLAGS+=" -D__USE_MINGW_ANSI_STDIO=1 -Wno-error=format"
	CXXFLAGS+=" -D__USE_MINGW_ANSI_STDIO=1"
	CPPFLAGS+=" -D__USE_MINGW_ANSI_STDIO=1"
	for builddir in python{2,3}-build; do
		cp -r ${_realname}-$pkgver $builddir
		pushd $builddir
		PYTHON=${MINGW_PREFIX}/bin/${builddir%-build} \
			./configure --prefix=${MINGW_PREFIX} \
				--build=${MINGW_CHOST} \
				--host=${MINGW_CHOST} \
				--target=${MINGW_CHOST} \
				--enable-introspection \
				--disable-silent-rules
			make VERBOSE=1
		popd
	done
}

package_python2-gobject2() {
  pkgdesc="Python 2 bindings for GObject2 (mingw-w64)"
  depends=("${_mingw_suff}-glib2" "${_mingw_suff}-python2")

  cd python2-build
  make DESTDIR="$pkgdir" install

  find "${pkgdir}${MINGW_PREFIX}"/share/pygobject -name '*.py' -exec \
    sed -i '1s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|' {} +

  #rm -r "${pkgdir}${MINGW_PREFIX}"/share/gtk-doc
}

package_python3-gobject2() {
	pkgdesc="Python 3 bindings for GObject2 (mingw-w64)"
	depends=("${_mingw_suff}-glib2" "${_mingw_suff}-python3")

	cd python3-build
	make DESTDIR="$pkgdir" install
	# Delete devel stuff
	rm -r "${pkgdir}${MINGW_PREFIX}"/{include,lib/pkgconfig,share/{gtk-doc,pygobject/xsl}}
}

package_mingw-w64-i686-python2-gobject2() {
	package_python2-gobject2
}

package_mingw-w64-i686-python3-gobject2() {
	package_python3-gobject2
}

package_mingw-w64-x86_64-python2-gobject2() {
	package_python2-gobject2
}

package_mingw-w64-x86_64-python3-gobject2() {
	package_python3-gobject2
}
