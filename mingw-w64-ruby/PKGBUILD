# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=ruby
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=2.1.0
pkgrel=1
pkgdesc="An object-oriented language for quick and easy programming (mingw-w64)"
arch=('any')
url="http://www.ruby-lang.org/en"
license=("BSD, custom")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config")
depends=("${_mingw_suff}-crt" "${_mingw_suff}-libyaml" "${_mingw_suff}-openssl" "${_mingw_suff}-tk" "${_mingw_suff}-ncurses" "${_mingw_suff}-libffi")
options=('staticlibs' 'strip')
source=("http://cache.ruby-lang.org/pub/ruby/ruby-${pkgver}.tar.bz2"
        '0001-Fix-installing-gem-from-file-without-dependencies.patch')
md5sums=('1546eeb763ac7754365664be763a1e8f'
         '61a1507f260866805fc6451c14733b25')

prepare() {
	cd $srcdir/ruby-${pkgver}
	patch -p1 -i $srcdir/0001-Fix-installing-gem-from-file-without-dependencies.patch
}

build() {
	pushd ${MINGW_PREFIX} > /dev/null
	local PREFIX_WIN=`pwd -W`
	popd > /dev/null
	
	local _ffi=$(pkg-config --cflags libffi)
	local FFI_INC=${PREFIX_WIN}${_ffi#-I${MINGW_PREFIX}}
	#echo "FFI_INC $FFI_INC"
	#pushd "${FFI_INC}" > /dev/null
	#local FFI_INC=`pwd -W`
	#popd > /dev/null
	
    export CPPFLAGS="-DFD_SETSIZE=2048 -I${FFI_INC} -I${PREFIX_WIN}/include -I${PREFIX_WIN}/include/ncurses"
	export CFLAGS="-O2 -pipe -I${PREFIX_WIN}/include -I${PREFIX_WIN}/include/ncurses -I${FFI_INC}"
	export CXXFLAGS="-O2 -pipe -I${PREFIX_WIN}/include -I${PREFIX_WIN}/include/ncurses -I${FFI_INC}"
	export LDFLAGS="-L${PREFIX_WIN}/lib"
    mkdir -p "${srcdir}/build-${MINGW_CHOST}"
    cd "${srcdir}/build-${MINGW_CHOST}"
    ../ruby-${pkgver}/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --target=${MINGW_CHOST} \
      --disable-install-doc \
      --disable-install-rdoc \
      --disable-install-capi
    make
}

package() {
    cd "${srcdir}/build-${MINGW_CHOST}"
    make DESTDIR="$pkgdir" install
    rm -r "${pkgdir}${MINGW_PREFIX}/share"
}
