# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=tk
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=8.6.1
pkgrel=1
pkgdesc="A windowing toolkit for use with tcl (mingw-w64)"
arch=('any')
url="http://tcl.sourceforge.net"
license=("custom")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config")
depends=("${_mingw_suff}-crt" "${_mingw_suff}-tcl>=$pkgver")
options=('strip' 'staticlibs')
source=("http://downloads.sourceforge.net/sourceforge/tcl/tk${pkgver}-src.tar.gz"
#"tk-mingw-w64-compatibility-fix.patch"
"tk-8.6.1-prevent-tclStubsPtr-segfault.patch"
"tk-8.6.1-mingwexcept.patch")
sha1sums=('ecfcc20833c04d6890b14a7920a04d16f2123a51'
          'c95d3128d971ecd8ec6e79fcc17e5b0c20f9b666'
          '3469412bda746bcf1ecbcc516532ed168fd34cc6')

prepare() {
	cd "$srcdir/tk$pkgver"
	patch -p1 -i ${srcdir}/tk-8.6.1-prevent-tclStubsPtr-segfault.patch
	patch -p1 -i ${srcdir}/tk-8.6.1-mingwexcept.patch
}

build() {
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${CARCH}"
    cp -r "$srcdir/tk$pkgver/"* "${srcdir}/${pkgname}-${pkgver}-build-${CARCH}/"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${CARCH}"
    [ "${MINGW_CHOST}" = 'x86_64-w64-mingw32' ] && enable64bit='--enable-64bit'
    "${srcdir}"/${_realname}${pkgver}/win/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --with-tcl=${MINGW_PREFIX}/lib $enable64bit
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}-build-${CARCH}"
    make -j1 install INSTALL_ROOT="$pkgdir"
	cp "${pkgdir}${MINGW_PREFIX}/bin/wish86.exe" "${pkgdir}${MINGW_PREFIX}/bin/wish.exe"
    find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    #find "${pkgdir}${MINGW_PREFIX}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    #find "${pkgdir}${MINGW_PREFIX}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    find "${pkgdir}${MINGW_PREFIX}" -name '*.sh' -o -name'*.dll' -o -name '*.exe' | xargs -rtl1 chmod 755
    find "${pkgdir}${MINGW_PREFIX}" -name '*.sh' | xargs -rtl1 sed -i "s,libtk86.a,libtk86.dll.a,g"
    mv "${pkgdir}${MINGW_PREFIX}/lib/libtk86.a" "${pkgdir}${MINGW_PREFIX}/lib/libtk86.dll.a"
    ln -s "${pkgdir}${MINGW_PREFIX}/lib/libtk86.dll.a" "${pkgdir}${MINGW_PREFIX}/lib/libtk.dll.a"
    mkdir -p "${pkgdir}${MINGW_PREFIX}/include/tk-private/"{generic,win}
    find generic win -name "*.h" -exec cp -p '{}' "${pkgdir}${MINGW_PREFIX}"/include/tk-private/'{}' ';'
    ( cd "${pkgdir}${MINGW_PREFIX}/include"
      for i in *.h ; do
        [ -f "${pkgdir}${MINGW_PREFIX}/include/tk-private/generic/$i" ] && ln -sf ../../$i "${pkgdir}${MINGW_PREFIX}/include/tk-private/generic" ;
      done
    ) || true
    chmod a-x "${pkgdir}${MINGW_PREFIX}/lib/"*/pkgIndex.tcl
    sed -e "s#${srcdir}/tk${pkgver}/unix#${pkgdir}${MINGW_PREFIX}/lib#" \
      -e "s#${srcdir}/tk${pkgver}#${pkgdir}${MINGW_PREFIX}/include#" \
      -i "${pkgdir}${MINGW_PREFIX}/lib/tkConfig.sh"
    rm "${pkgdir}${MINGW_PREFIX}/lib/tk${pkgver%.*}/tkAppInit.c"
}
