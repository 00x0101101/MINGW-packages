# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=boost
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=1.55.0
_boostver=${pkgver//./_}
pkgrel=1
pkgdesc="Free peer-reviewed portable C++ source libraries (mingw-w64)"
arch=('any')
url="http://www.boost.org/"
license=('custom')
depends=("${_mingw_suff}-crt" "${_mingw_suff}-zlib" "${_mingw_suff}-bzip2" "${_mingw_suff}-icu")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-bzip2" "${_mingw_suff}-icu")
options=('!strip' '!buildflags' 'staticlibs')
source=(http://downloads.sourceforge.net/sourceforge/boost/boost_${_boostver}.tar.bz2
		boost-1.48.0-add-bjam-man-page.patch
		boost-1.50.0-fix-non-utf8-files.patch
		boost-1.50.0-pool.patch
		boost-1.54.0-bind-static_assert.patch
		boost-1.54.0-bootstrap.patch
		boost-1.54.0-concept-unused_typedef.patch
		boost-1.54.0-date_time-unused_typedef-2.patch
		boost-1.54.0-date_time-unused_typedef.patch
		boost-1.54.0-locale-unused_typedef.patch
		boost-1.54.0-math-unused_typedef-2.patch
		boost-1.54.0-mpi-unused_typedef.patch
		boost-1.54.0-mpl-print.patch
		boost-1.54.0-numeric-unused_typedef.patch
		boost-1.54.0-property_tree-unused_typedef.patch
		boost-1.54.0-python-unused_typedef.patch
		boost-1.54.0-random-unused_typedef.patch
		boost-1.54.0-spirit-unused_typedef-2.patch
		boost-1.54.0-spirit-unused_typedef.patch
		boost-1.54.0-static_warning-unused_typedef.patch
		boost-1.54.0-tuple-unused_typedef.patch
		boost-mingw.patch)
md5sums=('d6eef4b4cacb2183f2bf265a5a03a354'
         'dff8166486150121bf89d7050c7e5c49'
         'ef427ef3a47f8fdeeb6b4ae3246aa84f'
         'a69f826fd911dcf34189aaaacf16853f'
         '8b6978fd4ea68bc80b59a96186c8dd4c'
         '7a370bc224e59340b723e775d77b22bf'
         '0ccf6fc4f24afa513063868c51bddd7e'
         '3505488d2674588461fea2a2042de4a8'
         'e322f437a7067b30dd13443a3a0fee4a'
         '18963bb39f778b0f0ed7894a5b48845d'
         '84aeb88d0428267ed9c1e3cf098b93a1'
         'fe9a4ef96b48c56c2791598ae19e53cc'
         'c3aabfe9baa6ca34187e03fd338366dc'
         'dffcb97e434b7017eea1717c2aa8c3ad'
         '867983ebe812a2858ed900cb407944b6'
         '4e652869f58b4ce601715b9fb2c2b9ee'
         'b466c41f2ae6f40da127c4a28f563cee'
         '047b713a5cd5d0d2171b2cb7520c233a'
         '69521d5d7382793e5ea1da17a6288ccd'
         'dbf74ec90edac5439cc96b39b30ba98b'
         'c29dbf9746fea0688f87c8668903e12a'
         'c003363c91f6f16294a77c5562c80dbd')

prepare() {
  cd "${srcdir}/boost_${_boostver}"
  
  patch -p1 -i ${srcdir}/boost-1.50.0-fix-non-utf8-files.patch
  patch -p1 -i ${srcdir}/boost-1.48.0-add-bjam-man-page.patch
  patch -p1 -i ${srcdir}/boost-1.50.0-pool.patch
  patch -p1 -i ${srcdir}/boost-1.54.0-bind-static_assert.patch
  patch -p1 -i ${srcdir}/boost-1.54.0-concept-unused_typedef.patch
  patch -p1 -i ${srcdir}/boost-1.54.0-mpl-print.patch
  patch -p1 -i ${srcdir}/boost-1.54.0-static_warning-unused_typedef.patch
  patch -p1 -i ${srcdir}/boost-1.54.0-math-unused_typedef-2.patch
  patch -p1 -i ${srcdir}/boost-1.54.0-tuple-unused_typedef.patch
  patch -p1 -i ${srcdir}/boost-1.54.0-random-unused_typedef.patch
  patch -p1 -i ${srcdir}/boost-1.54.0-date_time-unused_typedef.patch
  patch -p1 -i ${srcdir}/boost-1.54.0-date_time-unused_typedef-2.patch
  patch -p1 -i ${srcdir}/boost-1.54.0-spirit-unused_typedef.patch
  patch -p1 -i ${srcdir}/boost-1.54.0-spirit-unused_typedef-2.patch
  patch -p1 -i ${srcdir}/boost-1.54.0-numeric-unused_typedef.patch
  patch -p1 -i ${srcdir}/boost-1.54.0-property_tree-unused_typedef.patch
  patch -p1 -i ${srcdir}/boost-1.54.0-mpi-unused_typedef.patch
  patch -p1 -i ${srcdir}/boost-1.54.0-python-unused_typedef.patch
  patch -p1 -i ${srcdir}/boost-mingw.patch
  patch -p1 -i ${srcdir}/boost-1.54.0-bootstrap.patch
}

build() {
	mkdir -p "${srcdir}/boost-${CARCH}"
	lndir "${srcdir}/boost_${_boostver}" "${srcdir}/boost-${CARCH}"
	cd "${srcdir}/boost-${CARCH}"
	./bootstrap.sh --with-icu=${MINGW_PREFIX}
	unset LDFLAGS CPPFLAGS

	case ${CARCH} in
		i686)
			local _model=32
		;;
		x86_64)
			local _model=64
		;;
	esac
	local _bvar="variant=release threading=single,multi threadapi=win32 \
		link=shared,static debug-symbols=on pch=off link=shared toolset=gcc"
	local _bflag="-d2 --layout=tagged address-model=$( [[ ${CARCH} == i686 ]] && echo 32 || echo 64 ) \
		${_bvar} --without-mpi --without-python "


	./b2 \
		toolset=gcc \
		variant=release \
		threading=multi \
		threadapi=win32 \
		link=shared,static \
		runtime-link=shared \
		--prefix=${pkgdir}${MINGW_PREFIX} \
		-sHAVE_ICU=1 \
		-sICU_PATH=${MINGW_PREFIX} \
		-sICU_LINK="-L${MINGW_PREFIX}/lib -licuuc -licuin -licudt" \
		-sICONV_PATH=${MINGW_PREFIX} \
		-sICONV_LINK="-L${MINGW_PREFIX}/lib -liconv" \
		-sNO_BZIP2 \
		-sBZIP2_BINARY=bz2 \
		-sBZIP2_INCLUDE=${MINGW_PREFIX}/include \
		-sBZIP2_LIBPATH=${MINGW_PREFIX}/lib \
		-sNO_ZLIB \
		-sZLIB_BINARY=z \
		-sZLIB_INCLUDE=${MINGW_PREFIX}/include \
		-sZLIB_LIBPATH=${MINGW_PREFIX}/zlib \
		pch=off \
		-d2 \
		--without-mpi \
		--without-python \
		address-model=${_model} \
		--layout=tagged
}

package() {
	cd "${srcdir}/boost-${CARCH}"
	case ${CARCH} in
		i686)
			local _model=32
		;;
		x86_64)
			local _model=64
		;;
	esac
    ./b2 \
	    toolset=gcc \
		variant=release \
		threading=multi \
		threadapi=win32 \
		link=shared,static \
		runtime-link=shared \
		--prefix=${pkgdir}${MINGW_PREFIX} \
		-sHAVE_ICU=1 \
		-sICU_PATH=${MINGW_PREFIX} \
		-sICU_LINK="-L${MINGW_PREFIX}/lib -licuuc -licuin -licudt" \
		-sICONV_PATH=${MINGW_PREFIX} \
		-sICONV_LINK="-L${MINGW_PREFIX}/lib -liconv" \
		-sNO_BZIP2 \
		-sBZIP2_BINARY=bz2 \
		-sBZIP2_INCLUDE=${MINGW_PREFIX}/include \
		-sBZIP2_LIBPATH=${MINGW_PREFIX}/lib \
		-sNO_ZLIB \
		-sZLIB_BINARY=z \
		-sZLIB_INCLUDE=${MINGW_PREFIX}/include \
		-sZLIB_LIBPATH=${MINGW_PREFIX}/zlib \
		--without-mpi \
		--without-python \
		address-model=${_model} \
		--layout=tagged install

    mkdir -p ${pkgdir}${MINGW_PREFIX}/bin
    mv ${pkgdir}${MINGW_PREFIX}/lib/*.dll ${pkgdir}${MINGW_PREFIX}/bin
    strip -x ${pkgdir}${MINGW_PREFIX}/bin/*.dll
    strip -g ${pkgdir}${MINGW_PREFIX}/lib/*.a
}
