# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=SuiteSparse
pkgname=("${MINGW_PACKAGE_PREFIX}-suitesparse")
pkgver=4.4.3
pkgrel=1
pkgdesc='A suite of sparse matrix algorithms (mingw-w64)'
license=('GPL')
groups=("${MINGW_PACKAGE_PREFIX}")
arch=('any')
source=("http://faculty.cse.tamu.edu/davis/${_realname}/${_realname}-${pkgver}.tar.gz"
        "0001-Win32-Do-not-link-librt.patch"
        "0002-Win32-Add-strip-trailing-cr-to-diff-for-crlf.patch")
depends=("${MINGW_PACKAGE_PREFIX}-openblas")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc-fortran")
md5sums=('b2e6f2373d480aa6f0ea5ca3d5d531c3'
         '7af25ee389fb79459615221a58ba465e'
         '1773305ad707db5b88d253ea914b975b')

prepare() {
  cd "${srcdir}"/${_realname}
  patch -p1 -i "${srcdir}"/0001-Win32-Do-not-link-librt.patch
  patch -p1 -i "${srcdir}"/0002-Win32-Add-strip-trailing-cr-to-diff-for-crlf.patch
}

build() {
  [ -d "${srcdir}"/build-${CARCH} ] && rm -rf "${srcdir}"/build-${CARCH}
  cp -rf "${srcdir}"/${_realname} build-${CARCH}
  cd "${srcdir}"/build-${CARCH}

  # https://www.gnu.org/software/octave/doc/interpreter/Compiling-Octave-with-64_002dbit-Indexing.html says
  # "On 64-bit Windows systems, use -DLONGBLAS="long long" instead.", but that seems wrong. If 64-bit indexing
  # is wanted, then long long should be used on 32-bit Windows too I expect (unless LONGBLAS refers to pointer
  # size in some instances?)
  CFLAGS='-DLONGBLAS="long long"' \
  CXXFLAGS='-DLONGBLAS="long long"' \
  CC=gcc CXX=g++ \
    make
}

check() {
  cd "${srcdir}"/build-${CARCH}
  make check
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make install DESTDIR="${pkgdir}"
}
