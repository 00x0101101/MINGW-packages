# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=pango
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=1.36.2
pkgrel=1
pkgdesc="A library for layout and rendering of text (mingw-w64)"
arch=(any)
url="http://www.pango.org"
license=("LGPL")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config")
depends=("${_mingw_suff}-gcc-libs" 
		"${_mingw_suff}-freetype"
		"${_mingw_suff}-fontconfig"
		"${_mingw_suff}-cairo"
		"${_mingw_suff}-glib2"
		"${_mingw_suff}-harfbuzz" )
options=('staticlibs' 'strip')
source=("http://ftp.gnome.org/pub/gnome/sources/pango/${pkgver:0:4}/pango-${pkgver}.tar.xz"
		"pango_enable_static_build.patch")
sha256sums=('f07f9392c9cf20daf5c17a210b2c3f3823d517e1917b72f20bb19353b2bc2c63'
            '9657bfd3a4cca308026a2024b49a01376f948973d713ff711b26ebfd2621b1b3')

prepare() {
	cd "$srcdir/pango-$pkgver"
	patch -p0 configure < ../pango_enable_static_build.patch
	sed -i 's/have_libthai=true/have_libthai=false/' configure
}

build() {
	CFLAGS+=" -mms-bitfields"
	CXXFLAGS+=" -mms-bitfields"
    mkdir -p "${srcdir}/build-${MINGW_CHOST}"
    cd "${srcdir}/build-${MINGW_CHOST}"
    "${srcdir}"/${_realname}-${pkgver}/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --with-included-modules=yes \
      --with-dynamic-modules=no \
      --disable-introspection \
      --disable-debug \
      --enable-shared \
      --enable-static
    make
}

package() {
    cd "${srcdir}/build-${MINGW_CHOST}"
    make DESTDIR="$pkgdir" install
    find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    rm -r "${pkgdir}${MINGW_PREFIX}/share"
}
