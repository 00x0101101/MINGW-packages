# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=pkgconf
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.9.6
pkgrel=4
pkgdesc='pkg-config compatible utility which does not depend on glib'
url='https://github.com/nenolod/pkgconf'
arch=('any')
license=('ISC')
options=('debug' '!strip')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
conflicts=("${MINGW_PACKAGE_PREFIX}-pkg-config")
provides=("${MINGW_PACKAGE_PREFIX}-pkg-config")
source=(http://rabbit.dereferenced.org/~nenolod/distfiles//$_realname-$pkgver.tar.bz2
        0001-fix-windows-separator.patch
        0002-relocate.patch)
sha256sums=('c40a77543fe5c7259b813f70ddaf229c2760a250d1cf2ccef60b81fb1d3fc299'
            'f7b3d51121844999a5a2c2b662d1c69f589d7f023a8c42931425937e64cff1dd'
            '108e30feb249e68a2c494723485cafaa0e47158103699dca8d5916620361228a')

prepare() {
  cd $_realname-$pkgver
  rm -f pathtools.c pathtools.h m4/as-ac-expand.m4 > /dev/null 2>&1 || true
  patch -p1 -i ${srcdir}/0001-fix-windows-separator.patch
  patch -p1 -i ${srcdir}/0002-relocate.patch
  
  autoreconf -vfi
}

build() {
  cd $_realname-$pkgver
  [ -d ${CARCH} ] || mkdir ${CARCH}
  cd ${CARCH}
  ../configure --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --sysconfdir=${MINGW_PREFIX}/etc \
    --mandir=${MINGW_PREFIX}/share/man \
    --infodir=${MINGW_PREFIX}/share/info \
    --localstatedir=${MINGW_PREFIX}/var
  make
}

package() {
  cd $_realname-$pkgver/${CARCH}
  make DESTDIR="$pkgdir" install
  cp "${pkgdir}${MINGW_PREFIX}"/bin/pkgconf "${pkgdir}${MINGW_PREFIX}"/bin/pkg-config
}
