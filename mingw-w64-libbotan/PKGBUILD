# Maintainer: Nazar Mishturak <nazar m x at gmail dot com>

_realname=libbotan
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.11.21
pkgrel=1
arch=('any')
url='http://botan.randombit.net/'
pkgdesc='Botan is a C++ cryptography library released under the permissive Simplified BSD license. (mingw-w64)'
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-bzip2"
         "${MINGW_PACKAGE_PREFIX}-xz"
         "${MINGW_PACKAGE_PREFIX}-sqlite3"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-boost")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-doxygen")
license=('BSD')
options=('strip' 'staticlibs' 'docs')
source=(http://botan.randombit.net/releases/Botan-${pkgver}.tgz{,.asc}
        'boost-link.patch'
        'winsock-link.patch'
        'disable-mkgmtime.patch')
sha1sums=('513099c151898c629b334165b7d2cc2b4c06e2a1'
          'SKIP'
          '407e623b46ef1b288b90195d9acadb34611b088b'
          '867667533df37c6b88b267c0e95b979d8115dea7'
          '57289f42ad28111d5a5a05eab58ac5887d7e1f84')
validpgpkeys=('621DAF6411E1851C4CF9A2E16211EBF1EFBADFBC')

prepare() {
  cd "${srcdir}/Botan-${pkgver}"
  patch -p1 -i "${srcdir}/boost-link.patch"
  patch -p1 -i "${srcdir}/winsock-link.patch"
  patch -p1 -i "${srcdir}/disable-mkgmtime.patch"
}

build() {
  cd "${srcdir}/Botan-${pkgver}"
  
  local BUILD_TYPE_FLAGS='--build-mode=release'
  if check_option 'debug' 'y'; then
    BUILD_TYPE_FLAGS='--build-mode=debug --debug-mode'
  fi
  
  # Set prefix to mingw{32,64} not full path. Remove starting slash
  ${MINGW_PREFIX}/bin/python3 configure.py \
                              --disable-shared \
                              --os=mingw \
                              --cc=gcc \
                              --cpu=${CARCH} \
                              --destdir='$(DESTDIR)' \
                              ${BUILD_TYPE_FLAGS} \
                              --makefile-style=gmake \
                              --link-method=copy \
                              --with-doxygen \
                              --with-boost \
                              --with-bzip2 \
                              --with-lzma \
                              --with-sqlite3 \
                              --with-zlib

    make
}

check() {
  cd "${srcdir}/Botan-${pkgver}"
  ./botan-test
}

package() {
  cd "${srcdir}/Botan-${pkgver}"
  make DESTDIR="${pkgdir}${MINGW_PREFIX}" install
}
