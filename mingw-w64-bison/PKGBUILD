# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_realname=bison
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.0.2
pkgrel=1
pkgdesc="The GNU general-purpose parser generator"
arch=('any')
license=('GPL3')
url="http://www.gnu.org/software/bison/bison.html"
depends=("${MINGW_PACKAGE_PREFIX}-m4")
options=('debug' '!strip')
source=(http://ftp.gnu.org/gnu/bison/${_realname}-${pkgver}.tar.xz
        001-use-gnu_printf.patch
        002-add-relocation-support.patch
        003-create_pipe-uses-O_TEXT-not-O_BINARY-mode.patch)
md5sums=('146be9ff9fbd27497f0bf2286a5a2082'
         '817bbd1f0de3d7f2ab1287b67b267895'
         '7bb3dac2a01d7f78bb6038f953a42b39'
         'cc720e2a9fc8f7849b0cfb165caf80fa')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}

  patch -p1 -i "${srcdir}"/001-use-gnu_printf.patch
  patch -p1 -i "${srcdir}"/002-add-relocation-support.patch
  patch -p1 -i "${srcdir}"/003-create_pipe-uses-O_TEXT-not-O_BINARY-mode.patch

  #autoreconf -vfi
}

build() {
  cd ${srcdir}/${_realname}-${pkgver}
  M4=$(cygpath -wm ${MINGW_PREFIX})/bin/m4.exe \
    ./configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --target=${MINGW_CHOST}
  make
}

check() {
  cd ${srcdir}/${_realname}-${pkgver}
  make -j1 check
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
