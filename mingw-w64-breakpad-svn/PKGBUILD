# Maintainer: Renato Silva <br.renatosilva@gmail.com>

_name=breakpad
url='https://code.google.com/p/google-breakpad'
pkgdesc='An open-source multi-platform crash reporting system (mingw-w64)'
license=('BSD')
arch=(any)
pkgver=r1413
pkgrel=1

makedepends=(${MINGW_PACKAGE_PREFIX}-curl subversion git gyp)
pkgname=(${MINGW_PACKAGE_PREFIX}-${_name}-svn)
provides=(${MINGW_PACKAGE_PREFIX}-${_name})
conflicts=(${MINGW_PACKAGE_PREFIX}-${_name})
source=(${_name}.svn::"svn+http://google-breakpad.googlecode.com/svn/trunk"
        0002-Avoid-O_BINARY-redefinition-warning-on-Cygwin.patch
        0003-Build-minidump_upload-sym_upload-for-all-targets.patch
        0004-Remove-local-copy-of-curl-headers.patch
        0005-Add-support-for-DWARF-in-PECOFF-as-used-by-Cygwin-an.patch
        0006-Fix-building-minidump-processor-for-MinGW.patch
        0007-Build-PECOFF-DWARF-dump_syms-for-MinGW.patch
        0008-Fix-building-client-library-and-upload-tools-for-Min.patch
        0009-Fix-common.gypi-to-allow-gyp-to-generate-Makefiles-o.patch
        0010-Teach-gyp-generated-Makefile-to-build-crash_generati.patch
        0011-Default-debug_file-to-the-code_file-basename.patch
        0012-Fix-building-windows-client-unit-tests-with-gcc.patch
        0013-Add-symsrv_convert.patch
        0014-Build-MinGW-client-library-using-gyp-generated-Makef.patch
        0015-Use-a-default-debug-id-of-all-zeros-when-no-CV-recor.patch
        0016-Increase-RA-scanning-to-60-words.patch
        0017-Use-__USE_MINGW_ANSI_STDIO-for-MinGW-build.patch
        0018-Provide-a-working-strtok_r-for-MinGW.patch
        0019-Add-a-README.md-with-notes-on-what-is-left-to-do.patch
        0020-Support-for-multiple-upload-files-in-Breakpad.patch
        0021-Fallback-to-synthesizing-a-debug-id-from-version-and.patch
        0022-Fix-typo-in-the-name-of-the-info-entry-containing-up.patch
        0023-Fix-a-missing-r-in-crash_generation_app.patch
        0024-Disable-timeout-waiting-for-crash-reporting-server.patch
        0026-Fixes-for-sign-compare-warnings-due-to-signedness-of.patch
        gyp-regenerate-no-circular-check.patch)

pkgver() {
    cd "${srcdir}/${_name}.svn"
    printf "r%s" "$(svnversion | tr -d 'A-z')"
}

prepare() {
    msg2 "Creating patched-source directory"
    [[ -d "${srcdir}/${_name}.patched" ]] && rm -rf "${srcdir}/${_name}.patched"
    mkdir -p "${srcdir}/${_name}.patched"
    cp -r "${srcdir}/${_name}.svn"/* "${srcdir}/${_name}.patched"

    # Apply patches
    cd "${srcdir}/${_name}.patched"
    for patch in "${srcdir}"/0*.patch; do
        patch -p1 -i "${patch}"
    done
    patch -p2 -i "${srcdir}/gyp-regenerate-no-circular-check.patch"

    msg2 "Running autoreconf"
    autoreconf -sif
}

build() {
    msg2 "Synchronizing build directory"
    rsync --recursive --times --links "${srcdir}/${_name}.patched"/* "${srcdir}/${_name}.build.${CARCH}"

    cd "${srcdir}/${_name}.build.${CARCH}"
    ./configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST}

    make CXXFLAGS="-Wno-error=reorder -DUNICODE -D_UNICODE -g" AR=${MINGW_PREFIX}/bin/ar RC=${MINGW_PREFIX}/bin/windres
}

package() {
    cd "${srcdir}/${_name}.build.${CARCH}"
    make CXXFLAGS="-Wno-error=reorder -DUNICODE -D_UNICODE -g" AR=${MINGW_PREFIX}/bin/ar RC=${MINGW_PREFIX}/bin/windres DESTDIR="${pkgdir}" install

    # headers aren't really designed to be installed, so we need to do some
    # rewriting to make them usefully included using "include <breakpad/somepath/someheader>"
    find ${pkgdir} -name *.h -exec sed -i -e's#client/windows/#breakpad/client/windows/#' {} \;
    find ${pkgdir} -name *.h -exec sed -i -e's#common/scoped_ptr#breakpad/common/scoped_ptr#' {} \;
    find ${pkgdir} -name *.h -exec sed -i -e's#common/windows/#breakpad/common/windows/#' {} \;
    find ${pkgdir} -name *.h -exec sed -i -e's#google_breakpad/common/#breakpad/common/#' {} \;

    # Install a single client library
    cd "${pkgdir}${MINGW_PREFIX}/lib"
    local _windir=$(cygpath -wm ${srcdir})
    ${MINGW_PREFIX}/bin/ar -M <<EOF
CREATE libbreakpad_client.a
ADDLIB ${_windir}/${_name}.build.${CARCH}/src/out/Debug/obj.target/client/windows/crash_generation/libcrash_generation_client.a
ADDLIB ${_windir}/${_name}.build.${CARCH}/src/out/Debug/obj.target/client/windows/crash_generation/libcrash_generation_server.a
ADDLIB ${_windir}/${_name}.build.${CARCH}/src/out/Debug/obj.target/client/windows/handler/libexception_handler.a
ADDLIB ${_windir}/${_name}.build.${CARCH}/src/out/Debug/obj.target/client/windows/libcommon.a
ADDLIB ${_windir}/${_name}.build.${CARCH}/src/out/Debug/obj.target/client/windows/sender/libcrash_report_sender.a
SAVE
END
EOF

    cp -v "${srcdir}/${_name}.build.${CARCH}/breakpad-client.pc" pkgconfig

    # License
    cd "${pkgdir}${MINGW_PREFIX}/share"
    mkdir -p licenses/${_name}
    mv doc/${_name}-0.1 doc/${_name}
    mv -v doc/${_name}/LICENSE licenses/${_name}/LICENSE
}

md5sums=('SKIP'
         'a9ee932aa9f3ac69aa08a6b6cf43e845'
         '5e63b989875806fc6059006f7de40ddf'
         'f9d396b9bc810432c1fed58f1eacec32'
         '5ebfc15cf736bbf5126cf74f1e7a30f2'
         'af74ac40ab80fa978b804237bb58678b'
         '9f84c77c5af90420f270da4bd25c6bdc'
         '857ed38082d4104bc6270dbee4709d31'
         'd7efa94811919ee3d33e156c3a456de1'
         '9f17e884fe98ab177f7664eb37cb486b'
         'b4dda80e8996247b12f2621d25c5a0be'
         '83b180b0d1f8ba2236373db66cdb8929'
         '4f1d52e55959c0fa065dac304e689aa1'
         'a2929f603ad96d8369ed2871d74ef1e5'
         '36d6d548d91e75c79a3da0edc65de423'
         'f34933fa734468f6f82ac5dc609df0f9'
         '8db35ef4f22e2c146059ff1c200c4948'
         '696a6e6fdc44e9bbaae11f67f548ab5c'
         '2b5874e8ebc2c401995666b9dc22b11d'
         '947c452c1eb370d129d8a1dd8e7e3d30'
         '7f08437be9bab28d9d4cfbaa06a341bd'
         '21178343e9433e1629e34ef37364bfa5'
         '6d60d14c043a16deed5152455b6ce5c1'
         'e49ab09a126650a2890d50eb9dadeee4'
         '4b5a73a41be9f4909feb952c3ec5086f'
         '58e93eee7934e96ede810bbfd42374ca')
