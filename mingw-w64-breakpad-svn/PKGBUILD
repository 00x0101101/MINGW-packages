# Maintainer: Renato Silva <br.renatosilva@gmail.com>

_name=breakpad
url='https://code.google.com/p/google-breakpad'
pkgdesc='An open-source multi-platform crash reporting system (mingw-w64)'
license=(BSD)
arch=(any)
pkgver=r1402
pkgrel=1

makedepends=(${MINGW_PACKAGE_PREFIX}-curl subversion git gyp)
pkgname=(${MINGW_PACKAGE_PREFIX}-${_name}-svn)
provides=(${MINGW_PACKAGE_PREFIX}-${_name})
conflicts=(${MINGW_PACKAGE_PREFIX}-${_name})
md5sums=(SKIP SKIP)
source=(${_name}.svn::"svn+http://google-breakpad.googlecode.com/svn/trunk#revision=1402"
        ${_name}.patches.git::"git+https://github.com/jon-turney/mingw64-i686-google-breakpad-cygport.git")

pkgver() {
    cd "${srcdir}/${_name}.svn"
    printf "r%s" "$(svnversion | tr -d 'A-z')"
}

prepare() {
    msg2 "Creating patched-source directory"
    [[ -d "${srcdir}/${_name}.patched" ]] && rm -rf "${srcdir}/${_name}.patched"
    mkdir -p "${srcdir}/${_name}.patched"
    cp -r "${srcdir}/${_name}.svn"/* "${srcdir}/${_name}.patched"

    # Apply patches
    cd "${srcdir}/${_name}.patched"
    for patch in "${srcdir}/${_name}.patches.git"/0*.patch; do
        patch -p1 < "${patch}"
    done
    patch -p2 < "${srcdir}/${_name}.patches.git/gyp-regenerate-no-circular-check.patch"

    msg2 "Running autoreconf"
    autoreconf -sif
}

build() {
    msg2 "Synchronizing build directory"
    rsync --recursive --times --links "${srcdir}/${_name}.patched"/* "${srcdir}/${_name}.build.${CARCH}"

    cd "${srcdir}/${_name}.build.${CARCH}"
    ./configure --prefix=${MINGW_PREFIX}
    make CXXFLAGS="-Wno-error=reorder -DUNICODE -D_UNICODE"
}

package() {
    cd "${srcdir}/${_name}.build.${CARCH}"
    make DESTDIR="${pkgdir}" install

    # Install a single client library
    cd "${pkgdir}${MINGW_PREFIX}/lib"
    ar -r libbreakpad_client.a "${srcdir}/${_name}.build.${CARCH}/src/out/Debug/obj.target/client/windows/crash_generation/libcrash_generation_client.a"
    ar -r libbreakpad_client.a "${srcdir}/${_name}.build.${CARCH}/src/out/Debug/obj.target/client/windows/crash_generation/libcrash_generation_server.a"
    ar -r libbreakpad_client.a "${srcdir}/${_name}.build.${CARCH}/src/out/Debug/obj.target/client/windows/handler/libexception_handler.a"
    ar -r libbreakpad_client.a "${srcdir}/${_name}.build.${CARCH}/src/out/Debug/obj.target/client/windows/libcommon.a"
    ar -r libbreakpad_client.a "${srcdir}/${_name}.build.${CARCH}/src/out/Debug/obj.target/client/windows/sender/libcrash_report_sender.a"
    cp -v "${srcdir}/${_name}.build.${CARCH}/breakpad-client.pc" pkgconfig

    # License
    cd "${pkgdir}${MINGW_PREFIX}/share"
    mkdir -p licenses/${_name}
    mv doc/${_name}-0.1 doc/${_name}
    mv -v doc/${_name}/LICENSE licenses/${_name}/LICENSE
}
