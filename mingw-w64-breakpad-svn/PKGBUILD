# Maintainer: Jon Turney <jon.turney@dronecode.org.uk>
# Contributor: Renato Silva <br.renatosilva@gmail.com>
# Contributor: Alexey Pavlov <alexpux@gmail.com>

_realname=breakpad
pkgbase=mingw-w64-${_realname}-svn
pkgname=(${MINGW_PACKAGE_PREFIX}-${_realname}-svn)
provides=(${MINGW_PACKAGE_PREFIX}-${_realname})
conflicts=(${MINGW_PACKAGE_PREFIX}-${_realname})
pkgver=r1471
pkgrel=1
url='https://code.google.com/p/google-breakpad'
pkgdesc='An open-source multi-platform crash reporting system (mingw-w64)'
license=('BSD')
arch=('any')
depends=(${MINGW_PACKAGE_PREFIX}-gcc-libs)
makedepends=(${MINGW_PACKAGE_PREFIX}-curl rsync subversion git gyp)
source=(${_realname}.svn::"svn+http://google-breakpad.googlecode.com/svn/trunk#revision=1471"
        0003-Build-minidump_upload-sym_upload-for-all-targets.patch
        0004-Remove-local-copy-of-curl-headers.patch
        0005-Add-support-for-DWARF-in-PECOFF-as-used-by-Cygwin-an.patch
        0006-Fix-building-minidump-processor-for-MinGW.patch
        0007-Build-PECOFF-DWARF-dump_syms-for-MinGW.patch
        0008-Fix-building-client-library-and-upload-tools-for-Min.patch
        0009-Fix-common.gypi-to-allow-gyp-to-generate-Makefiles-o.patch
        0010-Teach-gyp-generated-Makefile-to-build-crash_generati.patch
        0011-Default-debug_file-to-the-code_file-basename.patch
        0012-Fix-building-windows-client-unit-tests-with-gcc.patch
        0013-Add-symsrv_convert.patch
        0014-Build-MinGW-client-library-using-gyp-generated-Makef.patch
        0015-Use-a-default-debug-id-of-all-zeros-when-no-CV-recor.patch
        0016-Increase-RA-scanning-to-60-words.patch
        0017-Use-__USE_MINGW_ANSI_STDIO-for-MinGW-build.patch
        0018-Provide-a-working-strtok_r-for-MinGW.patch
        0019-Add-a-README.md-with-notes-on-what-is-left-to-do.patch
        0020-Support-for-multiple-upload-files-in-Breakpad.patch
        0021-Fallback-to-synthesizing-a-debug-id-from-version-and.patch
        0022-Fix-typo-in-the-name-of-the-info-entry-containing-up.patch
        0023-Fix-a-missing-r-in-crash_generation_app.patch
        0024-Disable-timeout-waiting-for-crash-reporting-server.patch
        0026-Fixes-for-sign-compare-warnings-due-to-signedness-of.patch
        0027-google-breakpad-0.1.svn.1413-libtool.patch
        0028-pecoff-fix-assign-const.patch
        gyp-regenerate-no-circular-check.patch
        Makefile
        README.sample)

pkgver() {
    cd "${srcdir}/${_realname}.svn"
    printf "r%s" "$(svnversion | tr -d 'A-z')"
}

prepare() {
    msg2 "Creating patched-source directory"
    [[ -d "${srcdir}/${_realname}.patched" ]] && rm -rf "${srcdir}/${_realname}.patched"
    mkdir -p "${srcdir}/${_realname}.patched"
    cp -r "${srcdir}/${_realname}.svn"/* "${srcdir}/${_realname}.patched"

    # Apply patches
    cd "${srcdir}/${_realname}.patched"
    for patch in "${srcdir}"/0*.patch; do
        msg2 "Applying ${patch} ..."
        patch -p1 -i "${patch}"
    done
    patch -p2 -i "${srcdir}/gyp-regenerate-no-circular-check.patch"

    msg2 "Running autoreconf"
    autoreconf -sif
}

build() {
    msg2 "Synchronizing build directory"
    rsync --recursive --times --links "${srcdir}/${_realname}.patched"/* "${srcdir}/${_realname}.build.${CARCH}"

    cd "${srcdir}/${_realname}.build.${CARCH}"
    ./configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST}

    make CXXFLAGS="-Wno-error=reorder -DUNICODE -D_UNICODE -g" AR=${MINGW_PREFIX}/bin/ar RC=${MINGW_PREFIX}/bin/windres
}

package() {
    cd "${srcdir}/${_realname}.build.${CARCH}"
    make CXXFLAGS="-Wno-error=reorder -DUNICODE -D_UNICODE -g" AR=${MINGW_PREFIX}/bin/ar RC=${MINGW_PREFIX}/bin/windres DESTDIR="${pkgdir}" install

    # Install headers
    mkdir -p "${pkgdir}${MINGW_PREFIX}/include/breakpad"
    cd "${pkgdir}${MINGW_PREFIX}/include/breakpad"

    mkdir -p "${pkgdir}${MINGW_PREFIX}/include/breakpad/common/"
    cp -v ${srcdir}/${_realname}.build.${CARCH}/src/google_breakpad/common/* common/
    cp -v ${srcdir}/${_realname}.build.${CARCH}/src/common/scoped_ptr.h common/

    mkdir -p "${pkgdir}${MINGW_PREFIX}/include/breakpad/common/windows"
    cp -v ${srcdir}/${_realname}.build.${CARCH}/src/common/windows/string_utils-inl.h common/windows/

    mkdir -p "${pkgdir}${MINGW_PREFIX}/include/breakpad/client/windows/crash_generation"
    cp -v ${srcdir}/${_realname}.build.${CARCH}/src/client/windows/crash_generation/client_info.h client/windows/crash_generation/
    cp -v ${srcdir}/${_realname}.build.${CARCH}/src/client/windows/crash_generation/crash_generation_client.h client/windows/crash_generation/
    cp -v ${srcdir}/${_realname}.build.${CARCH}/src/client/windows/crash_generation/crash_generation_server.h client/windows/crash_generation/
    cp -v ${srcdir}/${_realname}.build.${CARCH}/src/client/windows/crash_generation/minidump_generator.h client/windows/crash_generation/

    mkdir -p "${pkgdir}${MINGW_PREFIX}/include/breakpad/client/windows/common"
    cp -v ${srcdir}/${_realname}.build.${CARCH}/src/client/windows/common/ipc_protocol.h client/windows/common/

    mkdir -p "${pkgdir}${MINGW_PREFIX}/include/breakpad/client/windows/handler"
    cp -v ${srcdir}/${_realname}.build.${CARCH}/src/client/windows/handler/exception_handler.h client/windows/handler/

    mkdir -p "${pkgdir}${MINGW_PREFIX}/include/breakpad/client/windows/sender"
    cp -v ${srcdir}/${_realname}.build.${CARCH}/src/client/windows/sender/crash_report_sender.h client/windows/sender/

    # Headers aren't really designed to be installed, so we need to do some
    # rewriting to make them usefully included using "include <breakpad/somepath/someheader>"
    find ${pkgdir} -name *.h -exec sed -i -e's#client/windows/#breakpad/client/windows/#' {} \;
    find ${pkgdir} -name *.h -exec sed -i -e's#common/scoped_ptr#breakpad/common/scoped_ptr#' {} \;
    find ${pkgdir} -name *.h -exec sed -i -e's#common/windows/#breakpad/common/windows/#' {} \;
    find ${pkgdir} -name *.h -exec sed -i -e's#google_breakpad/common/#breakpad/common/#' {} \;

    # Install a single client library
    cd "${pkgdir}${MINGW_PREFIX}/lib"
    local _windir=$(cygpath -wm ${srcdir})
    ${MINGW_PREFIX}/bin/ar -M <<EOF
CREATE libbreakpad_client.a
ADDLIB ${_windir}/${_realname}.build.${CARCH}/src/out/Debug/obj.target/client/windows/crash_generation/libcrash_generation_client.a
ADDLIB ${_windir}/${_realname}.build.${CARCH}/src/out/Debug/obj.target/client/windows/crash_generation/libcrash_generation_server.a
ADDLIB ${_windir}/${_realname}.build.${CARCH}/src/out/Debug/obj.target/client/windows/handler/libexception_handler.a
ADDLIB ${_windir}/${_realname}.build.${CARCH}/src/out/Debug/obj.target/client/windows/libcommon.a
ADDLIB ${_windir}/${_realname}.build.${CARCH}/src/out/Debug/obj.target/client/windows/sender/libcrash_report_sender.a
SAVE
END
EOF

    # Install the pkgconfig file
    cp -v "${srcdir}/${_realname}.build.${CARCH}/breakpad-client.pc" pkgconfig

    # License
    cd "${pkgdir}${MINGW_PREFIX}/share"
    mkdir -p licenses/${_realname}
    mv doc/${_realname}-0.1 doc/${_realname}
    mv -v doc/${_realname}/LICENSE licenses/${_realname}/LICENSE

    # Install crash_generation_app sample source and instructions
    mkdir -p "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/sample"
    cd "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/sample"
    cp -v ${srcdir}/README.sample .
    cp -v ${srcdir}/Makefile .
    cp -v ${srcdir}/${_realname}.build.${CARCH}/src/client/windows/tests/crash_generation_app/abstract_class.cc .
    cp -v ${srcdir}/${_realname}.build.${CARCH}/src/client/windows/tests/crash_generation_app/abstract_class.h .
    cp -v ${srcdir}/${_realname}.build.${CARCH}/src/client/windows/tests/crash_generation_app/crash_generation_app.cc .
    cp -v ${srcdir}/${_realname}.build.${CARCH}/src/client/windows/tests/crash_generation_app/crash_generation_app.h .
    cp -v ${srcdir}/${_realname}.build.${CARCH}/src/client/windows/tests/crash_generation_app/crash_generation_app.ico .
    cp -v ${srcdir}/${_realname}.build.${CARCH}/src/client/windows/tests/crash_generation_app/resource.h .
    cp -v ${srcdir}/${_realname}.build.${CARCH}/src/client/windows/tests/crash_generation_app/resource.rc .
    cp -v ${srcdir}/${_realname}.build.${CARCH}/src/client/windows/tests/crash_generation_app/small.ico .

    # Rewrite includes in sample for the rewritten installed headers
    sed -i -e's#client/windows/tests/crash_generation_app/##' ${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/sample/*
    sed -i -e's#client/windows/#breakpad/client/windows/#' ${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/sample/*
}

sha256sums=('SKIP'
            '37c5761db8940286b29d702ac6909d3e968cffda06c8d8381da3b7868c8a5083'
            '7fcadddfd522d3deb96f7841dc625dcafff355d9c4c8d1d31bd415d6fd0fbc62'
            '8d1625d0caa7800933d62df906cdd91e98dae66ac8bd5e806a672fdb5b26a3e8'
            '271a27514e3e6d4ba6698b4ebb2cdafa32c8a61009adf1d48916c800880f4ff6'
            '1d0ac9faac4940ce30663009dcf7509e2ff29f92285856d286301e41ee4681d4'
            '0ce4126f31b7ba96ca477c08d2be9441e73ebd69dc870c796bd861f408f4f1e1'
            '94e27eebec67251e838b4ac9392227174bb90ddf52eb6874db5da6ebe36aa00f'
            '7e5e48803e30709123a6a7cebf1b80463de927d68ff8333da3f39ef2c2b6d6a0'
            'd9a77efc0c20ec0df9f5f82b906c6c9532a835c924b5f136b76b08e886ff7ac1'
            '3c1494240cc19711519414d99b6e1295c2083598765b9c6933706e3cd6480aea'
            '67649a1bd5e6831a4dbb6278cbd5ae65c90b58ede5fd0a2e32463598e9e58b8b'
            '9c2439c90b04493b7627b11ff5312039a823c1d353ae31ab6ea78f2018db239f'
            '15823f6f55d001f6131b34f7c226b6f7102a0b7bcd952f833015f0def16785e3'
            'd2157859f277e3d661516cb749fc46217ada572f4dd03eb4f9c735d094c37470'
            '6090b0abd6e0c86572fdf5d83e2f3c8e6562b9214785d3adb62b6efa106ced8a'
            'd4083617fea7c34035486b0092ca5dce6a8f0502022c369d5e8aaa68651694ee'
            '7888061f05eaa075d22085b5ae9e0d5af6293fff5aa4f70f0701e913e40a82ba'
            '4145d9f6743d26b411a851519fe8430ccf717d75405462c2a84a8b3d8ccfecc6'
            'c6358db6cd50cdb6660b1897562dfc13bbce9f042b3c9801bc56032aacaef0be'
            'ac812a4d0d41455b5d0e5a3f22db110226c2cfbb815c1f54861f417c1d2d0a58'
            'c027f523edd94b60912dd7442e89ac0e7302e0a4619a97d93da33fb91361aa29'
            '727abceb4977ac04e1db97c157c8e28c165282aa810590649631de5c2f708856'
            '0f323f917786fb32ccf9bb6aba719b923c59a2a7e785d1e663f731bec9a3ac66'
            '6c349e47bb41b4ae2f2ad04ce0cf2138361b9ed507db7255db388638974e8693'
            '61350dff3a1e1503fcd8d731b50d7fa663ab69eaecfaa1cd51170c7ed3a1a750'
            '318bf4ab1778d2f192c55ef9e23fc5424c6f64c171051b18c0a40276af7e503a'
            'b1a4025d888bcd1d752b3b6c93d6e6ae353eae050197b1ae2c54f9b90bfb1292'
            '0b1421202edef83db517a310824ee1f7680e0a80229f5a63aa60cc2ef6b93b19')
