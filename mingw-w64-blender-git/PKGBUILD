# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=blender
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}-git"
provides=("${_mingw_suff}-${_realname}")
conflicts=("${_mingw_suff}-${_realname}")
blender_version=2.69
pkgver=r52227.6a2c467
pkgrel=1
pkgdesc="A fully integrated 3D graphics creation suite (mingw-w64)"
arch=('any')
license=('GPL')
url="http://www.blender.org"
depends=("${_mingw_suff}-libpng"
		"${_mingw_suff}-libtiff"
		"${_mingw_suff}-openexr"
		"${_mingw_suff}-python3"
		#"${_mingw_suff}-desktop-file-utils"
		#"${_mingw_suff}-shared-mime-info"
		#"${_mingw_suff}-hicolor-icon-theme"
		#"${_mingw_suff}-xdg-utils"
		"${_mingw_suff}-glew"
		"${_mingw_suff}-freetype"
		"${_mingw_suff}-openal"
		"${_mingw_suff}-ffmpeg"
		"${_mingw_suff}-fftw"
		"${_mingw_suff}-boost"
		"${_mingw_suff}-opencollada"
		"${_mingw_suff}-openimageio"
		"${_mingw_suff}-libsndfile"
		#"${_mingw_suff}-jack"
		"${_mingw_suff}-opencolorio"
		"${_mingw_suff}-openshadinglanguage"
		"${_mingw_suff}-glew"
		"${_mingw_suff}-wintab-sdk")
makedepends=("${_mingw_suff}-cmake" "${_mingw_suff}-boost")
#[[ $CARCH == x86_64 ]] && makedepends+=('cuda')
optdepends=('cuda: cycles renderer cuda support')
options=(!strip staticlibs !buildflags)
#install=blender.install
source=("${_realname}"::"git://git.blender.org/blender.git"
		"translations"::"git://git.blender.org/blender-translations.git"
		"addons"::"git://git.blender.org/blender-addons.git"
		"addons-contrib"::"git://git.blender.org/blender-addons-contrib.git"
		"mingw-w64-cmake.patch"
		"fix-mingw-w64-definitions.patch"
		"dont-install-thirdparties.patch")
md5sums=('SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'd10c1c251a87afde3e9b33b89a825e49'
         'f0e42986a86128df355242db8e419700'
         '2947b07a04faa1fe2abd88fa2c0f3ea9')

pkgver() {
  cd "$srcdir/$_realname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/$_realname"
	patch -p1 -i ${srcdir}/mingw-w64-cmake.patch
	patch -p1 -i ${srcdir}/fix-mingw-w64-definitions.patch
	patch -p1 -i ${srcdir}/dont-install-thirdparties.patch
	
	cp -rf $srcdir/translations/* $srcdir/$_realname/release/datafiles/locale/
	cp -rf $srcdir/addons/* $srcdir/$_realname/release/scripts/addons/
	cp -rf $srcdir/addons-contrib/* $srcdir/$_realname/release/scripts/addons_contrib/
}

build() {
	
	pushd ${MINGW_PREFIX} > /dev/null
	export PREFIX_DEPS=`pwd -W`
	popd > /dev/null

	mkdir $srcdir/build-${MINGW_CHOST}
	cd $srcdir/build-${MINGW_CHOST}

	[[ $CARCH == i686 ]] && ENABLESSE2="-DSUPPORT_SSE2_BUILD=OFF"
	[[ $CARCH == i686 ]] && BUILDCUDA="OFF" || BUILDCUDA="ON"
	[[ $CARCH == i686 ]] && PMARCH="i686" || PMARCH="x86-64"
	#unset CFLAGS
	#unset CXXFLAGS
	#CFLAGS+=" -D__USE_MINGW_ANSI_STDIO=1"
	#CXXFLAGS+=" -D__USE_MINGW_ANSI_STDIO=1"
	CFLAGS="-O2 -pipe -march=${PMARCH} -mtune=generic -D__USE_MINGW_ANSI_STDIO=1"
	CXXFLAGS="$CFLAGS"
	${MINGW_PREFIX}/bin/cmake \
		-G"MSYS Makefiles" \
		-DMINGW_LIBS=${PREFIX_DEPS} \
		-DCMAKE_BUILD_TYPE=Release \
		-DWITH_SYSTEM_GLEW=ON \
		-DWITH_SYSTEM_OPENJPEG=ON \
		-DWITH_INSTALL_PORTABLE=OFF \
		-DWITH_PYTHON_INSTALL=OFF \
		-DWITH_PYTHON_INSTALL_NUMPY=OFF \
		-DWITH_OPENCOLLADA=ON \
		-DWITH_GAMEENGINE=ON \
		-DWITH_RAYOPTIMIZATION=ON \
		-DWITH_JACK=OFF \
		-DWITH_PLAYER=ON \
		-DWITH_CODEC_FFMPEG=ON \
		-DWITH_CODEC_SNDFILE=ON \
		-DWITH_CYCLES=ON \
		-DWITH_CYCLES_CUDA_BINARIES=OFF \
		-DWITH_CYCLES_OSL=ON \
		-DWITH_FFTW3=ON \
		-DWITH_MOD_OCEANSIM=ON \
		-DPYTHON_VERSION=3.3 \
		-DCYCLES_OSL=${PREFIX_DEPS} \
		-DWITH_IMAGE_REDCODE=ON \
		-DWITH_OPENMP=OFF \
		../$_realname

	# Remove the openjpeg line once blender accepts my patch
	make # -j1 VERBOSE=1
}

package() {	
	cd "$srcdir/build-${MINGW_CHOST}"
	make install
	mkdir -p ${pkgdir}${MINGW_PREFIX}/lib
	cp -r bin ${pkgdir}${MINGW_PREFIX}/
	rm ${pkgdir}${MINGW_PREFIX}/bin/msgfmt.exe
	mv ${pkgdir}${MINGW_PREFIX}/bin/*.a ${pkgdir}${MINGW_PREFIX}/lib/

	${MINGW_REFIX}/bin/python3 -m compileall "${pkgdir}${MINGW_PREFIX}/bin/$blender_version"
	${MINGW_REFIX}/bin/python3 -m compileall \
	${pkgdir}${MINGW_PREFIX}/bin/$blender_version/scripts/startup \
	${pkgdir}${MINGW_PREFIX}/bin/$blender_version/scripts/modules \
	${pkgdir}${MINGW_PREFIX}/bin/$blender_version/scripts/addons
}

