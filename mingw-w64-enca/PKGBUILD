# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=enca
_mingw_suff=mingw-w64-${CARCH}
pkgname=("${_mingw_suff}-${_realname}")
pkgver=1.14
pkgrel=1
pkgdesc="Charset analyser and converter (mingw-w64)"
arch=('any')
url="http://cihar.com/software/enca/"
options=('strip' 'staticlibs')
license=('GPL')
depends=("${_mingw_suff}-crt" "${_mingw_suff}-recode")
makedepends=("${_mingw_suff}-gcc")
source=("http://dl.cihar.com/enca/enca-${pkgver}.tar.bz2"
        'configure.patch')
md5sums=('03792f25aed003d3ea33b3ce1be1f256'
         '6dcdf755664b93764daa9554534f0e5c')

prepare() {
    cd "${srcdir}/enca-${pkgver}"

    patch -Np1 -i "${srcdir}/configure.patch"
    ./autogen.sh
    make distclean
}

build() {
	mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}

	${srcdir}/enca-${pkgver}/configure \
		--prefix=${MINGW_PREFIX} \
		--build=${MINGW_CHOST} \
		--host=${MINGW_CHOST} \
		--libexecdir=${MINGW_PREFIX}/lib \
		--mandir=${MINGW_PREFIX}/share/man \
		--with-librecode=${MINGW_PREFIX} \
		--enable-external
	make
}

package() {
	cd ${srcdir}/build-${MINGW_CHOST}

	make DESTDIR="${pkgdir}" install
	rm -r "${pkgdir}${MINGW_PREFIX}"/share/man
}
