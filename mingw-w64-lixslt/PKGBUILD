# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libxslt
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=1.1.28
pkgrel=1
pkgdesc="XML stylesheet transformation library (mingw-w64)"
arch=('any')
url="http://xmlsoft.org/XSLT/"
license=('custom')
depends=("${_mingw_suff}-crt" "${_mingw_suff}-libxml2" "${_mingw_suff}-libgcrypt")
groups=("${_mingw_suff}")
makedepends=("${_mingw_suff}-gcc")
options=('strip' 'staticlibs')
source=("http://xmlsoft.org/sources/libxslt-${pkgver}.tar.gz"
	"libxslt-1.1.26-w64.patch"
	"libxslt-1.1.27-disable_static_modules.patch"
	"libxslt-1.1.28-win32-shared.patch"
	"libxslt.m4-libxslt-1.1.26.patch"
	0002-python-linking-on.mingw.patch
	0003-fix-concurrent-directory-creation.all.patch
	0004-add-missing-include-for-python.all.patch
	0005-fix-freelocales-export.all.patch)
md5sums=('9667bf6f9310b957254fdcf6596600b7'
         'b994c0d9df0f644e219cf63561ee0d4e'
         '558b6714cf324a7504deb18411792900'
         '19646f155b518b40cb63e41270215964'
         '61b520bbd42006c16bbfc85bc5342b96'
         'a944fcf65f9948bb9dba325a8f6e7c5c'
         'a5b1902b4e254934032fb19f46170418'
         '267c16ba85010d47a86827da1f339bdd'
         'ee6e3e375b8ea2f42c7039e13f14f0c8')

prepare() {
  cd "$srcdir/libxslt-${pkgver}"
  patch -p1 -i "${srcdir}/libxslt-1.1.26-w64.patch"
  patch -p1 -i "${srcdir}/libxslt-1.1.27-disable_static_modules.patch"
  patch -p1 -i "${srcdir}/libxslt-1.1.28-win32-shared.patch"
  patch -p1 -i "${srcdir}/libxslt.m4-libxslt-1.1.26.patch"
  
  patch -p1 -i "${srcdir}/0002-python-linking-on.mingw.patch"
  patch -p1 -i "${srcdir}/0003-fix-concurrent-directory-creation.all.patch"
  patch -p1 -i "${srcdir}/0004-add-missing-include-for-python.all.patch"
  patch -p1 -i "${srcdir}/0005-fix-freelocales-export.all.patch"
  
  libtoolize --copy --force
  aclocal
  automake --add-missing
  autoconf
}

build() {
    mkdir -p "$srcdir/build-${CARCH}" && cd "$srcdir/build-${CARCH}"
    $srcdir/libxslt-${pkgver}/configure \
      --prefix=${MINGW_PREFIX} \
	  --build=${MINGW_CHOST} \
	  --host=${MINGW_CHOST} \
      --target=${MINGW_CHOST} \
      --enable-shared \
      --with-libxml-prefix=${MINGW_PREFIX} \
      --without-python
    make
}

package() {
    cd "$srcdir/build-${CARCH}"
    make DESTDIR="$pkgdir" install
    rm -r "${pkgdir}${MINGW_PREFIX}"/share
    # strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    # strip --strip-all "$pkgdir"/usr/${_arch}/bin/*.exe
    # strip -g "$pkgdir"/usr/${_arch}/lib/*.a
}

