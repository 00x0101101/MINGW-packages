# Maintainer: Renato Silva <br.renatosilva@gmail.com>

_name=xmlstarlet
license=(MIT)
url="http://xmlstar.sourceforge.net"
pkgdesc="Command-line XML toolkit"
pkgver=r678.9a470e3
pkgrel=1
arch=(any)

pkgname=(${MINGW_PACKAGE_PREFIX}-${_name}-git)
provides=(${MINGW_PACKAGE_PREFIX}-${_name})
conflicts=(${MINGW_PACKAGE_PREFIX}-${_name})

md5sums=(SKIP)
source=(${_name}::"git://xmlstar.git.sourceforge.net/gitroot/xmlstar/xmlstar")
makedepends=(${MINGW_PACKAGE_PREFIX}-libxml2
             ${MINGW_PACKAGE_PREFIX}-libxslt)

prepare() {
    cd "$srcdir/${_name}"
    autoreconf -sif
}

pkgver() {
    cd "$srcdir/${_name}"
    printf "r%s.%s" $(git rev-list --count HEAD) $(git rev-parse --short HEAD)
}

build() {
    cd "$srcdir/${_name}"
    ./configure --prefix=${MINGW_PREFIX}
    make
}

package() {
    cd "$srcdir/${_name}"
    make DESTDIR="$pkgdir/" install
    local license_dir="${pkgdir}${MINGW_PREFIX}/share/licenses/${_name}"
    mkdir -p "$license_dir"
    cp -v Copyright "$license_dir/Copyright"
    mv -v "${pkgdir}${MINGW_PREFIX}/bin/xml.exe" "${pkgdir}${MINGW_PREFIX}/bin/xmlstarlet.exe"
}
