# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libmariadbclient
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.2.0
pkgrel=1
pkgdesc="MariaDB client libraries (mingw-w64)"
arch=('any')
url="http://mariadb.org"
license=('LGPL')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-zlib") #"${MINGW_PACKAGE_PREFIX}-sqlite3"
options=('!strip' 'staticlibs')
source=(#"https://downloads.mariadb.org/interstitial/connector-c-${pkgver}/source-tgz/mariadb-connector-c-${pkgver}-src.tar.gz"
        http://mirror.mephi.ru/mariadb/connector-c-${pkgver}/mariadb-connector-c-${pkgver}-src.tar.gz{,.asc}
        'fix-libnames-mingw.patch'
        'use_fopen_for_xp_compatibility.patch'
        'fix-size-t-defined.patch')
sha256sums=('3825b068d38bc19d6ad1eaecdd74bcd49d6ddd9d00559fb150e4e851a55bbbd4'
            'SKIP'
            '7d361086b805972cbe06e3d96aec9511d6f85031127ed49c86c4d5e80f5f4147'
            '81ea9f16845c14a35e9c587ae84dc23c383c8be6d4afcf0587b84972a8f81b4e'
            'f0cba818d125687dc8d04f501484be8d159a1989ff12b5130c2fd213add3e519')

prepare() {
  cd ${srcdir}/mariadb-connector-c-${pkgver}-src
  patch -p1 -i ${srcdir}/fix-libnames-mingw.patch
  patch -p0 -i ${srcdir}/use_fopen_for_xp_compatibility.patch
  patch -p0 -i ${srcdir}/fix-size-t-defined.patch
}

build() {
  rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}
  
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G "MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=RELEASE \
    -DWITH_EXTERNAL_ZLIB=ON \
    -DWITH_SQLITE=OFF \
    -DWITH_OPENSSL=ON \
    -DWITH_MYSQLCOMPAT=OFF \
    ../mariadb-connector-c-${pkgver}-src

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install

  ln -s "${pkgdir}${MINGW_PREFIX}"/include/mariadb "${pkgdir}${MINGW_PREFIX}"/include/mysql

  cp "${pkgdir}${MINGW_PREFIX}"/lib/{libmariadb,libmysqlclient}.dll.a
  cp "${pkgdir}${MINGW_PREFIX}"/lib/{libmariadb,libmysqlclient_r}.dll.a
  cp "${pkgdir}${MINGW_PREFIX}"/lib/{libmariadbclient,libmysqlclient}.a
  cp "${pkgdir}${MINGW_PREFIX}"/lib/{libmariadbclient,libmysqlclient_r}.a
}
