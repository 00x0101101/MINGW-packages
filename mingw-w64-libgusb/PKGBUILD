# Maintainer: Ignacio Casal Quinteiro <icq@gnome.org>

_realname=libgusb
_tarballname=gusb
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.2.3
pkgrel=1
arch=('any')
pkgdesc="GLib wrapper around libusb1 (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-libusb"
         "${MINGW_PACKAGE_PREFIX}-glib2")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-vala"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection")
options=('strip' 'staticlibs')
license=("LGPL")
url="https://github.com/hughsie/libgusb"
source=("https://github.com/hughsie/${_realname}/archive/${_tarballname}_${pkgver//./_}.tar.gz"
        0001-give-cc-to-gir-scanner.all.patch)
sha256sums=('c55dc5735b24eb4ce8e9413be7c65f3177a891d049edaea63f55dd2c011d5e0a'
            'eda4802ca9009eeb58359df4140accf0bacac415387c7e5ad3577048835b7a71')

prepare() {
  cd "${srcdir}"/${_realname}-${_tarballname}_${pkgver//./_}
  patch -p1 -i ${srcdir}/0001-give-cc-to-gir-scanner.all.patch
  gtkdocize
  autoreconf -f -i
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ../${_realname}-${_tarballname}_${pkgver//./_}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib \
    --enable-introspection

  make
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make DESTDIR=$pkgdir install
}
