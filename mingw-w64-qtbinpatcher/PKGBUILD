# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=qtbinpatcher
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.0.0
pkgrel=1
pkgdesc="Patcher for Qt libraries (mingw-w64)"
arch=('any')
url="http://tver-soft.org"
license=("GPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-cmake")
options=('staticlibs' 'strip')
source=(
        "http://tver-soft.org/.files/QtBinPatcher/2.0.0/qtbinpatcher-2.0.0-src.zip"
        #"http://sourceforge.net/projects/qtx64/files/external-binaries/qtbinpatcher/qtbinpatcher-2.0.0-src.zip"
        qtbinpatcher-2.0-mingw-native-separator.patch
        qtbinpatcher-2.0-custom-layout.patch
        qtbinpatcher-2.0-cmake-install.patch)
md5sums=('242eb86514b51abb652d726d64ae9741'
         'bbdbb93dbb3e1e507d1cf3a0cea4c114'
         '5384287cc102bc4923ca01b1652a1609'
         '3199d3a6daf2c20d7181620ca74c8b8b')

prepare() {
  cd ${srcdir}
  patch -p1 -i ${srcdir}/qtbinpatcher-2.0-mingw-native-separator.patch
  patch -p1 -i ${srcdir}/qtbinpatcher-2.0-custom-layout.patch
  patch -p1 -i ${srcdir}/qtbinpatcher-2.0-cmake-install.patch
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}
  
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${pkgdir}${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    ../
  make
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}

  make install
}
