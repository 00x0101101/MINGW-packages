# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=qtbinpatcher
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.1.4
pkgrel=3
pkgdesc="Patcher for Qt libraries (mingw-w64)"
arch=('any')
url="http://tver-soft.org"
license=("GPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-cmake")
options=('staticlibs' 'strip')
source=(${_realname}-${pkgver}.tar.gz::https://github.com/tver-soft/QtBinPatcher/archive/v${pkgver}.tar.gz
        #http://tver-soft.org/dl/${_realname}-${pkgver}.tar.bz2
        001-minimize-files-touching.patch
        002-qt-5.5-compatibility.patch)
md5sums=('cb7dcc9a40b8e46bb09a768426b7025b'
         'c3c99f967b7c619d351740f155adc012'
         '86f2aca492184fb8ff8e40b6b94d5ef1')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-minimize-files-touching.patch
  patch -p1 -i ${srcdir}/002-qt-5.5-compatibility.patch
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}

  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${pkgdir}${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    ../${_realname}-${pkgver}
  make
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}

  make install
}
