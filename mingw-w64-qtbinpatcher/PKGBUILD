# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=qtbinpatcher
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=1.1.2
pkgrel=2
pkgdesc="Patcher for Qt libraries (mingw-w64)"
arch=('any')
url="http://tver-soft.org"
license=("GPL")
makedepends=("${_mingw_suff}-gcc")
options=('staticlibs' 'strip')
source=("qtbinpatcher-${pkgver}.tar.bz2"
    mingw-native-separator.patch
    mingw-custom-layout.patch)
md5sums=('09ff6e8934b8be5022ff4a09e77b4d59'
         'a1b30de9faf00a61cd0ffdde6a9d9537'
         '6b7c5438150ebff061e6f25685eebfd8')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p0 -i ${srcdir}/mingw-native-separator.patch
  patch -p0 -i ${srcdir}/mingw-custom-layout.patch
}

build() {
  cd ${srcdir}/${_realname}-${pkgver}
  rm out/*.o || true
  make -f Makefile.win.msys
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}

  install -dm755 ${pkgdir}${MINGW_PREFIX}/bin
  install -m755 out/qtbinpatcher.exe "${pkgdir}${MINGW_PREFIX}"/bin/qtbinpatcher.exe
}
