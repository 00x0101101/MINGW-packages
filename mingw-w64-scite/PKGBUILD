# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=scite
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}"
         "${MINGW_PACKAGE_PREFIX}-${_realname}-defaults")
pkgver=3.5.3
pkgrel=1
arch=('any')
url='http://www.scintilla.org/SciTE.html'
license=('custom:scite')
source=("http://downloads.sourceforge.net/scintilla/${_realname}${pkgver//./}.tgz"
        "0001-makefiles.patch"
        "0002-paths.patch")
md5sums=('44df4b3e25d346189ddc688c689dcf05'
         '886216497dad9a1d54cedbf436cf110f'
         'ce2853918be638ec465fe314484f3af6')

prepare() {
  cd "${srcdir}"
  patch -p0 -i "${srcdir}"/0001-makefiles.patch
  patch -p0 -i "${srcdir}"/0002-paths.patch
  mkdir "${srcdir}"/build-${CARCH}
  cp -r "${srcdir}"/{scite,scintilla} "${srcdir}"/build-${CARCH}
}

build() {
  cd "${srcdir}"/build-${CARCH}/scintilla/gtk
  make
  cd "${srcdir}"/build-${CARCH}/scintilla/win32
  make
  cd "${srcdir}"/build-${CARCH}/scite/win32
  make
}

package_mingw-w64-scite() {
  pkgdesc='Editor with facilities for building and running programs (mingw-w64)'
  depends=("${MINGW_PACKAGE_PREFIX}-gtk3"
           "${MINGW_PACKAGE_PREFIX}-pkg-config")
  optdepends=("${MINGW_PACKAGE_PREFIX}-${_realname}-defaults: Default language files")

  cd "${srcdir}"/build-${CARCH}

  mkdir -p "${pkgdir}"${MINGW_PREFIX}/bin
  cp scintilla/bin/lib{Scintilla,SciLexer}.dll "${pkgdir}"${MINGW_PREFIX}/bin
  cp scite/bin/SciTE.exe "${pkgdir}"${MINGW_PREFIX}/bin

  mkdir -p "${pkgdir}"${MINGW_PREFIX}/lib
  cp scintilla/bin/libScintillaGtk.a "${pkgdir}"${MINGW_PREFIX}/lib

  mkdir -p "${pkgdir}"${MINGW_PREFIX}/include/scintilla
  cp scintilla/include/{*.h,*.iface} "${pkgdir}"${MINGW_PREFIX}/include/scintilla

  mkdir -p "${pkgdir}"${MINGW_PREFIX}/share/scite
  cp scite/bin/SciTEGlobal.properties "${pkgdir}"${MINGW_PREFIX}/share/scite

  mkdir -p "${pkgdir}"${MINGW_PREFIX}/share/doc/scite
  cp scite/doc/{*.png,*.jpg,*.html} "${pkgdir}"${MINGW_PREFIX}/share/doc/scite

  mkdir -p "${pkgdir}"${MINGW_PREFIX}/share/man/man1
  cp scite/doc/scite.1 "${pkgdir}"${MINGW_PREFIX}/share/man/man1/SciTE.1

  mkdir -p "${pkgdir}"${MINGW_PREFIX}/share/licenses/scite
  cp scintilla/License.txt "${pkgdir}"${MINGW_PREFIX}/share/licenses/scite/LICENSE-scintilla
  cp scite/License.txt "${pkgdir}"${MINGW_PREFIX}/share/licenses/scite/LICENSE-scite
  cp scite/lua/COPYRIGHT "${pkgdir}"${MINGW_PREFIX}/share/licenses/scite/LICENSE-lua
}

package_mingw-w64-scite-defaults() {
  pkgdesc='Default language files for the SciTE editor (mingw-w64)'
  depends=("${MINGW_PACKAGE_PREFIX}-${_realname}")
  
  cd "${srcdir}"/build-${CARCH}

  mkdir -p "${pkgdir}"${MINGW_PREFIX}/share/scite
  cp scite/bin/*.properties "${pkgdir}"${MINGW_PREFIX}/share/scite
  rm -f "${pkgdir}"${MINGW_PREFIX}/share/scite/SciTEGlobal.properties
}

package_mingw-w64-i686-scite() {
  package_mingw-w64-scite
}

package_mingw-w64-i686-scite-defaults() {
  package_mingw-w64-scite-defaults
}

package_mingw-w64-x86_64-scite() {
  package_mingw-w64-scite
}

package_mingw-w64-x86_64-scite-defaults() {
  package_mingw-w64-scite-defaults
}
