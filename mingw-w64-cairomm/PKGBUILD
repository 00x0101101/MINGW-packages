# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=cairomm
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=1.10.0
pkgrel=1
pkgdesc="C++ bindings to Cairo vector graphics library (mingw-w64)"
arch=('any')
url="http://www.cairographics.org"
license=("LGPL, MPL")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config" "${_mingw_suff}-glib2")
depends=("${_mingw_suff}-crt" "${_mingw_suff}-libsigc++" "${_mingw_suff}-cairo")
options=(strip staticlibs)
source=("http://www.cairographics.org/releases/cairomm-${pkgver}.tar.gz")
sha1sums=('f08bf8a331067f0d1e876523f07238fba6b26b99')

build() {
	CPPFLAGS+=" -D_REENTRANT"
	export lt_cv_deplibs_check_method='pass_all'
	mkdir -p "${srcdir}/build-${MINGW_CHOST}"
	cd "${srcdir}/build-${MINGW_CHOST}"
	"${srcdir}"/${_realname}-${pkgver}/configure \
		--prefix=${MINGW_PREFIX} \
		--build=${MINGW_CHOST} \
		--host=${MINGW_CHOST} \
		--enable-shared \
		--enable-static \
		--disable-documentation
	make
}

package() {
	cd "${srcdir}/build-${MINGW_CHOST}"
	make DESTDIR="$pkgdir" install
	find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
}