# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libxslt
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=1.1.28
pkgrel=3
pkgdesc="XML stylesheet transformation library (mingw-w64)"
arch=('any')
url="http://xmlsoft.org/XSLT/"
license=('custom')
depends=("${_mingw_suff}-gcc-libs" "${_mingw_suff}-libxml2" "${_mingw_suff}-libgcrypt")
groups=("${_mingw_suff}")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config")
options=('strip' 'staticlibs')
case $MINGW_CHOST in
	i686*)
		install=${_realname}-32.install
	;;
	x86_64*)
		install=${_realname}-64.install
	;;
esac
source=("http://xmlsoft.org/sources/libxslt-${pkgver}.tar.gz"
		"libxslt-1.1.26-w64.patch"
		"libxslt-1.1.27-disable_static_modules.patch"
		"libxslt-1.1.28-win32-shared.patch"
		"libxslt.m4-libxslt-1.1.26.patch"
		'0002-python-linking-on.mingw.patch'
		'0003-fix-concurrent-directory-creation.all.patch'
		'0004-add-missing-include-for-python.all.patch'
		'0005-fix-freelocales-export.all.patch')
md5sums=('9667bf6f9310b957254fdcf6596600b7'
         'b994c0d9df0f644e219cf63561ee0d4e'
         '558b6714cf324a7504deb18411792900'
         '19646f155b518b40cb63e41270215964'
         '61b520bbd42006c16bbfc85bc5342b96'
         'fa008485e7ae4d812235a1437dfd512f'
         'cf3c1fb253a4a3bdeb099a0f6c55220a'
         '267c16ba85010d47a86827da1f339bdd'
         'ee6e3e375b8ea2f42c7039e13f14f0c8')

prepare() {
	cd "$srcdir/libxslt-${pkgver}"
	patch -p1 -i "${srcdir}/libxslt-1.1.26-w64.patch"
	patch -p1 -i "${srcdir}/libxslt-1.1.27-disable_static_modules.patch"
	patch -p1 -i "${srcdir}/libxslt-1.1.28-win32-shared.patch"
	patch -p1 -i "${srcdir}/libxslt.m4-libxslt-1.1.26.patch"

	patch -p1 -i "${srcdir}/0002-python-linking-on.mingw.patch"
	patch -p1 -i "${srcdir}/0003-fix-concurrent-directory-creation.all.patch"
	patch -p1 -i "${srcdir}/0004-add-missing-include-for-python.all.patch"
	patch -p1 -i "${srcdir}/0005-fix-freelocales-export.all.patch"

	libtoolize --copy --force
	aclocal
	automake --add-missing
	autoconf
}

build() {
	mkdir -p "$srcdir/build-${CARCH}" && cd "$srcdir/build-${CARCH}"
	$srcdir/libxslt-${pkgver}/configure \
		--prefix=${MINGW_PREFIX} \
		--build=${MINGW_CHOST} \
		--host=${MINGW_CHOST} \
		--target=${MINGW_CHOST} \
		--enable-shared \
		--with-crypto \
		--with-plugins \
		--with-libxml-prefix=${MINGW_PREFIX} \
		--with-python=${MINGW_PREFIX}/bin/python2

	# Hack for mingw python
	pushd ${MINGW_PREFIX} > /dev/null
	export PREFIX_DEPS=`pwd -W`
	popd > /dev/null
	find . -type f -name "Makefile" -exec sed -i "s|${PREFIX_DEPS}|${MINGW_PREFIX}|g" {} \;
	make
}

package() {
	cd "$srcdir/build-${CARCH}"
	make DESTDIR="$pkgdir" install
	#rm -r "${pkgdir}${MINGW_PREFIX}"/share
}

