# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gstreamer
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=1.2.2
pkgrel=1
pkgdesc="GStreamer Multimedia Framework (mingw-w64)"
arch=('any')
url="http://gstreamer.freedesktop.org/"
license=("LGPL")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config" "intltool" "gtk-doc" "flex" "bison")
depends=(
		"${_mingw_suff}-gcc-libs"
		"${_mingw_suff}-libxml2"
		"${_mingw_suff}-glib2"
		)
options=(!libtool strip staticlibs)
source=("${url}/src/gstreamer/gstreamer-${pkgver}.tar.xz"
		0004-fix-pkgconfig-invocation.mingw.patch)
md5sums=('4293ca4d8333690d5acdffe3ad354924'
         'f2f2392fccc8302734c4e9e7fa0c8581')

prepare() {
	cd ${srcdir}/${_realname}-${pkgver}
	patch -p1 -i ${srcdir}/0004-fix-pkgconfig-invocation.mingw.patch
}

build() {
	mkdir -p "${srcdir}/build-${MINGW_CHOST}"
	[[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
	cp -r "${srcdir}/${_realname}-${pkgver}" "${srcdir}/build-${MINGW_CHOST}"
	cd "${srcdir}/build-${MINGW_CHOST}"
	./configure \
		--prefix=${MINGW_PREFIX} \
		--build=${MINGW_CHOST} \
		--host=${MINGW_CHOST} \
		--disable-static \
		--enable-shared \
		--enable-silent-rules \
		--disable-poisoning \
		--disable-gtk-doc \
		--enable-docbook \
		--enable-check \
		--enable-debug
	make
}

package() {
	cd "${srcdir}/build-${MINGW_CHOST}"
	make -j1 DESTDIR="$pkgdir" install
	find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
}
