# From Greblus: http://blog.greblus.net/2014/11/18/msys2mingw-w64-cython-i-ipython-notebook/

_realname=zeromq
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.1.2
pkgrel=1
pkgdesc="Fast messaging system built on sockets, C and C++ bindings. aka 0MQ, ZMQ. (mingw-w64)"
arch=('any')
url="http://www.zeromq.org"
license=("LGPL")
depends=("${MINGW_PACKAGE_PREFIX}-libsodium")
makedepends=('')
options=('staticlibs' '!strip' '!buildflags')
source=("http://download.zeromq.org/${_realname}-${pkgver}.tar.gz")
md5sums=('159c0c56a895472f02668e692d122685')

build() {
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p ${srcdir}/build-${CARCH}
  cd ${srcdir}/build-${CARCH}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --with-libsodium

  make
}

package() {
  mkdir -p "${pkgdir}${MINGW_PREFIX}/bin"

  cd "${srcdir}/build-${CARCH}"
  make DESTDIR="${pkgdir}" install
}
