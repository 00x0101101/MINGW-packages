# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=nlopt
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.4.2
pkgrel=1
pkgdesc="A library for nonlinear optimization (mingw-w64)"
arch=('any')
makedepends=("${MINGW_PACKAGE_PREFIX}-swig")
url='http://ab-initio.mit.edu/wiki/index.php/NLopt'
license=('LGPLv2.1+')

source=("https://github.com/stevengj/nlopt/archive/${_realname}-${pkgver}.tar.gz"
        "config.sub"
        "config.guess")

sha256sums=('d838b5b4b1c6b6493666ff61a8817a4ebcee924f54fb95f6f64e5f727ddbf2a6'
            '75d5d255a2a273b6e651f82eecfabf6cbcd8eaeae70e86b417384c8f4a58d8d3'
            '467e0886e3d4b06087e704dc9e22bbb8f93eb8c7b81aeae1ee0000d06aa4c45f')

prepare() {
  cd "${srcdir}"/${_realname}-${_realname}-${pkgver}
  cp -f "${srcdir}"/config.{sub,guess} .
  ./autogen.sh --no-configure
}

build() {
  cd "$srcdir"/${_realname}-${_realname}-${pkgver}
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}
  ../${_realname}-${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-maintainer-mode \
    --enable-static \
    --enable-shared \
    --without-python

  # -j1 because build system is a bit racy around the swig scm and scm.in file.
  make -j1
}

check() {
  cd "${srcdir}"/build-${CARCH}
  make check
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make install DESTDIR="${pkgdir}"
}
