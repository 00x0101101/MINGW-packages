# Maintainer: Martell Malone <martellmalone@gmail.com>

_realname=luajit
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgver=v2.0.3.40.g3f2e4ec
pkgrel=1
pkgdesc="Just-in-time compiler and drop-in replacement for Lua 5.1 (mingw-w64)"
arch=('any')
url="http://luajit.org/"
license=('MIT')
depends=('winpty-git')
conflicts=("${MINGW_PACKAGE_PREFIX}-lua51")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "git")
options=('strip' 'staticlibs')
source=("${_realname}"::"git+http://luajit.org/git/luajit-2.0.git")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_realname}"
  git describe --tags | sed 's|-|.|g'
}

prepare() {
  sed -i "s|export PREFIX= /usr/local|export PREFIX="${pkgdir}${MINGW_PREFIX}"|g" $srcdir/$_realname/Makefile
}

build() {
  cd "${srcdir}/${_realname}"
  make
}


package() {
  cd "${srcdir}/${_realname}"
  make install

  _exename=$_realname
  echo "#!/usr/bin/env bash" > "${pkgdir}${MINGW_PREFIX}/bin/${_exename}"
  echo '/usr/bin/winpty "$( dirname ${BASH_SOURCE[0]} )/'${_exename}'.exe" "$@"' >> "${pkgdir}${MINGW_PREFIX}/bin/${_exename}"

  install -v -Dm644 "$srcdir/$_realname/src/lua51.dll" "${pkgdir}${MINGW_PREFIX}/bin/lua51.dll" || exit 1
  install -v -Dm644 "$srcdir/$_realname/src/luajit.exe" "${pkgdir}${MINGW_PREFIX}/bin/luajit.exe" || exit 1

}
