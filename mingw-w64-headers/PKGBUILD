# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=headers
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}-svn"
pkgdesc="MinGW-w64 headers for Windows"
_ver_base=4.0.0
pkgver=4.0.0.6468
pkgrel=1
arch=('any')
url="http://mingw-w64.sourceforge.net"
license=('custom')
groups=("${_mingw_suff}-toolchain" "${_mingw_suff}")
depends=()
makedepends=('subversion')
optdepends=()
provides=("${_mingw_suff}-${_realname}")
conflicts=("${_mingw_suff}-${_realname}")
replaces=()
backup=()
options=('!strip' '!libtool' '!emptydirs')
source=("${_realname}"::'svn+svn://svn.code.sf.net/p/mingw-w64/code/trunk/mingw-w64-headers'
		0001-d3d9.h-Added-__uuidof-support-to-interfaces.patch
		0002-Add-missing-API-to-directx9-shader-header.patch)
md5sums=('SKIP'
         'e0a110be67cc8f0fedddcd3b8b913074'
         '067a17efcdc4b0bfaff82cbed9009cd4')

pkgver() {
  cd "$srcdir/$_realname"
  local ver="$(svnversion)"
  printf "%s.%s" "${_ver_base}" "${ver//[[:alpha:]]}"
}

prepare() {
	cd ${srcdir}/${_realname}
	patch -p1 -i ${srcdir}/0001-d3d9.h-Added-__uuidof-support-to-interfaces.patch
	patch -p1 -i ${srcdir}/0002-Add-missing-API-to-directx9-shader-header.patch
}

build() {
    msg "Configuring ${MINGW_CHOST} headers"
    mkdir -p ${srcdir}/headers-${MINGW_CHOST} && cd ${srcdir}/headers-${MINGW_CHOST}
    ${srcdir}/${_realname}/configure \
		--build=${MINGW_CHOST} \
		--host=${MINGW_CHOST} \
		--target=${MINGW_CHOST} \
		--prefix=${MINGW_PREFIX}/${MINGW_CHOST} \
		--enable-sdk=all \
		--enable-secure-api
}

package() {
    msg "Installing ${MINGW_CHOST} headers"
    cd ${srcdir}/headers-${MINGW_CHOST}
    make DESTDIR=${pkgdir} install
	
	#mkdir -p ${pkgdir}${MINGW_PREFIX}/${MINGW_CHOST}
	#mv ${pkgdir}${MINGW_PREFIX}/include ${pkgdir}${MINGW_PREFIX}/${MINGW_CHOST}/
	
    rm ${pkgdir}${MINGW_PREFIX}/${MINGW_CHOST}/include/pthread_signal.h
    rm ${pkgdir}${MINGW_PREFIX}/${MINGW_CHOST}/include/pthread_time.h
    rm ${pkgdir}${MINGW_PREFIX}/${MINGW_CHOST}/include/pthread_unistd.h

	msg "Installing MinGW-w64 licenses"
	install -Dm644 ${srcdir}/${_realname}/ddk/readme.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/${pkgname}/ddk-readme.txt
	install -Dm644 ${srcdir}/${_realname}/direct-x/COPYING.LIB ${pkgdir}${MINGW_PREFIX}/share/licenses/${pkgname}/direct-x-COPYING.LIB
	install -Dm644 ${srcdir}/${_realname}/direct-x/readme.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/${pkgname}/direct-x-readme.txt	
}
