# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname='mingw-w64-headers-svn'
pkgdesc="MinGW-w64 headers for Windows"
_ver_base=4.0.0
pkgver=4.0.0.6378
pkgrel=1
arch=('i686' 'x86_64')
url="http://mingw-w64.sourceforge.net"
license=('custom')
groups=('mingw-w64-toolchain' 'mingw-w64')
depends=()
makedepends=('subversion')
optdepends=()
provides=('mingw-w64-headers')
conflicts=('mingw-w64-headers')
replaces=()
backup=()
options=('!strip' '!libtool' '!emptydirs')
source=("${pkgname}"::'svn+svn://svn.code.sf.net/p/mingw-w64/code/trunk/mingw-w64-headers')
md5sums=(SKIP)

pkgver() {
  cd "$srcdir/$pkgname"
  local ver="$(svnversion)"
  printf "%s.%s" "${_ver_base}" "${ver//[[:alpha:]]}"
}

build() {
    msg "Configuring ${MINGW_CHOST} headers"
    mkdir -p ${srcdir}/headers-${MINGW_CHOST} && cd ${srcdir}/headers-${MINGW_CHOST}
    ${srcdir}/${pkgname}/configure \
		--build=${MINGW_CHOST} \
		--host=${MINGW_CHOST} \
		--target=${MINGW_CHOST} \
		--prefix=${MINGW_PREFIX} \
		--enable-sdk=all \
		--enable-secure-api
}

package() {
    msg "Installing ${MINGW_CHOST} headers"
    cd ${srcdir}/headers-${MINGW_CHOST}
    make DESTDIR=${pkgdir} install
	
	mkdir -p ${pkgdir}${MINGW_PREFIX}/${MINGW_CHOST}
	mv ${pkgdir}${MINGW_PREFIX}/include ${pkgdir}${MINGW_PREFIX}/${MINGW_CHOST}/
	
    rm ${pkgdir}${MINGW_PREFIX}/${MINGW_CHOST}/include/pthread_signal.h
    rm ${pkgdir}${MINGW_PREFIX}/${MINGW_CHOST}/include/pthread_time.h
    rm ${pkgdir}${MINGW_PREFIX}/${MINGW_CHOST}/include/pthread_unistd.h

	msg "Installing MinGW-w64 licenses"
	install -Dm644 ${srcdir}/${pkgname}/ddk/readme.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/${pkgname}/ddk-readme.txt
	install -Dm644 ${srcdir}/${pkgname}/direct-x/COPYING.LIB ${pkgdir}${MINGW_PREFIX}/share/licenses/${pkgname}/direct-x-COPYING.LIB
	install -Dm644 ${srcdir}/${pkgname}/direct-x/readme.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/${pkgname}/direct-x-readme.txt	
}
