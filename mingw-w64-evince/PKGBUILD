# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=evince
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.13.3
pkgrel=1
arch=('any')
pkgdesc="Document (PostScript, PDF) viewer (mingw-w64)"
options=(strip staticlibs)
depends=("${MINGW_PACKAGE_PREFIX}-glib2"
        "${MINGW_PACKAGE_PREFIX}-cairo"
        "${MINGW_PACKAGE_PREFIX}-gtk3"
        "${MINGW_PACKAGE_PREFIX}-libgxps"
        "${MINGW_PACKAGE_PREFIX}-libspectre"
        "${MINGW_PACKAGE_PREFIX}-libtiff"
        "${MINGW_PACKAGE_PREFIX}-djvulibre"
        "${MINGW_PACKAGE_PREFIX}-gsettings-desktop-schemas"
        "${MINGW_PACKAGE_PREFIX}-poppler"
        "${MINGW_PACKAGE_PREFIX}-adwaita-icon-theme")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
            "${MINGW_PACKAGE_PREFIX}-pkg-config"
            "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
            "make"
            "libtool"
            "automake-wrapper"
            "autoconf"
            "intltool"
            "itstool"
            "gettext-devel"
            "yelp-tools")
license=("GPL 2")
url="http://www.gnome.org"
install=evince-${CARCH}.install
source=(http://ftp.gnome.org/pub/gnome/sources/${_realname}/${pkgver:0:4}/${_realname}-$pkgver.tar.xz)
sha256sums=('44a223fe847d5c8da9e883060931e86155cb8d72f0b29de86b083c199d256ffd')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  #autopoint --force
  AUTOPOINT='intltoolize --automake --copy' autoreconf -f -i
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib \
    --disable-dbus \
    --disable-nautilus
  make
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make DESTDIR=$pkgdir install
}
