# Maintainer: Lubosz Sarnecki <lubosz@gmail.com>

_realname=cogl
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgtag="1.18.2"
pkgver=2.0.0.3265.58dec43
pkgrel=1
pkgdesc="An object oriented GL/GLES Abstraction/Utility Layer. (mingw-w64)"
arch=('any')
url="http://www.clutter-project.org/"
license=('GPL2')
makedepends=('automake' 'libtool' 'intltool' 'itstool' 'gtk-doc' 'gnome-doc-utils' 'yelp-tools')
depends=("${MINGW_PACKAGE_PREFIX}-pango"
         "${MINGW_PACKAGE_PREFIX}-gdk-pixbuf2"
         "${MINGW_PACKAGE_PREFIX}-mesa"
        )
options=(!libtool strip staticlibs)
source=("git://git.gnome.org/$_realname#tag=${pkgtag}"
        "0001-msys2-fix-introspection-build.patch")
md5sums=("SKIP"
         "66134daa1df08ebfa143b12c08f3290e")

pkgver() {
  cd $_realname
  major=$(grep "cogl_major_version]," configure.ac | sed 's/m4_define(\[cogl_major_version\], \[//' | sed 's/\])//')
  minor=$(grep "cogl_minor_version]," configure.ac | sed 's/m4_define(\[cogl_minor_version\], \[//' | sed 's/\])//')
  micro=$(grep "cogl_micro_version]," configure.ac | head -1 | sed 's/m4_define(\[cogl_micro_version\], \[//' | sed 's/\])//')
  hash=$(git log --pretty=format:'%h' -n 1)
  revision=$(git rev-list --count HEAD)
  echo $major.$minor.$micro.$revision.$hash
}

prepare() {
  cd $_realname
  git am ../0001-msys2-fix-introspection-build.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  cp -r "${srcdir}/${_realname}" "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --disable-static \
    --enable-shared \
    --enable-silent-rules \
    --disable-gtk-doc
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 DESTDIR="$pkgdir" install
}
